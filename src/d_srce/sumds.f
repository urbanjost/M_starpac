*SUMDS
      SUBROUTINE SUMDS(X, N, LO, MID, HI, XMEANW, SUMDA, SUMD2, SUMD3,
     +   SUMD4)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     A ROUTINE TO CALCULATE UNWEIGHTED SUMS OF POWERS OF
C     DIFFERENCES FROM THE WEIGHTED MEAN FOR A SORTED
C     VECTOR IN WHICH THE MIDTH ELEMENT IS THE ELEMENT
C     CLOSEST TO ZERO.  USED BY THE STAT FAMILY.
C
C     WRITTEN BY - JOHN E. KOONTZ
C                  STATISTICAL ENGINEERING DIVISION
C                  NATIONAL BUREAU OF STANDARDS
C
C     CREATION DATE  -  MAY 17, 1982
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      DOUBLE PRECISION
     +   SUMD2,SUMD3,SUMD4,SUMDA,XMEANW
      INTEGER
     +   HI,LO,MID,N
C
C  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   X(N)
C
C  LOCAL SCALARS
      DOUBLE PRECISION
     +   DIFF
      INTEGER
     +   CURHI,CURLO,I,IREV
C
C  INTRINSIC FUNCTIONS
      INTRINSIC ABS
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     INTEGER CURHI
C        THE UPPER BOUND OF THE CURRENT INTERVAL.
C     INTEGER CURLO
C        THE LOWER BOUND OF THE CURRENT INTERVAL.
C     DOUBLE PRECISION DIFF
C        THE DIFFERENCES BETWEEN X(I) AND XMEANW.
C     INTEGER HI
C        INPUT PARAMETER.  THE UPPER BOUND OF THE INITIAL
C        INTERVAL.
C     INTEGER I
C        A LOOP PARAMETER.
C     INTEGER IREV
C        A VARIABLE WHICH RUNS IN THE SAME INTERVAL AS I, BUT
C        IN THE REVERSE ORDER.
C     INTEGER LO
C        INPUT PARAMETER.  THE LOWER BOUND OF THE INITIAL
C        INTERVAL.
C     INTEGER MID
C        INPUT PARAMETER.  THE INDEX OF THE ELEMENT IN X CLOSEST TO
C        ZERO IN VALUE.  THE POINT OUT FROM WHICH THE SUMMING IS
C        DONE.
C     INTEGER N
C        INPUT PARAMETER.  THE LENGTH OF THE ARRAY X.
C     DOUBLE PRECISION SUMDA
C        OUTPUT PARAMETER.  THE SUM OF THE ABSOLUTE VALUES OF THE
C        DIFFERENCES DIFF.
C     DOUBLE PRECISION SUMD2
C        OUTPUT PARAMETER.  THE SUM OF THE SQUARES OF THE
C        DIFFERENCES DIFF.
C     DOUBLE PRECISION SUMD3
C        OUTPUT PARAMETER.  THE SUM OF THE CUBES OF THE
C        DIFFERENCES DIFF.
C     DOUBLE PRECISION SUMD4
C        OUTPUT PARAMETER.  THE SUM OF THE HYPERCUBES OF THE
C        DIFFERENCES DIFF.
C     DOUBLE PRECISION X(N)
C        INPUT PARAMETER.  THE DATA ARRAY X IN WHICH THE SUMS ARE TAKEN.
C     DOUBLE PRECISION XMEANW
C        INPUT PARAMETER.  THE WEIGHTED MEAN OF X.
C
C     INITIALIZE SUMMATION VARIABLES.
C
      DIFF = X(MID) - XMEANW
      SUMDA = ABS(DIFF)
      SUMD2 = DIFF*DIFF
      SUMD3 = DIFF*DIFF*DIFF
      SUMD4 = DIFF*DIFF*DIFF*DIFF
      CURLO = MID - 1
      CURHI = MID + 1
C
C     SUM OUTWARDS FROM THE VALUE NEAREST ZERO.  THAT IS, SUM FROM
C     THE LEAST IN MAGNITUDE TO THE GREATEST.
C
   10 IF (CURHI.GT.HI .OR. CURLO.LT.LO) GO TO 30
      IF (ABS(X(CURHI)).LT.ABS(X(CURLO))) GO TO 20
      DIFF = X(CURLO) - XMEANW
      SUMDA = SUMDA + ABS(DIFF)
      SUMD2 = SUMD2 + DIFF*DIFF
      SUMD3 = SUMD3 + DIFF*DIFF*DIFF
      SUMD4 = SUMD4 + DIFF*DIFF*DIFF*DIFF
      CURLO = CURLO - 1
      GO TO 10
   20 DIFF = X(CURHI) - XMEANW
      SUMDA = SUMDA + ABS(DIFF)
      SUMD2 = SUMD2 + DIFF*DIFF
      SUMD3 = SUMD3 + DIFF*DIFF*DIFF
      SUMD4 = SUMD4 + DIFF*DIFF*DIFF*DIFF
      CURHI = CURHI + 1
      GO TO 10
C
C     SUM UP ANY VALUES WHICH MAY REMAIN BECAUSE OF AN OFF
C     CENTER ZERO POINT.
C
   30 IF (CURHI.GT.HI) GO TO 50
      DO 40 I=CURHI,HI
         DIFF = X(I) - XMEANW
         SUMDA = SUMDA + ABS(DIFF)
         SUMD2 = SUMD2 + DIFF*DIFF
         SUMD3 = SUMD3 + DIFF*DIFF*DIFF
         SUMD4 = SUMD4 + DIFF*DIFF*DIFF*DIFF
   40 CONTINUE
   50 IF (CURLO.LT.LO) GO TO 70
      DO 60 IREV=LO,CURLO
         I = LO - IREV + CURLO
         DIFF = X(I) - XMEANW
         SUMDA = SUMDA + ABS(DIFF)
         SUMD2 = SUMD2 + DIFF*DIFF
         SUMD3 = SUMD3 + DIFF*DIFF*DIFF
         SUMD4 = SUMD4 + DIFF*DIFF*DIFF*DIFF
   60 CONTINUE
   70 RETURN
      END
