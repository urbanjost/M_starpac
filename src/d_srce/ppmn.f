*PPMN
      SUBROUTINE PPMN (YM, YMMISS, X, XMISS, N, M, IYM, ISCHCK, ISYM,
     +  LISYM, ISIZE, NOUT, YMN, YMX, XMN, XMX, MISS, ILOG)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS IS THE MAIN ROUTINE FOR PAGE PLOTS
C
C     WRITTEN BY  --
C                 JANET R. DONALDSON
C                 STATISTICAL ENGINEERING DIVISION/BOULDER
C                 NATIONAL BUREAU OF STANDARDS
C
C     CREATION DATE  -  NOVEMBER 21, 1980
C
C     INPUT ARGUMENTS - (IN ORDER OF USAGE)
C
C        X  THE N VECTOR OF OBSERVATIONS FOR THE X
C           COORDINATES
C        Y  THE N BY M MATRIX OF OBSERVATIONS FOR THE Y (VERTICAL)
C           COORDINATES
C           THE ITH COLUMN IS PLOTTED USING THE FOLLOWING SYMBOL
C               1 = A    2 = B    3 = C    4 = D
C               5 = E    6 = F    7 = G    8 = H
C               9 = I   10 = J   11 = K   12 = L
C              13 = M   14 = N   15 = O   16 = P
C              17 = Q   18 = R   19 = S   20 = T
C              21 = U   22 = V   23 = W   24 = Y
C              25 (AND ABOVE) = Z
C           THE NUMBERS 1 TO 9 INDICATE MULTIPLE POINTS ON A GIVEN
C           PLOT LOCATION, WHERE THE NUMBER INDICATES HOW MANY POINTS
C           ARE REPRESENTED
C           NOTE THAT  X  IS NOT USED AS A PLOTTING SYMBOL EXCEPT TO
C           INDICATE THAT MORE THAN 9 POINTS FELL ON THE SAME PLOT
C           LOCATION
C        N  THE INTEGER NUMBER OF OBSERVATIONS TO BE PLOTTED (IN EACH
C           COLUMN)
C        M  THE NUMBER OF COLUMNS IN THE Y ARRAY TO BE PLOTTED VERSUS X
C           FOR THE CASE OF A VECTOR Y, M MUST BE EQUAL TO 1
C    IYM  THE ACTUAL INTEGER VALUE OF THE ROW DIMENSION OF THE Y ARRAY
C           WHEN Y IS A VECTOR (M.EQ.1) IYM SHOULD BE SET EQUAL TO N
C   ISCHCK  THE INTEGER VALUE INDICATING HOW THE PLOTTING SYMBOLS
C           WILL BE DESIGNATED, WHERE
C           0 INDICATES THE PLOTTING SYMBOLS HAVE NOT BEEN DESIGNATED IN
C             THE N VECTOR ISYM AND ONLY THE SYMBOL + IS TO BE USED
C           1 INDICATES THE PLOTTING SYMBOLS HAVE BEEN DESIGNATED IN THE
C             N VECTOR ISYM
C           2 INDICATES THAT M SERIES ARE BEING PLOTTED.
C             SYMBOL I+4 WILL BE USED FOR COLUMN I OF YM.
C     ISYM  THE N VECTOR OF INTEGERS DETERMINING THE PLOTTING SYMBOLS TO
C           USED FOR THE S SERIES OF PLOTS, WHERE
C               1 = +    2 = .    3 = *    4 = -
C               5 = A    6 = B    7 = C    8 = D
C               9 = E   10 = F   11 = G   12 = H
C              13 = I   14 = J   15 = K   16 = L
C              17 = M   18 = N   19 = O   20 = P
C              21 = Q   22 = R   23 = S   24 = T
C              25 = U   26 = V   27 = W   28 = Y
C              29 (AND ABOVE) = Z
C           THE NUMBERS 1 TO 9 INDICATE MULTIPLE POINTS ON A GIVEN
C           PLOT LOCATION, WHERE THE NUMBER INDICATES HOW MANY POINTS
C           ARE REPRESENTED
C           NOTE THAT  X  IS NOT USED AS A PLOTTING SYMBOL EXCEPT TO
C           INDICATE THAT MORE THAN 9 POINTS FELL ON THE SAME PLOT
C           LOCATION
C    ISIZE  THE INTEGER CODE FOR THE PLOT SIZE, WHERE
C           0 INDICATES A PLOT 100 COL BY 50 ROWS
C           1 INDICATES A PLOT 50 COL BY  50 ROWS
C     NOUT  THE INTEGER VALUE INDICATING HOW MANY OF THE POINTS WHICH
C           FELL OUTSIDE OF THE GRAPH LIMITS ARE TO BE LISTED
C           IF XLB.EQ.XUB AND YLB.EQ.YUB, NOUT SHOULD BE SET TO ZERO
C     XLB  THE MINIMUM VALUE OF X TO BE PLOTTED(IE, THE LOWER BOUND FOR
C           THE X AXIS), WHERE IF XLB=XUB THE ROUTINE WILL DETERMINE
C           THIS VALUE FROM THE MINIMUM VALUE OF THE X VECTOR
C     XUB  THE MAXIMUM VALUE OF X TO BE PLOTTED(IE, THE UPPER BOUND FOR
C           THE X AXIS), WHERE IF XLB=XUB THE ROUTINE WILL DETERMINE
C           THIS VALUE FROM THE MAXIMUM VALUE OF THE X VECTOR
C     YLB  THE MINIMUM VALUE OF Y TO BE PLOTTED(IE, THE LOWER BOUND FOR
C           THE Y AXIS), WHERE IF YLB=YUB THE ROUTINE WILL DETERMINE
C           THIS VALUE FROM THE MINIMUM VALUE OF THE Y VECTOR
C     YUB  THE MAXIMUM VALUE OF Y TO BE PLOTTED(IE, THE UPPER BOUND FOR
C           THE Y AXIS), WHERE IF YLB=YUB THE ROUTINE WILL DETERMINE
C           THIS VALUE FROM THE MAXIMUM VALUE OF THE Y VECTOR
C     ILOG  THE INTEGER INDICATOR VARIABLE USED TO DETERMINE WHETHER
C           THE Y AXIS SCALE IS TO BE LOG OR NOT
C           IF ILOG.EQ.0, THE SCALE IS NOT LOG
C           IF ILOG.NE.0, THE SCALE IS LOG
C
C
C     ADDITIONAL VARIABLES USED - (IN ALPHABETICAL ORDER)
C
C    ALINE  THE VECTOR OF THE CURRENT PLOT LINE
C   ALPHAI  THE PLOT AXIS SYMBOL I
C   AXISCH  THE Y A AXIS SYMBOL TO BE USED FOR THE CURRENT LINE,
C           EITHER I OR -
C   ALPHAX  THE PLOTTING SYMBOL X DESIGNATING MORE THAN 9 POINTS FELL ON
C           A SINGLE PLOTTING LOCATION
C    BLANK  THE PLOTTING SYMBOL BLANK
C     DELX  THE RANGE OF THE X AXIS
C     DELY  THE RANGE OF THE Y AXIS
C   HYPHEN  THE PLOT AXIS SYMBOL -
C       IC  THE COUNT OF THE NUMBER OF VALUES FALLING OUTSIDE OF THE
C           GRAPH BOUNDS
C     ICOL  THE COLUMN LOCATION FOR THE PLOT LINE
C     IOUT  THE MINIMUM OF NOUT OR 50, INDICATING HOW MANY OF THE
C           POINTS WHICH FELL OUTSIDE OF THE GRAPH LIMITS WILL ACTUALLY
C           BE LISTED
C   IPCODE  THE INTEGER CODE, USED IN ERROR CHECKING, WHICH DETERMINES
C           WHICH PLOT ROUTINE HAS BEEN CALLED
C      IPR  THE UNIT NUMBER OF THE PRINTER
C    ITEST  THE INDICATOR VARIABLE FOR WHETHER THE X AXIS LABELS ARE
C           PRINTED IN E OR F FORMAT
C      KSS  AN INTEGER VECTOR USED IN DETERMINING THE PLOT SYMBOL
C           NEEDED
C       NN  THE NUMBER OF Y LABELS TO BE LISTED ON THE LEFT AXIS,
C           (DEPENDENT ON THE GRAPH SIZE)
C   NUMCOL  THE INTEGER VALUE OF THE NUMBER OF COLUMNS IN THE GRAPH
C   NUMLAB  THE INTEGER NUMBER OF X LABELS TO BE LISTED AT THE BOTTOM
C           OF THE GRAPH (DEPENDENT ON GRAPH SIZE)
C   NUMROW  THE INTEGER NUMBER OF ROWS IN THE GRAPH
C      SYM  THE VECTOR OF PLOT SYMBOL ASSIGNMENTS (SEE IS ABOVE)
C     SYM1  THE VECTOR OF INTEGER VALUES USED TO INDICATE MULTIPLE
C           POINTS ON THE SAME PLOT LOCATION
C     TEMP  THE ARRAY OF VALUES TO BE PRINTED WHICH FALL OUTSIDE THE
C           GRAPH LIMITS
C   XLABEL  THE VECTOR OF X AXIS LABELS
C       XMN  THE MINIMUM X VALUE TO BE PLOTTED, COMPUTED FROM DATA OR
C           ASSIGNED BY XLB
C   XWIDTH  THE VALUE OF AN INDIVIDUAL X AXIS GRAPH INTERVAL
C       XMX  THE MAXIMUM X VALUE TO BE PLOTTED, COMPUTED FROM DATA OR
C           ASSIGNED BY XUB
C   YLABEL  THE VALUE OF THE Y AXIS LABEL TO BE PRINTED
C   YLOWER  THE LOWER BOUND FOR Y VALUES TO BE PLOTTED ON THE CURRENT
C           LINE
C       YMN  THE MINIMUM Y VALUE TO BE PLOTTED, COMPUTED FROM DATA OR
C           ASSIGNED BY YLB
C   YUPPER  THE UPPER BOUND FOR Y VALUES TO BE PLOTTED ON THE CURRENT
C           LINE
C   YWIDTH  THE VALUE OF AN INDIVIDUAL Y AXIS GRAPH INTERVAL
C       YMX  THE MAXIMUM Y VALUE TO BE PLOTTED, COMPUTED FROM DATA OR
C           ASSIGNED BY YUB
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      DOUBLE PRECISION
     +   XMISS,XMN,XMX,YMN,YMX
      INTEGER
     +   ILOG,ISCHCK,ISIZE,IYM,LISYM,M,N,NOUT
      LOGICAL
     +   MISS
C
C  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   X(N),YM(IYM,M),YMMISS(M)
      INTEGER
     +   ISYM(LISYM)
C
C  SCALARS IN COMMON
      INTEGER
     +   IERR
C
C  LOCAL SCALARS
      DOUBLE PRECISION
     +   DELX,DELY,TN,TX,XDMN,XDMX,XWIDTH,XX,YDMN,YDMX,YL,YLOWER,
     +   YUPPER,YWIDTH,YY
      INTEGER
     +   I,IC,ICOL,IK,ILOGX,ILOGY,IOUT,IPRT,IROW,IT,ITEST,J,JCOL,K,
     +   L,NLABLX,NLABLY,NLU,NN,NUMCOL,NUMCP2,NUMROW
      CHARACTER
     +   ALPHAI*1,ALPHAX*1,AXISCH*1,BLANK*1,HYPHEN*1,FMT*4,
     +   XLFMT*205,XLFMT2*205
C
C  LOCAL ARRAYS
      DOUBLE PRECISION
     +   TEMP(50,2),XLABEL(20),YLABEL(20)
      INTEGER
     +   ALINE(105),ILOGXY(2),ISIZXY(2),ISPACE(20),KSS(101)
      CHARACTER
     +   CLINE(105)*1,ITEMP(50)*1,SYM(30)*1,SYM1(9)*1
C
C  EXTERNAL FUNCTIONS
      LOGICAL
     +   MVCHK
      EXTERNAL MVCHK
C
C  EXTERNAL SUBROUTINES
      EXTERNAL IPRINT,LOGLMT,PRTCNT
C
C  INTRINSIC FUNCTIONS
      INTRINSIC ABS,LOG10,MAX,MIN
C
C  COMMON BLOCKS
      COMMON /ERRCHK/IERR
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     INTEGER ALINE(105)
C     CHARACTER*1 ALPHAI, ALPHAX
C     CHARACTER*1 AXISCH
C     CHARACTER*1 BLANK
C     CHARACTER*1 CLINE(105)
C     DOUBLE PRECISION DELX, DELY
C     CHARACTER FMT*4
C        THE FORMAT FOR THE X-AXIS LABELS
C     CHARACTER*1 HYPHEN
C     INTEGER I, IC, ICOL, IERR, IK
C     INTEGER ILOG
C        THE TWO DIGIT INTEGER, PQ, USED TO SELECT AXIS SCALE, WHERE
C        P DESIGNATES THE X-AXIS AND Q DESIGNATES THE Y-AXIS.
C        IF P.EQ.0 (Q.EQ.0), THEN THE X-AXIS (Y-AXIS) IS LINEAR.
C        IF P.NE.0 (Q.NE.0), THEN THE X-AXIS (Y-AXIS) IS LOG.
C     INTEGER ILOGX
C        THE VALUES OF P AS SPECIFIED BY ILOG.
C     INTEGER ILOGXY(2)
C        THE VALUES OF P AND Q AS SPECIFIED BY ILOG.
C     INTEGER ILOGY
C        THE VALUES OF Q AS SPECIFIED BY ILOG.
C     INTEGER IOUT
C     INTEGER IPRT, IROW
C     INTEGER ISCHCK
C        THE INTEGER VALUE INDICATING HOW THE PLOTTING SYMBOLS
C           WILL BE DESIGNATED, WHERE
C           0 INDICATES THE PLOTTING SYMBOLS HAVE NOT BEEN DESIGNATED IN
C             THE N VECTOR ISYM AND ONLY THE SYMBOL + IS TO BE USED
C           1 INDICATES THE PLOTTING SYMBOLS HAVE BEEN DESIGNATED IN THE
C             N VECTOR ISYM
C           2 INDICATES THAT M SERIES ARE BEING PLOTTED.
C             SYMBOL I+4 WILL BE USED FOR COLUMN I OF YM.
C     INTEGER ISIZE
C        THE TWO DIGIT INTEGER, PQ, USED TO SELECT AXIS SIZE, WHERE
C        P DESIGNATES THE X-AXIS AND Q DESIGNATES THE Y-AXIS.
C        IF P.EQ.0 (Q.EQ.0), THEN THE X-AXIS (Y-AXIS) IS THE MAXIMUM.
C        IF P.NE.0 (Q.NE.0), THEN THE X-AXIS (Y-AXIS) IS HALF THE MAXIMU
C     INTEGER ISIZXY(2)
C     INTEGER ISPACE(20)
C        THE SPACING FOR THE X-AXIS LABELS
C     INTEGER ISYM(LISYM)
C     INTEGER IT
C     CHARACTER*1 ITEMP(50)
C     INTEGER ITEST
C     INTEGER IYM
C     INTEGER J, JCOL
C     INTEGER K
C     INTEGER KSS(101)
C     INTEGER L
C     INTEGER LISYM
C     INTEGER M
C     LOGICAL MISS
C     INTEGER N
C     INTEGER NLABLX, NLABLY, NLU
C     INTEGER NN, NOUT, NUMCOL, NUMCP2, NUMROW
C     CHARACTER*1 SYM(30), SYM1(9)
C     DOUBLE PRECISION TEMP(50, 2)
C     DOUBLE PRECISION TN, TX
C     DOUBLE PRECISION X(N)
C     DOUBLE PRECISION XDMN, XDMX
C        THE X-AXIS DATA LIMITS ACTUALLY USED.
C     DOUBLE PRECISION XLABEL(20)
C        THE X-AXIS LABLES.
C     CHARACTER XLFMT*205, XLFMT2*205
C        THE FORMATS USED TO PRINT THE X-AXIS
C     DOUBLE PRECISION XMISS, XMN, XMX, XWIDTH, XX
C     DOUBLE PRECISION YDMN, YDMX
C        THE Y-AXIS DATA LIMITS ACTUALLY USED.
C     DOUBLE PRECISION YL
C     DOUBLE PRECISION YLABEL(20)
C        THE Y-AXIS LABLES.
C     DOUBLE PRECISION YLOWER
C     DOUBLE PRECISION YM(IYM, M)
C     DOUBLE PRECISION YMMISS(M)
C     DOUBLE PRECISION YMN, YMX, YUPPER, YWIDTH, YY
C
      DATA BLANK/' '/,HYPHEN/'-'/,ALPHAI/'I'/,ALPHAX/'X'/
      DATA SYM( 1)/'+'/,SYM( 2)/'.'/,SYM( 3)/'*'/,SYM( 4)/'-'/,
     +     SYM( 5)/'A'/,SYM( 6)/'B'/,SYM( 7)/'C'/,SYM( 8)/'D'/,
     +     SYM( 9)/'E'/,SYM(10)/'F'/,SYM(11)/'G'/,SYM(12)/'H'/,
     +     SYM(13)/'I'/,SYM(14)/'J'/,SYM(15)/'K'/,SYM(16)/'L'/,
     +     SYM(17)/'M'/,SYM(18)/'N'/,SYM(19)/'O'/,SYM(20)/'P'/,
     +     SYM(21)/'Q'/,SYM(22)/'R'/,SYM(23)/'S'/,SYM(24)/'T'/,
     +     SYM(25)/'U'/,SYM(26)/'V'/,SYM(27)/'W'/,SYM(28)/'Y'/,
     +     SYM(29)/'Z'/,SYM(30)/'Z'/
      DATA SYM1(1)/'1'/,SYM1(2)/'2'/,SYM1(3)/'3'/,SYM1(4)/'4'/,
     +     SYM1(5)/'5'/,SYM1(6)/'6'/,SYM1(7)/'7'/,SYM1(8)/'8'/,
     +     SYM1(9)/'9'/
C
      CALL IPRINT(IPRT)
C
C     DEFINE THE NUMBER OF ROWS AND COLUMNS WITHIN THE PLOT
C
      CALL PRTCNT(MAX(0,ISIZE),2,ISIZXY)
      IF (ISIZXY(1).EQ.0) THEN
        NUMCOL=101
      ELSE
        NUMCOL = 51
      END IF
      IF (ISIZXY(2).EQ.0) THEN
        NUMROW = 51
      ELSE
        NUMROW = 26
      END IF
C
      NUMCP2 = NUMCOL + 2
      NN=(NUMROW-1)/5
C
C     ADJUST FOR LOG PLOTS IF NECESSARY AND FIND AXIS LABELS
C
      CALL PRTCNT (MAX(0,ILOG),2,ILOGXY)
      ILOGX = ILOGXY(1)
      ILOGY = ILOGXY(2)
      CALL LOGLMT (ILOGY, YMN, YMX, YLABEL, NUMROW, 5, DELY, YWIDTH,
     +  NLABLY, YDMN, YDMX)
      CALL LOGLMT (ILOGX, XMN, XMX, XLABEL, NUMCOL, 10, DELX, XWIDTH,
     +  NLABLX, XDMN, XDMX)
C
C     TEST FOR FORMAT FOR Y AXIS LABELS
C
      ITEST=0
      TX=YMX
      TN=YMN
      IF (ILOGY.EQ.0) GO TO 190
      TX=10.0D0**TX
      TN=10.0D0**TN
  190 IF ((TN .GE. 1.0D6 .OR. TN .LE. (-1.0D5)) .OR.
     +   (ABS(TN).GT.0.0D0.AND.ABS(TN).LT.0.001D0)) ITEST=1
      IF ((TX .GE. 1.0D6 .OR. TX .LE. (-1.0D5)) .OR.
     +   (ABS(TX).GT.0.0D0.AND.ABS(TX).LT.0.001D0)) ITEST=1
C
C     BLANK OUT THE PLOT PRINT LINE CLINE
C
      DO 200 ICOL=1,105
         CLINE(ICOL)=BLANK
  200 CONTINUE
C
C     WRITE OUT THE TOP HORIZONTAL AXIS OF THE PLOT
C
      DO 210 ICOL=1,NUMCOL
         CLINE(ICOL)=HYPHEN
  210 CONTINUE
      CLINE(1)=ALPHAI
      IF (ILOGX.EQ.0) THEN
        DO 215 ICOL=11,NUMCOL,10
           CLINE(ICOL)=ALPHAI
  215   CONTINUE
      ELSE
        DO 216 IK = NLABLX, 1, -1
          ICOL = ((LOG10(XLABEL(IK))-XMN)/XWIDTH)+1.5D0
          CLINE(ICOL) = ALPHAI
  216   CONTINUE
      END IF
      CLINE(NUMCOL+1)=HYPHEN
      CLINE(NUMCOL+2)=BLANK
      WRITE (IPRT, 1004) HYPHEN,(CLINE(I),I=1,NUMCOL),HYPHEN
      DO 217 ICOL=1,105
         CLINE(ICOL)=BLANK
  217 CONTINUE
C
C     DETERMINE AND WRITE OUT THE PLOT POSITIONS ONE LINE AT A TIME.
C
      L=-1
      IK=1
      DO 320 IROW=1,NUMROW
         L=L+1
         IF (L.GT.NN) L=1
         DO 235 ICOL=1,NUMCOL
            ALINE(ICOL)=0
            KSS(ICOL)=0
  235    CONTINUE
         YUPPER=YMX+(1.5D0-IROW)*YWIDTH
         YLOWER=YMX+(0.5D0-IROW)*YWIDTH
      DO 255 I = 1, N
C
         IF (MISS .AND. MVCHK(X(I), XMISS)) GO TO 255
C
         IF (ILOGX.EQ.0) THEN
           XX=X(I)
         ELSE
           XX=LOG10(X(I))
         END IF
         DO 250 J=1,M
C
            IF (MISS .AND. MVCHK(YM(I,J), YMMISS(J))) GO TO 250
               IF (ILOGY.EQ.0) THEN
                 YY=YM(I,J)
               ELSE
                 YY=LOG10(YM(I,J))
               END IF
               IF (((YLOWER.LE.YY).AND.(YY.LT.YUPPER)) .AND.
     +             ((YY.GE.YDMN).AND.(YY.LE.YDMX))) THEN
                  IF ((XX.GE.XDMN) .AND. (XX.LE.XDMX)) THEN
                     ICOL=((XX-XMN)/XWIDTH)+1.5D0
                     ALINE(ICOL) = ALINE(ICOL) + 1
C     SIMPLE PLOTTING (PLT)
                     KSS(ICOL)=1
C     SYMBOL PLOTS (SPLT)
                     IF (ISCHCK .EQ. 1) KSS(ICOL) = ISYM(I)
C     MULTIPLE PLOTS (MPLT)
                     IF (ISCHCK .EQ. 2) KSS(ICOL) = J + 4
                     IF (KSS(ICOL).GT.30) KSS(ICOL)=30
                     IF (KSS(ICOL).LT.1) KSS(ICOL)=1
                  END IF
               END IF
  250       CONTINUE
  255    CONTINUE
         DO 290 ICOL=1,NUMCOL
            IF (ALINE(ICOL).EQ.0) THEN
              CLINE(ICOL)=BLANK
            ELSE
              IF (ALINE(ICOL).EQ.1) THEN
                K=KSS(ICOL)
                CLINE(ICOL)=SYM(K)
              ELSE
                IF (ALINE(ICOL).LE.9) THEN
                  CLINE(ICOL)=SYM1(ALINE(ICOL))
                ELSE
                  CLINE(ICOL)=ALPHAX
                END IF
              END IF
            END IF
  290    CONTINUE
         CLINE(NUMCOL+2)=HYPHEN
         AXISCH=HYPHEN
         IF (ILOGY.EQ.0) THEN
           YL = YLABEL(IK)
         ELSE
           YL = LOG10(YLABEL(IK))
         END IF
         IF ((YLOWER.GT.YL) .OR. (YL.GE.YUPPER)) THEN
C
C          PRINT LINE WITHOUT LABEL
C
           CLINE(NUMCP2)=ALPHAI
           WRITE(IPRT, 1008) ALPHAI, (CLINE(ICOL), ICOL=1,NUMCP2)
         ELSE
C
C          PRINT LINE WITH LABEL
C
           IF (ITEST.EQ.0) THEN
             WRITE(IPRT,1006) YLABEL(IK),AXISCH,
     +         (CLINE(ICOL),ICOL=1,NUMCP2)
           ELSE
             WRITE(IPRT,1007) YLABEL(IK),AXISCH,
     +         (CLINE(ICOL),ICOL=1,NUMCP2)
           END IF
           IK=IK+1
         END IF
  320 CONTINUE
C
C     WRITE OUT THE BOTTOM HORIZONTAL AXIS AND THE X AXIS LABELS.
C
      DO 330 ICOL=1,NUMCOL
         CLINE(ICOL)=HYPHEN
  330 CONTINUE
      CLINE(1)=ALPHAI
      IF (ILOGX.EQ.0) THEN
        NLU = NLABLX+1
        DO 340 ICOL=NUMCOL,1,-10
           CLINE(ICOL)=ALPHAI
           NLU = NLU - 1
           ISPACE(NLU) = 1
  340   CONTINUE
      ELSE
        JCOL = 1
        CLINE(JCOL) = ALPHAI
        NLU = NLABLX
        DO 345 IK = NLABLX, 1, -1
          ICOL = ((LOG10(XLABEL(IK))-XMN)/XWIDTH)+1.5
          CLINE(ICOL) = ALPHAI
          IF (ICOL-JCOL.GE.10) THEN
            ISPACE(NLU) = ICOL-JCOL-9
            NLU = NLU - 1
            XLABEL(NLU) = XLABEL(IK)
            JCOL = ICOL
          END IF
  345   CONTINUE
      END IF
      CLINE(NUMCOL+1)=HYPHEN
      CLINE(NUMCOL+2)=BLANK
C
      WRITE(IPRT, 1004) HYPHEN, (CLINE(ICOL), ICOL = 1, NUMCP2)
C
C     CHECK X-AXIS LABELS FOR FORMAT
C
      FMT = 'F9.4'
      DO 350 I=1,NLABLX
        IF (((ABS(XLABEL(I)).GT.0.0D0) .AND. (ABS(XLABEL(I)).LT.0.01D0))
     +      .OR.
     +      ((XLABEL(I).GE.1.0D4) .OR. (XLABEL(I).LE.(-1.0D3)))) THEN
          FMT = 'D9.4'
           GO TO 355
         END IF
  350 CONTINUE
  355 CONTINUE
      WRITE(XLFMT2,1000) NLABLX-NLU
      WRITE(XLFMT,XLFMT2) (FMT, ISPACE(I), I=NLABLX,NLU+1,-1), FMT
      WRITE(IPRT, XLFMT) (XLABEL(I),I=NLABLX,NLU,-1)
C
C     DETERMINE VALUES TO BE LISTED IF OUTSIDE OF AXIS LIMITS.
C
      IC = 0
      IOUT = MIN(NOUT,50)
      IF (IOUT.GE.0) THEN
        DO 180 I = 1, N
C
          IF (MISS .AND. MVCHK(X(I), XMISS)) GO TO 180
C
          IF (ILOGX.EQ.0) THEN
            XX = X(I)
          ELSE
            XX = LOG10(X(I))
          END IF
           DO 175 J=1,M
C
              IF (MISS .AND. MVCHK(YM(I,J), YMMISS(J))) GO TO 175
C
              IF (ILOGY.EQ.0) THEN
                YY = YM(I,J)
              ELSE
                YY = LOG10(YM(I,J))
              END IF
C
              IF (((YDMN .LE. YY) .AND. (YY .LE. YDMX)) .AND.
     +            ((XDMN .LE. XX) .AND. (XX .LE. XDMX))) GO TO 175
C
              IC=IC+1
              IF (IC.GT.IOUT) GO TO 175
              TEMP(IC,1)=X(I)
              TEMP(IC,2)=YM(I,J)
              IT=1
              IF (ISCHCK.EQ.1) IT=ISYM(I)
              IF (ISCHCK.EQ.2) IT=J+4
              IF (IT.LT.1) IT=1
              IF (IT.GT.30) IT=30
              ITEMP(IC) = SYM(IT)
  175      CONTINUE
  180   CONTINUE
      END IF
C
C     CHECK FOR POINTS OUTSIDE OF GRAPH LIMITS AND LIST IF REQUESTED
C     THE TOTAL NUMBER OF POINTS TO BE PLOTTED IS N*M
C
      IF (IC.EQ.0) RETURN
      IF (IOUT .LT. 0) RETURN
      WRITE (IPRT, 1010) IC
      IF (IOUT.LE.0) RETURN
      WRITE (IPRT, 1016)
      IF (IC.LE.IOUT) GO TO 360
      IC=IOUT
      WRITE(IPRT, 1011) IOUT
      GO TO 370
  360 WRITE(IPRT, 1012)
  370 WRITE(IPRT, 1013) (TEMP(I,1), TEMP(I,2), ITEMP(I), I = 1, IC)
      RETURN
C
C     FORMAT STATEMENTS
C
 1000 FORMAT ('(''(14X'',', I2, '('', '', A4, '','', I2, ''X''),',
     +  ''', '', A4, '')'')')
 1004 FORMAT (' ',16X, A1, 105A1)
 1006 FORMAT(4X,F11.4,1X,A1,1X,105A1)
 1007 FORMAT (' ', E14.7, 1X, A1, 1X, 105A1)
 1008 FORMAT (' ', 15X, A1, 1X, 105A1)
 1010 FORMAT(16X,6H**NOTE,I4,43H VALUES FELL OUTSIDE THE SPECIFIED LIMIT
     +S**)
 1011 FORMAT ('1', 15X, 10HTHE FIRST , I3,
     +   35H VALUES OUTSIDE THE PLOT LIMITS ARE/ 22X,
     + 33H X               Y            SYM)
 1012 FORMAT ('1', 15X, 38HTHE VALUES OUTSIDE THE PLOT LIMITS ARE/ 22X,
     + 33H X               Y            SYM)
 1013 FORMAT (15X,2E15.8,9X,A1)
 1016 FORMAT (16X, 22HSEE NEXT PAGE FOR LIST)
C
      END
