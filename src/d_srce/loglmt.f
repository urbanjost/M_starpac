*LOGLMT
      SUBROUTINE LOGLMT (ILOGY, YMN, YMX, YLABEL, NUMROW, ISTEP, DELY,
     +  YWIDTH, NLABLY, YDMN, YDMX)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     ADJUST PLOT LIMITS FOR LOG PLOTS, AND COMPUTE LOG AXIS LABELS
C
C     WRITTEN BY - JANET R. DONALDSON
C                  STATISTICAL ENGINEERING DIVISION
C                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE  -  DECEMBER 2, 1985
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      DOUBLE PRECISION
     +   DELY,YDMN,YDMX,YMN,YMX,YWIDTH
      INTEGER
     +   ILOGY,ISTEP,NLABLY,NUMROW
C
C  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   YLABEL(20)
C
C  LOCAL SCALARS
      DOUBLE PRECISION
     +   YNLOG,YXLOG
      INTEGER
     +   IYN,IYNLOG,IYX,IYXLOG,J,K
C
C  INTRINSIC FUNCTIONS
      INTRINSIC LOG10,MIN,MOD,NINT,DBLE
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     DOUBLE PRECISION DELY
C     INTEGER ILOGY, ISTEP, IYN, IYNLOG, IYX, IYXLOG, J, K
C     INTEGER NLABLY, NUMROW
C     DOUBLE PRECISION YLABEL(20)
C        THE Y-AXIS LABLES.
C     DOUBLE PRECISION YDMN, YDMX
C        THE Y-AXIS DATA LIMITS ACTUALLY USED.
C     DOUBLE PRECISION YMN, YMX
C        THE Y-AXIS PLOT LIMITS ACTUALLY USED.
C     DOUBLE PRECISION YNLOG, YWIDTH, YXLOG
C
      IF (ILOGY.EQ.0) THEN
C
        YDMN = YMN
        YDMX = YMX
C
C       DETERMINE THE VALUE OF A DIVISION
C
        DELY=YMX-YMN
        YWIDTH=DELY/(NUMROW-1)
C
C       COMPUTE EVENLY SPACED LABELS FOR NON-LOG AXIS
C
        NLABLY = 0
        DO 10 J = 1, NUMROW, ISTEP
          NLABLY = NLABLY + 1
          YLABEL(NLABLY) = YMX+(1-J)*YWIDTH
   10   CONTINUE
          IF (MOD(NUMROW,ISTEP).EQ.1) YLABEL(NLABLY) = YMN
          IF (YMX.EQ.(-YMN) .AND. MOD(NLABLY,2).EQ.1) THEN
             YLABEL(NLABLY/2+1) = 0.0
          END IF
      ELSE
C
        YDMN = LOG10(YMN)
        YDMX = LOG10(YMX)
C
C       ADJUST AXIS LIMITS FOR LOG AXIS IF NECESSARY
C
        YXLOG=LOG10(YMX)
        IYXLOG=YXLOG
        IF (YMX.LT.1.0D0) IYXLOG=IYXLOG-1
        YNLOG=LOG10(YMN)
        IF ((YXLOG-YNLOG).LE.0.92082D0) THEN
C
C         RANGE IS LESS THAN .9 DECADES
C
          IYNLOG=YNLOG
          IF (YMN.LT.1.0D0) IYNLOG=IYNLOG-1
          IF (IYXLOG.LE.IYNLOG) THEN
C
C           VALUES FALL IN THE SAME DECADE
C
            YNLOG=IYNLOG
            YXLOG=YNLOG+1.0D0
            IYXLOG=YXLOG
          ELSE
C
C           VALUES FALL INTO TWO DECADES
C
            IYN=NINT(YMN/(10.0D0**IYNLOG))
            YNLOG=MIN(LOG10(IYN*(10.0D0**IYNLOG)),YNLOG)
            YXLOG=YNLOG+1.0D0
            IYXLOG=YXLOG
          END IF
        END IF
        YMX=YXLOG
        YMN=YNLOG
C
C       DETERMINE THE VALUE OF A DIVISION
C
        DELY=YMX-YMN
        YWIDTH=DELY/(NUMROW-1)
C
C       COMPUTE AXIS LABELS
C
        YLABEL(1)=10.0D0**YXLOG
        IYX=YLABEL(1)/(10.0D0**IYXLOG)
        K=2
        IF (YXLOG-YNLOG.GT.3.0D0) K=5
        IF (YXLOG-YNLOG.GT.6.0D0) K=10
C
        NLABLY = 1
C
        IF (YXLOG-YNLOG.LE.12.0D0) THEN
C
C       SET NICE LABELS
C
          IF (K.EQ.10) IYX=1
          IF (IYX.NE.1.AND.(IYX-((IYX/K)*K)).NE.0) IYX=((IYX/K)*K)
          IF (IYX.LE.1) THEN
            IYX=10
            IYXLOG=IYXLOG-1
          END IF
          IF (YLABEL(1)-IYX*(10.0D0**IYXLOG).GT.0.0D0 .AND.
     +     YXLOG-(YXLOG-YNLOG)/(2.0D0*(NUMROW-1)) .GT.
     +     LOG10(DBLE(IYX))+IYXLOG) IYX=IYX+K
          IYX=IYX-K
          IF (IYX.LE.0) THEN
            IYX=10
            IYXLOG=IYXLOG-1
          END IF
          DO 155 J=2,20
             NLABLY=NLABLY+1
             YLABEL(NLABLY)=IYX*(10.0D0**IYXLOG)
             IF (YLABEL(NLABLY).LE.10.0D0**YNLOG) GO TO 156
             IYX=IYX-K
             IF (IYX.GT.0) GO TO 155
             IYX=10
             IYXLOG=IYXLOG-1
  155     CONTINUE
  156     YLABEL(NLABLY)=10.0D0**YNLOG
        ELSE
C
C         COMPUTE EVENLY SPACED LABELS
C
          DO 160 J = ISTEP, NUMROW, ISTEP
            NLABLY = NLABLY + 1
            YLABEL(NLABLY) = 10.0D0**(YMX+(1.0D0-J)*YWIDTH)
  160     CONTINUE
        END IF
      END IF
C
      RETURN
      END
