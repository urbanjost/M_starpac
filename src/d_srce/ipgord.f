*IPGORD
      SUBROUTINE IPGORD (PERI, NF, N, FREQ, XAXIS, YAXIS, ISYM, NPTS,
     +   LPCV)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS ROUTINE PRODUCES CO-ORDINATES FOR THE SPECTRAL PLOTS.
C
C     WRITTEN BY  -  JANET R. DONALDSON
C                    STATISTICAL ENGINEERING DIVISION
C                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE  -  DECEMBER 7, 1981
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      INTEGER
     +   LPCV,N,NF,NPTS
C
C  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   FREQ(NF),PERI(NF),XAXIS(LPCV),YAXIS(LPCV)
      INTEGER
     +   ISYM(LPCV)
C
C  LOCAL SCALARS
      DOUBLE PRECISION
     +   CI,CIMID
      INTEGER
     +   I,II,NLIM
C
C  LOCAL ARRAYS
      DOUBLE PRECISION
     +   S(40)
C
C  INTRINSIC FUNCTIONS
      INTRINSIC MOD,DBLE,SQRT
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     DOUBLE PRECISION CI, CIMID
C        THE WIDTH AND MIDPOINT OF THE TEST INTERVAL FOR WHITE NOISE.
C     DOUBLE PRECISION FREQ(NF)
C        THE ARRAY IN WHICH THE FREQUENCIES AT WHICH THE PERIODOGRAM
C        WAS ESTIMATED ARE STORED.
C     INTEGER I
C        AN INDEX VARIABLE
C     INTEGER II
C        AN INDEX VARIABLE
C     INTEGER ISYM(LPCV)
C        THE ARRAY CONTAINING THE CODE FOR THE PLOT SYMBOLS.
C     INTEGER LPCV
C        THE LENGTH OF THE PLOT CO-ORDINATE VECTORS.
C     INTEGER N
C        THE ACTUAL NUMBER OF OBSERVATIONS IN THE SERIES FROM WHICH
C        THE PERIODOGRAM WAS COMPUTED.
C     INTEGER NF
C        THE NUMBER OF FREQUENCIES FOR WHICH THE SPECTRAL ESTIMATES
C        ARE ESTIMATED.
C     INTEGER NLIM
C        THE NUMBER OF POINTS AT WHICH THE WHITE NOISE LIMITS ARE TO
C        PLOTTED.
C     INTEGER NPTS
C        THE NUMBER OF X, Y CO-ORDINATES TO BE PLOTTED.
C     DOUBLE PRECISION PERI(NF)
C        THE ARRAY CONTAINING THE INTEGRATED PERIODOGRAM VALUES.
C     DOUBLE PRECISION S(40)
C        VALUES USED TO COMPUTE THE CONFIDENCE LIMITS.
C     DOUBLE PRECISION XAXIS(LPCV), YAXIS(LPCV)
C        THE X, Y CO-ORDINATES FOR THE SPECTRAL PLOTS.
C
      DATA S(1)/0.975D0/, S(2)/0.842D0/, S(3)/0.708D0/
      DATA S(4)/0.624D0/, S(5)/0.563D0/
      DATA S(6)/0.519D0/, S(7)/0.483D0/, S(8)/0.454D0/
      DATA S(9)/0.430D0/, S(10)/0.409D0/
      DATA S(11)/0.391D0/, S(12)/0.375D0/, S(13)/0.361D0/
      DATA S(14)/0.349D0/, S(15)/0.338D0/
      DATA S(16)/0.327D0/, S(17)/0.318D0/, S(18)/0.309D0/
      DATA S(19)/0.301D0/, S(20)/0.294D0/
      DATA S(21)/0.287D0/, S(22)/0.281D0/, S(23)/0.275D0/
      DATA S(24)/0.269D0/, S(25)/0.264D0/
      DATA S(26)/0.259D0/, S(27)/0.254D0/, S(28)/0.250D0/
      DATA S(29)/0.246D0/, S(30)/0.242D0/
      DATA S(31)/0.238D0/, S(32)/0.234D0/, S(33)/0.231D0/
      DATA S(34)/0.227D0/, S(35)/0.224D0/
      DATA S(36)/0.221D0/, S(37)/0.218D0/, S(38)/0.215D0/
      DATA S(39)/0.213D0/, S(40)/0.210D0/
C
      I = N + MOD(N,2) - 1
C
      IF (I .LE. 40) THEN
         CI = S(I)
      ELSE
         CI = 1.36D0 / SQRT(DBLE(I))
      END IF
C
      DO 10 I = 1, NF
         XAXIS(I) = FREQ(I)
         YAXIS(I) = PERI(I)
         ISYM(I) = 1
   10 CONTINUE
C
      NLIM = 101
C
      II = NF
C
      DO 30 I = 1, NLIM, 2
         CIMID = DBLE(I-1) / DBLE(NLIM-1)
         IF (CIMID - CI .LT. 0.0D0) GO TO 20
         II = II + 1
         XAXIS(II) = CIMID / 2.0D0
         YAXIS(II) = CIMID - CI
         ISYM(II) = 2
   20    IF (CIMID + CI .GT. 1.0D0) GO TO 30
         II = II + 1
         XAXIS(II) = CIMID / 2.0D0
         YAXIS(II) = CIMID + CI
         ISYM(II) = 2
   30 CONTINUE
C
      NPTS = II
C
      RETURN
      END
