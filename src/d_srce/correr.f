*CORRER
      SUBROUTINE CORRER(NMSUB, M, N, IYM, IVCV, LDSTAK, ICOR, NPRT)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS ROUTINE CHECKS FOR ERRORS IN THE INPUT
C     PARAMETERS.  IF ANY ARE FOUND A FLAG IS SET AND
C     COMPUTATION STOPS
C
C     WRITTEN BY -
C        LINDA L. MITCHELL
C        STATISTICAL ENGINEERING DIVISION
C        NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE  -  MAY 17, 1982
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      INTEGER
     +   ICOR,IVCV,IYM,LDSTAK,M,N,NPRT
C
C  ARRAY ARGUMENTS
      CHARACTER
     +   NMSUB(6)*1
C
C  SCALARS IN COMMON
      INTEGER
     +   IERR
C
C  LOCAL SCALARS
      INTEGER
     +   IP,IPRT,IS,LDSMIN
      LOGICAL
     +   ERR01,ERR02,ERR03,ERR04,ERR05,HEAD
C
C  LOCAL ARRAYS
      CHARACTER
     +   LIVCV(8)*1,LIYM(8)*1,LLDS(8)*1,LM(8)*1,LN(8)*1,
     +   LTHREE(8)*1,LTWO(8)*1
C
C  EXTERNAL SUBROUTINES
      EXTERNAL EISGE,IPRINT,LDSCMP
C
C  INTRINSIC FUNCTIONS
      INTRINSIC MAX
C
C  COMMON BLOCKS
      COMMON /ERRCHK/IERR
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     LOGICAL ERR01, ERR02, ERR03, ERR04, ERR05
C                  VALUE(S) INDICATING WHETHER OR ERROR WAS DETECTED
C                  (TRUE) OR NOT (FALSE).
C     LOGICAL HEAD
C                  A FLAG INDICATING WHETHER THE HEADING SHOULD BE
C                  PRINTED (TRUE) OR NOT (FALSE).  IF A HEADING IS
C                  PRINTED, THE VALUE OF HEAD WILL BE CHANGED TO FALSE.
C     INTEGER ICOR
C                  DETERMINES WHICH SUBROUTINE CALLED CORRER
C                  IF =1, THEN CALLED BY CORR
C                     =2, THEN CALLED BY CORRS
C     INTEGER IERR
C                  A FLAG, THAT WHEN RETURNED BY THIS ROUTINE DESIGNATES
C                  WHETHER  ERRORS WERE DETECTED IN THE PARAMETER LIST
C                  IF IERR .EQ. 0, NO ERRORS WERE DETECTED
C                  IF IERR .EQ. 1, ERRORS HAVE BEEN DETECTED
C     INTEGER IP
C                  A TEMPORARY VARIABLE USED FOR COMPUTING LDSMIN.
C     INTEGER IPRT
C                  THE LOGICAL OUTPUT UNIT
C     INTEGER IS
C                  A TEMPORARY VARIABLE USED FOR COMPUTING LDSMIN.
C     INTEGER IVCV
C                  THE DIMENSION OF SC
C     INTEGER IYM
C                  ACTUAL ROW DIMENSION OF YM
C     INTEGER LDSMIN
C                  THE MINIMUM LENGTH ALLOWED FOR DSTAK
C     INTEGER LDSTAK
C                  THE LENGTH OF THE VECTOR DSTAK IN COMMON CSTAK
C     CHARACTER*1 LIVCV(8), LIYM(8), LLDS(8), LM(8), LN(8), LTHREE(8),
C    *  LTWO(8)
C                  THE ARRAY(S) CONTAINING THE NAME(S) OF THE
C                  VARIABLE(S) CHECKED FOR ERRORS.
C     INTEGER M
C                  NUMBER OF VARIABLES
C     INTEGER N
C                  THE NUMBER OFOBSERVATIONS
C     CHARACTER*1 NMSUB(6)
C                  THE NAME OF THE CALLING SUBROUTINE.
C     INTEGER NPRT
C                  THE VARIABLE CONTROLLING THE PRINTED OUTPUT.
C                  IF NPRT=0, OUTPUT IS SUPPRESSED, OTHERWISE IT IS NOT
C
C
C     SET UP NAME ARRAYS
C
      DATA  LIVCV(1), LIVCV(2), LIVCV(3), LIVCV(4), LIVCV(5), LIVCV(6),
     +      LIVCV(7), LIVCV(8) / 'I', 'V', 'C', 'V', ' ', ' ', ' ', ' '/
      DATA   LIYM(1),  LIYM(2),  LIYM(3),  LIYM(4),  LIYM(5),  LIYM(6),
     +       LIYM(7),  LIYM(8) / 'I', 'Y', 'M', ' ', ' ', ' ', ' ', ' '/
      DATA   LLDS(1),  LLDS(2),  LLDS(3),  LLDS(4),  LLDS(5),  LLDS(6),
     +       LLDS(7),  LLDS(8) / 'L', 'D', 'S', 'T', 'A', 'K', ' ', ' '/
      DATA     LM(1),    LM(2),    LM(3),    LM(4),    LM(5),    LM(6),
     +         LM(7),    LM(8) / 'M', ' ', ' ', ' ', ' ', ' ', ' ', ' '/
      DATA     LN(1),    LN(2),    LN(3),    LN(4),    LN(5),    LN(6),
     +         LN(7),    LN(8) / 'N', ' ', ' ', ' ', ' ', ' ', ' ', ' '/
      DATA LTHREE(1),LTHREE(2),LTHREE(3),LTHREE(4),LTHREE(5),LTHREE(6),
     +     LTHREE(7),LTHREE(8) / 'T', 'H', 'R', 'E', 'E', ' ', ' ', ' '/
      DATA   LTWO(1),  LTWO(2),  LTWO(3),  LTWO(4),  LTWO(5),  LTWO(6),
     +       LTWO(7),  LTWO(8) / 'T', 'W', 'O', ' ', ' ', ' ', ' ', ' '/
C
      CALL IPRINT(IPRT)
C
      HEAD = .TRUE.
C
      IERR = 0
C
C     BEGIN ERROR CHECKING. THE COMMENTS STATE WHAT CONSTITUTES AN
C     ERROR. COMMENTS ENCLOSED BY STARS(**) INDICATE THE ERROR CONDITION
C
C     **NUMBER OF VARIABLES LESS THAN 2**
C
      CALL EISGE(NMSUB, LM, M, 2, 2, HEAD, ERR01, LTWO)
C
C     **NUMBER OF OBSERVATIONS LESS THAN 3**
C
      CALL EISGE(NMSUB, LN, N, 3, 2, HEAD, ERR02, LTHREE)
C
C     **OBSERVATION MATRIX DIMENSIONED LESS THAN N**
C
      ERR03 = .FALSE.
      IF (.NOT.ERR02) CALL EISGE(NMSUB, LIYM, IYM, N, 3, HEAD, ERR03,
     +   LN)
C
C     **SC MATRIX DIMENSIONED LESS THAN M**
C
      ERR04 = .FALSE.
      IF ((ICOR.EQ.2) .AND. (.NOT.ERR01)) CALL EISGE(NMSUB, LIVCV,
     +   IVCV, M, 3, HEAD, ERR04, LM)
C
      IF (ERR01 .OR. ERR02) GO TO 40
      IS = 0
      IF (ICOR.EQ.1) IS = 1
C
      IP = 1
      IF (NPRT.EQ.0) IP = 0
C
C
C     CHECK FOR ENOUGH COMMON FOR THIS PROBLEM
C
      CALL LDSCMP(12, 0, IP*MAX(N,M), 0, 0, 0, 'D',
     +   IS*M*M + IP*(MAX(N,M)+M+N*(M+3)+6*M*M), LDSMIN)
C
      CALL EISGE(NMSUB, LLDS, LDSTAK, LDSMIN, 9, HEAD, ERR05, LLDS)
C
      IF (ERR01 .OR. ERR02 .OR. ERR03 .OR. ERR04 .OR. ERR05) GO TO 40
      RETURN
C
C     SET IERR TO INDICATE AN ERROR
C
   40 CONTINUE
      IERR = 1
      RETURN
      END
