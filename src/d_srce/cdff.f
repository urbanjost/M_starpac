*CDFF
      DOUBLE PRECISION FUNCTION CDFF(F, DF1, DF2)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS ROUTINE COMPUTES THE CUMULATIVE DISTRIBUTION FUNCTION FOR
C     THE F DISTRIBUTION.  THE APPROXIMATION USED DEPENDS ON THE
C     DEGREES OF FREEDOM IN THE NUMERATOR AND DENOMINATOR.
C     1.  IF BOTH DEGREES OF FREEDOM ARE SMALL (LESS THAN 4000),
C         THE CDF IS COMPUTED FROM THE INCOMPLETE BETA FUNCTION
C         USING EQUATION 5.45 OF STATISTICAL COMPUTING, BY KENNEDY AND
C         GENTLE.
C     2.  IF ONE OF THE DEGREES OF FREEDOM IS LARGE (GREATER THAN OR
C         EQUAL TO 4000) AND THE OTHER IS MODERATELY LARGE (GREATER THAN
C         OR EQUAL TO 100), THE CDF IS APPROXIMATED BY A NORMAL DISTRIB-
C         BUTION AS SHOWN IN EQUATION 20 ON PAGE 83 OF DISTRIBUTIONS IN
C         STATISTICS - CONTINUOUS UNIVARIATE DISTRIBUTIONS - 2, BY
C         JOHNSON AND KOTZ.
C     3.  IF ONE OF THE DEGREES OF FREEDOM IS SMALL (LESS THAN 100) AND
C         THE OTHER IS LARGE (EXCEEDING 4000), THE CDF IS APPROXIMATED
C         BY A CHI SQUARED DISTRIBUTION AS SHOWN IN THE THIRD EQUATION
C         ON PAGE 84 OF DISTRIBUTIONS IN STATISTICS - CONTINUOUS
C         UNIVARIATE DISTRIBUTIONS - 2, BY JOHNSON AND KOTZ.
C
C     WRITTEN BY - JANET R. DONALDSON
C                  STATISTICAL ENGINEERING DIVISION
C                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE - DECEMBER 7, 1981
C                     THIS ROUTINE IS MODELED AFTER DATAPAC ROUTINE FCDF.
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      DOUBLE PRECISION
     +   DF1,DF2,F
C
C  LOCAL SCALARS
      DOUBLE PRECISION
     +   FTRANS
C
C  EXTERNAL FUNCTIONS
      DOUBLE PRECISION
     +   DBETAI,CDFCHI,CDFNML
      EXTERNAL DBETAI,CDFCHI,CDFNML
C
C  INTRINSIC FUNCTIONS
      INTRINSIC SQRT
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     DOUBLE PRECISION DF1
C        THE DEGREES OF FREEDOM IN THE NUMERATOR.
C     DOUBLE PRECISION DF2
C        THE DEGREES OF FREEDOM IN THE DENOMINATOR.
C     DOUBLE PRECISION F
C        THE PERCENTAGE POINT FROM THE F DISTRIBUTION.
C     DOUBLE PRECISION FTRANS
C        TRANSFORMS OF THE F PERCENTAGE POINT, USED FOR THE VARIOUS
C        APPROXIMATING DISTRIBUTIONS.
C
C     COMMENCE BODY OF ROUTINE
C
      IF (F.GT.0.0D0) GO TO 5
      CDFF = 0.0D0
      RETURN
C
    5 CONTINUE
C
      IF ((DF1.GT.4000.0D0) .OR. (DF2.GT.4000.0D0)) GO TO 10
C
C     BOTH DEGREES OF FREEDOM ARE LESS THAN OR EQUAL TO 4000.  USE
C     THE INCOMPLETE BETA FUNCTION TO COMPUTE THE F CDF.
C
      FTRANS = DF2 / (DF2 + DF1 * F)
C
      CDFF = 1.0D0 - DBETAI(FTRANS, 0.5D0*DF2, 0.5D0*DF1)
      RETURN
C
   10 IF ((DF1.LE.100.0D0) .OR. (DF2.LE.100.0D0)) GO TO 20
C
C     BOTH DEGREES OF FREEDOM EXCEED 4000.  USE THE NORMAL APPROXIMATION
C     TO COMPUTE THE F CDF.
C
      FTRANS =
     +   (((1.0D0-(2.0D0/(9.0D0*DF2)))*
     +     (F**(1.0D0/3.0D0)))-(1.0D0-(2.0D0/(9.0D0*DF1)))) /
     +   SQRT(((F**(2.0D0/3.0D0))/(4.5D0*DF2))+(2.0D0/(9.0D0*DF1)))
C
      CDFF = CDFNML(FTRANS)
      RETURN
C
   20 IF (DF1.GT.100.0D0) GO TO 30
C
C     THE DEGREES OF FREEDOM IN THE DENOMINATOR EXCEEDS 4000 AND THE
C     DEGREES OF FREEDOM IN NUMERATOR IS LESS THAN OR EQUAL TO 100.
C     USE THE CHI SQUARE APPROXIMATION TO COMPUTE THE F CDF.
C
      FTRANS =
     +   (DF1 + (DF1/DF2)*(0.5D0*DF1-1.0D0))/
     +   ((1.0D0/F)+(DF1/DF2)*0.5D0)
C
      CDFF = CDFCHI(FTRANS, DF1)
C
      RETURN
C
   30 CONTINUE
C
C     THE DEGREES OF FREEDOM IN THE NUMERATOR EXCEEDS 4000 AND THE
C     DEGREES OF FREEDOM IN DENOMINATOR IS LESS THAN OR EQUAL TO 100.
C     USE THE CHI SQUARE APPROXIMATION TO COMPUTE THE F CDF.
C
      FTRANS =
     +   (DF2+(DF2/DF1)*(0.5D0*DF2-1.0D0))/(F+(DF2/DF1)*0.5D0)
C
      CDFF = 1.0D0 - CDFCHI(FTRANS, DF2)
C
      RETURN
C
      END
