*CCFSDM
      SUBROUTINE CCFSDM (CCOV11, CCOV22, SDRHOC, LAGMAX, NCC, LAGLST, N,
     +   NLPP12, NLPP21, ICCOV, INLPPC)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS IS THE MAIN SUBROUTINE FOR COMPUTING THE STANDARD ERROR
C     OF THE CROSS CORRELATIONS WHEN THERE ARE MISSING VALUES.
C
C     WRITTEN BY - JANET R. DONALDSON
C                  STATISTICAL ENGINEERING DIVISION
C                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE  -  NOVEMBER 21, 1980
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      INTEGER
     +   ICCOV,INLPPC,LAGLST,LAGMAX,N,NCC
C
C  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   CCOV11(ICCOV),CCOV22(ICCOV),SDRHOC(NCC)
      INTEGER
     +   NLPP12(INLPPC),NLPP21(INLPPC)
C
C  LOCAL SCALARS
      DOUBLE PRECISION
     +   DIV,FPLM,SUM
      INTEGER
     +   I,ILAST,K,K0,KM,KP
C
C  EXTERNAL FUNCTIONS
      DOUBLE PRECISION
     +   D1MACH
      EXTERNAL D1MACH
C
C  INTRINSIC FUNCTIONS
      INTRINSIC MIN,SQRT
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     DOUBLE PRECISION CCOV11(ICCOV), CCOV22(ICCOV)
C        THE ARRAYS IN WHICH THE AUTOCOVARIANCES ARE STORED.
C     DOUBLE PRECISION DIV
C        THE SQUARE ROOT OF THE PRODUCT OF THE AUTOCOVARIANCE
C        FUNCTION VALUES AT LAG ZERO.
C     DOUBLE PRECISION FPLM
C        THE FLOATING POINT LARGEST MAGNITUDE.
C     INTEGER I
C        INDEXING VARIABLES.
C     INTEGER ICCOV
C        THE DIMENSION OF THE COVARIANCE VECTORS.
C     INTEGER ILAST
C        THE LAST LAG AT WHICH THE STANDARD ERRORS ARE TO BE COMPUTED.
C     INTEGER INLPPC
C        THE DIMENSION OF THE LAGGED PRODUCT PAIR COUNT VECTORS.
C     INTEGER K
C        INDEXING VARIABLES.
C     INTEGER KM, KP, K0
C        THE LOCATIONS IN THE ARRAYS -RHOC- AND -SDRHOC-
C        OF THE LAG -K, K AND 0, RESPECTIVELY.
C     INTEGER LAGLST
C        THE LAST AUTOCORRELATION COMPUTED BEFORE A MISSING
C        AUTOCORRELATION WAS INCOUNTERED IN EITHER SERIES.
C     INTEGER LAGMAX
C        THE MAXIMUM LAG VALUE USED.
C     INTEGER N
C        THE INTEGER NUMBER OF OBSERVATIONS IN THE SERIES
C     INTEGER NCC
C       THE NUMBER OF CROSS CORRELATIONS COMPUTED.
C     INTEGER NLPP12(INLPPC), NLPP21(INLPPC)
C        THE ARRAYS IN WHICH THE NUMBER OF OBSERVATIONS USED TO
C        COMPUTE EACH CROSS CORRELATION ARE STORED.
C     DOUBLE PRECISION SDRHOC(NCC)
C        THE ARRAY IN WHICH THE STANDARD ERRORS OF THE CROSS CORRELATION
C        ARE STORED
C     DOUBLE PRECISION SUM
C         A SUMMING VARIABLE.
C
      FPLM = D1MACH(2)
C
      DIV = CCOV11(1) * CCOV22(1)
C
      K0 = LAGMAX + 1
C
      SUM = 0.0D0
      DO 10 I = 1, LAGLST
         SUM = SUM + CCOV11(I+1) * CCOV22(I+1)
   10 CONTINUE
      SUM = SUM / DIV
      SDRHOC(K0) = FPLM
      IF (NLPP12(1) .GE. 1) SDRHOC(K0) =
     +   SQRT(N + 2.0D0 * SUM) / NLPP12(1)
C
      DO 30 K = 1, LAGMAX
         SUM = 0.0D0
         ILAST = MIN(LAGLST, N-LAGLST)
         DO 20 I = 1, ILAST
            SUM = SUM + (N-K-I) * CCOV11(I+1) * CCOV22(I+1)
   20    CONTINUE
         SUM = SUM / DIV
         KM = K0 - K
         SDRHOC(KM) = FPLM
         IF (NLPP21(K+1) .GE. 1) SDRHOC(KM) =
     +      SQRT((N - K) + 2.0D0 * SUM) * (N - K)/ (N * NLPP21(K+1))
C
         KP = K0 + K
         SDRHOC(KP) = FPLM
         IF (NLPP12(K+1) .GE. 1) SDRHOC(KP) =
     +      SQRT((N - K) + 2.0D0 * SUM) * (N - K) / (N * NLPP12(K+1))
   30 CONTINUE
C
      RETURN
      END
