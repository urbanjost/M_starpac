*UASEST
      SUBROUTINE UASEST (IAR, VAR, PHI, NF, FREQ, DELTA, SPCA, LPHI,
     +   ISPC)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS ROUTINE CALCULATES THE AUTOREGRESSIVE SPECTRUM.  IT IS
C        MODELED AFTER SUBROUTINE UASEC BY DICK JONES.
C
C     WRITTEN BY  -  JANET R. DONALDSON
C                    STATISTICAL ENGINEERING DIVISION
C                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE  -  DECEMBER 7, 1981
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      DOUBLE PRECISION
     +   DELTA,VAR
      INTEGER
     +   IAR,ISPC,LPHI,NF
C
C  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   FREQ(NF),PHI(LPHI),SPCA(ISPC)
C
C  LOCAL SCALARS
      DOUBLE PRECISION
     +   ARG,EI,ER,EXI,EXR,EXRTMP,PI,TI,TR
      INTEGER
     +   I,J
C
C  EXTERNAL SUBROUTINES
      EXTERNAL GETPI
C
C  INTRINSIC FUNCTIONS
      INTRINSIC COS,SIN
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     DOUBLE PRECISION ARG
C        THE ARGUMENT FOR THE SINE AND COSINE FUNCTIONS USED IN
C        CALCULATIONS OF THE SPECTRUM.
C     DOUBLE PRECISION DELTA
C        THE SAMPLING INTERVAL.
C     DOUBLE PRECISION EI, ER, EXI, EXR, EXRTMP
C        'COMPLEX' VARIABLES USED IN THE COMPUTATIONS.
C     DOUBLE PRECISION FREQ(NF)
C        THE ARRAY CONTAINING THE FREQUENCIES AT WHICH THE SPECTRUM
C        IS TO BE COMPUTED.
C     INTEGER I
C        AN INDEX VARIABLE.
C     INTEGER IAR
C        THE ORDER OF THE AUTOREGRESSIVE PROCESS CHOSEN.
C     INTEGER ISPC
C        THE LENGTH OF THE ARRAY SPCA.
C     INTEGER J
C        AN INDEX VARIABLE.
C     INTEGER LPHI
C        THE LENGTH OF THE ARRAY PHI.
C     INTEGER NF
C        THE NUMBER OF FREQUENCIES AT WHICH THE SPECTRUM IS TO BE
C        ESTIMATED.
C     DOUBLE PRECISION PHI(LPHI)
C        THE ARRAY OF AUTOREGRESSIVE COEFFICIENTS FOR THE
C        SELECTED ORDER.
C     DOUBLE PRECISION PI
C        THE VALUE OF PI.
C     DOUBLE PRECISION SPCA(ISPC)
C        THE ARRAY IN WHICH THE AUTOREGRESSIVE SPECTRUM IS STORED.
C     DOUBLE PRECISION TI, TR
C        A VARIABLE USED IN THE COMPUTATIONS.
C     DOUBLE PRECISION VAR
C        THE ONE STEP PREDICTION VARIANCE FOR THE SELECTED ORDER (IAR).
C
      CALL GETPI(PI)
C
      DO 20 J=1,NF
         SPCA(J) = DELTA * VAR
         IF (IAR.GE.1) THEN
            IF (DELTA.EQ.1.0D0) THEN
               IF (FREQ(J).EQ.0.0D0) THEN
                  ER = 1.0D0
                  EI = 0.0D0
               ELSE IF (FREQ(J).EQ.0.25D0) THEN
                  ER = 0.0D0
                  EI = 1.0D0
               ELSE IF (FREQ(J).EQ.0.5D0) THEN
                  ER = -1.0D0
                  EI = 0.0D0
               ELSE
                  ARG = 2.0D0 * PI * DELTA * FREQ(J)
                  ER = COS(ARG)
                  EI = SIN(ARG)
               END IF
            ELSE
               ARG = 2.0D0 * PI * DELTA * FREQ(J)
               ER = COS(ARG)
               EI = SIN(ARG)
            END IF
            EXR = 1.0D0
            EXI = 0.0D0
            TR = 1.0D0
            TI = 0.0D0
            DO 10 I=1,IAR
               EXRTMP = EXR*ER - EXI*EI
               EXI = EXR*EI + EXI*ER
               EXR = EXRTMP
               TR = TR - PHI(I) * EXR
               TI = TI - PHI(I) * EXI
   10       CONTINUE
            SPCA(J) = SPCA(J)/(TR*TR + TI*TI)
         END IF
   20 CONTINUE
      RETURN
      END
