*VPMN
      SUBROUTINE VPMN (YM, YMMISS, N, M, IYM, NSAMPL, ISCHCK, ISYM,
     +  LISYM, ISIZE, YMN, YMX, XLB, XINC, MISS, ILOG, IRLIN, IBAR)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS ROUTINE PRODUCES VERTICAL PLOTS
C
C     WRITTEN BY - JANET R. DONALDSON
C                  STATISTICAL ENGINEERING DIVISION
C                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE  -  NOVEMBER 21, 1980
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      DOUBLE PRECISION
     +   XINC,XLB,YMN,YMX
      INTEGER
     +   IBAR,ILOG,IRLIN,ISCHCK,ISIZE,IYM,LISYM,M,N,NSAMPL
      LOGICAL
     +   MISS
C
C  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   YM(IYM,M),YMMISS(M)
      INTEGER
     +   ISYM(LISYM)
C
C  SCALARS IN COMMON
      INTEGER
     +   IERR
C
C  LOCAL SCALARS
      DOUBLE PRECISION
     +   REFPT,XLABEL,YDMN,YDMX,YWIDTH,YY
      INTEGER
     +   I,IEND,ILOGY,IMAX,IMIN,IPOINT,IPRT,IPTSYM,IREFPT,J,NUMCOL,
     +   NUSED
      LOGICAL
     +   IFMISS
      CHARACTER
     +   I0*1,IBLANK*1,IM*1,IPLTCH*1,IREFCH*1,FMT*72
C
C  LOCAL ARRAYS
      INTEGER
     +   ICOUNT(103)
      CHARACTER
     +   LINE(103)*1
C
C  EXTERNAL FUNCTIONS
      LOGICAL
     +   MVCHK
      EXTERNAL MVCHK
C
C  EXTERNAL SUBROUTINES
      EXTERNAL AMEAN,AMEANM,IPRINT,PLINE,PLTPLX,PLTSYM,SETIV,VPHEAD
C
C  INTRINSIC FUNCTIONS
      INTRINSIC LOG10,MAX,MIN
C
C  COMMON BLOCKS
      COMMON /ERRCHK/IERR
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     CHARACTER FMT*72
C        THE FORMAT FOR THE X-AXIS LABELS
C     INTEGER I
C        AN INDEXING VARIABLE.
C     INTEGER IBAR
C        THE VARIABLE USED TO DETERMINE IF SINGLE POINTS (IBAR .EQ. 0)
C        OR BARS (IBAR .NE. 0) ARE TO BE PLOTTED.
C     CHARACTER*1 IBLANK
C        THE PLOT SYMBOL BLANK.
C     INTEGER ICOUNT(103)
C        THE NUMBER OF PLOT SYMBOLS AT EACH LOCATION.
C     INTEGER IEND
C        THE NUMBER OF LOCATIONS IN THE PLOT STRING.
C     INTEGER IERR
C        THE INTEGER VALUE RETURNED BY THIS ROUTINE DESIGNATING
C        WHETHER ANY ERRORS WERE DETECTED IN THE PARAMETER LIST
C        IF IERR .EQ. 0, NO ERRORS WERE DETECTED
C        IF IERR .EQ. 1, ERRORS HAVE BEEN DETECTED
C     LOGICAL IFMISS
C        THE INDICATOR VARIABLE USED TO DETERMINE WHETHER THE
C        INPUT SERIES HAS MISSING DATA (TRUE) OR NOT (FALSE).
C     INTEGER ILOG
C        THE TWO DIGIT INTEGER, PQ, USED TO SELECT AXIS SCALE, WHERE
C        P DESIGNATES THE X-AXIS AND Q DESIGNATES THE Y-AXIS.
C        IF P.EQ.0 (Q.EQ.0), THEN THE X-AXIS (Y-AXIS) IS LINEAR.
C        IF P.NE.0 (Q.NE.0), THEN THE X-AXIS (Y-AXIS) IS LOG.
C     INTEGER ILOGY
C        THE VALUE OF Q (SEE ILOG).
C     CHARACTER*1 IM
C        THE PLOT SYMBOL M.
C     INTEGER IMAX
C        THE LARGEST LOCATION IN THE PLOT STRING BEING DEFINED.
C     INTEGER IMIN
C        THE SMALLEST LOCATION IN THE PLOT STRING BEING DEFINED.
C     CHARACTER*1 IPLTCH
C        THE PLOT CHARACTER USED FOR A GIVEN LINE OF THE PLOT.
C     INTEGER IPOINT
C        THE LOCATION IN THE PLOT STRING OF THE VALUE BEING PLOTTED.
C     INTEGER IPRT
C        *
C     INTEGER IPTSYM
C        AN INDICATOR VARIABLE USED TO DESIGNATE THE TYPE
C        OF PLOT.  IF ISCHCK = 1, THE PLOT IS A SYMPLE PAGE
C        OR VERTICAL PLOT.  IF ISCHCK = 2, THE PLOT IS A SYMBOL
C        PLOT.  IF ISCHCK = 3, THE PLOT IS A MULTIVARIATE PLOT.
C     CHARACTER*1 IREFCH
C        THE PLOT SYMBOL USED TO IDENTIFY THE PLOT REFERENCE LINE.
C     INTEGER IREFPT
C        THE LOCATION IN THE PLOT STRING FOR THE VALUE ZERO, OR
C        SERIES MEAN, WHICH EVER WAS REQUESTED.
C     INTEGER IRLIN
C        THE INDICATOR VARIABLE USED TO DESIGNATE WHETHER ZERO OR THE
C        SERIES MEAN IS TO BE PLOTTED AS A REFERENCE LINE, OR WHETHER
C        NO REFERENCE LINE IS TO BE PLOTTED.
C        IF IRLIN .LE. -1, NO REFERENCE LINE IS PLOTTED.
C        IF IRLIN .EQ.  0, ZERO IS PLOTTED AS THE REFERENCE LINE.
C        IF IRLIN .GE.  1, THE SERIES MEAN IS PLOTTED.
C     INTEGER ISCHCK
C        THE INTEGER VALUE INDICATING HOW THE PLOTTING SYMBOLS
C           WILL BE DESIGNATED, WHERE
C           0 INDICATES THE PLOTTING SYMBOLS HAVE NOT BEEN DESIGNATED IN
C             THE N VECTOR ISYM AND ONLY THE SYMBOL + IS TO BE USED
C           1 INDICATES THE PLOTTING SYMBOLS HAVE BEEN DESIGNATED IN THE
C             N VECTOR ISYM
C           2 INDICATES THAT M SERIES ARE BEING PLOTTED.
C             SYMBOL I+4 WILL BE USED FOR COLUMN I OF YM.
C     INTEGER ISIZE
C        THE TWO DIGIT INTEGER, PQ, USED TO SELECT AXIS SIZE, WHERE
C        P DESIGNATES THE X-AXIS AND Q DESIGNATES THE Y-AXIS.
C        IF P.EQ.0 (Q.EQ.0), THEN THE X-AXIS (Y-AXIS) IS THE MAXIMUM.
C        IF P.NE.0 (Q.NE.0), THEN THE X-AXIS (Y-AXIS) IS HALF THE MAXIMU
C     INTEGER ISYM(LISYM)
C        VECTOR CONTAINING SYMBOL DESIGNATIONS FOR PLOTTING
C     INTEGER IYM
C        THE EXACT VALUE OF THE FIRST DIMENSION OF THE MATRIX YM.
C     CHARACTER*1 I0
C        THE PLOT SYMBOL -0-.
C     INTEGER J
C        AN INDEX VARIABLE.
C     CHARACTER*1 LINE(103)
C        THE VECTOR USED FOR THE PLOT STRING.
C     INTEGER LISYM
C        *
C     INTEGER M
C        THE NUMBER OF COLUMNS OF DATA IN YM.
C     LOGICAL MISS
C        *
C     INTEGER N
C        THE INTEGER NUMBER OF OBSERVATIONS .
C     INTEGER NSAMPL
C        THE SAMPLING FREQUENCY ACTUALLY USED.
C     INTEGER NUMCOL
C        *
C     INTEGER NUSED
C        THE NUMBER OF ACTIVE OBSERVATIONS.
C     DOUBLE PRECISION REFPT
C        THE VALUE ZERO, OR THE MEAN OF THE SERIES, WHICH EVER IS
C        REQUESTED, USED AS A PLOT REFERENCE LINE.
C     DOUBLE PRECISION XINC
C        THE VARIABLE USED TO SPECIFY THE INCREMENT FOR XLABEL.
C     DOUBLE PRECISION XLABEL
C        THE VALUE USED FOR THE LABELS ON THE RIGHT SIDE OF THE PLOT.
C     DOUBLE PRECISION XLB
C        THE STARTING VALUE FOR LABELS ON THE RIGHT SIDE OF THE GRAPH.
C     DOUBLE PRECISION YDMN, YDMX
C        THE Y-AXIS DATA LIMITS ACTUALLY USED.
C     DOUBLE PRECISION YM(IYM,M)
C        THE VECTOR CONTAINING THE OBSERVED TIME SERIES
C     DOUBLE PRECISION YMMISS(M)
C        THE USER SUPPLIED CODE WHICH IS USED TO DETERMINE WHETHER OR
C        NOT AN OBSERVATION IN THE SERIES IS MISSING.  IF YM(I) = YMMISS
C        THE VALUE IS ASSUMED MISSING, OTHERWISE IT IS NOT.
C     DOUBLE PRECISION YMN, YMX
C        THE GRAPH AXIS LOWER AND UPPER LIMITS ACTUALLY USED.
C     DOUBLE PRECISION YWIDTH
C        THE SCALE INTERVAL OF THE PLOT.
C     DOUBLE PRECISION YY
C        THE VALUE OF YM ACTUALLY BEING PLOTTED
C
C
      DATA IBLANK/' '/, IM/'M'/, I0/'0'/
C
C     PRINT PLOT HEADINGS
C
      CALL IPRINT(IPRT)
      CALL VPHEAD(NSAMPL, IRLIN, IBAR,
     +            REFPT, YWIDTH, YMN, YMX, ISIZE, ILOG,
     +            LINE, NUMCOL, ILOGY, YDMN, YDMX)
      IEND = NUMCOL + 2
C
C     COMPUTE REFERENCE POINT OF GRAPH, IF REQUIRED.
C
      IF (IRLIN.GE.0) THEN
        IF (IRLIN.EQ.0) THEN
C
C         REFERENCE POINT IS ZERO
C
          REFPT = 0.0D0
          IREFCH = I0
        ELSE
C
C         REFERENCE POINT IS MEAN
C
          IF (MISS) THEN
            CALL AMEANM(YM, YMMISS, N, NUSED, REFPT)
          ELSE
            CALL AMEAN(YM, N, REFPT)
          END IF
          IF (ILOGY.NE.0) REFPT = LOG10(REFPT)
          IREFCH = IM
        END IF
C
C       COMPUTE LOCATION OF REFPT IN PLOT STRING
C
        CALL PLTPLX(REFPT, YMN, YWIDTH, IREFPT, IEND)
      ELSE
        IREFPT = 1
      END IF
C
C     BEGIN PLOTTING
C
      IPTSYM = ISCHCK + 1
      XLABEL = XLB
      DO 50 I=1,N,NSAMPL
         CALL PLINE(1, IEND, IBLANK, LINE)
         CALL SETIV(ICOUNT, IEND, 0)
         IFMISS = .FALSE.
         IPOINT = 1
         DO 30 J=1,M
           IF (MISS) THEN
             IFMISS = (IFMISS .OR. (MVCHK(YM(I,J),YMMISS(J))))
             IF (.NOT.(MVCHK(YM(I,J),YMMISS(J)))) THEN
               IF (ILOGY.EQ.0) THEN
                 YY = YM(I,J)
               ELSE
                 YY = LOG10(YM(I,J))
               END IF
               IF ((YY.GE.YDMN) .AND. (YY.LE.YDMX)) THEN
                CALL PLTPLX(YY, YMN, YWIDTH, IPOINT, IEND)
                CALL PLTSYM(IPTSYM, I, J, ISYM, N, IPOINT, LINE, ICOUNT)
                IPLTCH = LINE(IPOINT)
               ELSE
                IPOINT = IREFPT
                IPLTCH = IBLANK
               END IF
             END IF
           ELSE
             IF (ILOGY.EQ.0) THEN
               YY = YM(I,J)
             ELSE
               YY = LOG10(YM(I,J))
             END IF
             IF ((YY.GE.YDMN) .AND. (YY.LE. YDMX)) THEN
                CALL PLTPLX(YY, YMN, YWIDTH, IPOINT, IEND)
                CALL PLTSYM(IPTSYM, I, J, ISYM, N, IPOINT, LINE, ICOUNT)
                IPLTCH = LINE(IPOINT)
             ELSE
                IPOINT = IREFPT
                IPLTCH = IBLANK
             END IF
           END IF
   30    CONTINUE
         IF ((IBAR.GE.1) .AND. (.NOT.IFMISS)) THEN
            IMIN = MIN(IPOINT,IREFPT)
            IMAX = MAX(IPOINT,IREFPT)
            CALL PLINE(IMIN, IMAX, IPLTCH, LINE)
         ELSE IF (IRLIN.GE.0) THEN
            LINE(IREFPT) = IREFCH
         END IF
C
         IF (IFMISS) THEN
           WRITE(FMT,1000) NUMCOL, '8H MISSING'
           WRITE(IPRT,FMT) XLABEL, (LINE(J), J=1,IEND)
         ELSE
           WRITE(FMT,1000) NUMCOL, '1X, G11.5 '
           IF (M.EQ.1) THEN
             WRITE(IPRT,FMT) XLABEL, (LINE(J),J=1,IEND), YM(I,1)
           ELSE
             WRITE(IPRT,FMT) XLABEL, (LINE(J),J=1,IEND)
           END IF
         END IF
         XLABEL = XLABEL + XINC*NSAMPL
   50 CONTINUE
C
      RETURN
C
C     FORMAT STATEMENTS
C
 1000 FORMAT('(1X,G11.5,1X,A1,''I'',', I3, 'A1,''I'',A1,',A10,')')
      END
