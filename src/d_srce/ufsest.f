*UFSEST
      SUBROUTINE UFSEST(ACOV, W, LAG, SPCF, ISPCF, LACOV, LW, NF, FREQ,
     +   DELTA)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS ROUTINE COMPUTES THE SPECTRUM, SPCF, AND
C     THEIR LOWER AND UPPER CONFIDENCE LIMITS, SPCLCL AND SPCUCL,
C     RESPECTIVELY.
C
C     WRITTEN BY - JANET R. DONALDSON
C                  STATISTICAL ENGINEERING DIVISION
C                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE  -  DECEMBER 7, 1981
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      DOUBLE PRECISION
     +   DELTA
      INTEGER
     +   ISPCF,LACOV,LAG,LW,NF
C
C  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   ACOV(LACOV),FREQ(NF),SPCF(ISPCF),W(LW)
C
C  LOCAL SCALARS
      DOUBLE PRECISION
     +   C,PI,V0,V1,V2
      INTEGER
     +   I,K,KK
C
C  EXTERNAL SUBROUTINES
      EXTERNAL GETPI
C
C  INTRINSIC FUNCTIONS
      INTRINSIC COS
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     DOUBLE PRECISION ACOV(LACOV)
C        THE AUTOCOVARIANCES OF THE SERIES.
C     DOUBLE PRECISION C
C        A VALUE USED TO COMPUTE THE SPECTRUM VALUES.
C     DOUBLE PRECISION DELTA
C        THE SAMPLING INTERVAL.
C     DOUBLE PRECISION FREQ(NF)
C        THE VECTOR OF FREQUENCIES AT WHICH THE SPECTRUM IS TO BE
C        COMPUTED.
C     INTEGER I
C        AN INDEX VARIABLE
C     INTEGER ISPCF
C         THE ACTUAL FIRST DIMENSION OF THE SPECTRUM ARRAYS.
C     INTEGER K, KK
C        INDEXING VARIABLES.
C     INTEGER LACOV
C        THE LENGTH OF VECTOR ACOV.
C     INTEGER LAG
C        THE LAG WINDOW TRUCCATION POINT.
C     INTEGER LW
C        THE LENGTH OF THE VECTOR W.
C     INTEGER NF
C        THE NUMBER OF FREQUENCIES AT WHICH THE SPECTRUM IS
C        TO BE COMPUTED.
C     DOUBLE PRECISION PI
C        THE VALUE OF PI.
C     DOUBLE PRECISION SPCF(ISPCF)
C        THE ARRAYS IN WHICH THE SPECTRUM IS STORED.
C     DOUBLE PRECISION V0, V1, V2
C        CONSTANTS USED FOR COMPUTING THE SPECTRUM VALUES.
C     DOUBLE PRECISION W(LW)
C        THE VECTOR OF LAG WINDOWS.
C
      CALL GETPI(PI)
C
C        COMPUTE THE SPECTRUM AND ITS CONFIDENCE LIMITS.
C
      DO 20 I=1,NF
         C = COS(2.0D0*PI*FREQ(I))
         V0 = 0.0D0
         V1 = 0.0D0
         DO 10 K=1,LAG
            KK = LAG + 1 - K
            V2 = 2.0D0*C*V1 - V0 + W(KK+1)*ACOV(KK+1)
            V0 = V1
            V1 = V2
   10    CONTINUE
         SPCF(I) = DELTA*(ACOV(1)*W(1)+2.0D0*(V1*C-V0))
         IF (SPCF(I).LT.0.0D0) SPCF(I) = 0.0D0
   20 CONTINUE
      RETURN
      END
