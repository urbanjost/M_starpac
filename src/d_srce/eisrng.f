*EISRNG
      SUBROUTINE EISRNG (NMSUB, ISEED, ISEEDU, HEAD)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS ROUTINE CHECKS WHETHER THE INPUT VARIABLE ISEED IS
C     WITHIN [0, 2**MDIG], AND, IF NONZERO, IS ODD.
C
C     IF ISEED IS WITHIN [0, 2**MDIG] THEN
C        ISEEDU = ISEED-MOD(ISEED,2)+1
C     ELSE
C        ISEEDU = MIN[ ABS(ISEED)-MOD(ABS(ISEED),2)+1, 2**(MDIG-1)-1]
C                 AND AN ERROR MESSAGE IS PRINTED.
C
C     WRITTEN BY  -  JANET R. DONALDSON
C                    CENTER FOR COMPUTING AND APPLIED MATHEMATICS
C                    NATIONAL INSTITUTE OF STANDARDS AND TECHNOLOGY
C                    BOULDER, COLORADO
C
C     CREATION DATE  -  JANUARY 17, 1990
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      INTEGER
     +   ISEED,ISEEDU
      LOGICAL
     +   HEAD
C
C  ARRAY ARGUMENTS
      CHARACTER
     +   NMSUB(6)*1
C
C  LOCAL SCALARS
      INTEGER
     +   IPRT,MDIG
C
C  EXTERNAL FUNCTIONS
      INTEGER
     +   I1MACH
      EXTERNAL I1MACH
C
C  EXTERNAL SUBROUTINES
      EXTERNAL EHDR,IPRINT
C
C  INTRINSIC FUNCTIONS
      INTRINSIC ABS,MIN,MOD
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     LOGICAL HEAD
C        A FLAG INDICATING WHETHER THE HEADING SHOULD BE PRINTED
C        (TRUE) OR NOT (FALSE).  IF A HEADING IS PRINTED, THE VALUE
C        OF HEAD WILL BE CHANGED TO FALSE.
C     INTEGER IPRT
C        THE UNIT NUMBER FOR PRINTED OUTPUT.
C     INTEGER ISEED
C        THE VALUE OF THE SEED BEING TESTED.
C     INTEGER ISEEDU
C        THE VALUE OF THE SEED ACTUALLY USED BY NRAND AND NRANDC.
C     INTEGER MDIG
C        A LOWER BOUND ON THE NUMBER OF BINARY DIGITS AVAILABLE
C        FOR REPRESENTING INTEGERS, INCLUDING THE SIGN BIT.
C     CHARACTER*1 NMSUB(6)
C        THE CHARACTERS OF THE CALLING SUBROUTINES NAME.
C
      MDIG = MIN(I1MACH(8)+1,32)
C
C     CHECK FOR VIOLATIONS
C
      IF ((ISEED.EQ.0) .OR.
     +    ((ISEED.GE.1) .AND.
     +     (ISEED.LE.2**(MDIG-1)-1) .AND.
     +     (MOD(ISEED,2).EQ.1))) THEN
C
C     SUPPLIED SEED WILL BE USED
C
         ISEEDU = ISEED
      ELSE
C
C     VIOLATIONS FOUND
C
         ISEEDU = MIN( ABS(ISEED)+MOD(ABS(ISEED),2)-1, 2**(MDIG-1)-1)
         CALL IPRINT(IPRT)
         CALL EHDR(NMSUB, HEAD)
         WRITE (IPRT, 1010) MDIG-1,ISEEDU
      END IF
C
      RETURN
C
C     FORMAT STATEMENTS
C
 1010 FORMAT(/
     +   ' THE VALUE OF ISEED MUST BE BETWEEN 0 AND 2**',I2,' - 1,'/
     +   ' INCLUSIVE, AND, IF ISEED IS NOT 0, ISEED MUST BE ODD.  THE'/
     +   ' SEED ACTUALLY USED BY THE RANDOM NUMBER GENERATOR HAS BEEN'/
     +   ' SET TO', I10,'.')
C
      END
