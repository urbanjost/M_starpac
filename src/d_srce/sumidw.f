*SUMIDW
      SUBROUTINE SUMIDW(X, W, N, XMEANW, SUMDI)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     A ROUTINE TO CALCULATE THE SUM OF THE PRODUCTS OF I AND THE I TH
C     DIFFERENCE AMONG THE ELEMENTS OF THE VECTOR X - XMEANW WHICH
C     ARE NOT WEIGHTED ZERO.  (ELEMENTS WEIGHTED ZERO DO NOT
C     PRODUCE AN INCREMENT IN I.)
C
C     WRITTEN BY - JOHN E. KOONTZ
C                  STATISTICAL ENGINEERING DIVISION
C                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE  -  MAY 17, 1982
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      DOUBLE PRECISION
     +   SUMDI,XMEANW
      INTEGER
     +   N
C
C  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   W(N),X(N)
C
C  LOCAL SCALARS
      DOUBLE PRECISION
     +   DIFF,I
      INTEGER
     +   K
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     DOUBLE PRECISION DIFF
C        THE DIFFERENCE OF A VALUE OF X AND THE WEIGHTED MEAN OF X.
C     DOUBLE PRECISION I
C        I COUNTS FROM 1 TO THE NUMBER OF NON-ZERO WEIGHTED ELEMENTS
C        FOUND IN X.
C     INTEGER K
C        A LOOP INDEX.
C     INTEGER N
C        INPUT PARAMETER.  THE NUMBER OF ELEMENTS IN X.
C     DOUBLE PRECISION SUMDI
C        OUTPUT PARAMETER.  THE SUM OF THE PRODUCTS OF I AND THE
C        NON-ZERO WEIGHTED ELEMENTS OF THE VECTOR X - XMEANW.
C     DOUBLE PRECISION W(N)
C        INPUT PARAMETER.  THE WEIGHTS VECTOR FOR X.
C     DOUBLE PRECISION X(N)
C        INPUT PARAMETER.  THE VECTOR OF N DATA VALUES.
C     DOUBLE PRECISION XMEANW
C        INPUT PARAMETER.  THE WEIGHTED MEAN OF X.
C
      I = 0.0D0
      SUMDI = 0.0D0
      DO 10 K=1,N
         IF (W(K).EQ.0.0D0) GO TO 10
         DIFF = X(K) - XMEANW
         I = I + 1.0D0
         SUMDI = SUMDI + I*DIFF
   10 CONTINUE
      RETURN
      END
