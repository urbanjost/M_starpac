*CDFCHI
      DOUBLE PRECISION FUNCTION CDFCHI(CHISQR, DF)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS ROUTINE COMPUTES THE CHI SQUARE CUMULATIVE DISTRIBUTION
C     FUNCTION FROM THE INCOMPLETE GAMMA FUNCTION RATIO AS DISCUSSED IN
C     CHAPTER 17 OF DISTRIBUTIONS IN STATISTICS - CONTINUOUS UNIVARIATE
C     DISTRIBUTIONS - 1, BY JOHNSON AND KOTZ.
C
C     WRITTEN BY - JANET R. DONALDSON
C                  STATISTICAL ENGINEERING DIVISION
C                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE - DECEMBER 7, 1981
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      DOUBLE PRECISION
     +   CHISQR,DF
C
C  LOCAL SCALARS
      DOUBLE PRECISION
     +   FPLPM
C
C  EXTERNAL FUNCTIONS
      DOUBLE PRECISION
     +   DGAMI,DGAMMA,D1MACH
      LOGICAL
     +   MVCHK
      EXTERNAL DGAMI,DGAMMA,D1MACH,MVCHK
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     DOUBLE PRECISION CHISQR
C        THE PERCENT POINT FROM THE CHI SQUARED DISTRIBUTION.
C     DOUBLE PRECISION DF
C        THE DEGREES OF FREEDOM.
C     DOUBLE PRECISION FPLPM
C        THE DOUBLE PRECISION FLOATING POINT LARGEST POSITIVE MAGNITUDE.
C
C     COMMENCE BODY OF ROUTINE
C
      FPLPM = D1MACH(2)
C
      CDFCHI = DGAMI(0.5D0*DF, 0.5D0*CHISQR)
      IF (MVCHK(CDFCHI,FPLPM)) THEN
         CDFCHI = 1.0D0
      ELSE
         CDFCHI = CDFCHI / DGAMMA(0.5D0*DF)
         IF (CDFCHI.LT.0.0D0) CDFCHI = 0.0D0
         IF (CDFCHI.GT.1.0D0) CDFCHI = 1.0D0
      END IF
C
      RETURN
      END
