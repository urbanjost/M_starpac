*BFSMN
      SUBROUTINE BFSMN(SPCF1, SPCF2, CEVEN, CODD, W, LW, LAG, DF, NPRT,
     +   NF, CSPC2, PHAS, FREQ, NPTS, XAXIS, YAXIS, ISYM, LPCV, ALPHA,
     +   LAGMX1, DELTA)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS ROUTINE COMPUTES THE SQUARED COHERENCY AND PHASE COMPONENTS
C     OF A BIVARIATE SPECTRUM.
C
C     REFERENCE - JENKINS AND WATTS
C                 SPECTRAL ANALYSIS AND ITS APPLICATIONS
C
C     WRITTEN BY - STEPHEN M. KEEFER AND JANET R. DONALDSON
C                  STATISTICAL ENGINEERING DIVISION
C                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE  -  DECEMBER 2, 1985
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      DOUBLE PRECISION
     +   ALPHA,DELTA,DF
      INTEGER
     +   LAG,LAGMX1,LPCV,LW,NF,NPRT,NPTS
C
C  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   CEVEN(*),CODD(*),CSPC2(*),FREQ(*),PHAS(*),SPCF1(*),SPCF2(*),
     +   W(*),XAXIS(*),YAXIS(*)
      INTEGER
     +   ISYM(*)
C
C  LOCAL SCALARS
      DOUBLE PRECISION
     +   ARG,BARL,BARQ,BARY,C,CI,FAC,FPLM,FPLRS,FPSPM,G,PI,PIT2,SN,V0,
     +   V1,V2,Z0,Z1,Z2
      INTEGER
     +   I,K
C
C  EXTERNAL FUNCTIONS
      DOUBLE PRECISION
     +   PPFNML,D1MACH
      EXTERNAL PPFNML,D1MACH
C
C  EXTERNAL SUBROUTINES
      EXTERNAL GETPI
C
C  INTRINSIC FUNCTIONS
      INTRINSIC ATAN2,COS,LOG,SIGN,SIN,SQRT,TANH
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     DOUBLE PRECISION ALPHA
C        THE DESIRED CONFIDENCE LEVEL.
C     DOUBLE PRECISION ARG
C        AN ARGUMENT USED IN THE SPECTRUM COMPUTATIONS.
C     DOUBLE PRECISION BARL
C        THE SMOOTHED COSPECTRAL ESTIMATES.
C     DOUBLE PRECISION BARQ
C        THE SMOOTHED QUADRATURE SPECTRAL ESTIMATES.
C     DOUBLE PRECISION BARY
C        A TRANSFORMATION OF THE SQUARED COHERENCY COMPONENT.
C     DOUBLE PRECISION C
C        AN ARGUMENT USED IN THE SPECTRUM COMPUTATIONS.
C     DOUBLE PRECISION CEVEN(LAGMX1)
C        THE SUMS OF THE COVARIANCES FOR EACH LAG.
C     DOUBLE PRECISION CI
C        THE CONFIDENCE INTERVAL FOR THE SQUARED COHERENCY COMPONENT.
C     DOUBLE PRECISION CODD(LAGMX1)
C        THE DIFFERENCES OF THE AUTOCOVARIANCES FOR EACH LAG.
C     DOUBLE PRECISION CSPC2(NF)
C        THE SQUARED COHERENCY COMPONENT OF THE BIVARIATE SPECTRA.
C     DOUBLE PRECISION DELTA
C        THE SAMPLING INTERVAL.
C     DOUBLE PRECISION DF
C        THE EFFECTIVE DEGREES OF FREEDOM.
C     DOUBLE PRECISION FAC
C        THE CONVERSION FACTOR FROM RADIANS TO DEGREES.
C     DOUBLE PRECISION FPLM
C        THE FLOATING POINT LARGEST MAGNITUDE.
C     DOUBLE PRECISION FPLRS
C        THE FLOATING POINT LARGEST RELATIVE SPACING.
C     DOUBLE PRECISION FPSPM
C        THE FLOATING POINT SMALLEST POSITIVE MAGNITUDE.
C     DOUBLE PRECISION FREQ(NF)
C        THE FREQUENCIES AT WHICH THE SPECTRUM IS COMPUTED.
C     DOUBLE PRECISION G
C        AN ARGUMENT USED IN THE COMPUTATION OF THE ALPHA PERCENT
C        SIGNIFICANCE LEVEL.
C     INTEGER I
C        AN INDEX VALUE.
C     INTEGER ISYM(LPCV)
C        THE VECTOR CONTAINING THE CODES FOR THE PLOT SYMBOLS.
C     INTEGER K
C         AN INDEX VALUE.
C     INTEGER LAG
C        THE LAG WINDOW TRUNCATION POINT USED FOR A SPECIFIC WINDOW.
C     INTEGER LAGMX1
C        THE VALUE LAGMAX+1.
C     INTEGER LPCV
C        THE LENGTH OF THE PLOT CO-ORDINATE VECTORS.
C     INTEGER LW
C         THE LENGTH OF VECTOR W.
C     INTEGER NF
C        THE NUMBER OF FREQUENCIES AT WHICH THE SPECTRUM IS
C        TO BE COMPUTED.
C     INTEGER NPRT
C        A CODE USED TO SPECIFY THE TYPE OF PLOT.
C        IF NPRT = 0 THE PLOT IS SUPPRESSED.
C        IF NPRT = 2 THE PLOT IS PROVIDED.
C     INTEGER NPTS
C        THE NUMBER OF X, Y CO-ORDINATES TO BE PLOTTED.
C     DOUBLE PRECISION PHAS(NF)
C        THE PHASE COMPONENT OF THE BIVARIATE SPECTRA.
C     DOUBLE PRECISION PI, PIT2
C        THE VALUE OF PI AND PI*2.
C     DOUBLE PRECISION SN
C        AN ARGUMENT USED IN THE COMPUTATION OF THE SPECTRUM.
C     DOUBLE PRECISION SPCF1(NF), SPCF2(NF)
C        THE UNIVARIATE SPECTRUM FOR EACH SERIES.
C     DOUBLE PRECISION V0, V1, V2
C        ARGUMENTS USED IN THE COMPUTATION OF THE SPECTRUM.
C     DOUBLE PRECISION W(LW)
C        THE WINDOW.
C     DOUBLE PRECISION XAXIS(LPCV)
C        THE X AXIS VALUES FOR THE SPECTRUM PLOTS.
C     DOUBLE PRECISION YAXIS(LPCV)
C        THE Y AXIS VALUES FOR THE SPECTRUM PLOTS.
C     DOUBLE PRECISION Z0, Z1, Z2
C        ARGUMENTS USED IN THE COMPUTATION OF THE SPECTRUM.
C
C
      CALL GETPI(PI)
      PIT2 = PI*2.0D0
C
      FPSPM = D1MACH(1)
      FPLM = D1MACH(2)
      FPLRS = D1MACH(4)
C
      FAC = 180.0D0/PI
C
C
C     COMPUTE SMOOTHED CO-SPECTRAL ESTIMATE
C
      DO 40 I=1,NF
C
C           COMPUTE SMOOTHED CO- AND QUADRATURE SPECTRA USING
C           THE ALGORITHM SHOWN ON PAGE 420 OF JENKINS AND WATTS
C
         IF (FREQ(I).EQ.0.0D0) THEN
            C = 1.0D0
            SN = 0.0D0
         ELSE IF (FREQ(I).EQ.0.25D0) THEN
            C = 0.0D0
            SN = 1.0D0
         ELSE IF (FREQ(I).EQ.0.5D0) THEN
            C = -1.0D0
            SN = 0.0
         ELSE
            ARG = PIT2*FREQ(I)
            C = COS(ARG)
            SN = SIN(ARG)
         END IF
         V0 = 0.0D0
         V1 = 0.0D0
         Z0 = 0.0D0
         Z1 = 0.0D0
         DO 10 K=LAG-1,1,-1
            V2 = 2.0D0*C*V1 - V0 + W(K+1)*CEVEN(K+1)
            Z2 = 2.0D0*C*Z1 - Z0 + W(K+1)*CODD(K+1)
            V0 = V1
            V1 = V2
            Z0 = Z1
            Z1 = Z2
   10    CONTINUE
         BARL = DELTA*(CEVEN(1)+2.0D0*(V1*C-V0))
         BARQ = 2.0D0*DELTA*Z1*SN
C
C     COMPUTE THE SMOOTHED SQUARED COHERENCY SPECTRA
C
         IF (SPCF1(I)*SPCF2(I).GT.0.0D0) THEN
            CSPC2(I) = (BARL*BARL+BARQ*BARQ)
            CSPC2(I) = CSPC2(I)/(SPCF1(I)*SPCF2(I))
         ELSE
            CSPC2(I) = FPLM
         END IF
C
C     COMPUTE PHASE (IN RADIANS)
C
         IF ((BARQ.NE.0.0D0) .OR. (BARL.NE.0.0D0)) THEN
            PHAS(I) = ATAN2(-BARQ,BARL)
         ELSE
            IF (I.EQ.1) THEN
               PHAS(I) = 0.0D0
            ELSE
               PHAS(I) = SIGN(PI,PHAS(I-1))
            END IF
         END IF
   40 CONTINUE
C
      IF (NPRT.EQ.0) RETURN
C
C     COMPUTE SMOOTHED SQUARED COHERENCY PLOT VECTORS
C
      CI = PPFNML(ALPHA)*SQRT(1.0D0/DF)
      G = 2.0D0/DF
      G = 1.0D0 - (1.0D0-ALPHA)**(G/(1.0D0-G))
      NPTS = 0
      DO 60 I=1,NF
         NPTS = NPTS + 1
C
C     COMPUTE 95 PER CENT SIGNIFICANCE LEVEL
C
         YAXIS(NPTS) = G
         XAXIS(NPTS) = FREQ(I)
         ISYM(NPTS) = 4
         IF (SPCF1(I)*SPCF2(I).LE.0.0D0) GO TO 60
C
C     COMPUTE COHERENCE SPECTRAL ESTIMATE
C
         IF (CSPC2(I).GT.1.0D0) GO TO 60
         NPTS = NPTS + 1
         YAXIS(NPTS) = CSPC2(I)
         XAXIS(NPTS) = FREQ(I)
         ISYM(NPTS) = 1
         IF (CSPC2(I).LT.G) GO TO 60
C
C     COMPUTE CONFIDENCE INTERVAL
C
         BARY = SQRT(CSPC2(I))
         BARY = 0.5D0*(LOG((1.0D0+BARY)/(1.0D0-BARY)))
         NPTS = NPTS + 1
         YAXIS(NPTS) = (TANH(BARY+CI))*(TANH(BARY+CI))
         XAXIS(NPTS) = FREQ(I)
         ISYM(NPTS) = 2
         NPTS = NPTS + 1
         YAXIS(NPTS) = (TANH(BARY-CI))*(TANH(BARY-CI))
         XAXIS(NPTS) = FREQ(I)
         ISYM(NPTS) = 2
   60 CONTINUE
C
      RETURN
C
      END
