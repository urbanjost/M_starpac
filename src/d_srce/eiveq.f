*EIVEQ
      SUBROUTINE EIVEQ (NMSUB, NMVAR1, IVEC, N, IVAL, NEQMN, HEAD, NEQ,
     +                  NNE, MSGTYP, ERROR, NMVAR2, NMVAR3)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS ROUTINE CHECKS WHETHER THE NUMBER OF ELEMENTS OF IVEC EQUAL
C     TO IVAL IS AT LEAST NEQMN.
C
C     WRITTEN BY  -  JANET R. DONALDSON
C                    STATISTICAL ENGINEERING DIVISION
C                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE  -  AUGUST 3, 1987
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      INTEGER
     +   IVAL,MSGTYP,N,NEQ,NEQMN,NNE
      LOGICAL
     +   ERROR,HEAD
C
C  ARRAY ARGUMENTS
      INTEGER
     +   IVEC(*)
      CHARACTER
     +   NMSUB(6)*1,NMVAR1(8)*1,NMVAR2(8)*1,NMVAR3(8)*1
C
C  LOCAL SCALARS
      INTEGER
     +   I,IPRT
C
C  EXTERNAL SUBROUTINES
      EXTERNAL EHDR,IPRINT
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     LOGICAL ERROR
C        VALUE(S) INDICATING WHETHER AN ERROR WAS DETECTED (TRUE) OR NOT
C        (FALSE).
C     LOGICAL HEAD
C        A FLAG INDICATING WHETHER THE HEADING SHOULD BE PRINTED
C        (TRUE) OR NOT (FALSE).  IF A HEADING IS PRINTED, THE VALUE
C        OF HEAD WILL BE CHANGED TO FALSE.
C     INTEGER I
C        AN INDEX ARGUMENT.
C     INTEGER IPRT
C        THE UNIT NUMBER FOR PRINTED OUTPUT.
C     INTEGER IVEC(N)
C        THE VECTOR BEING CHECKED.
C     INTEGER MSGTYP
C        AN ARGUMENT USED TO INDICATE THE TYPE OF MESSAGE TO BE
C        PRINTED, WHERE IF ERROR IS TRUE AND
C        MSGTYP = 1, THE INPUT VALUE WAS TOO SMALL BASED ON LIMITS
C                    IMPOSED BY STARPAC.
C     INTEGER N
C        THE NUMBER OF OBSERVATIONS.
C     CHARACTER*1 NMSUB(6)
C        THE CHARACTERS OF THE CALLING SUBROUTINES NAME.
C     CHARACTER*1 NMVAR1(8)
C        THE CHARACTERS OF THE NAME OF THE ARGUMENT BEING CHECKED.
C     CHARACTER*1 NMVAR2(8)
C        THE CHARACTERS OF THE NAME OF THE ARGUMENT BEING CHECKED
C        AGAINST.
C     CHARACTER*1 NMVAR3(8)
C        THE CHARACTERS OF THE NAME OF THE ARGUMENT THAT THE ELEMENTS
C        MUST BE EQUAL TO.
C     INTEGER NEQ
C        THE NUMBER OF ELEMENTS EQUAL TO IVAL.
C     INTEGER NEQMN
C        THE MINIMUM NUMBER OF ELEMENTS EQUAL TO IVAL WHICH IS OK.
C     INTEGER NNE
C        THE NUMBER OF ELEMENTS NOT EQUAL TO IVAL.
C
      ERROR = .FALSE.
C
      IF (N.LE.0) RETURN
C
C     CHECK FOR VALUES EQUAL TO IVAL
C
      NEQ = 0
      DO 10 I = 1, N
         IF (IVEC(I) .EQ. IVAL) NEQ = NEQ + 1
   10 CONTINUE
C
      NNE = N - NEQ
      IF (NEQ .GE. NEQMN) RETURN
C
C     INSUFFICIENT NUMBER OF ELEMENTS EQUAL TO IVAL.
C
      ERROR = .TRUE.
C
      CALL IPRINT(IPRT)
C
      CALL EHDR(NMSUB, HEAD)
C
      IF (MSGTYP.EQ.1) WRITE(IPRT, 1000)
     +   (NMVAR1(I),I=1,8), (NMVAR2(I),I=1,8), NEQ,
     +   (NMVAR2(I),I=1,8), (NMVAR3(I),I=1,8)
C
      RETURN
C
C     FORMAT STATEMENTS
C
 1000 FORMAT(
     +   ' THE NUMBER OF ELEMENTS IN ', 8A1,
     +   ' EQUAL TO ', 8A1, ' IS ', I6, '.'/
     +   ' THE NUMBER OF ELEMENTS EQUAL TO ', 8A1,
     +   ' MUST BE GREATER THAN OR EQUAL TO ', 8A1, '.')
C
      END
