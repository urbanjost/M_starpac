*SETFRQ
      SUBROUTINE SETFRQ (FREQ, NF, NPRT, FMIN, FMAX, H)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS ROUTINE COMPUTES THE FREQUENCIES AT WHICH THE
C     SPECTRUM IS TO BE ESTIMATED.
C
C     WRITTEN BY - JANET R. DONALDSON
C                  STATISTICAL ENGINEERING DIVISION
C                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE  -  NOVEMBER 26, 1980
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      DOUBLE PRECISION
     +   FMAX,FMIN,H
      INTEGER
     +   NF,NPRT
C
C  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   FREQ(NF)
C
C  LOCAL SCALARS
      DOUBLE PRECISION
     +   DELTAF
      INTEGER
     +   I
C
C  INTRINSIC FUNCTIONS
      INTRINSIC LOG10
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     DOUBLE PRECISION DELTAF
C        THE FREQUENCY INCREMENT.
C     DOUBLE PRECISION FMAX, FMIN
C        THE MAXIMUM AND MINIMUM FREQUENCIES AT WHICH THE
C        SPECTRUM IS TO BE ESTIMATED.
C     DOUBLE PRECISION FREQ(NF)
C        THE VECTOR OF FREQUENCIES AT WHICH THE SPECTRUM IS TO BE
C        ESTIMATED.
C     DOUBLE PRECISION H
C        THE SAMPLING INTERVAL.
C     INTEGER I
C        AN INDEXING VARIABLE.
C     INTEGER NF
C        THE NUMBER OF FREQUENCIES FOR WHICH THE SPECTRAL ESTIMATES
C        ARE TO BE ESTIMATED.
C     INTEGER NPRT
C        A CODE USED TO SPECIFY THE TYPE OF PLOT, WHERE IF
C        NPRT EQUALS 2 THE FREQUENCY SCALE IS LINEAR, AND IF
C        NPRT EQUALS 3 THE FREQUENCY SCALE IS LOG.
C
      IF (NPRT .EQ. 3) GO TO 20
C
C     COMPUTE FREQUENCY VALUES FOR LINEAR SCALE
C
      FREQ(1) = FMIN
C
      IF (NF .EQ. 1) RETURN
C
      DELTAF = (FMAX - FMIN) / (H * (NF - 1))
      DO 10 I = 2, NF
         FREQ(I) = FREQ(I-1) + DELTAF
   10 CONTINUE
C
      FREQ(NF) = FMAX
      RETURN
C
   20 CONTINUE
C
C     COMPUTE FREQUENCY VALUES FOR LOG SCALE
C
      DELTAF = (LOG10(FMAX) - LOG10(FMIN)) / (H * (NF - 1))
C
      FREQ(1) = FMIN
C
      IF (NF .EQ. 1) RETURN
C
      DO 30 I = 2, NF
         FREQ(I) = 10.0D0**(LOG10(FREQ(I-1)) + DELTAF)
   30 CONTINUE
C
      FREQ(NF) = FMAX
C
C
      RETURN
      END
