*FLTARM
      SUBROUTINE FLTARM (Y, YMISS, N, IAR, PHI, YF, YFMISS, NYF)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS ROUTINE FILTERS THE INPUT SERIES Y, WHICH CONTAINS MISSING
C     DATA, USING THE IAR TERMS OF THE AUTOREGRESSIVE FILTER PHI,
C     COPYING THE FILTERED SERIES INTO YF.
C
C     WRITTEN BY  -  JANET R. DONALDSON
C                    STATISTICAL ENGINEERING DEVISION
C                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE  -  NOVEMBER 26, 1980
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      DOUBLE PRECISION
     +   YFMISS,YMISS
      INTEGER
     +   IAR,N,NYF
C
C  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   PHI(*),Y(*),YF(*)
C
C  LOCAL SCALARS
      DOUBLE PRECISION
     +   FPLM,TEMP
      INTEGER
     +   I,I1,J,K
C
C  EXTERNAL FUNCTIONS
      DOUBLE PRECISION
     +   D1MACH
      LOGICAL
     +   MVCHK
      EXTERNAL D1MACH,MVCHK
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     DOUBLE PRECISION FPLM
C        THE FLOATING POINT LARGEST MAGNITUDE.
C     INTEGER I
C        AN INDEXING VARIABLE.
C     INTEGER IAR
C        THE NUMBER OF FILTER TERMS.
C     INTEGER I1, J, K
C        INDEXING VARIABLES.
C     INTEGER N
C        THE NUMBER OF OBSERVATIONS IN THE SERIES Y.
C     INTEGER NYF
C        THE NUMBER OF OBSERVATIONS IN THE FILTERED SERIES YF.
C     DOUBLE PRECISION PHI(IAR)
C        THE ARRAY IN WHICH THE FILTER COEFFICIENTS ARE STORED.
C     DOUBLE PRECISION TEMP
C        A TEMPORARY STORAGE LOCATION.
C     DOUBLE PRECISION Y(N)
C        THE VECTOR CONTAINING THE OBSERVED TIME SERIES.
C     DOUBLE PRECISION YF(N)
C        THE VECTOR IN WHICH THE FILTERED SERIES IS RETURNED.
C     DOUBLE PRECISION YFMISS
C        THE MISSING VALUE CODE USED IN THE FILTERED SERIES TO
C        INDICATE THE VALUE COULD NOT BE COMPUTED DUE TO MISSING DATA.
C     DOUBLE PRECISION YMISS
C        THE MISSING VALUE CODE USED IN THE INPUT SERIES TO INDICATE
C        AN OBSERVATION IS MISSING.
C
      FPLM = D1MACH(2)
C
      YFMISS = FPLM
C
      DO 10 I = 1, N
         YF(I) = Y(I)
   10 CONTINUE
C
      NYF = N - IAR
C
      DO 50 I = 1, NYF
         TEMP = YFMISS
         K = I + IAR
         IF (MVCHK(YF(K), YMISS)) GO TO 40
         TEMP = YF(K)
         DO 30 J = 1, IAR
            K = K - 1
            IF (.NOT. MVCHK(YF(K), YMISS)) GO TO 20
            TEMP = YFMISS
            GO TO 40
   20       CONTINUE
            TEMP = TEMP - PHI(J) * YF(K)
   30    CONTINUE
   40    YF(I) = TEMP
   50 CONTINUE
C
      I1 = NYF + 1
C
      DO 60 I = I1, N
         YF(I) = 0.0D0
   60 CONTINUE
      RETURN
      END
