*SPPLTD
      SUBROUTINE SPPLTD (SPCMN, SPCMX, ALOW, AUP, YPLTMN, YPLTMX,
     +   CILOW, CIMID, CIUP, YMAX)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS ROUTINE SETS VARIOUS Y AXIS LIMITS FOR DECIBLE
C     SPECTRUM PLOTS.
C
C     WRITTEN BY - JANET R. DONALDSON
C                  STATISTICAL ENGINEERING DIVISION
C                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE  -  DECEMBER 7, 1981
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      DOUBLE PRECISION
     +   ALOW,AUP,CILOW,CIMID,CIUP,SPCMN,SPCMX,YMAX,YPLTMN,YPLTMX
C
C  LOCAL SCALARS
      DOUBLE PRECISION
     +   RNGMN,YMIN
C
C  INTRINSIC FUNCTIONS
      INTRINSIC ABS,LOG10
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     DOUBLE PRECISION ALOW, AUP
C        FACTORS USED TO COMPUTE THE CONFIDENCE INTERVALS.
C     DOUBLE PRECISION CILOW, CIMID, CIUP
C        THE Y CORDNATES FOR THE LOWER  MID AND UPPER CONFIDENCE
C        INTERVAL POINTS.
C     DOUBLE PRECISION RNGMN
C        THE MINIMUM Y AXIS RANGE FOR THE PLOT.
C     DOUBLE PRECISION SPCMN, SPCMX
C        THE MINIMUM AND MAXIMUM SPECTRAL VALUE TO BE PLOTTED.
C     DOUBLE PRECISION YMAX, YMIN
C        THE MAXIMUM AND MINIMUM ACTUAL SPECTRAL VALUE
C        (IN DECIBELS) TO BE PLOTTED.
C     DOUBLE PRECISION YPLTMN, YPLTMX
C        THE MINIMUM AND MAXIMUM VAUES TO BE PLOTTED FOR THE Y AXIS.
C
C     SET CO-ORDINATES FOR DECIBLE PLOTS
C
      YMAX = 10.0D0 * LOG10(SPCMX)
      YMIN = 10.0D0 * LOG10(SPCMN) - YMAX
C
      YPLTMX = 0.0D0
      RNGMN = 20.0D0 * (LOG10(AUP) - LOG10(ALOW))
      IF (ABS(YMIN) .LT. RNGMN) YPLTMX = (RNGMN + YMIN) * 0.5D0
      YPLTMN = YMIN - YPLTMX
      CIUP = YPLTMX
      CIMID = CIUP - 10.0D0 * LOG10(AUP)
      CILOW = CIMID + 10.0D0 * LOG10(ALOW)
C
      RETURN
      END
