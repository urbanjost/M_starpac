*GFSEST
      SUBROUTINE GFSEST (H, K, NF, FREQ, GAIN, DELTA)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS ROUTINE COMPUTES THE GAIN FUNCTION OF AN INPUT SYMMETRIC
C     LINEAR FILTER AT THE FREQUENCIES SPECIFIED IN FREQ USING THE
C     TRANSFORM ALGORITHM SHOWN ON PAGE 311 OF JENKINS AND WATTS.
C
C     WRITTEN BY - JANET R. DONALDSON
C                  STATISTICAL ENGINEERING DIVISION
C                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE  -  NOVEMBER 26, 1980
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      DOUBLE PRECISION
     +   DELTA
      INTEGER
     +   K,NF
C
C  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   FREQ(NF),GAIN(NF),H(K)
C
C  LOCAL SCALARS
      DOUBLE PRECISION
     +   C,PI,V0,V1,V2
      INTEGER
     +   I,J,KHALF,KMID
C
C  EXTERNAL SUBROUTINES
      EXTERNAL GETPI
C
C  INTRINSIC FUNCTIONS
      INTRINSIC ABS,COS
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     DOUBLE PRECISION C
C        A VALUE USED TO COMPUTE THE GAIN FUNCTION.
C     DOUBLE PRECISION DELTA
C        THE SAMPLING INTERVAL OF THE ORIGINAL SERIES.
C     DOUBLE PRECISION FREQ(NF)
C        THE VECTOR OF FREQUENCIES AT WHICH THE GAIN FUNCTION IS TO BE
C        ESTIMATED.
C     DOUBLE PRECISION GAIN(NF)
C        THE ARRAY IN WHICH THE GAIN FUNCTION ESTIMATES ARE RETURNED.
C     DOUBLE PRECISION H(K)
C        THE SYMMETRIC LINEAR FILTER WEIGHTS.
C     INTEGER I, J
C        INDEX VARIABLES.
C     INTEGER K
C        THE NUMBER OF TERMS IN THE SYMMETRIC LINEAR FILTER.
C     INTEGER KHALF, KMID
C        HALF THE FILTER LENGTH, AND THE MIDPOINT OF THE FILTER,
C        RESPECTIVELY.
C     INTEGER NF
C        THE NUMBER OF FREQUENCIES FOR WHICH THE GAIN FUNCTION ESTIMATES
C        ARE TO BE ESTIMATED.
C     DOUBLE PRECISION PI
C        THE VALUE OF PI.
C     DOUBLE PRECISION V0, V1, V2
C        CONSTANTS USED FOR COMPUTING THE GAIN FUNCTION ESTIMATES.
C
      CALL GETPI(PI)
C
C        COMPUTE GAIN FUNCTION ESTIMATES AND THEIR CONFIDENCE LIMITS.
C
      KMID = (K+1) / 2
      KHALF = KMID - 1
C
      DO 30 I = 1, NF
         C = COS(2.0D0 * PI * FREQ(I))
         V0 = 0.0D0
         V1 = 0.0D0
         DO 20 J = 1, KHALF
            V2 = 2.0D0 * C * V1 - V0 + H(J)
            V0 = V1
            V1 = V2
   20    CONTINUE
         GAIN(I) = ABS(DELTA * (H(KMID) + 2.0D0 * (V1 * C - V0)))
   30 CONTINUE
      RETURN
      END
