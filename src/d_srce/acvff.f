*ACVFF
      SUBROUTINE ACVFF (YFFT, N, NFFT, YMEAN, ACOV, LAGMAX, LACOV,
     +   LYFFT, WORK, LWORK)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS ROUTINE COMPUTES THE ACVF OF A SERIES USING TWO
C     PASSES OF A FFT.
C
C     WRITTEN BY - JANET R. DONALDSON
C                  STATISTICAL ENGINEERING DIVISION
C                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE  -  NOVEMBER 21, 1980
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      DOUBLE PRECISION
     +   YMEAN
      INTEGER
     +   LACOV,LAGMAX,LWORK,LYFFT,N,NFFT
C
C  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   ACOV(*),WORK(*),YFFT(*)
C
C  LOCAL SCALARS
      DOUBLE PRECISION
     +   FAC
      INTEGER
     +   I,ISN,N1,NF,NFFT2
C
C  EXTERNAL SUBROUTINES
      EXTERNAL AMEAN,FFT,FFTCT,REALTR
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     DOUBLE PRECISION ACOV(LACOV)
C        THE ARRAY IN WHICH THE AUTOCOVARIANCES ARE STORED
C     DOUBLE PRECISION FAC
C        A FACTOR USED IN THE COMPUTATIONS.
C     INTEGER I
C        AN INDEX VARIABLE.
C     INTEGER ISN
C        AN INDICATOR VARIABLE USED BY THE FFT ROUTINES.
C     INTEGER LACOV
C        THE LENGTH OF THE VECTOR ACOV.
C     INTEGER LAGMAX
C        THE MAXIMUM LAG TO BE USED.
C     INTEGER LWORK
C        THE LENGTH OF THE VECTOR WORK.
C     INTEGER LYFFT
C        THE LENGTH OF THE VECTOR YFFT.
C     INTEGER N
C        THE NUMBER OF OBSERVATIONS IN THE SERIES
C     INTEGER NF
C        THE NUMBER OF FOURIER FREQUENCIES.
C     INTEGER NFFT
C        THE NUMBER OF OBSERVATIONS IN THE EXTENDED SERIES.
C     INTEGER NFFT2
C        THE EFFECTIVE NUMBER OF OBSERVATIONS FOR THE FFT TRANSFORM.
C     INTEGER N1
C        THE VALUE N + 1.
C     DOUBLE PRECISION WORK(LWORK)
C        THE WORK AREA FOR THE COMPUTATIONS.
C     DOUBLE PRECISION YFFT(LYFFT)
C        THE VECTOR CONTAINING THE OBSERVED SERIES
C     DOUBLE PRECISION YMEAN
C        THE MEAN OF THE OBSERVED TIME SERIES
C
C     COMPUTE ARITHMETIC MEAN
C
      CALL AMEAN(YFFT, N, YMEAN)
C
C     SUBTRACT THE MEAN FROM THE SERIES
C
      DO 10 I = 1, N
         YFFT(I) = YFFT(I) - YMEAN
   10 CONTINUE
C
C     APPEND ZEROS
C
      N1 = N + 1
C
      DO 20 I = N1, NFFT
         YFFT(I) = 0.0D0
   20 CONTINUE
C
C     COMPUTE AUTOCOVARIANCES.
C
      ISN = 2
      NFFT2 = (NFFT - 2)/ 2
C
      CALL FFT(YFFT(1), YFFT(2), NFFT2, NFFT2, NFFT2, ISN)
      CALL REALTR (YFFT(1), YFFT(2), NFFT2, ISN)
C
      NF = NFFT2 + 1
C
      DO 30 I = 1, NF
         WORK(I) = YFFT(2*I-1)*YFFT(2*I-1) + YFFT(2*I)*YFFT(2*I)
   30 CONTINUE
C
      CALL FFTCT (WORK, NFFT2, LWORK)
C
      FAC = 1.0D0 / (4 * (NFFT - 2) * N)
C
      ACOV(1) = WORK(1) * FAC
C
      DO 40 I = 1, LAGMAX
         ACOV(I+1) = WORK(I+1) * FAC
   40 CONTINUE
C
      RETURN
      END
