*DCSEVL
      DOUBLE PRECISION FUNCTION DCSEVL (X, A, N)
C
C     LATEST REVISION  -  OCTOBER 3, 1983  (JRD)
C
C EVALUATE THE N-TERM CHEBYSHEV SERIES A AT X.  ADAPTED FROM
C R. BROUCKE, ALGORITHM 446, C.A.C.M., 16, 254 (1973).
C
C             INPUT ARGUMENTS --
C X      DBLE PREC VALUE AT WHICH THE SERIES IS TO BE EVALUATED.
C A      DBLE PREC ARRAY OF N TERMS OF A CHEBYSHEV SERIES.  IN EVAL-
C        UATING A, ONLY HALF THE FIRST COEF IS SUMMED.
C N      NUMBER OF TERMS IN ARRAY A.
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      DOUBLE PRECISION X
      INTEGER N
C
C  ARRAY ARGUMENTS
      DOUBLE PRECISION A(N)
C
C  LOCAL SCALARS
      DOUBLE PRECISION B0,B1,B2,TWOX
      INTEGER I,NI1
C
C  EXTERNAL SUBROUTINES
      EXTERNAL XERROR
C
C
      IF (N.LT.1) CALL XERROR ('DCSEVL  NUMBER OF TERMS LE 0', 28, 2,2)
      IF (N.GT.1000) CALL XERROR ('DCSEVL  NUMBER OF TERMS GT 1000',
     1  31, 3, 2)
      IF (X.LT.(-1.D0) .OR. X.GT.1.D0) CALL XERROR (
     1  'DCSEVL  X OUTSIDE (-1,+1)', 25, 1, 1)
C
      TWOX = 2.0D0*X
      B0 = 0.0D0
      B1 = 0.0D0
      B2 = 0.0D0
      DO 10 I=1,N
        B2 = B1
        B1 = B0
        NI1 = N-I+1
        B0 = TWOX*B1 - B2 + A(NI1)
 10   CONTINUE
C
      DCSEVL = 0.5D0 * (B0-B2)
C
      RETURN
      END
