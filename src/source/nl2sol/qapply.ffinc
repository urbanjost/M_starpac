!QAPPLY
      subroutine qapply(nn, n, p, j, r, ierr)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!  VARIABLE DECLARATIONS
!
!
!  SCALAR ARGUMENTS
      integer
     &   ierr,n,nn,p
!
!  ARRAY ARGUMENTS
      real(kind=wp) ::
     &   j(nn,p),r(n)
!
!  LOCAL SCALARS
      real(kind=wp) ::
     &   t
      integer
     &   i,k,l,nl1
!
!  EXTERNAL FUNCTIONS
      real(kind=wp) ::
     &   dotprd
      external dotprd
!
!  INTRINSIC FUNCTIONS
      intrinsic abs
!
!     *****PARAMETERS.
!     INTEGER NN, N, P, IERR
!     REAL(KIND=WP) :: J(NN,P), R(N)
!
!     =================================================================
!
!     *****PURPOSE.
!     THIS SUBROUTINE APPLIES TO R THE ORTHOGONAL TRANSFORMATIONS
!     STORED IN J BY QRFACT
!
!     *****PARAMETER DESCRIPTION.
!     ON INPUT.
!
!        NN IS THE ROW DIMENSION OF THE MATRIX J AS DECLARED IN
!             THE CALLING PROGRAM DIMENSION STATEMENT
!
!        N IS THE NUMBER OF ROWS OF J AND THE SIZE OF THE VECTOR R
!
!        P IS THE NUMBER OF COLUMNS OF J AND THE SIZE OF SIGMA
!
!        J CONTAINS ON AND BELOW ITS DIAGONAL THE COLUMN VECTORS
!             U WHICH DETERMINE THE HOUSEHOLDER TRANSFORMATIONS
!             IDENT - U*U.TRANSPOSE
!
!        R IS THE RIGHT HAND SIDE VECTOR TO WHICH THE ORTHOGONAL
!             TRANSFORMATIONS WILL BE APPLIED
!
!        IERR IF NON-ZERO INDICATES THAT NOT ALL THE TRANSFORMATIONS
!             WERE SUCCESSFULLY DETERMINED AND ONLY THE FIRST
!             ABS(IERR) - 1 TRANSFORMATIONS WILL BE USED
!
!     ON OUTPUT.
!
!        R HAS BEEN OVERWRITTEN BY ITS TRANSFORMED IMAGE
!
!     *****APPLICATION AND USAGE RESTRICTIONS.
!     NONE
!
!     *****ALGORITHM NOTES.
!     THE VECTORS U WHICH DETERMINE THE HOUSEHOLDER TRANSFORMATIONS
!     ARE NORMALIZED SO THAT THEIR 2-NORM SQUARED IS 2.  THE USE OF
!     THESE TRANSFORMATIONS HERE IS IN THE SPIRIT OF (1).
!
!     *****SUBROUTINES AND FUNCTIONS CALLED.
!
!     DOTPRD - FUNCTION, RETURNS THE INNER PRODUCT OF VECTORS
!
!     *****REFERENCES.
!     (1) BUSINGER, P. A., AND GOLUB, G. H. (1965), LINEAR LEAST SQUARES
!        SOLUTIONS BY HOUSEHOLDER TRANSFORMATIONS, NUMER. MATH. 7,
!        PP. 269-276.
!
!     *****HISTORY.
!     DESIGNED BY DAVID M. GAY, CODED BY STEPHEN C. PETERS (WINTER 1977)
!
!     *****GENERAL.
!
!     THIS SUBROUTINE WAS WRITTEN IN CONNECTION WITH RESEARCH
!     SUPPORTED BY THE NATIONAL SCIENCE FOUNDATION UNDER GRANTS
!     MCS-7600324, DCR75-10143, 76-14311DSS, AND MCS76-11989.
!
!     =================================================================
!
!     *****LOCAL VARIABLES.
!     INTEGER I, K, L, NL1
!     REAL(KIND=WP) :: T
!/
!     *****FUNCTIONS.
!     EXTERNAL DOTPRD
!     REAL(KIND=WP) :: DOTPRD
!
      k = p
      if (ierr .ne. 0) k = abs(ierr) - 1
      if ( k .eq. 0) go to 999
!
      do 20 l = 1, k
         nl1 = n - l + 1
         t = -dotprd(nl1, j(l,l), r(l))
!
         do 10 i = l, n
            r(i) = r(i) + t*j(i,l)
 10      continue
 20   continue
 999  return

      end subroutine qapply
