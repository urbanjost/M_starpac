!LITVMU
      subroutine litvmu(n, x, l, y)
!
!  ***  SOLVE  (L**T)*X = Y,  WHERE  L  IS AN  N X N  LOWER TRIANGULAR
!  ***  MATRIX STORED COMPACTLY BY ROWS.  X AND Y MAY OCCUPY THE SAME
!  ***  STORAGE.  ***
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      integer
     +   n
!
!  ARRAY ARGUMENTS
      real(kind=wp) ::
     +   l(1),x(n),y(n)
!
!  LOCAL SCALARS
      real(kind=wp) ::
     +   xi,zero
      integer
     +   i,i0,ii,ij,im1,j,np1
!
      data zero/0.0_wp/
!
      do 10 i = 1, n
 10      x(i) = y(i)
      np1 = n + 1
      i0 = n*(n+1)/2
      do 30 ii = 1, n
         i = np1 - ii
         xi = x(i)/l(i0)
         x(i) = xi
         if (i .le. 1) go to 999
         i0 = i0 - i
         if (xi .eq. zero) go to 30
         im1 = i - 1
         do 20 j = 1, im1
              ij = i0 + j
              x(j) = x(j) - xi*l(ij)
 20           continue
 30      continue
 999  return
      end subroutine litvmu
