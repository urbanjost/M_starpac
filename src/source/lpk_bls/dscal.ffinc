!DSCAL
      subroutine dscal(n,da,dx,incx)
!
!     REPLACE DOUBLE PRECISION DX BY DOUBLE PRECISION DA*DX.
!     FOR I = 0 TO N-1, REPLACE DX(1+I*INCX) WITH  DA * DX(1+I*INCX)
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      double precision da
      integer incx,n
!
!  ARRAY ARGUMENTS
      double precision dx(*)
!
!  LOCAL SCALARS
      integer i,m,mp1,ns
!
!  INTRINSIC FUNCTIONS
      intrinsic mod
!
      if(n.le.0)return
      if(incx.eq.1)goto 20
!
!        CODE FOR INCREMENTS NOT EQUAL TO 1.
!
      ns = n*incx
          do 10 i = 1,ns,incx
          dx(i) = da*dx(i)
   10     continue
      return
!
!        CODE FOR INCREMENTS EQUAL TO 1.
!
!
!        CLEAN-UP LOOP SO REMAINING VECTOR LENGTH IS A MULTIPLE OF 5.
!
   20 m = mod(n,5)
      if( m .eq. 0 ) go to 40
      do 30 i = 1,m
        dx(i) = da*dx(i)
   30 continue
      if( n .lt. 5 ) return
   40 mp1 = m + 1
      do 50 i = mp1,n,5
        dx(i) = da*dx(i)
        dx(i + 1) = da*dx(i + 1)
        dx(i + 2) = da*dx(i + 2)
        dx(i + 3) = da*dx(i + 3)
        dx(i + 4) = da*dx(i + 4)
   50 continue
      return
      end
