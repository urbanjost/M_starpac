!ISAMAX
      integer function isamax(n,sx,incx)
!
!     FIND SMALLEST INDEX OF MAXIMUM MAGNITUDE OF SINGLE PRECISION SX.
!     ISAMAX =  FIRST I, I = 1 TO N, TO MINIMIZE  ABS(SX(1-INCX+I*INCX))
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      integer incx,n
!
!  ARRAY ARGUMENTS
      real sx(*)
!
!  LOCAL SCALARS
      real smax,xmag
      integer i,ii,ns
!
!  INTRINSIC FUNCTIONS
      intrinsic abs
!
      isamax = 0
      if(n.le.0) return
      isamax = 1
      if(n.le.1)return
      if(incx.eq.1)goto 20
!
!        CODE FOR INCREMENTS NOT EQUAL TO 1.
!
      smax = abs(sx(1))
      ns = n*incx
      ii = 1
          do 10 i=1,ns,incx
          xmag = abs(sx(i))
          if(xmag.le.smax) go to 5
          isamax = ii
          smax = xmag
    5     ii = ii + 1
   10     continue
      return
!
!        CODE FOR INCREMENTS EQUAL TO 1.
!
   20 smax = abs(sx(1))
      do 30 i = 2,n
         xmag = abs(sx(i))
         if(xmag.le.smax) go to 30
         isamax = i
         smax = xmag
   30 continue
      return
      end
