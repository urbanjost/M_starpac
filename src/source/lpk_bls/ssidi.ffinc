!SSIDI
      subroutine ssidi(a,lda,n,kpvt,det,inert,work,job)
!
!     LATEST REVISION  -  JANUARY 24, 1990  (JRD)
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      integer job,lda,n
!
!  ARRAY ARGUMENTS
      real a(lda,*),det(2),work(*)
      integer inert(3),kpvt(*)
!
!  LOCAL SCALARS
      real ak,akkp1,akp1,d,t,temp,ten
      integer j,jb,k,km1,ks,kstep
      logical nodet,noert,noinv
!
!  EXTERNAL FUNCTIONS
!      REAL SDOT
!       EXTERNAL SDOT
!
!  EXTERNAL SUBROUTINES
!       EXTERNAL SAXPY,SSWAP
!
!  INTRINSIC FUNCTIONS
      intrinsic abs,iabs,mod
!
!     SSIDI COMPUTES THE DETERMINANT, INERTIA AND INVERSE
!     OF A REAL SYMMETRIC MATRIX USING THE FACTORS FROM SSIFA.
!
!     ON ENTRY
!
!        A       REAL(LDA,N)
!                THE OUTPUT FROM SSIFA.
!
!        LDA     INTEGER
!                THE LEADING DIMENSION OF THE ARRAY A.
!
!        N       INTEGER
!                THE ORDER OF THE MATRIX A.
!
!        KPVT    INTEGER(N)
!                THE PIVOT VECTOR FROM SSIFA.
!
!        WORK    REAL(N)
!                WORK VECTOR.  CONTENTS DESTROYED.
!
!        JOB     INTEGER
!                JOB HAS THE DECIMAL EXPANSION  ABC  WHERE
!                   IF  C .NE. 0, THE INVERSE IS COMPUTED,
!                   IF  B .NE. 0, THE DETERMINANT IS COMPUTED,
!                   IF  A .NE. 0, THE INERTIA IS COMPUTED.
!
!                FOR EXAMPLE, JOB = 111  GIVES ALL THREE.
!
!     ON RETURN
!
!        VARIABLES NOT REQUESTED BY JOB ARE NOT USED.
!
!        A      CONTAINS THE UPPER TRIANGLE OF THE INVERSE OF
!               THE ORIGINAL MATRIX.  THE STRICT LOWER TRIANGLE
!               IS NEVER REFERENCED.
!
!        DET    REAL(2)
!               DETERMINANT OF ORIGINAL MATRIX.
!               DETERMINANT = DET(1) * 10.0**DET(2)
!               WITH 1.0 .LE. ABS(DET(1)) .LT. 10.0
!               OR DET(1) = 0.0.
!
!        INERT  INTEGER(3)
!               THE INERTIA OF THE ORIGINAL MATRIX.
!               INERT(1)  =  NUMBER OF POSITIVE EIGENVALUES.
!               INERT(2)  =  NUMBER OF NEGATIVE EIGENVALUES.
!               INERT(3)  =  NUMBER OF ZERO EIGENVALUES.
!
!     ERROR CONDITION
!
!        A DIVISION BY ZERO MAY OCCUR IF THE INVERSE IS REQUESTED
!        AND  SSICO  HAS SET RCOND .EQ. 0.0
!        OR  SSIFA  HAS SET  INFO .NE. 0 .
!
!     LINPACK. THIS VERSION DATED 08/14/78 .
!     JAMES BUNCH, UNIV. CALIF. SAN DIEGO, ARGONNE NAT. LAB
!
!     SUBROUTINES AND FUNCTIONS
!
!     BLAS SAXPY,SDOT,SSWAP
!     FORTRAN ABS,IABS,MOD
!
!
      noinv = mod(job,10) .eq. 0
      nodet = mod(job,100)/10 .eq. 0
      noert = mod(job,1000)/100 .eq. 0
!
      ten = 10.0_wp
!
      if (nodet .and. noert) go to 140
         if (noert) go to 10
            inert(1) = 0
            inert(2) = 0
            inert(3) = 0
   10    continue
         if (nodet) go to 20
            det(1) = 1.0_wp
            det(2) = 0.0_wp
   20    continue
         t = 0.0_wp
         do 130 k = 1, n
            d = a(k,k)
!
!           CHECK IF 1 BY 1
!
            if (kpvt(k) .gt. 0) go to 50
!
!              2 BY 2 BLOCK
!              USE DET (D  S)  =  (D/T * C - T) * T  ,  T = ABS(S)
!                      (S  C)
!              TO AVOID UNDERFLOW/OVERFLOW TROUBLES.
!              TAKE TWO PASSES THROUGH SCALING.  USE  T  FOR FLAG.
!
               if (t .ne. 0.0_wp) go to 30
                  t = abs(a(k,k+1))
                  d = (d/t)*a(k+1,k+1) - t
               go to 40
   30          continue
                  d = t
                  t = 0.0_wp
   40          continue
   50       continue
!
            if (noert) go to 60
               if (d .gt. 0.0_wp) inert(1) = inert(1) + 1
               if (d .lt. 0.0_wp) inert(2) = inert(2) + 1
               if (d .eq. 0.0_wp) inert(3) = inert(3) + 1
   60       continue
!
            if (nodet) go to 120
               det(1) = d*det(1)
               if (det(1) .eq. 0.0_wp) go to 110
   70             if (abs(det(1)) .ge. 1.0_wp) go to 80
                     det(1) = ten*det(1)
                     det(2) = det(2) - 1.0_wp
                  go to 70
   80             continue
   90             if (abs(det(1)) .lt. ten) go to 100
                     det(1) = det(1)/ten
                     det(2) = det(2) + 1.0_wp
                  go to 90
  100             continue
  110          continue
  120       continue
  130    continue
  140 continue
!
!     COMPUTE INVERSE(A)
!
      if (noinv) go to 270
         k = 1
  150    if (k .gt. n) go to 260
            km1 = k - 1
            if (kpvt(k) .lt. 0) go to 180
!
!              1 BY 1
!
               a(k,k) = 1.0_wp/a(k,k)
               if (km1 .lt. 1) go to 170
                  call scopy(km1,a(1,k),1,work,1)
                  do 160 j = 1, km1
                     a(j,k) = sdot(j,a(1,j),1,work,1)
                     call saxpy(j-1,work(j),a(1,j),1,a(1,k),1)
  160             continue
                  a(k,k) = a(k,k) + sdot(km1,work,1,a(1,k),1)
  170          continue
               kstep = 1
            go to 220
  180       continue
!
!              2 BY 2
!
               t = abs(a(k,k+1))
               ak = a(k,k)/t
               akp1 = a(k+1,k+1)/t
               akkp1 = a(k,k+1)/t
               d = t*(ak*akp1 - 1.0_wp)
               a(k,k) = akp1/d
               a(k+1,k+1) = ak/d
               a(k,k+1) = -akkp1/d
               if (km1 .lt. 1) go to 210
                  call scopy(km1,a(1,k+1),1,work,1)
                  do 190 j = 1, km1
                     a(j,k+1) = sdot(j,a(1,j),1,work,1)
                     call saxpy(j-1,work(j),a(1,j),1,a(1,k+1),1)
  190             continue
                  a(k+1,k+1) = a(k+1,k+1) + sdot(km1,work,1,a(1,k+1),1)
                  a(k,k+1) = a(k,k+1) + sdot(km1,a(1,k),1,a(1,k+1),1)
                  call scopy(km1,a(1,k),1,work,1)
                  do 200 j = 1, km1
                     a(j,k) = sdot(j,a(1,j),1,work,1)
                     call saxpy(j-1,work(j),a(1,j),1,a(1,k),1)
  200             continue
                  a(k,k) = a(k,k) + sdot(km1,work,1,a(1,k),1)
  210          continue
               kstep = 2
  220       continue
!
!           SWAP
!
            ks = iabs(kpvt(k))
            if (ks .eq. k) go to 250
               call sswap(ks,a(1,ks),1,a(1,k),1)
               do 230 jb = ks, k
                  j = k + ks - jb
                  temp = a(j,k)
                  a(j,k) = a(ks,j)
                  a(ks,j) = temp
  230          continue
               if (kstep .eq. 1) go to 240
                  temp = a(ks,k+1)
                  a(ks,k+1) = a(k,k+1)
                  a(k,k+1) = temp
  240          continue
  250       continue
            k = k + kstep
         go to 150
  260    continue
  270 continue
      return
      end
