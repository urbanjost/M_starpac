!GFORD
     subroutine gford (freq, gain, isort, nf, xord, yord,&
     &   nord, ypltmn, ypltmx, nprt, igferr)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE PRODUCES ORDANENTS FOR THE GAIN FUNCTION PLOTS.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 26, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     real(kind=wp) ::&
     &   ypltmn,ypltmx
     integer&
     &   igferr,nf,nord,nprt
!
!  ARRAY ARGUMENTS
     real(kind=wp) ::&
     &   freq(nf),gain(nf),xord(nf),yord(nf)
     integer&
     &   isort(nf)
!
!  LOCAL SCALARS
     real(kind=wp) ::&
     &   gainmn,gainmx,ymax
     integer&
     &   i,j
!
!  EXTERNAL SUBROUTINES
!       EXTERNAL SPCCK
!
!  INTRINSIC FUNCTIONS
      intrinsic log10
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL FREQ(NF)
!        THE VECTOR OF FREQUENCIES AT WHICH THE SPECTRUM IS TO BE
!        ESTIMATED.
!     REAL GAIN(NF)
!        THE VECTOR IN WHICH THE GAIN FUNCTION IS STORED.
!     REAL GAINMN, GAINMX
!        THE MINIMUM AND MAXIMUM GAIN FUNCTION VALUE TO BE PLOTTED.
!     INTEGER I
!        AN INDEX VARIABLE
!     INTEGER IGFERR
!        AN ERROR FLAG INDICATING WHETHER COMPUTATIONS SEEM
!        TO HAVE PRODUCED REASONABLE RESULTS.
!     INTEGER ISORT(NF)
!        THE ARRAY USED FOR SORTING.
!     INTEGER J
!        AN INDEXING VARIABLE.
!     INTEGER NF
!        THE NUMBER OF FREQUENCIES FOR WHICH THE SPECTRAL ESTIMATES
!        ARE TO BE ESTIMATED.
!     INTEGER NORD
!        THE NUMBER OF ORDINATES TO BE PLOTTED.
!     INTEGER NPRT
!        A CODE USED TO SPECIFY THE TYPE OF PLOT, WHERE IF
!        NPRT < 0 THE PLOT IS DECIBELS/LINEAR
!        NPRT = 0 THE PLOT IS SUPPRESSED
!        NPRT > 0 THE PLOT IS LOG/LINEAR
!     REAL XORD(NF)
!        THE X ORDINATES FOR THE SPECTRUM PLOT.
!     REAL YMAX
!        THE MAXIMUM ACTUAL SPECTRUM VALUE (IN DECIBELS) TO BE PLOTTED.
!     REAL YORD(NF)
!        THE Y ORDINATES FOR THE SPECTRUM PLOTS.
!     REAL YPLTMN, YPLTMX
!        THE MINIMUM AND MAXIMUM VAUES TO BE PLOTTED FOR THE Y AXIS.
!
      igferr = 0
!
      call spcck (gain, isort, nf, gainmn, gainmx, nord, igferr)
!
      if (igferr .ne. 0) return
!
      j = 0
!
      if (nprt.ge.1) go to 30
!
!     SET ORDINATES FOR DECIBLE PLOTS
!
      ymax = 10.0e0 * log10(gainmx)
!
      ypltmx = 0.0e0
      ypltmn = 10.0e0 * log10(gainmn) - ymax
!
      do 10 i = 1, nf
         if (gain(i) .lt. gainmn) go to 10
         j = j + 1
         xord(j) = freq(i)
         yord(j) = 10.0e0 * log10(gain(i)) - ymax
   10 continue
!
      return
!
   30 continue
!
      ypltmx = gainmx
      ypltmn = gainmn
!
!     SET ORDINATES FOR LOG PLOTS
!
      do 50 i = 1, nf
         if (gain(i) .lt. gainmn) go to 50
         j = j + 1
         xord(j) = freq(i)
         yord(j) = gain(i)
   50 continue
!
      return
      end
