!GFORD
      SUBROUTINE GFORD (FREQ, GAIN, ISORT, NF, XORD, YORD,
     +   NORD, YPLTMN, YPLTMX, NPRT, IGFERR)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE PRODUCES ORDANENTS FOR THE GAIN FUNCTION PLOTS.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 26, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP)
     +   YPLTMN,YPLTMX
      INTEGER
     +   IGFERR,NF,NORD,NPRT
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP)
     +   FREQ(NF),GAIN(NF),XORD(NF),YORD(NF)
      INTEGER
     +   ISORT(NF)
!
!  LOCAL SCALARS
      REAL(KIND=WP)
     +   GAINMN,GAINMX,YMAX
      INTEGER
     +   I,J
!
!  EXTERNAL SUBROUTINES
      EXTERNAL SPCCK
!
!  INTRINSIC FUNCTIONS
      INTRINSIC LOG10
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL FREQ(NF)
!        THE VECTOR OF FREQUENCIES AT WHICH THE SPECTRUM IS TO BE
!        ESTIMATED.
!     REAL GAIN(NF)
!        THE VECTOR IN WHICH THE GAIN FUNCTION IS STORED.
!     REAL GAINMN, GAINMX
!        THE MINIMUM AND MAXIMUM GAIN FUNCTION VALUE TO BE PLOTTED.
!     INTEGER I
!        AN INDEX VARIABLE
!     INTEGER IGFERR
!        AN ERROR FLAG INDICATING WHETHER COMPUTATIONS SEEM
!        TO HAVE PRODUCED REASONABLE RESULTS.
!     INTEGER ISORT(NF)
!        THE ARRAY USED FOR SORTING.
!     INTEGER J
!        AN INDEXING VARIABLE.
!     INTEGER NF
!        THE NUMBER OF FREQUENCIES FOR WHICH THE SPECTRAL ESTIMATES
!        ARE TO BE ESTIMATED.
!     INTEGER NORD
!        THE NUMBER OF ORDINATES TO BE PLOTTED.
!     INTEGER NPRT
!        A CODE USED TO SPECIFY THE TYPE OF PLOT, WHERE IF
!        NPRT < 0 THE PLOT IS DECIBELS/LINEAR
!        NPRT = 0 THE PLOT IS SUPPRESSED
!        NPRT > 0 THE PLOT IS LOG/LINEAR
!     REAL XORD(NF)
!        THE X ORDINATES FOR THE SPECTRUM PLOT.
!     REAL YMAX
!        THE MAXIMUM ACTUAL SPECTRUM VALUE (IN DECIBELS) TO BE PLOTTED.
!     REAL YORD(NF)
!        THE Y ORDINATES FOR THE SPECTRUM PLOTS.
!     REAL YPLTMN, YPLTMX
!        THE MINIMUM AND MAXIMUM VAUES TO BE PLOTTED FOR THE Y AXIS.
!
      IGFERR = 0
!
      CALL SPCCK (GAIN, ISORT, NF, GAINMN, GAINMX, NORD, IGFERR)
!
      IF (IGFERR .NE. 0) RETURN
!
      J = 0
!
      IF (NPRT.GE.1) GO TO 30
!
!     SET ORDINATES FOR DECIBLE PLOTS
!
      YMAX = 10.0E0 * LOG10(GAINMX)
!
      YPLTMX = 0.0E0
      YPLTMN = 10.0E0 * LOG10(GAINMN) - YMAX
!
      DO 10 I = 1, NF
         IF (GAIN(I) .LT. GAINMN) GO TO 10
         J = J + 1
         XORD(J) = FREQ(I)
         YORD(J) = 10.0E0 * LOG10(GAIN(I)) - YMAX
   10 CONTINUE
!
      RETURN
!
   30 CONTINUE
!
      YPLTMX = GAINMX
      YPLTMN = GAINMN
!
!     SET ORDINATES FOR LOG PLOTS
!
      DO 50 I = 1, NF
         IF (GAIN(I) .LT. GAINMN) GO TO 50
         J = J + 1
         XORD(J) = FREQ(I)
         YORD(J) = GAIN(I)
   50 CONTINUE
!
      RETURN
      END
