!UASOUT
      SUBROUTINE UASOUT (XAXIS, YAXIS, ISYM, NPTS, BW, IDF, LAG,
     +   IAR, PHI, ISPCER, LPCV, XPLTMN, XPLTMX, YPLTMN, YPLTMX,
     +   FTEST, AIC, LAIC, VAR, NPRT, LAGMAX, AICPRT, N, NMSUB)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE PRODUCES THE SPECTRUM PLOTS FOR THE
!     AUTOREGRESSIVE SPECTRUM ESTIMATES.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  DECEMBER 7, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP)
     +   BW,VAR,XPLTMN,XPLTMX,YPLTMN,YPLTMX
      INTEGER
     +   IAR,IDF,ISPCER,LAG,LAGMAX,LAIC,LPCV,N,NPRT,NPTS
      LOGICAL
     +   AICPRT
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP)
     +   AIC(LAIC),FTEST(2,LAGMAX),PHI(LAGMAX),XAXIS(LPCV),YAXIS(LPCV)
      INTEGER
     +   ISYM(LPCV)
      CHARACTER
     +   NMSUB(6)*1
!
!  LOCAL SCALARS
      REAL(KIND=WP)
     +   XMN,XMX,YMN,YMX
      INTEGER
     +   ILOG,IPRT
      LOGICAL
     +   ERROR
!
!  LOCAL ARRAYS
      REAL(KIND=WP)
     +   PRHO(1)
!
!  EXTERNAL SUBROUTINES
      EXTERNAL AOSLST,IPRINT,PPLMT,PPMN,VERSP
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL AIC(LAIC)
!        THE ARRAY CONTAINING AKIAKES CRITERIA FOR EACH ORDER.
!     LOGICAL AICPRT
!        AN INDICATOR VARIABLE USED TO DETERMINE IF THE AKIAKE
!        INFORMATION CRITERIA AND CHI SQUARED STATISTICS SHOULD
!        BE PRINTED.
!     REAL BW
!        THE BANDWIDTH.
!     LOGICAL ERROR
!        AN ERROR FLAG
!     REAL FTEST(2,LAGMAX)
!        THE ARRAY IN WHICH THE F RATIO AND PROBABILITY ARE STORED.
!     INTEGER IAR
!        THE ORDER OF THE AUTOREGRESSIVE PROCESS CHOSEN.
!     INTEGER IDF
!        THE EFFECTIVE DEGREES OF FREEDOM.
!     INTEGER IPRT
!        THE LOGICAL UNIT NUMBER FOR THE OUTPUT.
!     INTEGER ISPCER
!        A VARIABLE USED TO DESIGNATE AN ERROR IN THE SPECTRAL
!        ESTIMATES.
!     INTEGER ISYM(LPCV)
!        THE ARRAY CONTAINING THE CODE FOR THE PLOT SYMBOLS.
!     INTEGER LAG
!        THE LAG WINDOW TRUNCATION POINT USED FOR THE FOURIER SPECTRUM.
!     INTEGER LAGMAX
!        THE MAXIMUM LAG VALUE TO BE USED.
!     INTEGER LAIC
!        THE LENGTH OF THE ARRAY AIC.
!     INTEGER LPCV
!        THE LENGTH OF THE PLOT CO-ORDINATE VECTORS.
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS IN THE SERIES.
!     CHARACTER*1 NMSUB(6)
!        THE NAME OF THE CALLING SUBROUTINE.
!     INTEGER NPRT
!        A CODE USED TO SPECIFY THE TYPE OF PLOT, WHERE IF
!        NPRT < 0 THE PLOT IS DECIBLES/LINEAR
!        NPRT = 0 THE PLOT IS SUPPRESSED
!        NPRT > 0 THE PLOT IS LOG/LINEAR
!     INTEGER NPTS
!        THE NUMBER OF X, Y CO-ORDINATES TO BE PLOTTED.
!     REAL PHI(LAGMAX)
!        THE ARRAY OF AUTOREGRESSIVE COEFFICIENTS FOR THE SELECTED
!        ORDER.
!     REAL PRHO(1)
!        A DUMMY VARIABLE.
!     REAL VAR
!        THE ONE STEP PREDICTION VARIANCE FOR ORDER IAR.
!     REAL XAXIS(LPCV)
!        THE X AXIS VALUES FOR THE SPECTRAL PLOT.
!     REAL XMN, XMX
!     REAL XPLTMN, XPLTMX
!        THE MINIMUM AND MAXIMUM VALUES TO BE PLOTTED FOR THE X AXIS.
!     REAL YAXIS(LPCV)
!        THE Y AXIS VALUES FOR THE SPECTRAL PLOT.
!     REAL YMN, YMX
!        *
!     REAL YPLTMN, YPLTMX
!        THE MINIMUM AND MAXIMUM VALUES TO BE PLOTTED FOR THE Y AXIS.
!
!     SET LOGICAL UNIT NUMBER FOR OUTPUT AND SET OUTPUT WIDTH.
!
      CALL IPRINT (IPRT)
!
      IF (.NOT. AICPRT) GO TO 5
!
!     PRINT AUTOREGRESSIVE MODEL ORDER SELECTION STATISTICS
!
      CALL VERSP(.TRUE.)
      WRITE(IPRT, 1007)
      CALL AOSLST (PRHO, AIC, FTEST, LAGMAX, LAIC, IAR, PHI, VAR,
     +   .FALSE., N)
      WRITE (IPRT, 1001)
!
    5 CONTINUE
      CALL VERSP(.TRUE.)
!
      WRITE(IPRT, 1002) LAG, BW, IDF
      WRITE(IPRT, 1000) IAR
      IF (ISPCER .EQ. 0) GO TO 10
      WRITE(IPRT, 1006)
      RETURN
!
   10 CONTINUE
      IF (NPRT.LE.0) THEN
        ILOG = 0
      ELSE
        ILOG = 1
      END IF
!
      CALL PPLMT(YAXIS, YAXIS, XAXIS, XAXIS, NPTS, 1, LPCV, YPLTMN,
     +  YPLTMX, YMN, YMX, XPLTMN, XPLTMX, XMN, XMX, ERROR, NMSUB,
     +  .FALSE.)
      IF (.NOT.ERROR)
     +   CALL PPMN (YAXIS, YAXIS, XAXIS, XAXIS, NPTS, 1, LPCV, 1, ISYM,
     +    LPCV, 0, -1, YMN, YMX, XMN, XMX, .FALSE., ILOG)
      IF (XPLTMN .NE. 0.0E0 .OR. XPLTMX .NE. 0.5E0) RETURN
      WRITE (IPRT, 1004)
!
      RETURN
!
!     FORMAT STATEMENTS
!
 1000 FORMAT (11H AND ORDER , I2, 28H AUTOREGRESSIVE SPECTRUM (.))
 1001 FORMAT ('1')
 1002 FORMAT (44H FOURIER SPECTRUM (+) (LAG WIND. TRUNC. PT.=, I5,
     +   1X, 5H/ BW=, F6.4, 1X, 6H/ EDF=, I6, ')')
 1004 FORMAT(5H+FREQ/
     +   7H PERIOD, 9X, 3HINF, 7X, 3H20., 7X, 3H10., 8X, 6H6.6667, 4X,
     +   2H5., 8X, 2H4., 8X, 6H3.3333, 4X, 6H2.8571, 4X, 3H2.5, 7X,
     +   6H2.2222, 4X, 2H2.)
!1005 FORMAT(5H+FREQ/
!    1   7H PERIOD, 9X, 3HINF, 7X, 3H10., 4X, 2H5., 8X, 6H3.3333, 4X,
!    2   3H2.5, 4X, 2H2.)
 1006 FORMAT(//
     +   56H ** THE PLOT HAS BEEN SUPRESSED BECAUSE FEWER THAN    **/
     +   56H ** FOUR VALID (POSITIVE) SPECTRAL ESTIMATES COULD BE **/
     +   56H ** COMPUTED.                                         **)
 1007 FORMAT (/42H AUTOREGRESSIVE ORDER SELECTION STATISTICS/)
      END
