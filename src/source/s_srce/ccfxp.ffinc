!CCFXP
      SUBROUTINE CCFXP (STORE, LAGMAX, M, CCOV, ICCOV, JCCOV, MISS,
     +   NLPPC,  INLPPC, JNLPPC, CMISS)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     ROUTINE TO LIST THE COMPUTED RESULTS FROM THE TIME SERIES
!     CROSS CORRELATION ROUTINES.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  DECEMBER 2, 1985
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP)
     +   CMISS
      INTEGER
     +   ICCOV,INLPPC,JCCOV,JNLPPC,LAGMAX,M
      LOGICAL
     +   MISS,STORE
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP)
     +   CCOV(ICCOV,JCCOV,*)
      INTEGER
     +   NLPPC(INLPPC,JNLPPC,*)
!
!  SCALARS IN COMMON
      INTEGER
     +   IERR
!
!  LOCAL SCALARS
      INTEGER
     +   I,IPRT,J,K,L,L1,LAG
!
!  LOCAL ARRAYS
      REAL(KIND=WP)
     +   CCF(16)
!
!  EXTERNAL FUNCTIONS
      LOGICAL
     +   MVCHK
      EXTERNAL MVCHK
!
!  EXTERNAL SUBROUTINES
      EXTERNAL IPRINT
!
!  INTRINSIC FUNCTIONS
      INTRINSIC SQRT
!
!  COMMON BLOCKS
      COMMON /ERRCHK/IERR
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL CCF(16)
!        AN ARRAY USED FOR PRINTING THE CCF.
!     REAL CCOV(ICCOV,JCCOV,M)
!        THE CROSS COVARIANCE ARRAY.
!     REAL CMISS
!        THE MISSING VALUE CODE FOR THE RETURNED CCVF ESTIMATES
!        (VECTOR CCOV).
!     INTEGER I
!        AN INDEXING VARIABLE.
!     INTEGER ICCOV
!        THE FIRST DIMENSION OF THE ARRAY CCOV.
!     INTEGER IERR
!        THE INTEGER VALUE RETURNED BY THIS ROUTINE DESIGNATING
!        WHETHER ANY ERRORS WERE DETECTED IN THE PARAMETER LIST
!        IF IERR .EQ. 0, NO ERRORS WERE DETECTED
!     INTEGER INLPPC
!        THE FIRST DIMENSION OF THE ARRAY NLPPC.
!     INTEGER IPRT
!        THE UNIT NUMBER FOR PRINTED OUTPUT.
!     INTEGER J
!        AN INDEXING VARIABLE.
!     INTEGER JCCOV, JNLPPC
!        THE SECOND DIMENSIONS OF THE ARRAYS CCOV AND NLPPC,
!        RESPECTIVELY.
!     INTEGER K
!        AN INDEXING VARIABLE.
!     INTEGER L1
!        AN INDEX VARIABLE.
!     INTEGER LAG
!        THE LAG VALUE AT WHICH THE DATA IS BEING PRINTED.
!     INTEGER LAGMAX
!        THE MAXIMUM LAG VALUE REQUESTED.
!     INTEGER M
!        THE NUMBER OF SERIES IN THE MULTIVARIATE TIME SERIES YM.
!     LOGICAL MISS
!        THE VALUE INDICATING WHETHER THE ANALYSIS INCLUDED MISSING
!        DATA (TRUE) OR NOT (FALSE).
!     INTEGER NLPPC(INLPPC,JNLPPC,M)
!        THE ARRAY CONTAINING THE NUMBER OF LAGGED PRODUCT PAIRS
!        USED TO COMPUTE EACH CCVF ESTIMATE.
!     LOGICAL STORE
!        THE VALUE INDICATING WHETHER THE RESULTS WERE RETURNED
!        TO THE USER (TRUE) OR NOT (FALSE).
!
      CALL IPRINT(IPRT)
!
!     PRINT IERR
!
      WRITE (IPRT, 1000) IERR
!
      IF (IERR.NE.0) RETURN
!
!     CHECK FOR STORED RESULTS
!
      IF (.NOT.STORE) RETURN
!
!     PRINT HEADING FOR CCVF
!
      WRITE (IPRT, 1010)
      WRITE (IPRT, 1040) ((J,K, K=1,M), J=1,M)
!
!     PRINT CROSS COVARIANCES
!
      LAG = 0
      WRITE (IPRT, 1060) LAG, ((CCOV(1,J,K), K=1,M), J=1,M)
      DO 10 LAG = 1, LAGMAX
         WRITE (IPRT, 1060) LAG, ((CCOV(LAG+1,J,K), K=1,M), J=1,M)
   10 CONTINUE
!
!     PRINT HEADING FOR CCF
!
      WRITE (IPRT, 1020)
      WRITE (IPRT, 1040) ((J,K, K=1,M), J=1,M)
!
!     PRINT CROSS CORRELATIONS
!
      LAG = 0
      I = 0
      DO 30 J = 1, M
         DO 20 K = 1, M
            I = I + 1
            CCF(I) = CCOV(1,J,K) / SQRT(CCOV(1,J,J)*CCOV(1,K,K))
   20    CONTINUE
   30 CONTINUE
      WRITE (IPRT, 1060) LAG, (CCF(L), L=1,I)
!
      DO 60 LAG = 1, LAGMAX
         I = 0
         DO 50 J = 1, M
            DO 40 K = 1, M
               I = I + 1
               IF (.NOT.MISS) GO TO 35
               CCF(I) = CMISS
               IF (MVCHK(CCOV(LAG+1,J,K),CMISS)) GO TO 40
   35          CCF(I) = CCOV(LAG+1,J,K) / SQRT(CCOV(1,J,J)*CCOV(1,K,K))
   40       CONTINUE
   50    CONTINUE
         WRITE (IPRT, 1060) LAG, (CCF(L1), L1=1,I)
   60 CONTINUE
!
!     CHECK FOR MISSING VALUES
!
      IF (.NOT.MISS) RETURN
!
!     PRINT HEADING FOR NUMBERS OF LAGGED PRODUCT PAIRS
!
      WRITE (IPRT, 1030)
      WRITE (IPRT, 1040) ((J,K, K=1,M), J=1,M)
!
!     PRINT NUMBERS OF LAGGED PRODUCT PAIRS FOR EACH CCVF
!
      LAG = 0
      WRITE (IPRT, 1070) LAG, ((NLPPC(1,J,K), K=1,M), J=1,M)
      DO 70 LAG = 1, LAGMAX
         WRITE (IPRT, 1070) LAG, ((NLPPC(LAG+1,J,K), K=1,M), J=1,M)
   70 CONTINUE
!
      RETURN
!
!     FORMAT STATEMENTS
!
 1000 FORMAT (//8H IERR = , I5)
 1010 FORMAT (// 6X, 6H  CCVF)
 1020 FORMAT (// 6X, 6H   CCF)
 1030 FORMAT (// 6X, 6H NLPPC)
 1040 FORMAT (1X, 3HLAG, 16(5X, I1, ',', I1))
 1060 FORMAT (1X, I3, 16F8.4)
 1070 FORMAT (1X, I3, 16I8)
      END
