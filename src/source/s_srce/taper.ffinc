!TAPER
      subroutine taper (y, n, taperp, yt)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS IS THE USER ROUTINE FOR APPLYING A SPLIT-COSINE-BELL
!     TAPER TO THE (CENTERED) OBSERVED SERIES Y, RETURNING THE TAPERED
!     SERIES IN YT.  THIS ROUTINE IS ADAPTED FROM BLOOMFIELDS
!     ROUTINE TAPER.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  DECEMBER 7, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     real(kind=wp) ::&
     &   taperp
     integer&
     &   n
!
!  ARRAY ARGUMENTS
     real(kind=wp) ::&
     &   y(*),yt(*)
!
!  SCALARS IN COMMON
     integer&
     &   ierr
!
!  LOCAL SCALARS
     real(kind=wp) ::&
     &   pi,weight
     integer&
     &   i,iprt,j,m
     logical&
     &   err01,head
!
!  LOCAL ARRAYS
     character&
     &   ln(8)*1,nmsub(6)*1
!
!  EXTERNAL SUBROUTINES
!      EXTERNAL CENTER,EISGE,GETPI,IPRINT
!
!  INTRINSIC FUNCTIONS
      intrinsic cos,int
!
!  COMMON BLOCKS
      common /errchk/ierr
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     LOGICAL ERR01
!        VALUE(S) INDICATING WHETHER AN ERROR WAS DETECTED (TRUE) OR NOT
!        (FALSE).
!     LOGICAL HEAD
!        A VARIABLE USED TO INDICARE WHETHER A HEADING IS NEEDED FOR
!        ERROR MESSAGES (TRUE) OR NOT (FALSE).
!     INTEGER I
!        AN INDEXING VARIABLE.
!     INTEGER IERR
!        THE INTEGER VALUE RETURNED BY THIS ROUTINE DESIGNATING
!        WHETHER ANY ERRORS WERE DETECTED IN THE PARAMETER LIST.
!        IF IERR .EQ. 0, NO ERRORS WERE DETECTED.
!        IF IERR .EQ. 1, ERRORS HAVE BEEN DETECTED.
!     INTEGER IPRT
!        THE LOGICAL UNIT NUMBER USED FOR OUTPUT.
!     INTEGER J
!        AN INDEXING VARIABLE.
!     CHARACTER*1 LN(8)
!        THE ARRAY(S) CONTAINING THE NAME(S) OF THE PARAMETER(S) CHECKED
!        FOR ERRORS.
!     INTEGER M
!        THE NUMBER OF POINTS AT EACH END OF THE SERIES TO BE
!        TAPERED.
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS IN THE SERIES Y.
!     CHARACTER*1 NMSUB(6)
!        THE ARRAY CONTAINING THE NAME OF THIS SUBROUTINE.
!     REAL PI
!        THE VALUE OF PI.
!     REAL TAPERP
!        THE TOTAL PERCENTAGE OF THE DATA TO BE TAPERED.
!     REAL WEIGHT
!        THE ITH TAPER WEIGHT.
!     REAL Y(N)
!        THE VECTOR CONTAINING THE OBSERVED TIME SERIES.
!     REAL YT(N)
!        THE VECTOR IN WHICH THE TAPERED SERIES IS RETURNED.
!
!     SET UP NAME ARRAYS
!
     data&
    &  nmsub(1),  nmsub(2),  nmsub(3),  nmsub(4),  nmsub(5),  nmsub(6)&
     & /     'T',       'A',       'P',       'E',       'R',       ' '/
     data&
    & ln(1), ln(2), ln(3), ln(4), ln(5), ln(6), ln(7), ln(8)&
     & /'N',' ',' ',' ',' ',' ',' ',' '/
!
!     SET UP FOR ERROR CHECKING
!
      ierr = 0
      head = .true.
!
!     CALL ERROR CHECKING ROUTINES
!
      call eisge(nmsub, ln, n, 17, 1, head, err01, ln)
      if (.not. err01) go to 5
!
      ierr = 1
      call iprint (iprt)
      write (iprt, 1000)
      return
!
    5 continue
!
      call center (y, n, yt)
!
      if ((taperp .le. 0.0e0) .or. (taperp .gt. 1.0e0)) return
!
      call getpi(pi)
!
      m = int(taperp * n + 0.5e0) / 2
      if (m .eq. 0) return
!
      do 20 i = 1, m
         weight = 0.5e0 - 0.5e0 * cos(pi * (i-0.5e0) / m)
         yt(i) = weight * yt(i)
         j = n + 1 - i
         yt(j) = weight * yt(j)
   20 continue
!
      return
!
!     FORMAT STATEMENTS
!
1000 format (/42h the correct form of the call statement is//&
     &   36h       call taper (y, n, taperp, yt))
      end
