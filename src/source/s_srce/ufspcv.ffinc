!UFSPCV
      SUBROUTINE UFSPCV (SPCF, SPCMN, SPCMX, FREQ, NF, XAXIS, YAXIS,
     &   ISYM, NPTS, ISPCF, LPCV, NSPC, BW, ALOW, AUP, XPLTMN, XPLTMX,
     &   YPLTMN, YPLTMX, NPRT)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE PRODUCES CO-ORDINATES FOR THE SPECTRUM PLOTS.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  DECEMBER 7, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) ::
     &   ALOW,AUP,BW,SPCMN,SPCMX,XPLTMN,XPLTMX,YPLTMN,YPLTMX
      INTEGER
     &   ISPCF,LPCV,NF,NPRT,NPTS,NSPC
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   FREQ(NF),SPCF(ISPCF),XAXIS(LPCV),YAXIS(LPCV)
      INTEGER
     &   ISYM(LPCV)
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     &   CILOW,CIMID,CIUP,YMAX
      INTEGER
     &   I,ISPCFW
!
!  EXTERNAL SUBROUTINES
      EXTERNAL SPPLTC,SPPLTD,SPPLTL
!
!  INTRINSIC FUNCTIONS
      INTRINSIC LOG10
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL ALOW, AUP
!        FACTORS USED TO COMPUTE THE CONFIDENCE INTERVALS.
!     REAL BW
!       THE BANDWIDTH.
!     REAL CILOW, CIMID, CIUP
!        THE Y AXIS VALUES FOR THE LOWER  MID AND UPPER CONFIDENCE
!        INTERVAL POINTS.
!     REAL FREQ(NF)
!        THE VECTOR OF FREQUENCIES AT WHICH THE SPECTRUM IS TO BE
!        COMPUTED.
!     INTEGER I
!        AN INDEX VARIABLE
!     INTEGER ISPCF
!         THE ACTUAL FIRST DIMENSION OF THE SPECTRUM ARRAYS.
!     INTEGER ISPCFW
!        AN INDEX VARIABLE.
!     INTEGER ISYM(LPCV)
!        THE ARRAY CONTAINING THE CODE FOR THE PLOT SYMBOLS.
!     INTEGER LPCV
!        THE LENGTH OF THE VECTORS USED FOR PLOTTING.
!     INTEGER NF
!        THE NUMBER OF FREQUENCIES AT WHICH THE SPECTRUM IS
!        TO BE COMPUTED.
!     INTEGER NPRT
!        A CODE USED TO SPECIFY THE TYPE OF PLOT, WHERE IF
!        NPRT < 0 THE PLOT IS DECIBLES/LINEAR
!        NPRT = 0 THE PLOT IS SUPPRESSED
!        NPRT > 0 THE PLOT IS LOG/LINEAR
!     INTEGER NPTS
!        THE NUMBER OF CO-ORDINATES TO BE PLOTTED.
!     INTEGER NSPC
!        THE NUMBER OF VALID SPECTRUM VALUES.
!     REAL SPCF(ISPCF)
!        THE ARRAYS IN WHICH THE SPECTRUM IS STORED
!        FOR EACH LAG WINDOW.
!     REAL SPCMN, SPCMX
!        THE MINIMUM AND MAXIMUM SPECTRUM VALUE TO BE PLOTTED.
!     REAL XAXIS(LPCV)
!        THE X AXIS VALUES FOR THE SPECTRUM PLOT.
!     REAL XPLTMN, XPLTMX
!        THE MINIMUM AND MAXIMUM VALUES TO BE PLOTTED FOR THE X AXIS.
!     REAL YAXIS(LPCV)
!        THE Y AXIS VALUES FOR THE SPECTRUM PLOTS.
!     REAL YMAX
!        THE MAXIMUM ACTUAL SPECTRUM VALUE (IN DECIBELS) TO BE PLOTTED.
!     REAL YPLTMN, YPLTMX
!        THE MINIMUM AND MAXIMUM VAUES TO BE PLOTTED FOR THE Y AXIS.
!
!
      ISPCFW = 0
!
      IF (NPRT .GE. 1) GO TO 30
!
!     SET VARIOUS Y AXIS VALUES FOR DECIBLE PLOTS
!
      CALL SPPLTD (SPCMN, SPCMX, ALOW, AUP, YPLTMN, YPLTMX,
     &   CILOW, CIMID, CIUP, YMAX)
!
!     SET CO-ORDINATES FOR DECIBLE PLOTS
!
      DO 10 I = 1, NF
         IF (SPCF(I) .LT. SPCMN) GO TO 10
         ISPCFW = ISPCFW + 1
         XAXIS(ISPCFW) = FREQ(I)
         YAXIS(ISPCFW) = 10.0E0 * LOG10(SPCF(I)) - YMAX
         ISYM(ISPCFW) = 1
   10 CONTINUE
!
      GO TO 70
!
   30 CONTINUE
!
!     SET VARIOUS Y AXIS VALUES FOR LOG PLOTS
!
      CALL SPPLTL (SPCMN, SPCMX, ALOW, AUP, YPLTMN, YPLTMX,
     &   CILOW, CIMID, CIUP)
!
!     SET CO-ORDINATES FOR LOG PLOTS
!
      DO 50 I = 1, NF
         IF (SPCF(I) .LT. SPCMN) GO TO 50
         ISPCFW = ISPCFW + 1
         XAXIS(ISPCFW) = FREQ(I)
         YAXIS(ISPCFW) = SPCF(I)
         ISYM(ISPCFW) = 1
   50 CONTINUE
!
   70 CONTINUE
!
      NPTS = NSPC
!
!     SET CO-ORDINATES FOR BANDWIDTH AND CONFIDENCE INTERVAL.
!
      CALL SPPLTC (XAXIS, YAXIS, ISYM, NPTS, XPLTMN, XPLTMX, BW, CILOW,
     &   CIMID, CIUP, LPCV)
!
      RETURN
      END
