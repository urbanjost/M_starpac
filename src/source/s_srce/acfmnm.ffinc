!ACFMNM
     subroutine acfmnm(y, ymiss, n, lagmax, rho, sdrho, nlppa, ymean,&
    &   prho, aic, ftest, phi, iar, ospvar, acov, lacov, laic, chia,&
     &   chiap, laglst, work, nprt)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS IS THE MAIN SUBROUTINE FOR COMPUTING AUTOCORRELATIONS AND
!     PARTIAL AUTOCORRELATIONS OF A TIME SERIES WITH MISSING DATA.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 21, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      real(kind=wp) :: chia,chiap,ospvar,ymean,ymiss
     integer&
     &   iar,lacov,laglst,lagmax,laic,n,nprt
!
!  ARRAY ARGUMENTS
     real(kind=wp) ::&
    &   acov(*),aic(*),ftest(2,*),phi(*),prho(*),rho(*),sdrho(*),&
     &   work(*),y(*)
     integer&
     &   nlppa(*)
!
!  LOCAL SCALARS
     real(kind=wp) ::&
     &   fplm
     integer&
     &   i,nused
!
!  EXTERNAL FUNCTIONS
     real(kind=wp) ::&
     &   r1mach
!       EXTERNAL R1MACH
!
!  EXTERNAL SUBROUTINES
!       EXTERNAL ACFSDM,ACVFM,AOS,CHIRHO
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL(KIND=WP) :: ACOV(LACOV)
!        THE AUTOCOVARIANCE FUNCTION ESTIMATE VECTOR.
!     REAL(KIND=WP) :: AIC(LAIC)
!        THE ARRAY CONTAINING AKAIAES CRITERION FOR EACH ORDER.
!     REAL(KIND=WP) :: CHIA, CHIAP
!        THE VARIABLES IN WHICH THE CHI SQUARE STATISTIC AND
!        CHI SQUARED STATISTIC PROBABILITY FOR THE AUTOCORRELATIONS
!        ARE STORED.
!     REAL(KIND=WP) :: FPLM
!        THE FLOATING POINT LARGEST MAGNITUDE.
!     REAL(KIND=WP) :: FTEST(2, LAGMAX)
!        THE ARRAY IN WHICH THE F RATIO AND PROBABILITY ARE STORED.
!     INTEGER I
!        AN INDEX VARIABLE.
!     INTEGER IAR
!        THE ORDER OF THE AUTOREGRESSIVE PROCESS CHOSEN.
!     INTEGER LACOV
!        THE LENGTH OF THE VECTOR ACOV.
!     INTEGER LAGLST
!        THE LAST LAG BEFORE MISSING DATA CAUSED THE ACVF OF THE
!        SERIES NOT TO BE COMPUTED.
!     INTEGER LAGMAX
!        THE MAXIMUM LAG VALUE TO BE USED.
!     INTEGER N
!        THE INTEGER NUMBER OF OBSERVATIONS IN THE SERIES
!     INTEGER LAIC
!        THE LENGTH OF THE VECTOR AIC.
!     INTEGER NLPPA(LACOV)
!        THE ARRAY CONTAINING THE NUMBERS OF LAGGED PRODUCT PAIRS
!        USED TO COMPUTE THE ACVF AT EACH LAG.
!     INTEGER NPRT
!        THE INDICATOR VARIABLE USED TO SPECIFY WHETHER OR NOT
!        PRINTED OUTPUT IS TO BE GIVEN, WHERE IF THE VALUE OF
!        NPRT IS ZERO, NO OUTPUT IS MADE.
!     INTEGER NUSED
!        THE NUMBER OF ACTIVE (NOT MISSING) OBSERVATIONS IN THE SERIES.
!     REAL(KIND=WP) :: OSPVAR
!        THE ONE STEP PREDICTION VARIANCE FOR THE SELECTED ORDER (IAR).
!     REAL(KIND=WP) :: PHI(LAGMAX)
!        THE ARRAY OF AUTOREGRESSIVE COEFFICIENTS FOR THE SELECTED
!        ORDER (IAR).
!     REAL(KIND=WP) :: PRHO(LAGMAX)
!        THE ARRAY IN WHICH THE PARTIAL AUTOCORRELATIONS ARE STORED
!     REAL(KIND=WP) :: RHO(LAGMAX)
!        THE ARRAY IN WHICH THE AUTOCORRELATIONS ARE STORED
!     REAL(KIND=WP) :: SDRHO(LAGMAX)
!        THE ARRAY IN WHICH THE STANDARD ERRORS OF THE AUTOCORRELATIONS
!        ARE STORED
!     REAL(KIND=WP) :: WORK(LAGMAX)
!        AN ARRAY USED IN THE COMPUTATIONS OF THE PARTIAL
!        AUTOCORRELATIONS COEFFICIENTS.
!     REAL(KIND=WP) :: Y(N)
!        THE VECTOR CONTAINING THE OBSERVED SERIES
!     REAL(KIND=WP) :: YMEAN
!        THE MEAN OF THE OBSERVED TIME SERIES
!     REAL(KIND=WP) :: YMISS
!        THE USER SUPPLIED CODE WHICH IS USED TO DETERMINE WHETHER
!        OR NOT AN OBSERVATION IN THE SERIES IS MISSING.
!        IF Y(I) .EQ. YMISS, THE VALUE IS ASSUMED MISSING.
!        IF Y(I) .NE. YMISS, THE VALUE IS ASSUMED NOT MISSING.
!
!
      fplm = r1mach(2)
!
!     COMPUTE AUTOCOVARIANCES OF THE SERIES WITH MISSING DATA.
!
      call acvfm(y, ymiss, n, ymean, acov, lagmax, laglst, nlppa, lacov)
!
      if (nlppa(1) .eq. 0 .or. acov(1) .eq. 0.0_wp) return
!
      if (nprt .eq. 0) return
!
!     COMPUTE PARTIAL AUTOCORRELATIONS AND THE AUTOREGRESSIVE MODEL
!     ORDER SELECTION STATISTICS IF THERE WERE NO MISSING DATA.
!
     if (nlppa(1) .eq. n) call aos (n, lagmax, acov, prho, iar,&
     &   ospvar, phi, work, aic, ftest, lacov, laic)
!
!     COMPUTE AUTOCORRELATIONS.
!
      do 10 i = 1, lagmax
         if (nlppa(i+1) .ge. 1) rho(i) = acov(i+1) / acov(1)
   10 continue
!
!     PRESET SDRHO VALUES FOR PRINTING ROUTINE
!
      do 20 i = laglst, lagmax
         sdrho(i) = fplm
   20 continue
!
!     COMPUTE STANDARD ERROR OF AUTOCORRELATIONS.
!
      call acfsdm (rho, sdrho, laglst, n, nlppa(2))
!
      if (laglst .eq. 0) return
!
!     COMPUTE CHI STATISTIC BASED ON AUTOCORRELATION VALUES
!
      nused = nlppa(1)
!
      call chirho (rho, nused, laglst, chia, chiap)
!
      return
      end
