!ACFMNM
      SUBROUTINE ACFMNM(Y, YMISS, N, LAGMAX, RHO, SDRHO, NLPPA, YMEAN,
     +   PRHO, AIC, FTEST, PHI, IAR, OSPVAR, ACOV, LACOV, LAIC, CHIA,
     +   CHIAP, LAGLST, WORK, NPRT)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS IS THE MAIN SUBROUTINE FOR COMPUTING AUTOCORRELATIONS AND
!     PARTIAL AUTOCORRELATIONS OF A TIME SERIES WITH MISSING DATA.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 21, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP)
     +   CHIA,CHIAP,OSPVAR,YMEAN,YMISS
      INTEGER
     +   IAR,LACOV,LAGLST,LAGMAX,LAIC,N,NPRT
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP)
     +   ACOV(*),AIC(*),FTEST(2,*),PHI(*),PRHO(*),RHO(*),SDRHO(*),
     +   WORK(*),Y(*)
      INTEGER
     +   NLPPA(*)
!
!  LOCAL SCALARS
      REAL(KIND=WP)
     +   FPLM
      INTEGER
     +   I,NUSED
!
!  EXTERNAL FUNCTIONS
      REAL(KIND=WP)
     +   R1MACH
      EXTERNAL R1MACH
!
!  EXTERNAL SUBROUTINES
      EXTERNAL ACFSDM,ACVFM,AOS,CHIRHO
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL ACOV(LACOV)
!        THE AUTOCOVARIANCE FUNCTION ESTIMATE VECTOR.
!     REAL AIC(LAIC)
!        THE ARRAY CONTAINING AKAIAES CRITERION FOR EACH ORDER.
!     REAL CHIA, CHIAP
!        THE VARIABLES IN WHICH THE CHI SQUARE STATISTIC AND
!        CHI SQUARED STATISTIC PROBABILITY FOR THE AUTOCORRELATIONS
!        ARE STORED.
!     REAL FPLM
!        THE FLOATING POINT LARGEST MAGNITUDE.
!     REAL FTEST(2, LAGMAX)
!        THE ARRAY IN WHICH THE F RATIO AND PROBABILITY ARE STORED.
!     INTEGER I
!        AN INDEX VARIABLE.
!     INTEGER IAR
!        THE ORDER OF THE AUTOREGRESSIVE PROCESS CHOSEN.
!     INTEGER LACOV
!        THE LENGTH OF THE VECTOR ACOV.
!     INTEGER LAGLST
!        THE LAST LAG BEFORE MISSING DATA CAUSED THE ACVF OF THE
!        SERIES NOT TO BE COMPUTED.
!     INTEGER LAGMAX
!        THE MAXIMUM LAG VALUE TO BE USED.
!     INTEGER N
!        THE INTEGER NUMBER OF OBSERVATIONS IN THE SERIES
!     INTEGER LAIC
!        THE LENGTH OF THE VECTOR AIC.
!     INTEGER NLPPA(LACOV)
!        THE ARRAY CONTAINING THE NUMBERS OF LAGGED PRODUCT PAIRS
!        USED TO COMPUTE THE ACVF AT EACH LAG.
!     INTEGER NPRT
!        THE INDICATOR VARIABLE USED TO SPECIFY WHETHER OR NOT
!        PRINTED OUTPUT IS TO BE GIVEN, WHERE IF THE VALUE OF
!        NPRT IS ZERO, NO OUTPUT IS MADE.
!     INTEGER NUSED
!        THE NUMBER OF ACTIVE (NOT MISSING) OBSERVATIONS IN THE SERIES.
!     REAL OSPVAR
!        THE ONE STEP PREDICTION VARIANCE FOR THE SELECTED ORDER (IAR).
!     REAL PHI(LAGMAX)
!        THE ARRAY OF AUTOREGRESSIVE COEFFICIENTS FOR THE SELECTED
!        ORDER (IAR).
!     REAL PRHO(LAGMAX)
!        THE ARRAY IN WHICH THE PARTIAL AUTOCORRELATIONS ARE STORED
!     REAL RHO(LAGMAX)
!        THE ARRAY IN WHICH THE AUTOCORRELATIONS ARE STORED
!     REAL SDRHO(LAGMAX)
!        THE ARRAY IN WHICH THE STANDARD ERRORS OF THE AUTOCORRELATIONS
!        ARE STORED
!     REAL WORK(LAGMAX)
!        AN ARRAY USED IN THE COMPUTATIONS OF THE PARTIAL
!        AUTOCORRELATIONS COEFFICIENTS.
!     REAL Y(N)
!        THE VECTOR CONTAINING THE OBSERVED SERIES
!     REAL YMEAN
!        THE MEAN OF THE OBSERVED TIME SERIES
!     REAL YMISS
!        THE USER SUPPLIED CODE WHICH IS USED TO DETERMINE WHETHER
!        OR NOT AN OBSERVATION IN THE SERIES IS MISSING.
!        IF Y(I) .EQ. YMISS, THE VALUE IS ASSUMED MISSING.
!        IF Y(I) .NE. YMISS, THE VALUE IS ASSUMED NOT MISSING.
!
!
      FPLM = R1MACH(2)
!
!     COMPUTE AUTOCOVARIANCES OF THE SERIES WITH MISSING DATA.
!
      CALL ACVFM(Y, YMISS, N, YMEAN, ACOV, LAGMAX, LAGLST, NLPPA, LACOV)
!
      IF (NLPPA(1) .EQ. 0 .OR. ACOV(1) .EQ. 0.0E0) RETURN
!
      IF (NPRT .EQ. 0) RETURN
!
!     COMPUTE PARTIAL AUTOCORRELATIONS AND THE AUTOREGRESSIVE MODEL
!     ORDER SELECTION STATISTICS IF THERE WERE NO MISSING DATA.
!
      IF (NLPPA(1) .EQ. N) CALL AOS (N, LAGMAX, ACOV, PRHO, IAR,
     +   OSPVAR, PHI, WORK, AIC, FTEST, LACOV, LAIC)
!
!     COMPUTE AUTOCORRELATIONS.
!
      DO 10 I = 1, LAGMAX
         IF (NLPPA(I+1) .GE. 1) RHO(I) = ACOV(I+1) / ACOV(1)
   10 CONTINUE
!
!     PRESET SDRHO VALUES FOR PRINTING ROUTINE
!
      DO 20 I = LAGLST, LAGMAX
         SDRHO(I) = FPLM
   20 CONTINUE
!
!     COMPUTE STANDARD ERROR OF AUTOCORRELATIONS.
!
      CALL ACFSDM (RHO, SDRHO, LAGLST, N, NLPPA(2))
!
      IF (LAGLST .EQ. 0) RETURN
!
!     COMPUTE CHI STATISTIC BASED ON AUTOCORRELATION VALUES
!
      NUSED = NLPPA(1)
!
      CALL CHIRHO (RHO, NUSED, LAGLST, CHIA, CHIAP)
!
      RETURN
      END
