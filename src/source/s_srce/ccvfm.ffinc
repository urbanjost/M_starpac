!CCVFM
      SUBROUTINE CCVFM(Y1, Y1MISS, Y2, Y2MISS, N, NC, Y1MEAN, Y2MEAN,
     +   CCOV12, CCOV21, ICCOV, NLPP12, NLPP21)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE COMPUTES THE CROSS COVARIANCE FUNCTION
!     BETWEEN TWO SERIES WHEN MISSING DATA ARE INVOLVED.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 21, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP)
     +   Y1MEAN,Y1MISS,Y2MEAN,Y2MISS
      INTEGER
     +   ICCOV,N,NC
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP)
     +   CCOV12(ICCOV),CCOV21(ICCOV),Y1(N),Y2(N)
      INTEGER
     +   NLPP12(ICCOV),NLPP21(ICCOV)
!
!  LOCAL SCALARS
      REAL(KIND=WP)
     +   DOTXY,FPLM
      INTEGER
     +   LAG,NDOTXY
!
!  EXTERNAL FUNCTIONS
      REAL(KIND=WP)
     +   R1MACH
      EXTERNAL R1MACH
!
!  EXTERNAL SUBROUTINES
      EXTERNAL DOTCM
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL CCOV12(ICCOV), CCOV21(ICCOV)
!        THE ARRAYS IN WHICH THE CCVF FOR SERIES 1 LAGGED
!        BEHIND SERIES 2 AND VISA VERSA, RESPECTIVELY, ARE
!        STORED.
!     REAL DOTXY
!        VARIOUS CROSS PRUDUCTS BETWEEN SERIES Y1 AND Y2.
!     REAL FPLM
!        THE FLOATING POINT LARGEST MAGNITUDE.
!     INTEGER ICCOV
!        THE ROW DIMENSION OF THE ARRAYS CCOV12 AND CCOV21.
!     INTEGER LAG
!        THE INDEXING VARIABLE INDICATING THE LAG VALUE OF THE
!        AUTOCORRELATION BEING COMPUTED.
!     INTEGER N
!        THE INTEGER NUMBER OF OBSERVATIONS IN THE SERIES
!     INTEGER NC
!        THE NUMBER OF CROSS CORRELATIONS DESIRED.
!     INTEGER NLPP12(ICCOV), NLPP21(ICCOV)
!        THE NUMBER OF LAGGED PRODUCT PAIRS USED TO COMPUTE THE CCVF
!        FOR EACH PAIR OF SERIES AT EACH LAG.
!     INTEGER NDOTXY
!        THE NUMBER OF OBSERVATIONS USED TO COMPUTE DOTXY.
!     REAL Y1(N), Y1MEAN, Y1MISS
!        THE FIRST SERIES, AND ITS MEAN, AND MISSING VALUE CODE.
!     REAL Y2(N), Y2MEAN, Y2MISS
!        THE SECOND SERIES, AND ITS MEAN, AND MISSING VALUE CODE.
!
      FPLM = R1MACH(2)
!
!     COMPUTE THE CROSS COVARIANCES
!
      CALL DOTCM (Y1, Y1MEAN, Y1MISS, N, Y2, Y2MEAN, Y2MISS, N, DOTXY,
     +   NDOTXY)
!
      NLPP12(1) = NDOTXY
      CCOV12(1) = FPLM
      IF (NDOTXY .GE. 1) CCOV12(1) = DOTXY / NDOTXY
!
      CCOV21(1) = CCOV12(1)
      NLPP21(1) = NDOTXY
!
      DO 10 LAG = 1, NC
!
         CALL DOTCM (Y1, Y1MEAN, Y1MISS, N, Y2(LAG+1), Y2MEAN, Y2MISS,
     +       N-LAG, DOTXY, NDOTXY)
!
         NLPP12(LAG+1) = NDOTXY
         CCOV12(LAG+1) = FPLM
         IF (NDOTXY .GE. 1)
     +      CCOV12(LAG+1) = DOTXY * (N-LAG) / (N*NDOTXY)
!
         CALL DOTCM (Y2, Y2MEAN, Y2MISS, N, Y1(LAG+1), Y1MEAN, Y1MISS,
     +      N-LAG, DOTXY, NDOTXY)
!
         NLPP21(LAG+1) = NDOTXY
         CCOV21(LAG+1) = FPLM
         IF (NDOTXY .GE. 1)
     +      CCOV21(LAG+1) = DOTXY * (N-LAG) / (N*NDOTXY)
!
   10 CONTINUE
!
      RETURN
      END
