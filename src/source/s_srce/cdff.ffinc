!CDFF
      real(kind=wp) function cdff(f, df1, df2)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE COMPUTES THE CUMULATIVE DISTRIBUTION FUNCTION FOR
!     THE F DISTRIBUTION.  THE APPROXIMATION USED DEPENDS ON THE
!     DEGREES OF FREEDOM IN THE NUMERATOR AND DENOMINATOR.
!     1.  IF BOTH DEGREES OF FREEDOM ARE SMALL (LESS THAN 4000),
!         THE CDF IS COMPUTED FROM THE INCOMPLETE BETA FUNCTION
!         USING EQUATION 5.45 OF STATISTICAL COMPUTING, BY KENNEDY AND
!         GENTLE.
!     2.  IF ONE OF THE DEGREES OF FREEDOM IS LARGE (GREATER THAN OR
!         EQUAL TO 4000) AND THE OTHER IS MODERATELY LARGE (GREATER THAN
!         OR EQUAL TO 100), THE CDF IS APPROXIMATED BY A NORMAL DISTRIB-
!         BUTION AS SHOWN IN EQUATION 20 ON PAGE 83 OF DISTRIBUTIONS IN
!         STATISTICS - CONTINUOUS UNIVARIATE DISTRIBUTIONS - 2, BY
!         JOHNSON AND KOTZ.
!     3.  IF ONE OF THE DEGREES OF FREEDOM IS SMALL (LESS THAN 100) AND
!         THE OTHER IS LARGE (EXCEEDING 4000), THE CDF IS APPROXIMATED
!         BY A CHI SQUARED DISTRIBUTION AS SHOWN IN THE THIRD EQUATION
!         ON PAGE 84 OF DISTRIBUTIONS IN STATISTICS - CONTINUOUS
!         UNIVARIATE DISTRIBUTIONS - 2, BY JOHNSON AND KOTZ.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE - DECEMBER 7, 1981
!                     THIS ROUTINE IS MODELED AFTER DATAPAC ROUTINE FCDF.
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     real(kind=wp) ::&
     &   df1,df2,f
!
!  LOCAL SCALARS
     real(kind=wp) ::&
     &   ftrans
!
!  EXTERNAL FUNCTIONS
     real(kind=wp) ::&
     &    betai,cdfchi,cdfnml
!       EXTERNAL  BETAI,CDFCHI,CDFNML
!
!  INTRINSIC FUNCTIONS
      intrinsic sqrt
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL DF1
!        THE DEGREES OF FREEDOM IN THE NUMERATOR.
!     REAL DF2
!        THE DEGREES OF FREEDOM IN THE DENOMINATOR.
!     REAL F
!        THE PERCENTAGE POINT FROM THE F DISTRIBUTION.
!     REAL FTRANS
!        TRANSFORMS OF THE F PERCENTAGE POINT, USED FOR THE VARIOUS
!        APPROXIMATING DISTRIBUTIONS.
!
!     COMMENCE BODY OF ROUTINE
!
      if (f.gt.0.0e0) go to 5
      cdff = 0.0e0
      return
!
    5 continue
!
      if ((df1.gt.4000.0e0) .or. (df2.gt.4000.0e0)) go to 10
!
!     BOTH DEGREES OF FREEDOM ARE LESS THAN OR EQUAL TO 4000.  USE
!     THE INCOMPLETE BETA FUNCTION TO COMPUTE THE F CDF.
!
      ftrans = df2 / (df2 + df1 * f)
!
      cdff = 1.0e0 -  betai(ftrans, 0.5e0*df2, 0.5e0*df1)
      return
!
   10 if ((df1.le.100.0e0) .or. (df2.le.100.0e0)) go to 20
!
!     BOTH DEGREES OF FREEDOM EXCEED 4000.  USE THE NORMAL APPROXIMATION
!     TO COMPUTE THE F CDF.
!
     ftrans =&
    &   (((1.0e0-(2.0e0/(9.0e0*df2)))*&
    &     (f**(1.0e0/3.0e0)))-(1.0e0-(2.0e0/(9.0e0*df1)))) /&
     &   sqrt(((f**(2.0e0/3.0e0))/(4.5e0*df2))+(2.0e0/(9.0e0*df1)))
!
      cdff = cdfnml(ftrans)
      return
!
   20 if (df1.gt.100.0e0) go to 30
!
!     THE DEGREES OF FREEDOM IN THE DENOMINATOR EXCEEDS 4000 AND THE
!     DEGREES OF FREEDOM IN NUMERATOR IS LESS THAN OR EQUAL TO 100.
!     USE THE CHI SQUARE APPROXIMATION TO COMPUTE THE F CDF.
!
     ftrans =&
    &   (df1 + (df1/df2)*(0.5e0*df1-1.0e0))/&
     &   ((1.0e0/f)+(df1/df2)*0.5e0)
!
      cdff = cdfchi(ftrans, df1)
!
      return
!
   30 continue
!
!     THE DEGREES OF FREEDOM IN THE NUMERATOR EXCEEDS 4000 AND THE
!     DEGREES OF FREEDOM IN DENOMINATOR IS LESS THAN OR EQUAL TO 100.
!     USE THE CHI SQUARE APPROXIMATION TO COMPUTE THE F CDF.
!
     ftrans =&
     &   (df2+(df2/df1)*(0.5e0*df2-1.0e0))/(f+(df2/df1)*0.5e0)
!
      cdff = 1.0e0 - cdfchi(ftrans, df2)
!
      return
!
      end
