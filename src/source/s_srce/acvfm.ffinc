!ACVFM
      SUBROUTINE ACVFM (Y, YMISS, N, YMEAN, ACOV, LAGMAX,
     &   LAGLST, NLPPA, LACOV)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE COMPUTES THE AUTOCOVARIANCES WHEN MISSING DATA ARE
!     INVOLVED.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 21, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) :: YMEAN,YMISS
      INTEGER
     &   LACOV,LAGLST,LAGMAX,N
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   ACOV(*),Y(*)
      INTEGER
     &   NLPPA(*)
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     &   DOTXY,DOTYY,FPLM
      INTEGER
     &   LAG,NDOTXY,NDOTYY,NUSED
!
!  EXTERNAL FUNCTIONS
      REAL(KIND=WP) ::
     &   R1MACH
      INTEGER
     &   LSTLAG
      EXTERNAL R1MACH,LSTLAG
!
!  EXTERNAL SUBROUTINES
      EXTERNAL AMEANM,DOTCM
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL ACOV(LACOV)
!        THE ARRAY IN WHICH THE AUTOCOVARIANCES ARE STORED
!     REAL DOTXY, DOTYY
!        THE DOT PRODUCT BETWEEN VECTORS (Y(I) - YMEAN)) AND
!        (Y(LAG) - YMEAN)), AND (Y(I) - YMEAN)) AND (Y(I) - YMEAN)),
!        RESPECTIVELY.
!     REAL FPLM
!        THE FLOATING POINT LARGEST MAGNITUDE.
!     INTEGER LACOV
!        THE LENGTH OF THE VECTOR ACOV.
!     INTEGER LAG, LAGLST, LAGMAX
!        THE INDEXING VARIABLE INDICATING THE LAG VALUE OF THE
!        AUTOCORRELATION BEING COMPUTED, THE NUMBER OF AUTOCORRELATIONS
!        COMPUTED BEFORE A MISSING AUTOCORRELATION, AND THE NUMBER OF
!        AUTOCORRELATIONS DESIRED, RESPECTIVELY.
!     INTEGER N
!        THE INTEGER NUMBER OF OBSERVATIONS IN THE SERIES
!     INTEGER NDOTXY, NDOTYY
!        THE NUMBER OF OBSERVATIONS USED TO COMPUTE DOTXY AND
!        DOTYY, RESPECTIVELY.
!     INTEGER NLPPA(LACOV)
!        THE ARRAY CONTAINING THE NUMBERS OF LAGGED PRODUCT PAIRS
!        USED TO COMPUTE THE ACVF AT EACH LAG.
!     INTEGER NUSED
!        THE NUMBER OF ACTIVE OBSERVATIONS IN THE SERIES.
!     REAL Y(N)
!        THE VECTOR CONTAINING THE OBSERVED SERIES
!     REAL YMEAN
!        THE MEAN OF THE OBSERVED TIME SERIES
!     REAL YMISS
!        THE USER SUPPLIED CODE WHICH IS USED TO DETERMINE WHETHER OR
!        NOT AN OBSERVATION IN THE SERIES IS MISSING.  IF Y(I) = YMISS,
!        THE VALUE IS ASSUMED MISSING, OTHERWISE IT IS NOT.
!
!
      FPLM = R1MACH(2)
!
!     COMPUTE ARITHMETIC MEAN, WITH MISSING VALUES TAKEN INTO ACCOUNT
!
      CALL AMEANM (Y, YMISS, N, NUSED, YMEAN)
!
!     COMPUTE THE VARIANCE OF THE SERIES Y
!
      CALL DOTCM (Y, YMEAN, YMISS, N, Y, YMEAN, YMISS, N,
     &   DOTYY, NDOTYY)
      NLPPA(1) = NDOTYY
      IF (NLPPA(1).EQ.0) THEN
         LAGLST = 0
      ELSE
         ACOV(1) = DOTYY / NDOTYY
!
!     COMPUTE AUTOCORRELATIONS, WITH MISSING VALUES TAKEN INTO ACCOUNT
!
         DO 10 LAG = 1, LAGMAX
            CALL DOTCM (Y, YMEAN, YMISS, N, Y(LAG+1), YMEAN,
     &         YMISS, N - LAG, DOTXY, NDOTXY)
            NLPPA(LAG + 1) = NDOTXY
            ACOV(LAG + 1) = FPLM
            IF (NLPPA(LAG + 1) .LE. 0) GO TO 10
            ACOV(LAG + 1) = DOTXY * (N-LAG) / (NLPPA(LAG + 1) * N)
   10    CONTINUE
!
!     FIND THE LAST AUTOCORRELATION TO BE COMPUTED BEFORE
!     ONE COULD NOT BE COMPUTED DUE TO MISSING DATA
!
         LAGLST = LSTLAG(NLPPA, LAGMAX, LACOV)
      END IF
      RETURN
      END
