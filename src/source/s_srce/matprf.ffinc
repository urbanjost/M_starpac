!MATPRF
      SUBROUTINE MATPRF(X, Y, NC, MODE, CODE, LENGTH, MASK, LMASK)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THISROUTINE PRINTS A SQUARE MATRIX STORED IN SYMMETRIC
!     FORM.
!
!     WRITTEN BY - JOHN E. KOONTZ
!        STATISTICAL ENGINEERING DIVISION
!        NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  OCTOBER 3, 1983
!        BASED ON THE JULY 1982 VERSION OF MATPRT, BY LINDA L. MITCHELL.
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      INTEGER
     &   CODE,LENGTH,LMASK,MODE,NC
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   X(LENGTH),Y(LENGTH)
      INTEGER
     &   MASK(LMASK)
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     &   SQXII,SQYII
      INTEGER
     &   I,I0,II,IK,IMASK,IPRT,J,JMASK,K,KI,KK,KM,KMAX,KN,L,NF,
     &   NLINE
!
!  LOCAL ARRAYS
      REAL(KIND=WP) ::
     &   XLINE(10),YLINE(10)
      INTEGER
     &   INDW(10)
!
!  EXTERNAL FUNCTIONS
!      INTEGER
!     +   INPERL
!      EXTERNAL INPERL
!
!  EXTERNAL SUBROUTINES
!      EXTERNAL IPRINT
!
!  INTRINSIC FUNCTIONS
      INTRINSIC MIN,SQRT
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     INTEGER CODE
!        IF 1 -SINGLE PRINTED, X ONLY (Y IS DUMMY ARG)
!           2 -DOUBLE PRINTED LINE, BOTH X AND Y
!     INTEGER I
!        ROW NUMBER
!     INTEGER II
!        THE INDEX OF THE (I,I)TH ELEMENT OF THE VCV MATRIX
!     INTEGER IK
!        THE INDEX OF THE (I,K)TH ELEMENT OF THE VCV MATRIX
!     INTEGER I0
!        THE INDEX OF THE ((I,I)-1)TH ELEMENT OF THE VCV MATRIX
!     INTEGER IMASK
!        INDEX IN MASK FOR LABELLING OF THE ROW DIMENSION.
!     INTEGER INDW(10)
!        A WORK VECTOR FOR THE INDICES TO BE PRINTED FOR THE
!        MATRIX.
!     INTEGER IPRT
!        THE OUTPUT UNIT NUMBER
!     INTEGER J
!        FIRST COLUMN IN THE SET TO BE PRINTED
!     INTEGER JMASK
!        INDEX IN MASK FOR LABELLING OF THE COLUMN DIMENSION.
!     INTEGER K
!        COLUMN NUMBER IN THE POSSIBLE SET OF NF
!     INTEGER KI
!        THE INDEX OF THE (K,I)TH ELEMENT OF THE VCV MATRIX
!     INTEGER KK
!        THE INDEX OF THE (K,K)TH ELEMENT OF THE VCV MATRIX
!     INTEGER KM
!        LAST COLUMN IN THE SET
!        LIMITED TO VALUES OF J-1 PLUS A NUMBER BETWEEN 1 AND
!        NF (INCLUSIVE)
!     INTEGER KMAX
!        INDEX IN INDW OF THE LARGEST INDEX TO BE PRINTED FOR
!        MATRIX.
!     INTEGER KN
!        LAST COLUMN TO PRINT WHEN PRINTING LOWER TRIANGLE
!     INTEGER L
!        FIRST ROW TO PRINT FOR THIS SET
!     INTEGER LMASK
!        LENGTH OF MASK.
!     INTEGER LENGTH
!        LENGTH OF X AND Y
!     INTEGER MASK(LMASK)
!        MASK VECTOR FOR VCV.  THE INDEX OF THE ITH ELEMENT OF
!        MASK EQUAL TO ZERO IS THE LABEL IN THE OUTPUT OF VCV
!        IN OF THE ITH ROW AND ITH COLUMN.
!     INTEGER MODE
!        IF 0, LOWER TRIANGULAR PART PRINTED
!           1, LOWER TRIANGULAR PART IS PRINTED WITH
!              SQUARE ROOTS OF THE DIAGONAL
!           2, LOWER TRIANGLE PRINTED AS CORRELATION MATRIX
!              WITH SQUARE ROOTS ON THE DIAGONAL
!           3, FULL MATRIX PRINTED
!           4, FULL MATRIX PRINTED WITH CORRELATION MATRIX
!              PRINTED BELOW THE DIAGONAL
!     INTEGER NC
!        ROW AND COLUMN DIMENSION OF X
!     INTEGER NF
!        THE NUMBER OF COLUMNS THAT CAN BE PRINTED, GIVEN
!        THE WIDTH IWIDTH OF THE OUTPUT DEVICE.
!     INTEGER NLINE
!        THE NUMBER OF VALUES TO BE PRINTED EACH LINE.
!     REAL SQXII, SQYII
!        THE SQUARE ROOT OF THE (I,I)TH ELEMENT OF X AND Y.
!     REAL X(LENGTH)
!        INPUT SYMMETRIC ARRAY STORED ROW WISE
!     REAL XLINE(10)
!        THE CURRENT VALUES BEING PRINTED FROM ARRAY X.
!     REAL Y(LENGTH)
!        ARRAY TO BE PRINTED ON THE SECOND LEVEL IF CODE=2
!     REAL YLINE(10)
!        THE CURRENT VALUES BEING PRINTED FROM ARRAY Y.
!
!     BODY OF ROUTINE
!
      CALL IPRINT(IPRT)
!
      NF = INPERL(0)
!
      L = 1
      JMASK = 0
!
!     SELECT INITIAL COLUMN TO PRINT THIS PASS OF THE REPORT
!
      DO 90 J=1,NC,NF
         KN = MIN(NC,J+NF-1)
         KMAX = MIN(NC-J+1,NF)
!
!     GENERATE VECTOR OF COLUMN HEAD LABELS
!
         DO 20 K=1,KMAX
   10       IF (JMASK.GE.LMASK) GO TO 100
            JMASK = JMASK + 1
            IF (MASK(JMASK).NE.0) GO TO 10
            INDW(K) = JMASK
   20    CONTINUE
!
!     PRINT VECTOR OF COLUMN HEAD LABELS
!
         WRITE (IPRT,1000) (INDW(K),K=1,KMAX)
         WRITE (IPRT,1030)
         IF (MODE.LE.2) L = INDW(1)
!
!     PRINT ALL ROWS IN COLUMN RANGE FOR THIS PASS
!
         IMASK = L - 1
         DO 80 I=L,NC
            KM = KN
            IF (MODE.LE.2) KM = J + MIN(I-L,NF-1)
            NLINE = 0
            I0 = I*(I-1)/2
            II = I0 + I
            SQXII = SQRT(X(II))
            IF (CODE.EQ.2) THEN
               SQYII = SQRT(Y(II))
            ELSE
               SQYII = 1.0E0
            END IF
            DO 60 K=J,KM
               NLINE = NLINE + 1
               IF (K.GT.I) GO TO 30
               IK = I0 + K
               XLINE(NLINE) = X(IK)
               IF (CODE.EQ.2) YLINE(NLINE) = Y(IK)
               GO TO 40
   30          KI = K*(K-1)/2 + I
               XLINE(NLINE) = X(KI)
               IF (CODE.EQ.2) YLINE(NLINE) = Y(KI)
   40          IF (((MODE.NE.1) .AND. (MODE.NE.2)) .OR. (I.NE.K)) GO TO
     &            50
               XLINE(NLINE) = SQXII
               IF (CODE.EQ.2) YLINE(NLINE) = SQXII
   50          IF (((MODE.NE.2) .AND. (MODE.NE.4)) .OR. (K.GE.I)) GO TO
     &            60
               KK = K*(K-1)/2 + K
               XLINE(NLINE) = XLINE(NLINE)/(SQXII*SQRT(X(KK)))
               IF (CODE.EQ.2)
     &            YLINE(NLINE) = YLINE(NLINE)/(SQYII*SQRT(Y(KK)))
   60       CONTINUE
   70       IF (IMASK.GE.LMASK) GO TO 100
            IMASK = IMASK + 1
            IF (MASK(IMASK).NE.0) GO TO 70
            WRITE (IPRT,1010) IMASK, (XLINE(K),K=1,NLINE)
            IF (CODE.EQ.2) WRITE (IPRT,1020) (YLINE(K),K=1,NLINE)
            IF (CODE.EQ.2) WRITE (IPRT,1030)
   80    CONTINUE
   90 CONTINUE
      RETURN
!
  100 WRITE (IPRT,1040)
      RETURN
!
!     FORMAT STATEMENTS
!
!
 1000 FORMAT (/' ', 7HCOLUMN , 7(I9, 8X))
 1010 FORMAT (' ', I6, 1X, 7(3X, G14.7))
 1020 FORMAT (' ', 5X, 7(3X, G14.7))
 1030 FORMAT (' ')
 1040 FORMAT (/47H ERROR IN STARPAC.  MATPRF TRIES TO ACCESS MORE,
     &   29H ELEMENTS THAN EXIST IN MASK.)
      END
