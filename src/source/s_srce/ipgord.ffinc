!IPGORD
      SUBROUTINE IPGORD (PERI, NF, N, FREQ, XAXIS, YAXIS, ISYM, NPTS,
     +   LPCV)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE PRODUCES CO-ORDINATES FOR THE SPECTRAL PLOTS.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  DECEMBER 7, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      INTEGER
     +   LPCV,N,NF,NPTS
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP)
     +   FREQ(NF),PERI(NF),XAXIS(LPCV),YAXIS(LPCV)
      INTEGER
     +   ISYM(LPCV)
!
!  LOCAL SCALARS
      REAL(KIND=WP)
     +   CI,CIMID
      INTEGER
     +   I,II,NLIM
!
!  LOCAL ARRAYS
      REAL(KIND=WP)
     +   S(40)
!
!  INTRINSIC FUNCTIONS
      INTRINSIC MOD,REAL,SQRT
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL CI, CIMID
!        THE WIDTH AND MIDPOINT OF THE TEST INTERVAL FOR WHITE NOISE.
!     REAL FREQ(NF)
!        THE ARRAY IN WHICH THE FREQUENCIES AT WHICH THE PERIODOGRAM
!        WAS ESTIMATED ARE STORED.
!     INTEGER I
!        AN INDEX VARIABLE
!     INTEGER II
!        AN INDEX VARIABLE
!     INTEGER ISYM(LPCV)
!        THE ARRAY CONTAINING THE CODE FOR THE PLOT SYMBOLS.
!     INTEGER LPCV
!        THE LENGTH OF THE PLOT CO-ORDINATE VECTORS.
!     INTEGER N
!        THE ACTUAL NUMBER OF OBSERVATIONS IN THE SERIES FROM WHICH
!        THE PERIODOGRAM WAS COMPUTED.
!     INTEGER NF
!        THE NUMBER OF FREQUENCIES FOR WHICH THE SPECTRAL ESTIMATES
!        ARE ESTIMATED.
!     INTEGER NLIM
!        THE NUMBER OF POINTS AT WHICH THE WHITE NOISE LIMITS ARE TO
!        PLOTTED.
!     INTEGER NPTS
!        THE NUMBER OF X, Y CO-ORDINATES TO BE PLOTTED.
!     REAL PERI(NF)
!        THE ARRAY CONTAINING THE INTEGRATED PERIODOGRAM VALUES.
!     REAL S(40)
!        VALUES USED TO COMPUTE THE CONFIDENCE LIMITS.
!     REAL XAXIS(LPCV), YAXIS(LPCV)
!        THE X, Y CO-ORDINATES FOR THE SPECTRAL PLOTS.
!
      DATA S(1)/0.975E0/, S(2)/0.842E0/, S(3)/0.708E0/
      DATA S(4)/0.624E0/, S(5)/0.563E0/
      DATA S(6)/0.519E0/, S(7)/0.483E0/, S(8)/0.454E0/
      DATA S(9)/0.430E0/, S(10)/0.409E0/
      DATA S(11)/0.391E0/, S(12)/0.375E0/, S(13)/0.361E0/
      DATA S(14)/0.349E0/, S(15)/0.338E0/
      DATA S(16)/0.327E0/, S(17)/0.318E0/, S(18)/0.309E0/
      DATA S(19)/0.301E0/, S(20)/0.294E0/
      DATA S(21)/0.287E0/, S(22)/0.281E0/, S(23)/0.275E0/
      DATA S(24)/0.269E0/, S(25)/0.264E0/
      DATA S(26)/0.259E0/, S(27)/0.254E0/, S(28)/0.250E0/
      DATA S(29)/0.246E0/, S(30)/0.242E0/
      DATA S(31)/0.238E0/, S(32)/0.234E0/, S(33)/0.231E0/
      DATA S(34)/0.227E0/, S(35)/0.224E0/
      DATA S(36)/0.221E0/, S(37)/0.218E0/, S(38)/0.215E0/
      DATA S(39)/0.213E0/, S(40)/0.210E0/
!
      I = N + MOD(N,2) - 1
!
      IF (I .LE. 40) THEN
         CI = S(I)
      ELSE
         CI = 1.36E0 / SQRT(REAL(I))
      END IF
!
      DO 10 I = 1, NF
         XAXIS(I) = FREQ(I)
         YAXIS(I) = PERI(I)
         ISYM(I) = 1
   10 CONTINUE
!
      NLIM = 101
!
      II = NF
!
      DO 30 I = 1, NLIM, 2
         CIMID = REAL(I-1) / REAL(NLIM-1)
         IF (CIMID - CI .LT. 0.0E0) GO TO 20
         II = II + 1
         XAXIS(II) = CIMID / 2.0E0
         YAXIS(II) = CIMID - CI
         ISYM(II) = 2
   20    IF (CIMID + CI .GT. 1.0E0) GO TO 30
         II = II + 1
         XAXIS(II) = CIMID / 2.0E0
         YAXIS(II) = CIMID + CI
         ISYM(II) = 2
   30 CONTINUE
!
      NPTS = II
!
      RETURN
      END
