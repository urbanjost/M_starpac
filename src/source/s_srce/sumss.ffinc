!SUMSS
      subroutine sumss(x, n, lo, mid, hi, sum1, sum2, suma, xmean)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     A ROUTINE TO CALCULATE SUMS OF POWERS AND THE MEAN
!     FOR A SORTED VECTOR IN WHICH THE MIDTH ELEMENT IS
!     THE ELEMENT CLOSEST TO ZERO.  USED BY THE STAT
!     FAMILY.
!
!     WRITTEN BY - JOHN E. KOONTZ
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS
!
!     CREATION DATE  -  MAY 17, 1982
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     real(kind=wp) ::&
     &   sum1,sum2,suma,xmean
     integer&
     &   hi,lo,mid,n
!
!  ARRAY ARGUMENTS
     real(kind=wp) ::&
     &   x(n)
!
!  LOCAL SCALARS
     integer&
     &   curhi,curlo,i,irev
!
!  INTRINSIC FUNCTIONS
      intrinsic abs
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     INTEGER CURHI
!        THE UPPER BOUND OF THE CURRENT INTERVAL.
!     INTEGER CURLO
!        THE LOWER BOUND OF THE CURRENT INTERVAL.
!     INTEGER HI
!        INPUT PARAMETER.  THE UPPER BOUND OF THE INITIAL
!        INTERVAL.
!     INTEGER I
!        LOOP PARAMETER.
!     INTEGER IREV
!        A VARIABLE WHICH RUNS IN THE SAME INTERVAL AS I, BUT
!        IN THE REVERSE ORDER.
!     INTEGER LO
!        INPUT PARAMETER.  THE LOWER BOUND OF THE INITIAL INTERVAL.
!     INTEGER MID
!        INPUT PARAMETER.  THE INDEX OF THE ELEMENT IN X CLOSEST TO
!        ZERO IN VALUE.  THE POINT OUT FROM WHICH THE SUMMING IS
!        DONE.
!     INTEGER N
!        INPUT PARAMETER.  THE LENGTH OF THE ARRAY X.
!     REAL SUMA
!        OUTPUT PARAMETER.  THE SUM OF THE ABSOLUTE VALUES OF THE
!        ELEMENTS OF X.
!     REAL SUM1
!        OUTPUT PARAMETER.  THE SUM OF THE ELEMENTS OF X.
!     REAL SUM2
!        OUTPUT PARAMETER.  THE SUM OF THE SQUARES OF THE
!        ELEMENTS OF X.
!     REAL X(N)
!        INPUT PARAMETER.  THE DATA ARRAY X OVER WHICH THE SUMS ARE
!        TAKEN.
!     REAL XMEAN
!        OUTPUT PARAMETER.  THE UNWEIGHTED MEAN OF X.
!
!     INITIALIZE SUMMATION VARIABLES.
!
      sum1 = x(mid)
      sum2 = x(mid)*x(mid)
      suma = abs(x(mid))
      curlo = mid - 1
      curhi = mid + 1
!
!     SUM OUTWARDS FROM THE VALUE NEAREST ZERO.  THAT IS, SUM FROM
!     THE LEAST IN MAGNITUDE TO THE GREATEST.
!
   10 if (curhi.gt.hi .or. curlo.lt.lo) go to 30
      if (abs(x(curhi)).lt.abs(x(curlo))) go to 20
      sum1 = sum1 + x(curlo)
      sum2 = sum2 + x(curlo)*x(curlo)
      suma = suma + abs(x(curlo))
      curlo = curlo - 1
      go to 10
   20 sum1 = sum1 + x(curhi)
      sum2 = sum2 + x(curhi)*x(curhi)
      suma = suma + abs(x(curhi))
      curhi = curhi + 1
      go to 10
!
!     SUM UP ANY VALUES WHICH MAY REMAIN BECAUSE OF AN OFF
!     CENTER ZERO POINT.
!
   30 if (curhi.gt.hi) go to 50
      do 40 i=curhi,hi
         sum1 = sum1 + x(i)
         sum2 = sum2 + x(i)*x(i)
         suma = suma + abs(x(i))
   40 continue
   50 if (curlo.lt.lo) go to 70
      do 60 irev=lo,curlo
         i = lo - irev + curlo
         sum1 = sum1 + x(i)
         sum2 = sum2 + x(i)*x(i)
         suma = suma + abs(x(i))
   60 continue
   70 xmean = sum1/(hi-lo+1)
      return
      end
