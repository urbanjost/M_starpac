!VPLMT
      SUBROUTINE VPLMT (YM, YMMISS, N, M, IYM, YLB, YUB, YMN,
     +  YMX, ERROR, NMSUB, MISS, NSAMPL)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE SETS THE PLOT LIMITS FOR VERTICAL PLOTS
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  JANUARY 21, 1982
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP)
     +   YLB,YMN,YMX,YUB
      INTEGER
     +   IYM,M,N,NSAMPL
      LOGICAL
     +   ERROR,MISS
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP)
     +   YM(IYM,M),YMMISS(M)
      CHARACTER
     +   NMSUB(6)*1
!
!  LOCAL SCALARS
      INTEGER
     +   I,II,IPRT,J
      LOGICAL
     +   HEAD,SETLMT
!
!  EXTERNAL FUNCTIONS
      LOGICAL
     +   MVCHK
      EXTERNAL MVCHK
!
!  EXTERNAL SUBROUTINES
      EXTERNAL ADJLMT,EHDR,IPRINT
!
!  INTRINSIC FUNCTIONS
      INTRINSIC MAX,MIN
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     LOGICAL ERROR
!        A VALUE INDICATING WHETHER AN ERROR WAS DETECTED (TRUE)
!        OR NOT (FALSE).
!     LOGICAL HEAD
!        PRINT HEADING (HEAD=TRUE) OR NOT (HEAD=FALSE).
!     INTEGER I, II
!        INDEXING VARIABLES.
!     INTEGER IPRT
!        *
!     INTEGER IYM
!        ACTUAL ROW DIMENSION OF YM DECLARED IN THE USERS MAIN PROGRAM
!     INTEGER J
!        AN INDEX VARIABLE.
!     INTEGER M
!        THE NUMBER OF VECTORS IN YM
!     LOGICAL MISS
!        INDICATOR VARIABLE USED TO DESIGNATE WHETHER MISSING VALUES
!        MAY BE PRESENT (MISS = .TRUE.) OR NOT (MISS = .FALSE.)
!     INTEGER N
!        THE INTEGER NUMBER OF OBSERVATIONS .
!     CHARACTER*1 NMSUB(6)
!        THE CHARACTERS OF THE CALLING ROUTINES NAME.
!     INTEGER NSAMPL
!        *
!     LOGICAL SETLMT
!        AN INDICATOR VARIABLE USED TO DETERMINE IF STARTING VALUES
!        FOR XLB, XINC, YMN, YMX HAVE BEEN FOUND.
!     REAL YLB
!        THE USER SUPPLIED Y-AXIS LOWER BOUND.
!     REAL YM(IYM,M)
!        THE ARRAY CONTAINING THE DEPENDENT VARIABLE(S).
!     REAL YMMISS(M)
!        THE USER SUPPLIED CODE WHICH IS USED TO DETERMINE WHETHER OR
!        NOT AN OBSERVATION IS MISSING.
!        IF YM(I,J) = YMMISS(J), THE VALUE IS ASSUMED MISSING, OTHERWISE
!        IT IS NOT.
!     REAL YMN, YMX
!        THE Y-AXIS LOWER AND UPPER LIMITS ACTUALLY USED.
!     REAL YUB
!        THE USER SUPPLIED Y-AXIS UPPER BOUNDS.
!
      ERROR = .FALSE.
!
      IF (YLB.LT.YUB) THEN
!
!       SET LIMITS TO USER SPECIFIED VALUES
!
        YMN = YLB
        YMX = YUB
!
      ELSE
!
!       SET LIMITS TO RANGE OF VALUES WITHIN ANY USER SPECIFIED VALUES
!
        SETLMT = .FALSE.
        II = 1
!
!       FIND FIRST VALUE TO BE PLOTTED
!
        DO 20 I=1,N,NSAMPL
           DO 10 J=1,M
              IF (MISS .AND. MVCHK(YM(I,J),YMMISS(J))) GO TO 10
              IF ((YLB.LT.YUB) .AND. ((YM(I,J).LT.YLB) .OR.
     +           (YUB.LT.YM(I,J)))) GO TO 10
              IF (SETLMT) THEN
                YMN = MIN(YMN, YM(I,J))
                YMX = MAX(YMX, YM(I,J))
              ELSE
                YMN = YM(I,J)
                YMX = YM(I,J)
                SETLMT = .TRUE.
                II = I + NSAMPL
              END IF
   10      CONTINUE
           IF (SETLMT) GO TO 30
   20   CONTINUE
!
   30   IF (II.LE.1) THEN
!
!         NO VALUES TO BE PLOTTED.  PRINT ERROR MESSAGE
!
          ERROR = .TRUE.
          CALL IPRINT(IPRT)
          HEAD = .TRUE.
          CALL EHDR(NMSUB,HEAD)
          IF (YLB.GE.YUB) THEN
            WRITE (IPRT, 1010)
          ELSE
            WRITE (IPRT, 1020)
          END IF
          WRITE (IPRT, 1030)
!
        ELSE
!
!         FIND LIMITS FROM REMAINING VALUES
!
          IF (II.LE.N) THEN
            DO 50 I=II,N,NSAMPL
               DO 40 J=1,M
                  IF (MISS .AND. MVCHK(YM(I,J),YMMISS(J))) GO TO 40
                  IF ((YLB.LT.YUB) .AND. ((YM(I,J).LT.YLB) .OR.
     +               (YUB.LT.YM(I,J)))) GO TO 40
                  YMN = MIN(YMN, YM(I,J))
                  YMX = MAX(YMX, YM(I,J))
   40          CONTINUE
   50       CONTINUE
          END IF
        END IF
!
!       ADJUST Y AXIS LIMITS IF EQUAL
!
        IF (YMN .GE. YMX) CALL ADJLMT(YMN, YMX)
!
      END IF
!
      RETURN
!
!     FORMAT STATEMENTS
!
 1010 FORMAT (/
     +   44H NO NON-MISSING PLOT COORDINATES WERE FOUND.)
 1020 FORMAT (/
     +   40H NO NON-MISSING VALUES WERE FOUND WITHIN,
     +   26H THE USER SUPPLIED LIMITS.)
 1030 FORMAT (/
     +   30H THE PLOT HAS BEEN SUPPRESSED.)
      END
