!GFARF
      subroutine gfarf (phi, iar)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS IS THE USER CALLABLE ROUTINE TO COMPUTE THE GAIN
!     FUNCTION OF AN AUTOREGRESSIVE FILTER (SHORT CALL).
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 26, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     integer&
     &   iar
!
!  ARRAY ARGUMENTS
     real(kind=wp) ::&
     &   phi(*)
!
!  SCALARS IN COMMON
     integer&
     &   ierr
!
!  LOCAL SCALARS
     real(kind=wp) ::&
     &   delta,fmax,fmin,ypltmn,ypltmx
     integer&
     &   igferr,iprt,nf,nord,nprt
     logical&
     &   err01,head,sym
!
!  LOCAL ARRAYS
     real(kind=wp) ::&
     &   freq(101),gain(101),phas(101),xord(101),yord(101)
     integer&
     &   isort(101)
     character&
     &   liar(8)*1,nmsub(6)*1
!
!  EXTERNAL SUBROUTINES
!      EXTERNAL EISGE,GFAEST,GFORD,GFOUT,IPRINT,SETFRQ
!
!  COMMON BLOCKS
      common /errchk/ierr
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL(KIND=WP) :: DELTA
!        THE SAMPLING INTERVAL.
!     LOGICAL ERR01
!        VALUE(S) INDICATING WHETHER AN ERROR WAS DETECTED (TRUE) OR NOT
!        (FALSE).
!     REAL(KIND=WP) :: FMAX, FMIN
!        THE MINIMUM AND MAXIMUM FREQUENCY FOR WHICH THE GAIN
!        FUNCTION IS TO BE ESTIMATED.
!     REAL(KIND=WP) :: FREQ(101)
!        THE VECTOR OF FREQUENCIES AT WHICH THE GAIN FUNCTION
!        HAS BEEN ESTIMATED.
!     REAL(KIND=WP) :: GAIN(101)
!        THE VECTOR IN WHICH THE GAIN FUNCTION ESTIMATES ARE
!        STORED.
!     LOGICAL HEAD
!        A FLAG INDICATING WHETHER THE HEADING SHOULD BE PRINTED
!        (TRUE) OR NOT (FALSE).  IF A HEADING IS PRINTED, THE VALUE
!        OF HEAD WILL BE CHANGED TO FALSE.
!     INTEGER IAR
!        THE NUMBER OF FILTER COEFFICIENTS.
!     INTEGER IERR
!        THE INTEGER VALUE RETURNED BY THIS ROUTINE DESIGNATING
!        WHETHER ANY ERRORS WERE DETECTED IN THE PARAMETER LIST.
!        IF IERR .EQ. 0, NO ERRORS WERE DETECTED.
!        IF IERR .EQ. 1, ERRORS HAVE BEEN DETECTED.
!     INTEGER IGFERR
!        AN ERROR FLAG INDICATING WHETHER COMPUTATIONS SEEM
!        TO HAVE PRODUCED REASONABLE RESULTS.
!     INTEGER IPRT
!        THE UNIT NUMBER USED FOR OUTPUT.
!     INTEGER ISORT(101)
!        AN ARRAY USED FOR SORTING.
!     CHARACTER*1 LIAR(8)
!        THE ARRAY CONTAINING THE NAME OF THE VARIABLE IAR.
!     INTEGER NF
!        THE NUMBER OF FREQUENCIES AT WHICH THE GAIN FUNCTION
!        IS TO BE ESTIMATED.
!     CHARACTER*1 NMSUB(6)
!        THE ARRAY CONTAINING THE NAME OF THIS SUBROUTINE.
!     INTEGER NORD
!        THE NUMBER OF VALUES TO BE PLOTTED.
!     INTEGER NPRT
!        A CODE USED TO SPECIFY THE TYPE OF PLOT, WHERE IF
!        NPRT < 0 THE PLOT IS DECIBELS/LINEAR
!        NPRT = 0 THE PLOT IS SUPPRESSED
!        NPRT > 0 THE PLOT IS LOG/LINEAR
!     REAL(KIND=WP) :: PHAS(101)
!        THE ARRAY IN WHICH THE PHASE OF THE FILTER IS RETURNED.
!     REAL(KIND=WP) :: PHI(IAR)
!        THE VECTOR CONTAINING THE FILTER COEFFICIENTS.
!     LOGICAL SYM
!        THE VARIABLE USED TO INDICATE WHETHER THE GAIN FUNCTION
!        COMPUTED WAS FOR A SYMMETRIC OR AUTOREGRESSIVE FILTER.
!     REAL(KIND=WP) :: XORD(101)
!        THE X COORDINATES FOR THE GAIN FUNCTION PLOT
!     REAL(KIND=WP) :: YORD(101)
!        THE Y COORDINATES FOR THE GAIN FUNCTION PLOT
!     REAL(KIND=WP) :: YPLTMN, YPLTMX
!        THE MINIMUM AND MAXIMUM VALUES TO BE PLOTTED FOR THE Y AXIS.
!
!     SET UP NAME ARRAYS
!
     data&
    &  nmsub(1),  nmsub(2),  nmsub(3),  nmsub(4),  nmsub(5),  nmsub(6)&
     & /     'G',       'F',       'A',       'R',       'F',       ' '/
     data&
    &  liar(1), liar(2), liar(3), liar(4), liar(5), liar(6), liar(7),&
    &  liar(8)&
     & /  'I',   'A',   'R',   ' ',   ' ',   ' ',   ' ',   ' '/
!
!     SET UP FOR ERROR CHECKING
!
      ierr = 0
      head = .true.
!
!     CALL ERROR CHECKING ROUTINES
!
      call eisge(nmsub, liar, iar, 1, 1, head, err01, liar)
!
      if (.not. err01) go to 10
!
      ierr = 1
      call iprint (iprt)
      write (iprt, 1000)
      return
!
   10 continue
!
!     SET VARIOUS PROGRAM PARAMETERS
!
      sym = .false.
!
      nf = 101
!
      fmin = 0.0e0
      fmax = 0.5e0
!
      delta = 1.0e0
!
      nprt = -1
!
!     SET FREQUENCIES AT WHICH THE GAIN FUNCTION IS TO BE ESTIMATED
!
      call setfrq (freq, nf, nprt, fmin, fmax, delta)
!
!     COMPUTE THE GAIN FUNCTION
!
      call gfaest (phi, iar, nf, freq, gain, phas, delta)
!
!     SET THE COORDINATES FOR THE PLOT.
!
     call gford (freq, gain, isort, nf, xord, yord, nord,&
     &   ypltmn, ypltmx, nprt, igferr)
!
!     PLOT THE RESULTS.
!
     call gfout (xord, yord, nord, freq, phas, nf, iar, sym, fmin,&
     &   fmax, ypltmn, ypltmx, nprt, igferr, nmsub)
!
      return
!
!     FORMAT STATEMENTS
!
1000 format (/42h the correct form of the call statement is//&
     &   28h       call gfarf (phi, iar))
      end
