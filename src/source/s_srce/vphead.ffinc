!VPHEAD
     subroutine vphead(nsampl, irlin, ibar,&
    &                  refpt, ywidth, ymn, ymx,&
     &                  isize, ilog, line, numcol, ilogy, ydmn, ydmx)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE PRINTS THE HEADING FOR THE VERTICAL PLOT OUTPUT.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 21, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     real(kind=wp) ::&
     &   refpt,ydmn,ydmx,ymn,ymx,ywidth
     integer&
     &   ibar,ilog,ilogy,irlin,isize,nsampl,numcol
!
!  ARRAY ARGUMENTS
     character&
     &   line(103)*1
!
!  LOCAL SCALARS
     real(kind=wp) ::&
     &   dely
     integer&
     &   i,icol,ik,iprt,jcol,nlably,nlu
     character&
     &   fmt*4,ylfmt*205,ylfmt2*205
!
!  LOCAL ARRAYS
     real(kind=wp) ::&
     &   ylabel(20)
     integer&
     &   ispace(20)
     integer&
     & ilogy_(1)
!
!  EXTERNAL SUBROUTINES
!      EXTERNAL IPRINT,LOGLMT,PRTCNT
!
!  INTRINSIC FUNCTIONS
      intrinsic abs,log10,max,mod
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL DELY
!        THE SCALE INTERVAL OF THE PLOT.
!     CHARACTER FMT*4
!        *
!     INTEGER I
!        AN INDEXING VARIABLE.
!     INTEGER IBAR
!        THE VARIABLE USED TO DETERMINE IF SINGLE POINTS (IBAR .EQ. 0)
!        OR BARS (IBAR .NE. 0) ARE TO BE PLOTTED.
!     INTEGER ICOL, IK
!        *
!     INTEGER ILOG
!        THE TWO DIGIT INTEGER, PQ, USED TO SELECT AXIS SCALE, WHERE
!        P DESIGNATES THE X-AXIS AND Q DESIGNATES THE Y-AXIS.
!        IF P.EQ.0 (Q.EQ.0), THEN THE X-AXIS (Y-AXIS) IS LINEAR.
!        IF P.NE.0 (Q.NE.0), THEN THE X-AXIS (Y-AXIS) IS LOG.
!     INTEGER ILOGY
!        THE VALUE OF Q (SEE ILOG)
!     INTEGER IPRT
!        THE UNIT NUMBER FOR PRINTED OUTPUT.
!     INTEGER IRLIN
!        THE INDICATOR VARIABLE USED TO DESIGNATE WHETHER ZERO OR THE
!        SERIES MEAN IS TO BE PLOTTED AS A REFERENCE LINE, OR WHETHER
!        NO REFERENCE LINE IS TO BE PLOTTED.
!        IF IRLIN .LE. -1, NO REFERENCE LINE IS PLOTTED.
!        IF IRLIN .EQ.  0, ZERO IS PLOTTED AS THE REFERENCE LINE.
!        IF IRLIN .GE.  1, THE SERIES MEAN IS PLOTTED.
!     INTEGER ISIZE
!     INTEGER ISPACE(20)
!     INTEGER JCOL
!     CHARACTER*1 LINE(103)
!     INTEGER NLABLY
!     INTEGER NLU
!     INTEGER NSAMPL
!        THE SAMPLING FREQUENCY,
!        WHERE IF NSAMPL .LE. 1, EVERY POINT IS PLOTTED,
!                       = 2, EVERY OTHER POINT IS PLOTTED,
!                       = 3, EVERY THIRD POINT IS PLOTTED, ETC.
!     INTEGER NUMCOL
!        *
!     REAL REFPT
!        THE VALUE ZERO, OR THE MEAN OF THE SERIES, WHICH EVER IS
!        REQUESTED, USED AS A PLOT REFERENCE LINE.
!     REAL YDMN, YDMX
!        THE Y-AXIS DATA LIMITS ACTUALLY USED.
!     REAL YLABEL(20)
!        THE Y-AXIS LABLES.
!     CHARACTER YLFMT*205, YLFMT2*205
!        THE FORMATS USED TO PRINT THE X-AXIS
!     REAL YMN, YMX
!        THE GRAPH AXIS LOWER AND UPPER LIMITS ACTUALLY USED.
!     REAL YWIDTH
!        *
!
      call iprint(iprt)
!
!     CHECK PLOT SEQUENCE.
!
      if (nsampl.eq.2) then
         write(iprt, 1003)
      else if (nsampl.eq.3) then
         write(iprt, 1004)
      else if (nsampl.ge.4) then
         write(iprt, 1005) nsampl
      end if
!
!     PRINT HEADINGS FOR Y
!
      if (ibar.eq.0) then
         if (irlin.ge.1) then
            write(iprt, 1006)
         else if (irlin.eq.0) then
            write(iprt, 1007)
         end if
      end if
!
!     FIND SIZE OF PLOT TO BE CREATED
!
      if (mod(max(0,isize),10).eq.0) then
        numcol=101
      else
        numcol = 51
      end if
!
!     ADJUST FOR LOG PLOTS IF NECESSARY AND FIND AXIS LABELS
!
      call prtcnt (mod(max(0,ilog),10),1,ilogy_)
      ilogy=ilogy_(1)
     call loglmt (ilogy, ymn, ymx, ylabel, numcol, 10, dely, ywidth,&
     &  nlably, ydmn, ydmx)
!
!     WRITE OUT THE HORIZONTAL AXIS AND THE AXIS LABELS.
!
      do 330 icol=1,numcol
         line(icol)='-'
  330 continue
      line(1)='I'
      if (ilogy.eq.0) then
        nlu = nlably+1
        do 340 icol=numcol,1,-10
           line(icol)='I'
           nlu = nlu - 1
           ispace(nlu) = 1
  340   continue
      else
        jcol = 1
        line(jcol) = 'I'
        nlu = nlably
        do 345 ik = nlably,1,-1
          icol = ((log10(ylabel(ik))-ymn)/ywidth)+1.5e0
          line(icol) = 'I'
          if (icol-jcol.ge.10) then
            ispace(nlu) = icol-jcol-9
            nlu = nlu - 1
            ylabel(nlu) = ylabel(ik)
            jcol = icol
          end if
  345   continue
      end if
      line(numcol+1)='-'
      line(numcol+2)=' '
!
!     CHECK X-AXIS LABELS FOR FORMAT
!
      fmt = 'F9.4'
      do 350 i=1,nlably
        if (((abs(ylabel(i)).gt.0.0e0).and.&
    &        (abs(ylabel(i)).lt.0.01e0)) .or.&
     &       ((ylabel(i).ge.1.0e4).or.(ylabel(i).le.(-1.0e3)))) then
           fmt = 'E9.3'
           go to 355
         end if
  350 continue
  355 continue
      write(ylfmt2,1000) nlably-nlu
      write(ylfmt,ylfmt2) (fmt, ispace(i), i=nlably,nlu+1,-1), fmt
      write(iprt, ylfmt) (ylabel(i),i=nlably,nlu,-1)
!
      write(iprt, 1001) '-', (line(icol), icol=1,numcol+2)
!
      return
!
!     FORMAT STATEMENTS
!
1000 format ('(''(11X'',', i2, '('', '', A4, '','', I2, ''X''),',&
     &  ''', '', A4, '')'')')
 1001 format (' ',13x, a1, 105a1)
 1003 format(45h0note that every other point has been plotted)
 1004 format(45h0note that every third point has been plotted)
 1005 format(17h0note that every , i2, 25hth point has been plotted)
 1006 format(/' LOCATION OF MEAN IS GIVEN BY PLOT CHARACTER M')
 1007 format(/' LOCATION OF ZERO IS GIVEN BY PLOT CHARACTER 0')
      end
