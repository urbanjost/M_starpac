!ACFSDM
      subroutine acfsdm (rho, sdrho, nc, n, nlppa)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE COMPUTES THE STANDARD ERROR OF THE AUTOCORRELATIONS
!     WHEN MISSING DATA ARE INVOLVED.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 21, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     integer&
     &   n,nc
!
!  ARRAY ARGUMENTS
     real(kind=wp) ::&
     &   rho(*),sdrho(*)
     integer&
     &   nlppa(*)
!
!  LOCAL SCALARS
     integer&
     &   k,klast,lag
!
!  INTRINSIC FUNCTIONS
      intrinsic min,real,sqrt
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     INTEGER K
!        AN INDEX VARIABLE.
!     INTEGER KLAST
!        THE LAST LAG VALUE TO BE USED.
!     INTEGER LAG
!        THE INDEX VARIABLE USED TO INDICATE THE AUTOCORRELATION
!        BEING EXAMINED.
!     INTEGER N
!        THE INTEGER NUMBER OF OBSERVATIONS IN EACH SERIES
!     INTEGER NC
!        THE NUMBER OF AUTOCORRELATIONS COMPUTED
!     INTEGER NLPPA(NC)
!        THE ARRAY IN WHICH THE NUMBER OF OBSERVATIONS USED TO
!        COMPUTE EACH AUTOCORRELATION AND PARTIAL AUTOCORRELATION
!        IS STORED
!     REAL(KIND=WP) :: RHO(NC)
!        THE ARRAY IN WHICH THE AUTOCORRELATIONS ARE STORED
!     REAL(KIND=WP) :: SDRHO(NC)
!        THE ARRAY IN WHICH THE STANDARD ERRORS OF THE AUTOCORRELATIONS
!        ARE STORED
!
      sdrho(1) = sqrt(real(nlppa(1))) * (n-1) / (n * nlppa(1))
      if (nc .eq. 1) return
      do 20 lag = 2, nc
         sdrho(lag) = 0.0_wp
         klast = min(lag-1, n-lag)
         do 10 k = 1, klast
            sdrho(lag) = sdrho(lag) + (n-lag-k) * rho(k) * rho(k)
   10    continue
        sdrho(lag) = sqrt((n-lag) + 2.0_wp * sdrho(lag)) *&
     &      (n-lag) / (n*nlppa(lag))
   20 continue
      return
      end
