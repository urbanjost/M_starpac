!ARCOEF
      subroutine arcoef (acov, phi, rss, lag, lagmax, acov0)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE USES DURBINS RECURSIVE METHOD TO COMPUTE
!     THE AUTOREGRESSIVE COEFFICIENTS OF AN ORDER LAG PROCESS,
!     GIVEN ON INPUT THE COEFFICIENTS OF AN ORDER (LAG-1) PROCESS.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 21, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     real(kind=wp) ::&
     &   acov0,rss
     integer&
     &   lag,lagmax
!
!  ARRAY ARGUMENTS
     real(kind=wp) ::&
     &   acov(*),phi(*)
!
!  LOCAL SCALARS
     real(kind=wp) ::&
     &   sum1,sum2,t
     integer&
     &   j,l1,l2,lj
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL(KIND=WP) :: ACOV(LAGMAX)
!        THE ARRAY OF ACVF ESTIMATES FOR LAGS ONE TO LAGMAX.
!     REAL(KIND=WP) :: ACOV0
!        THE ACVF FOR LAG ZERO.
!     INTEGER J, LAG
!        INDEX VARIABLES.
!     INTEGER LAGMAX
!        THE MAXIMUM LAG VALUE USED.
!     INTEGER LJ, L2
!        INDEX VARIABLES.
!     REAL(KIND=WP) :: PHI(LAGMAX)
!        THE ARRAY OF AUTOREGRESSIVE COEFFICIENTS FOR AN AR
!        PROCESS OF ORDER LAG.
!     REAL(KIND=WP) :: RSS
!        THE ONE STEP PREDICTION RESIDUAL SUM OF SQUARES.
!     REAL(KIND=WP) :: SUM1, SUM2
!        VARIABLES USED IN THE COMPUTATIONS OF THE AUTOREGRESSIVE
!        COEFFICIENTS.
!     REAL(KIND=WP) :: T
!        A TEMPORARY STORAGE LOCATION.
!
      l1 = lag - 1
      sum1 = 0.0e0
      sum2 = 0.0e0
      do 10 j = 1, l1
         lj = lag - j
         sum1 = sum1 + phi(j) * acov(lj)
         sum2 = sum2 + phi(j) * acov(j)
   10 continue
      phi(lag) = (acov(lag) - sum1) / (acov0 - sum2)
      l2 = lag / 2
      do 20 j = 1, l2
         lj = lag - j
         t = phi(j) - phi(lag) * phi(lj)
         phi(lj) = phi(lj) - phi(lag) * phi(j)
         phi(j) = t
   20 continue
!
      rss = rss * (1.0e0 - phi(lag)*phi(lag))
!
      return
      end
