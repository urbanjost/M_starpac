!FLTAR
      subroutine fltar (y, n, iar, phi, yf, nyf)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE FILTERS THE INPUT SERIES Y USING THE IAR TERMS
!     OF THE AUTOREGRESSIVE FILTER PHI, COPYING THE FILTERED SERIES
!     INTO YF.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DEVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 26, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     integer&
     &   iar,n,nyf
!
!  ARRAY ARGUMENTS
     real(kind=wp) ::&
     &   phi(*),y(*),yf(*)
!
!  LOCAL SCALARS
     real(kind=wp) ::&
     &   temp
     integer&
     &   i,i1,j,k
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     INTEGER I
!        AN INDEXING VARIABLE.
!     INTEGER IAR
!        THE NUMBER OF FILTER TERMS.
!     INTEGER I1, J, K
!        INDEXING VARIABLES.
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS IN THE SERIES Y.
!     INTEGER NYF
!        THE NUMBER OF OBSERVATIONS IN THE FILTERED SERIES YF.
!     REAL(KIND=WP) :: PHI(IAR)
!        THE ARRAY IN WHICH THE FILTER COEFFICIENTS ARE STORED.
!     REAL(KIND=WP) :: TEMP
!        A TEMPORARY STORAGE LOCATION.
!     REAL(KIND=WP) :: Y(N)
!        THE VECTOR CONTAINING THE OBSERVED TIME SERIES.
!     REAL(KIND=WP) :: YF(N)
!        THE VECTOR IN WHICH THE FILTERED SERIES IS RETURNED.
!
      do 10 i = 1, n
         yf(i) = y(i)
   10 continue
!
      nyf = n - iar
!
      do 30 i = 1, nyf
         k = i + iar
         temp = yf(k)
         do 20 j = 1, iar
            k = k - 1
            temp = temp - phi(j) * yf(k)
   20    continue
         yf(i) = temp
   30 continue
!
      i1 = nyf + 1
!
      do 40 i = i1, n
         yf(i) = 0.0e0
   40 continue
      return
      end
