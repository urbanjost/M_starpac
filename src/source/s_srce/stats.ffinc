!STATS
      SUBROUTINE STATS(Y, N, LDSTAK, STS, NPRT)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS SUBROUTINE COMPUTES 53 DIFFERENT STATISTICS FOR A VECTOR
!     Y, WITH NO WEIGHTS SPECIFIED.  ONE PAGE OF AUTOMATIC
!     PRINTOUT IS PRODUCED.
!
!     WRITTEN BY - JANET R. DONALDSON, JOHN E. KOONTZ
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS
!
!     CREATION DATE  -  MAY 17, 1982
!        (EXTENSIVE REVISION OF OLDER VERSION)
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      INTEGER
     &   LDSTAK,N,NPRT
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   STS(53),Y(*)
!
!  SCALARS IN COMMON
      INTEGER
     &   IERR
!
!  ARRAYS IN COMMON
      DOUBLE PRECISION DSTAK(12)
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     &   ALPHA,SUMD2,SUMD3,SUMD4,SUMDA,SUMDI,SUMT1
      INTEGER
     &   IDP,IINT,IPRT,LSORT,MID,NALL0,NNZW
      LOGICAL
     &   STACK,WTS
!
!  LOCAL ARRAYS
      REAL(KIND=WP) ::
     &   WT(1)
      INTEGER
     &   ISTAK(12)
      CHARACTER
     &   NMSUB(6)*1
!
!  EXTERNAL FUNCTIONS
!      INTEGER
!     +   STKST
!      EXTERNAL STKST
!
!  EXTERNAL SUBROUTINES
!      EXTERNAL GENI,IPRINT,SRTIR,SRTRI,STAT1,STAT2,STATER,STKCLR,STKSET,
!     +   SUMBS,SUMDS,SUMID,SUMOT,SUMSS,SUMTS
!
!  COMMON BLOCKS
      COMMON /CSTAK/DSTAK
      COMMON /ERRCHK/IERR
!
!  EQUIVALENCES
      EQUIVALENCE (DSTAK(1),ISTAK(1))
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL ALPHA
!        THE PERCENTAGE TO TRIM FROM EACH END IN THE TRIMMED
!        MEANS.
!     DOUBLE PRECISION DSTAK(12)
!        THE DOUBLE PRECISION VERSION OF THE /CSTAK/ WORK AREA.
!     INTEGER IDP
!        FRAMEWORK CODE VALUE FOR DOUBLE PRECISION NUMBERS.
!     INTEGER IERR
!        THE CODE INDICATING WHETHER OR NOT AN ERROR HAS
!        BEEN DISCOVERED.  0 MEANS NO ERROR, NOT 0 MEANS
!        SOME ERROR EXISTS.
!     INTEGER IINT
!        THE CODE VALUE FOR INTEGER FOR FRAMEWORK.
!     INTEGER IPRT
!        THE NUMBER OF THE STANDARD OUTPUT UNIT.
!     INTEGER ISTAK(12)
!        THE INTEGER VERSION OF THE /CSTAK/ WORK AREA.
!     INTEGER LDSTAK
!        INPUT PARAMETER.  THE NUMBER OF DOUBLE PRECISION
!        ELEMENTS DIMENSIONED FOR DSTAK IN THE USER PROGRAM.
!     INTEGER LSORT
!        THE STARTING LOCATION IN ISTAK OF THE PERMUTATION
!        VECTOR.
!     INTEGER MID
!        IN THE SORTED ARRAY Y, A POINT EQUAL TO OR THE POINT
!        CLOSEST TO, ZERO.
!     INTEGER N
!        INPUT PARAMETER.  THE LENGTH OF Y.
!     INTEGER NALL0
!        THE NUMBER OF ALLOCATIONS OUTSTANDING AT THE TIME THIS ROUTINE
!        WAS CALLED.
!     CHARACTER*1 NMSUB(6)
!        THE ARRAY CONTAINING THE NAME OF THIS SUBROUTINE.
!     INTEGER NPRT
!        INPUT PARAMETER.  FLAG TO CONTROL OUTPUT.
!        0 MEANS NO OUTPUT.  OTHER VALUES MEAN OUTPUT.
!     INTEGER NNZW
!        NUMBER OF NONZERO WEIGHTS.
!     LOGICAL STACK
!        A FLAG INDICATING WHETHER THIS ROUTINE USES THE STACK (TRUE)
!        OR NOT (FALSE).
!     REAL STS(53)
!        OUTPUT PARAMETER.  THE VECTOR OF THE 53 STATISTICS COMPUTED.
!     REAL SUMDA
!        THE SUM OF THE ABSOLUTE VALUES OF THE DIFFERENCES FROM
!        THE MEAN.
!     REAL SUMDI
!        THE SUM OF THE PRODUCTS OF THE INDEX AND DIFFERENCES.
!     REAL SUMD2
!        THE SUM OF THE SQUARE OF THE DIFFERENCES.
!     REAL SUMD3
!        THE SUM OF THE CUBE OF THE DIFFERENCES.
!     REAL SUMD4
!        THE SUM OF THE 4TH POWERS OF THE DIFFERENCES.
!     REAL SUMT1
!        THE TRIMMED UNWEIGHTED SIMPLE SUM OF ELEMENTS IN Y.
!     REAL WT(1)
!        THE DUMMY WEIGHTS VECTOR.
!     LOGICAL WTS
!        A FLAG INDICATING WHETHER THERE ARE WEIGHTS (TRUE)
!        OR NOT (FALSE).
!     REAL Y(N)
!        INPUT PARAMETER.  THE VECTOR OF DATA POINTS ON WHICH
!        THE STATISTICS ARE COMPUTED.  Y IS SORTED, BUT RESTORED
!        TO ITS ORIGINAL ORDER AFTERWARDS.
!
!
!     INITIALIZE NAME VECTORS
!
      DATA  NMSUB(1), NMSUB(2), NMSUB(3), NMSUB(4), NMSUB(5), NMSUB(6)
     &   /       'S',      'T',      'A',      'T',      'S',      ' '/
!
      DATA ALPHA /0.25E0/
      DATA IDP /4/
      DATA IINT /2/
      DATA WTS /.FALSE./
      DATA STACK /.TRUE./
!
!     CHECK FOR ERRORS IN THE INPUT PARAMETERS.
!
      CALL STATER(NMSUB, WT, N, LDSTAK, WTS, NNZW, STACK, IERR)
      IF (IERR.NE.0) THEN
!
!     SET UP THE OUTPUT UNIT NUMBER.
!
         CALL IPRINT(IPRT)
         WRITE (IPRT,1000)
         RETURN
      END IF
!
!     SET UP FRAMEWORK AREA
!
      CALL STKSET (LDSTAK, IDP)
      NALL0 = STKST(1)
!
!     SET UP LSORT, THE PERMUTATION VECTOR.
!
      LSORT = STKGET(N,IINT)
      CALL GENI(ISTAK(LSORT), N, 1, 1)
!
!     SORT THE VECTOR Y.
!
      CALL SRTIR(ISTAK(LSORT), N, Y)
!
!     COMPUTE THE STATISTICS WHICH USE A SORTED ARRAY.
!
      CALL STAT1(Y, N, STS(5), STS(34), STS(35), STS(6),
     &   STS(11), 10, 0.0E0, 0.0E0, STS(44))
!
!     CALCULATE SUMS OF THE SORTED ARRAY.
!
      CALL SUMBS(Y, N, 1, MID, N)
      CALL SUMSS(Y, N, 1, MID, N, STS(38), STS(39), STS(42),
     &   STS(3))
      STS(4) = STS(3)
      CALL SUMTS(Y, N, ALPHA, SUMT1, STS(7))
      STS(8) = STS(7)
      CALL SUMDS(Y, N, 1, MID, N, STS(3), SUMDA, SUMD2, SUMD3, SUMD4)
!
!     RESTORE THE VECTOR Y TO ITS ORIGINAL ORDER.
!
      CALL SRTRI(Y, N, ISTAK(LSORT))
!
!     COMPUTE REST OF STATISTICS.
!
      CALL SUMID(Y, N, STS(3), SUMDI)
      CALL STAT2(Y, N, STS, SUMDA, SUMDI, SUMD2, SUMD3, SUMD4)
      IF (NPRT.NE.0) CALL SUMOT(STS, N, N, WTS)
!
!     RETURN THE VECTOR LSORT.
!
      CALL STKCLR(NALL0)
      RETURN
!
!     FORMAT STATEMENTS.
!
 1000 FORMAT (/42H THE CORRECT FORM OF THE CALL STATEMENT IS//
     &   '       CALL STATS (Y, N, LDSTAK, STS, NPRT)')
      END
