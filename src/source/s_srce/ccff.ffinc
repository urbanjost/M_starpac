!CCFF
      subroutine ccff (yfft1, yfft2, n, lyfft, ldstak)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS IS THE USER CALLABLE ROUTINE FOR COMPUTING THE CROSS
!     CORRELATIONS OF TWO TIME SERIES USING THE SINGLETON FFT
!     (SHORT CALL).
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 21, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     integer&
     &   ldstak,lyfft,n
!
!  ARRAY ARGUMENTS
     real(kind=wp) ::&
     &   yfft1(*),yfft2(*)
!
!  SCALARS IN COMMON
     integer&
     &   ierr
!
!  ARRAYS IN COMMON
      double precision dstak(12)
!
!  LOCAL SCALARS
     real(kind=wp) ::&
     &   y1mean,y1sd,y2mean,y2sd
     integer&
    &   iccov,ifp,inlppc,iprt,iym,iymfft,jccov,jnlppc,lagmax,&
     &   ldsmin,m,nall0,nfft,work
     logical&
     &   isfft,islong
!
!  LOCAL ARRAYS
     real(kind=wp) ::&
     &   ccov(101,2,2),rhoc(201),rstak(12),sdrhoc(201),stak(12)
     integer&
     &   ndum(1)
     character&
     &   nmsub(6)*1
!
!  EXTERNAL FUNCTIONS
!      INTEGER
!     +   STKST
!      EXTERNAL STKST
!
!  EXTERNAL SUBROUTINES
!      EXTERNAL ACVFF,CCFER,CCFMNF,CCFOUT,FFTLEN,IPRINT,LDSCMP,SETLAG,
!     +   STKCLR,STKSET
!
!  INTRINSIC FUNCTIONS
      intrinsic sqrt
!
!  COMMON BLOCKS
      common /cstak/dstak
      common /errchk/ierr
!
!  EQUIVALENCES
      equivalence (dstak(1),rstak(1))
      equivalence (dstak(1),stak(1))
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL CCOV(101, 2, 2)
!        THE CCVF MATRIX.
!     DOUBLE PRECISION DSTAK(12)
!        THE DOUBLE PRECISION VERSION OF THE /CSTAK/ WORK AREA.
!     INTEGER ICCOV
!        THE ACTUAL FIRST DIMENSION OF THE ARRAY CCOV, AS
!        SPECIFIED IN THE USERS PROGRAM.
!     INTEGER IERR
!        THE INTEGER VALUE RETURNED BY THIS ROUTINE DESIGNATING
!        WHETHER ANY ERRORS WERE DETECTED IN THE PARAMETER LIST
!        IF IERR .EQ. 0, NO ERRORS WERE DETECTED
!        IF IERR .EQ. 1, ERRORS HAVE BEEN DETECTED
!     INTEGER IFP
!        AN INDICATOR FOR STACK ALLOCATION TYPE, WHERE IFP=3 INDICATES
!        SINGLE PRECISION AND IFP=4 INDICATES DOUBLE PRECISION.
!     INTEGER INLPPC
!        THE ACTUAL FIRST DIMENSION OF THE ARRAY NLPPC AS SPECIFIEC
!        IN THE USERS PROGRAM.
!     INTEGER IPRT
!        THE UNIT NUMBER USED FOR OUTPUT.
!     LOGICAL ISFFT
!        THE INDICATOR VARIABLE USED TO DESIGNATE WHETHER THE CALLING
!        ROUTINE HAS SUFFIX F (ISFFT = TRUE) OR NOT (ISFFT = FALSE)
!     LOGICAL ISLONG
!        THE INDICATOR VARIABLE USED TO DESIGNATE WHETHER THE CALLING
!        ROUTINE HAS SUFFIX S (ISLONG = TRUE) OR NOT (ISLONG = FALSE)
!     INTEGER IYMFFT
!        THE ACTUAL FIRST DIMENSION OF THE MATRIX YMFFT AS
!        SPECIFIED IN THE USERS PROGRAM.
!     INTEGER JCCOV
!        THE ACTUAL SECOND DIMENSION OF THE ARRAY CCOV, AS
!        SPECIFIED IN THE USERS PROGRAM.
!     INTEGER JNLPPC
!        THE SECOND DIMENSION OF THE ARRAY NLPPC AS SPECIFIED
!        IN THE USERS PROGRAM.
!     INTEGER LAGMAX
!        THE MAXIMUM LAG VALUE REQUESTED.
!     INTEGER LDSMIN
!        THE MINIMUM LENGTH ALLOWED FOR THE ARRAY DSTAK.
!     INTEGER LDSTAK
!        THE LENGTH OF THE ARRAY DSTAK.
!     INTEGER LYFFT
!        THE NUMBER OF LOCATIONS IN THE ARRAY YFFT1 AND YFFT2.
!     INTEGER M
!        THE NUMBER OF SERIES BEING COMPARED, IE THE
!        NUMBER OF COLUMNS OF DATA IN YM.
!     INTEGER N
!        THE INTEGER NUMBER OF OBSERVATIONS IN EACH SERIES
!     INTEGER NALL0
!        THE NUMBER OF OUTSTANDING STACK ALLOCATIONS
!     INTEGER NDUM(1)
!        A DUMMY ARRAY.
!     INTEGER NFFT
!        THE NUMBER OF OBSERVATIONS IN THE EXTENDED SERIES.
!     CHARACTER*1 NMSUB(6)
!        THE NAME OF THE SUBROUTINE CALLING THE ERROR CHECKING
!        SUBROUTINE.
!     REAL RHOC(201)
!        THE ARRAY CONTAINING THE CCF.
!     REAL RSTAK(12)
!        THE REAL VERSION OF THE /CSTAK/ WORK AREA.
!     REAL SDRHOC(201)
!        THE ARRAY CONTAINING THE SD OF THE CCF.
!     REAL STAK(12)
!        THE USED VERSION OF THE /CSTAK/ WORK AREA.
!     INTEGER WORK
!        THE STARTING LOCATION IN DSTAK FOR
!        THE WORK ARRAY NEEDED BY THE FFT.
!     REAL YFFT1(N), Y1MEAN, Y1SD
!        THE FIRST SERIES, AND ITS MEAN AND STANDARD DEVIATION.
!     REAL YFFT2(N), Y2MEAN, Y2SD
!        THE SECOND SERIES, AND ITS MEAN AND STANDARD DEVIATION.
!
!     SET UP NAME ARRAYS
!
     data&
    &  nmsub(1),  nmsub(2),  nmsub(3),  nmsub(4),  nmsub(5),  nmsub(6)&
     & /     'C',       'C',       'F',       'F',       ' ',       ' '/
!
!     SET UP FOR ERROR CHECKING
!
      ierr = 0
      iccov = 101
      inlppc = 1
      iym = n
      jccov = 2
      jnlppc = 1
      lagmax = 1
      iymfft = lyfft
      m = 2
      nfft = n
      isfft = .true.
      islong = .false.
!
      if (n.ge.3) then
!
!     SET LARGEST LAG VALUE TO BE USED
!
        call setlag(n, lagmax)
!
!     SET LENGTH OF THE EXTENDED SERIES
!
        call fftlen(n+lagmax, 4, nfft)
      end if
!
      call ldscmp(1, 0, 0, 0, 0, 0, 'S', nfft, ldsmin)
!
     call ccfer(nmsub, n, lagmax, ldstak, ldsmin, iccov, jccov,&
     &  inlppc, jnlppc, m, lyfft, nfft, iym, iymfft, isfft, islong)
!
!     CHECK WHETHER AN ERROR HAS BEEN DETECTED
!
      if (ierr.eq.0) then
!
!       SET UP THE WORK AREA.
!
        call stkset (ldstak, 4)
        nall0 = stkst(1)
!
        ifp = 3
!
        work = stkget(nfft, ifp)
!
        if (ierr.eq.0) then
!
!         COMPUTE THE SERIES ACVF AND SD
!
         call acvff (yfft1, n, nfft, y1mean, ccov(1,1,1), lagmax, 101,&
     &       lyfft, stak(work), nfft)
          y1sd = sqrt(ccov(1,1,1) * n / (n-1))
!
         call acvff (yfft2, n, nfft, y2mean, ccov(1,2,2), lagmax, 101,&
     &       lyfft, stak(work), nfft)
          y2sd = sqrt(ccov(1,2,2) * n / (n-1))
!
!         CALL ROUTINE FOR MAIN AUTOCORRELATION COMPUTATIONS.
!
         if (ccov(1,1,1)*ccov(1,2,2) .ne. 0.0e0)&
    &       call ccfmnf (yfft1, yfft2, n, nfft, lagmax, 2*lagmax+1,&
    &       ccov(1,1,1), ccov(1,2,2), ccov(1,1,2), ccov(1,2,1), 101,&
     &       rhoc, sdrhoc, 1, lyfft, stak(work), nfft)
!
!         CALL ROUTINE TO PRINT OUT AUTOCORRELATIONS
!
         call ccfout (1, y1mean, y1sd, n, n, 2, y2mean, y2sd, n,&
    &       n, lagmax, 2*lagmax+1, rhoc, sdrhoc, .false., ndum, ndum,&
     &       1, 0.0e0, 0.0e0, .false.)
        end if
!
        call stkclr(nall0)
      end if
!
      if (ierr.ne.0) then
!
!     PRINT PROPER CALL SEQUENCE AND RETURN
!
        ierr = 1
        call iprint (iprt)
        write (iprt, 1000)
      end if
!
      return
!
!     FORMAT STATEMENTS
!
1000 format(/42h the correct form of the call statement is//&
     &  '       CALL CCFF (YFFT1, YFFT2, N, LYFFT, LDSTAK)')
      end
