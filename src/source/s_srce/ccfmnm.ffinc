!CCFMNM
      SUBROUTINE CCFMNM (Y1, Y1MISS, Y2, Y2MISS, N, LAGMAX, NCC,
     &   CCOV11, CCOV22, CCOV12, CCOV21, ICCOV, NLPP11, NLPP22,
     &   NLPP12, NLPP21, INLPPC, Y1MEAN, Y2MEAN, RHOC, SDRHOC, NPRT,
     &   LAGLST)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS IS THE MAIN SUBROUTINE FOR COMPUTING CROSS CORRELATIONS AND
!     THEIR STANDARD ERRORS WHEN MISSING DATA ARE INVOLVED.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 21, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) ::
     &   Y1MEAN,Y1MISS,Y2MEAN,Y2MISS
      INTEGER
     &   ICCOV,INLPPC,LAGLST,LAGMAX,N,NCC,NPRT
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   CCOV11(ICCOV),CCOV12(ICCOV),CCOV21(ICCOV),CCOV22(ICCOV),
     &   RHOC(NCC),SDRHOC(NCC),Y1(N),Y2(N)
      INTEGER
     &   NLPP11(INLPPC),NLPP12(INLPPC),NLPP21(INLPPC),
     &   NLPP22(INLPPC)
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     &   FAC,FPLM
      INTEGER
     &   I,I0,IM,IP
!
!  EXTERNAL FUNCTIONS
      REAL(KIND=WP) ::
     &   R1MACH
!       EXTERNAL R1MACH
!
!  EXTERNAL SUBROUTINES
!       EXTERNAL CCFSDM,CCVFM
!
!  INTRINSIC FUNCTIONS
      INTRINSIC SQRT
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL CCOV11(ICCOV), CCOV12(ICCOV)
!     REAL CCOV21(ICCOV), CCOV22(ICCOV)
!        THE ARRAY CONTAINING THE AUTOCOVARIANCE AND CROSS COVARIANCE
!        ESTIMATES FOR SERIES 1 AND 2.
!     REAL FAC
!        THE INVERSE OF THE SQUARE ROOT OF THE PRODUCT OF THE
!        AUTOCOVARIANCES AT LAG ZERO.
!     REAL FPLM
!        THE FLOATING POINT LARGEST MAGNITUDE.
!     INTEGER I
!        THE INDEXING VARIABLE FOR THE LAG.
!     INTEGER ICCOV
!        THE DIMENSION OF THE COVARIANCE VECTORS.
!     INTEGER IM
!        THE LOCATIONS IN THE VARIOUS CCF RELATED ARRAYS OF LAG -I.
!     INTEGER INLPPC
!        THE DIMENSION OF THE LAGGED PRODUCT PAIR COUNT VECTORS.
!     INTEGER IP
!        THE LOCATION IF THE VARIOUS CCF RELATED ARRAYS OF LAG I.
!     INTEGER I0
!        THE LOCATION IF THE VARIOUS CCF RELATED ARRAYS OF LAG 0.
!     INTEGER LAGLST
!        THE LAST LAG BEFORE MISSING DATA CAUSED THE ACVF OF EITHER
!        SERIES 1 OR 2 NOT TO BE COMPUTED.
!     INTEGER LAGMAX
!        THE MAXIMUM LAG VALUE TO BE USED.
!     INTEGER N
!        THE INTEGER NUMBER OF OBSERVATIONS IN THE SERIES
!     INTEGER NCC
!        THE NUMBER OF CROSS CORRELATIONS TO BE COMPUTED.
!     INTEGER NLPP11(INLPPC), NLPP12(INLPPC), NLPP21(INLPPC),
!    +        NLPP22(INLPPC)
!        THE NUMBERS OF LAGGED PRODUCT PAIRS USED TO COMPUTE
!        THE AUTOCOVARIANCE AND CROSS COVARIANCE ESTIMATES.
!     INTEGER NPRT
!        THE VARIABLE USED TO CONTROL PRINTED OUTPUT.
!     REAL RHOC(NCC)
!        THE ARRAY IN WHICH THE AUTO AND CROSS CORRELATIONS ARE STORED
!     REAL SDRHOC(NCC)
!        THE ARRAY CONTAINING THE STD. ERRORS OF THE CROSS CORRELATIONS.
!        ARE STORED
!     REAL Y1(N), Y1MEAN, Y1MISS
!        THE FIRST SERIES, AND ITS MEAN, AND MISSING VALUE CODE.
!     REAL Y2(N), Y2MEAN, Y2MISS
!        THE SECOND SERIES, AND ITS MEAN, AND MISSING VALUE CODE.
!
      FPLM = R1MACH(2)
!
!     COMPUTE AUTOCORRELATIONS AND STANDARD DEVIATION OF THE SERIES.
!
      CALL CCVFM(Y1, Y1MISS, Y2, Y2MISS, N, LAGMAX, Y1MEAN, Y2MEAN,
     &   CCOV12, CCOV21, ICCOV, NLPP12, NLPP21)
!
      IF (NPRT .EQ. 0 .OR. NLPP11(1) .EQ. 0) RETURN
      IF (CCOV11(1) *CCOV22(1) .EQ. 0.0E0) RETURN
!
      FAC = 1.0E0 / SQRT(CCOV11(1) * CCOV22(1))
!
      I0 = LAGMAX + 1
      RHOC(I0) = FPLM
      IF (NLPP12(1).GE.1) RHOC(I0) = CCOV12(1) * FAC
!
      DO 10 I = 1, LAGMAX
         IP = I0 + I
         RHOC(IP) = FPLM
         IF (NLPP12(I+1).GE.1) RHOC(IP) = CCOV12(I+1) * FAC
!
         IM = I0 - I
         RHOC(IM) = FPLM
         IF (NLPP21(I+1).GE.1) RHOC(IM) = CCOV21(I+1) * FAC
   10 CONTINUE
!
!     COMPUTE STANDARD ERROR OF AUTOCORRELATIONS.
!
      CALL CCFSDM (CCOV11, CCOV22, SDRHOC, LAGMAX, NCC, LAGLST, N,
     &   NLPP12, NLPP21, ICCOV, INLPPC)
!
      RETURN
      END
