!FLTSL
      SUBROUTINE FLTSL (Y, N, K, H, YF, NYF)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE FILTERS THE INPUT SERIES Y USING THE K TERMS
!     OF H, COPYING THE FILTERED SERIES INTO YF.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DEVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 26, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      INTEGER
     +   K,N,NYF
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP)
     +   H(K),Y(N),YF(N)
!
!  LOCAL SCALARS
      REAL(KIND=WP)
     +   TEMP
      INTEGER
     +   I,I1,IHM,IHP,IKMID,IM,IP,J,KHALF,KMID
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL H(K)
!        THE ARRAY IN WHICH THE FILTER COEFFICIENTS ARE STORED.
!     INTEGER I, IHM, IHP, IKMID, IM, IP
!        INDEXING VARIABLES.
!     INTEGER J
!        AN INDEXING VARIABLE.
!     INTEGER K
!        THE NUMBER OF FILTER TERMS.
!     INTEGER KHALF, KMID
!        THE HALF LENGTH OF THE FILTER AND THE MIDPOINT OF THE FILTER.
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS IN THE SERIES Y.
!     INTEGER NYF
!        THE NUMBER OF OBSERVATIONS IN THE FILTERED SERIES YF.
!     REAL TEMP
!        A TEMPORY STORAGE LOCATION.
!     REAL Y(N)
!        THE VECTOR CONTAINING THE OBSERVED TIME SERIES.
!     REAL YF(N)
!        THE VECTOR IN WHICH THE FILTERED SERIES IS RETURNED.
!
      DO 10 I = 1, N
         YF(I) = Y(I)
   10 CONTINUE
!
      NYF = N - (K - 1)
!
      KHALF = (K - 1) / 2
!
      KMID = KHALF + 1
!
      DO 30 I = 1, NYF
         IKMID = I + KHALF
         TEMP = H(KMID) * YF(IKMID)
         DO 20 J = 1, KHALF
            IP = IKMID + J
            IHP = KMID + J
            IM = IKMID - J
            IHM = KMID - J
            TEMP = TEMP + H(IHP)*YF(IP) + H(IHM)*YF(IM)
   20    CONTINUE
         YF(I) = TEMP
   30 CONTINUE
!
      I1 = NYF + 1
!
      DO 40 I = I1, N
         YF(I) = 0.0E0
   40 CONTINUE
      RETURN
      END
