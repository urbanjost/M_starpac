!UASFS
      SUBROUTINE UASFS (YFFT, N, LYFFT, LDSTAK, IAR, PHI, LAGMAX, LAG,
     +  NF, FMIN, FMAX, NPRT, SPCA, SPCF, FREQ)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS IS THE USER CALLABLE ROUTINE FOR AUTOREGRESSIVE
!     SPECTRUM ESTIMATION USING THE FFT (LONG CALL).
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  DECEMBER 7, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP)
     +   FMAX,FMIN
      INTEGER
     +   IAR,LAG,LAGMAX,LDSTAK,LYFFT,N,NF,NPRT
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP)
     +   FREQ(*),PHI(*),SPCA(*),SPCF(*),YFFT(*)
!
!  SCALARS IN COMMON
      INTEGER
     +   IERR
!
!  ARRAYS IN COMMON
      DOUBLE PRECISION DSTAK(12)
!
!  LOCAL SCALARS
      REAL(KIND=WP)
     +   ALPHA,DELTA,FMN,FMX,VAR,YMEAN
      INTEGER
     +   ACOV,AIC,FTEST,IA,IFP,IO,IPRT,ISORT,ISYM,LACOV,LAIC,
     +   LDSMIN,LPCV,LPHI,LSPC,LWORK,NALL0,NFFT,WORK,XAXIS,YAXIS
!
!  LOCAL ARRAYS
      REAL(KIND=WP)
     +   RSTAK(12)
      INTEGER
     +   ISTAK(12)
      LOGICAL
     +   OPTION(4)
      CHARACTER
     +   NMSUB(6)*1
!
!  EXTERNAL FUNCTIONS
      INTEGER
     +   STKGET,STKST
      EXTERNAL STKGET,STKST
!
!  EXTERNAL SUBROUTINES
      EXTERNAL ACVFF,AMEAN,IPRINT,LDSCMP,PARZEN,SETESL,STKCLR,STKSET,
     +   UASDV,UASER,UASVAR
!
!  INTRINSIC FUNCTIONS
      INTRINSIC MAX,MIN
!
!  COMMON BLOCKS
      COMMON /CSTAK/DSTAK
      COMMON /ERRCHK/IERR
!
!  EQUIVALENCES
      EQUIVALENCE (DSTAK(1),RSTAK(1))
      EQUIVALENCE (DSTAK(1),ISTAK(1))
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     INTEGER ACOV
!        THE STARTING LOCATION IN RSTAK FOR THE ARRAY OF
!        THE AUTOCOVARIANCE ARRAY.
!     INTEGER AIC
!        THE STARTING LOCATION IN THE STACK FOR
!        THE ARRAY CONTAINING THE AIC.
!     REAL ALPHA
!        THE DESIRED CONFIDENCE LEVEL.
!     REAL DELTA
!        THE SAMPLING INTERVAL.
!     DOUBLE PRECISION DSTAK(12)
!        THE DOUBLE PRECISION VERSION OF THE /CSTAK/ WORK AREA.
!     REAL FMAX, FMIN
!        THE MAXIMUM AND MINIMUM FREQUENCY FOR WHICH THE
!        SPECTRUM ESTIMATES ARE TO BE COMPUTED.
!     REAL FMN, FMX
!        THE MAXIMUM AND MINIMUM FREQUENCY ACTUALLY USED.
!     REAL FREQ(NF)
!        THE VECTOR OF FREQUENCIES AT WHICH THE SPECTRUM IS TO BE
!        ESTIMATED.
!     INTEGER FTEST
!        THE STARTING LOCATION IN THE STACK FOR
!        THE ARRAY IN WHICH THE F RATIO AND PROBABILITY ARE STORED.
!     INTEGER IA
!        A VARIABLE USED TO DETERMINE THE AMOUNT OF STORAGE REQUIRED,
!        BASED ON WHETHER OR NOT THE MODEL ORDER IS TO BE SELECTED OR
!        HAS BEEN PROVIDED.
!     INTEGER IAR
!        THE ORDER OF THE AUTOREGRESSIVE PROCESS CHOSEN.
!     INTEGER IERR
!        THE INTEGER VALUE RETURNED BY THIS ROUTINE DESIGNATING
!        WHETHER ANY ERRORS WERE DETECTED IN THE PARAMETER LIST
!        IF IERR .EQ. 0, NO ERRORS WERE DETECTED
!        IF IERR .NE. 0, ERRORS HAVE BEEN DETECTED
!     INTEGER IFP
!        AN INDICATOR FOR STACK ALLOCATION TYPE, WHERE IFP=3 INDICATES
!        REAL AND IFP=4 INDICATES DOUBLE PRECISION.
!     INTEGER IO
!        A VARIABLE USED TO DETERMINE THE AMOUNT OF STORAGE REQUIRED,
!        BASED ON PRINTED OUTPUT REQUESTED.
!     INTEGER IPRT
!        THE LOGICAL UNIT USED FOR PRINTED OUTPUT.
!     INTEGER ISORT
!        THE STARTING LOCATION IN ISTAK FOR
!        AN ARRAY USED FOR SORTING.
!     INTEGER ISTAK(12)
!        THE INTEGER VERSION OF THE /CSTAK/ WORK AREA.
!     INTEGER ISYM
!        THE STARTING LOCATION IN ISTAK FOR
!        THE ARRAY CONTAINING THE CODE FOR THE PLOT SYMBOLS.
!     INTEGER LACOV
!        THE LENGTH OF THE COVARIANCE ARRAYS.
!     INTEGER LAG
!        THE LAG WINDOW TRUNCATION POINT USED FOR A SPECIFIC WINDOW.
!     INTEGER LAGMAX
!        THE MAXIMUM LAG VALUE TO BE USED.
!     INTEGER LAIC
!        THE LENGTH OF THE ARRAY AIC.
!     INTEGER LDSMIN
!        THE MINIMUM LENGTH ALLOWED FOR DSTAK.
!     INTEGER LDSTAK
!        THE LENGTH OF THE VECTOR DSTAK IN COMMON CSTAK.
!     INTEGER LPCV
!        THE LENGTH OF THE PLOT CO-ORDINATE VECTORS.
!     INTEGER LPHI
!        THE LENGTH OF THE VECTOR PHI.
!     INTEGER LSPC
!         THE LENGTH OF THE SPECTRUM ARRAYS.
!     INTEGER LWORK
!        THE ACTUAL LENGTH OF THE WORK ARRAY.
!     INTEGER LYFFT
!        THE LENGTH OF THE VECTOR YFFT.
!     INTEGER N
!        THE INTEGER NUMBER OF OBSERVATIONS IN THE SERIES
!     INTEGER NALL0
!        THE NUMBER OF STACK ALLOCATIONS OUTSTANDING WHEN THIS ROUTINE
!        WAS CALLED.
!     INTEGER NF
!        THE NUMBER OF FREQUENCIES FOR WHICH THE SPECTRUM ESTIMATES
!        ARE TO BE ESTIMATED.
!     INTEGER NFFT
!        THE NUMBER OF OBSERVATIONS IN THE EXTENDED SERIES.
!     CHARACTER*1 NMSUB(6)
!        THE ARRAY CONTAINING THE NAME OF THIS SUBROUTINE.
!     INTEGER NPRT
!        A CODE USED TO SPECIFY THE TYPE OF PLOT, WHERE IF
!        NPRT < 0 THE PLOT IS DECIBLES/LINEAR
!        NPRT = 0 THE PLOT IS SUPPRESSED
!        NPRT > 0 THE PLOT IS LOG/LINEAR
!     LOGICAL OPTION(4)
!        AN INDICATOR ARRAY USED TO DESIGNATE WHETHER ANY OF THE
!        FOUR POSSIBLE OPTIONS (F, M, V, OR S) HAVE BEEN USED (TRUE)
!        OR NOT (FALSE).
!     EXTERNAL PARZEN
!        THE TYPE OF WINDOW TO BE USED.
!     REAL PHI(LAGMAX)
!        THE ARRAY OF AUTOREGRESSIVE COEFFICIENTS FOR THE
!        SELECTED ORDER.
!     REAL RSTAK(12)
!        THE REAL VERSION OF THE /CSTAK/ WORK AREA.
!     REAL SPCA(NF)
!        THE ARAY CONTAINING THE AUTOREGRESSIVE SPECTRUM ESTIMATES.
!     REAL SPCF(NF)
!        THE ARRAY CONTAINING THE FOURIER SPECTRUM ESTIMATES.
!     REAL VAR
!        THE ONE STEP PREDICTION VARIANCE.
!     INTEGER WORK
!        THE STARTING LOCATION IN THE STACK FOR
!        THE WORK ARRAY.
!     INTEGER XAXIS
!        THE STARTING LOCATION IN RSTAK FOR
!        THE X AXIS VALUES FOR THE SPECTRUM PLOT.
!     INTEGER YAXIS
!        THE STARTING LOCATION IN RSTAK FOR
!        THE Y AXIS VALUES FOR THE SPECTRUM PLOT.
!     REAL YFFT(LYFFT)
!        THE ARRAY CONTAINING THE OBSERVED TIME SERIES.
!     REAL YMEAN
!        THE MEAN OF THE OBSERVED TIME SERIES
!
!
!     SET UP NAME ARRAYS
!
      DATA
     +  NMSUB(1),  NMSUB(2),  NMSUB(3),  NMSUB(4),  NMSUB(5),  NMSUB(6)
     + /     'U',       'A',       'S',       'F',       'S',       ' '/
!
      IFP = 3
!
!     SET UP FOR ERROR CHECKING
!
      OPTION(1) = .TRUE.
      OPTION(2) = .FALSE.
      OPTION(3) = .FALSE.
      OPTION(4) = .TRUE.
!
!     SET EXTENDED SERIES LENGTH
!
      CALL SETESL(N+LAGMAX, 4, NFFT)
!
      IO = 1
      IF (NPRT .EQ. 0) IO = 0
      IA = 1
      IF (IAR .NE. 0) IA = 0
!
      CALL LDSCMP(7, 0, IO*(2*NF+5), 0, 0, 0, 'S',
     +   LAGMAX+1+NFFT+IA*(3*LAGMAX+1)+IO*(4*NF+10), LDSMIN)
!
      CALL UASER(NMSUB, N, YFFT, IAR, PHI, LAGMAX, LAG, LACOV,
     +   NF, LDSTAK, LDSMIN, LYFFT, NFFT, OPTION)
!
    5 IF (IERR.NE.0) THEN
         IERR = 1
         CALL IPRINT (IPRT)
         WRITE (IPRT, 1000)
         RETURN
      END IF
!
!     SET SIZE OF WORK AREA.
!
      CALL STKSET (LDSTAK, 4)
!
!     SAVE NUMBER OF OUTSTANDING STACK ALLOCATIONS.
!
      NALL0 = STKST(1)
!
!     SET VARIOUS PROGRAM PARAMETERS.
!
      LSPC = NF
      LPCV = 2*NF + 5
      LPHI = LAGMAX
      LACOV = LAGMAX + 1
      LWORK = NFFT
!
      FMN = MAX(FMIN, 0.0E0)
      FMX = MIN(FMAX, 0.5E0)
      IF (FMN.GE.FMX) THEN
        FMN = 0.0E0
        FMX = 0.5E0
      END IF
!
      ALPHA = 0.95E0
      DELTA = 1.0E0
!
      IF (IAR.GE.1) THEN
!
!     USER HAS CHOSEN ORDER AND SUPPLIED COEFFICIENTS.
!     COMPUTE RESIDUAL VARIANCE.
!
         CALL AMEAN(YFFT, N, YMEAN)
         CALL UASVAR (YFFT, YMEAN, N, IAR, PHI, VAR)
      END IF
!
!     COMPUTE AUTOCOVARIANCES
!
      ACOV = STKGET(LACOV, IFP)
      WORK = STKGET(LWORK, IFP)
!
      CALL ACVFF (YFFT, N, NFFT, YMEAN, RSTAK(ACOV), LAGMAX, LAGMAX+1,
     +   LYFFT, RSTAK(WORK), NFFT)
!
!     SET UP ADDITIONAL STACK WORK AREA, IF NEEDED.
!
      IF (IAR.EQ.0) THEN
         LAIC = LAGMAX+1
         AIC = STKGET(LAIC, IFP)
         FTEST = STKGET(2*LAGMAX, IFP)
      ELSE
         LAIC = LWORK
         AIC = WORK
         FTEST = WORK
      END IF
      IF (NPRT.NE.0) THEN
         XAXIS = STKGET(LPCV, IFP)
         YAXIS = STKGET(LPCV, IFP)
         ISYM = STKGET(LPCV, 2)
         ISORT = ISYM
      ELSE
         XAXIS = WORK
         YAXIS = WORK
         ISYM = WORK
         ISORT = WORK
      END IF
!
      IF (IERR.EQ.1) GO TO 5
!
!     CALL THE MAIN DRIVER FOR AUTOREGRESSIVE SPECTRUM ROUTINES.
!
      CALL UASDV(RSTAK(ACOV), SPCA, SPCF, LSPC, IAR, PHI, NF, FMN,
     +   FMX, FREQ, N, LAGMAX, RSTAK(FTEST), RSTAK(AIC), RSTAK(WORK),
     +   LACOV, LWORK, DELTA, ISTAK(ISORT), ISTAK(ISYM), RSTAK(XAXIS),
     +   RSTAK(YAXIS), LPCV, ALPHA, LAG, LAIC, LPHI, NPRT, VAR, PARZEN,
     +   NMSUB)
!
      CALL STKCLR(NALL0)
!
      RETURN
!
!     FORMAT STATEMENTS
!
 1000 FORMAT (/42H THE CORRECT FORM OF THE CALL STATEMENT IS//
     + '       CALL UASFS (YFFT, N, LYFFT, LDSTAK,'/
     + '      +            IAR, PHI, LAGMAX, LAG, NF, FMIN, FMAX, NPRT'/
     + '      +            SPCA, SPCF, FREQ)')
      END
