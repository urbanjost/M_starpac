!NLDRVA
     subroutine nldrva (mdl, drv, done, ifixd, par, npar, xm, n, m,&
     &   ixm, pv, d, weight, wt, lwt, stp, lstp, scl, lscl)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE COMPUTES THE ANALYTIC DERIVATIVE MATRIX (JACOBIAN)
!     USING USER-SUPPLIED ROUTINE DERIV.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  OCTOBER 3, 1983
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     integer&
     &   ixm,lscl,lstp,lwt,m,n,npar
     logical&
     &   done,weight
!
!  ARRAY ARGUMENTS
     real(kind=wp) ::&
    &   d(n,npar),par(npar),pv(n),scl(lscl),stp(lstp),wt(lwt),&
     &   xm(ixm,m)
     integer&
     &   ifixd(npar)
!
!  SUBROUTINE ARGUMENTS
       external drv,mdl
!
!  SCALARS IN COMMON
     integer&
     &   ierr
!
!  LOCAL SCALARS
     real(kind=wp) ::&
     &   wtsqrt
     integer&
     &   i,j,jpk
!
!  INTRINSIC FUNCTIONS
      intrinsic sqrt
!
!  COMMON BLOCKS
      common /errchk/ierr
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL(KIND=WP) :: D(N,NPAR)
!        THE FORWARD DIFFERENCE QUOTIENT APPROXIMATION TO THE
!        DERIVATIVE OF THE MODEL WITH RESPECT TO THE JTH PARAMETER.
!     EXTERNAL DRV
!        THE NAME OF THE USER SUPPLIED SUBROUTINE WHICH COMPUTES THE
!        DERIVATIVE (JACOBIAN) MATRIX OF THE MODEL.
!     LOGICAL DONE
!        THE VARIABLE USED TO INDICATE WHETHER THIS IS THE FINAL
!        COMPUTATION OF THE JACOBIAN OR NOT.
!     INTEGER I
!        AN INDEXING VARIABLE.
!     INTEGER IERR
!        THE INTEGER VALUE RETURNED BY THIS ROUTINE DESIGNATING
!        WHETHER ANY ERRORS WERE DETECTED IN THE PARAMETER LIST.
!        IF IERR .EQ. 0, NO ERRORS WERE DETECTED.
!        IF IERR .GE. 1, ERRORS WERE DETECTED.
!     INTEGER IFIXD(NPAR)
!        THE INDICATOR VALUES USED TO DESIGNATE WHETHER THE
!        PARAMETERS ARE TO BE OPTIMIZED OR ARE TO BE HELD FIXED.
!        IF IFIXED(I).NE.0, THEN PAR(I) WILL BE HELD FIXED.
!        IF IFIXED(I).EQ.0, THEN PAR(I) WILL BE OPTIMIZED.
!     INTEGER IXM
!        THE FIRST DIMENSION OF THE INDEPENDENT VARIABLE ARRAY.
!     INTEGER J
!        THE INDEX OF THE PARAMETER BEING EXAMINED.
!     INTEGER JPK
!        THE INDEX OF THE PACKED PARAMETERS.
!     INTEGER LSCL
!        THE ACTUAL LENGTH OF THE VECTOR SCL.
!     INTEGER LSTP
!        THE ACTUAL LENGTH OF THE VECTOR STP.
!     INTEGER LWT
!        THE ACTUAL LENGTH OF THE VECTOR WT.
!     INTEGER M
!        THE NUMBER OF INDEPENDENT VARIABLES.
!     EXTERNAL MDL
!        THE NAME OF THE USER SUPPLIED SUBROUTINE WHICH COMPUTES THE
!        PREDICTED VALUES BASED ON THE CURRENT PARAMETER ESTIMATES.
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS.
!     INTEGER NPAR
!        THE NUMBER OF PARAMETERS IN THE MODEL.
!     REAL(KIND=WP) :: PAR(NPAR)
!        THE CURRENT ESTIMATES OF THE PARAMETERS.
!     REAL(KIND=WP) :: PV(N)
!        THE PREDICTED VALUES BASED ON THE CURRENT PARAMETER ESTIMATES
!     REAL(KIND=WP) :: SCL(LSCL)
!        THE SCALE VALUES.
!     REAL(KIND=WP) :: STP(LSTP)
!        THE SELECTED RELATIVE STEP SIZES.
!     LOGICAL WEIGHT
!        THE VARIABLE USED TO INDICATE WHETHER WEIGHTED ANALYSIS IS TO
!        BE PERFORMED (TRUE) OR NOT (FALSE).
!     REAL(KIND=WP) :: WT(LWT)
!        THE USER SUPPLIED WEIGHTS.
!     REAL(KIND=WP) :: WTSQRT
!        THE SQUARE ROOT OF THE ITH WEIGHT.
!     REAL(KIND=WP) :: XM(IXM,M)
!        THE ARRAY IN WHICH ONE ROW OF THE INDEPENDENT VARIABLE ARRAY
!        IS STORED.
!
!+++++++++++++++++++++++++++++++  BODY  ++++++++++++++++++++++++++++++++
!
!     COMPUTE JACOBIAN
!
      call drv(par, npar, xm, n, m, ixm, d)
!
      jpk = 0
!
      do 20 j=1,npar
         if (ifixd(j).ne.0) go to 20
         jpk = jpk + 1
         do 10 i=1,n
            wtsqrt = 1.0e0
            if (weight .and. (.not.done)) wtsqrt = sqrt(wt(i))
            d(i,jpk) = -wtsqrt*d(i,j)
   10    continue
   20 continue
!
      return
!
      end
