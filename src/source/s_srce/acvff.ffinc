!ACVFF
     subroutine acvff (yfft, n, nfft, ymean, acov, lagmax, lacov,&
     &   lyfft, work, lwork)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE COMPUTES THE ACVF OF A SERIES USING TWO
!     PASSES OF A FFT.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 21, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     real(kind=wp) ::&
     &   ymean
     integer&
     &   lacov,lagmax,lwork,lyfft,n,nfft
!
!  ARRAY ARGUMENTS
     real(kind=wp) ::&
     &   acov(*),work(*),yfft(*)
!
!  LOCAL SCALARS
     real(kind=wp) ::&
     &   fac
     integer&
     &   i,isn,n1,nf,nfft2
!
!  EXTERNAL SUBROUTINES
!       EXTERNAL AMEAN,FFT,FFTCT,REALTR
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL ACOV(LACOV)
!        THE ARRAY IN WHICH THE AUTOCOVARIANCES ARE STORED
!     REAL FAC
!        A FACTOR USED IN THE COMPUTATIONS.
!     INTEGER I
!        AN INDEX VARIABLE.
!     INTEGER ISN
!        AN INDICATOR VARIABLE USED BY THE FFT ROUTINES.
!     INTEGER LACOV
!        THE LENGTH OF THE VECTOR ACOV.
!     INTEGER LAGMAX
!        THE MAXIMUM LAG TO BE USED.
!     INTEGER LWORK
!        THE LENGTH OF THE VECTOR WORK.
!     INTEGER LYFFT
!        THE LENGTH OF THE VECTOR YFFT.
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS IN THE SERIES
!     INTEGER NF
!        THE NUMBER OF FOURIER FREQUENCIES.
!     INTEGER NFFT
!        THE NUMBER OF OBSERVATIONS IN THE EXTENDED SERIES.
!     INTEGER NFFT2
!        THE EFFECTIVE NUMBER OF OBSERVATIONS FOR THE FFT TRANSFORM.
!     INTEGER N1
!        THE VALUE N + 1.
!     REAL WORK(LWORK)
!        THE WORK AREA FOR THE COMPUTATIONS.
!     REAL YFFT(LYFFT)
!        THE VECTOR CONTAINING THE OBSERVED SERIES
!     REAL YMEAN
!        THE MEAN OF THE OBSERVED TIME SERIES
!
!     COMPUTE ARITHMETIC MEAN
!
      call amean(yfft, n, ymean)
!
!     SUBTRACT THE MEAN FROM THE SERIES
!
      do 10 i = 1, n
         yfft(i) = yfft(i) - ymean
   10 continue
!
!     APPEND ZEROS
!
      n1 = n + 1
!
      do 20 i = n1, nfft
         yfft(i) = 0.0e0
   20 continue
!
!     COMPUTE AUTOCOVARIANCES.
!
      isn = 2
      nfft2 = (nfft - 2)/ 2
!
      call fft(yfft(1), yfft(2), nfft2, nfft2, nfft2, isn)
      call realtr (yfft(1), yfft(2), nfft2, isn)
!
      nf = nfft2 + 1
!
      do 30 i = 1, nf
         work(i) = yfft(2*i-1)*yfft(2*i-1) + yfft(2*i)*yfft(2*i)
   30 continue
!
      call fftct (work, nfft2, lwork)
!
      fac = 1.0e0 / (4 * (nfft - 2) * n)
!
      acov(1) = work(1) * fac
!
      do 40 i = 1, lagmax
         acov(i+1) = work(i+1) * fac
   40 continue
!
      return
      end
