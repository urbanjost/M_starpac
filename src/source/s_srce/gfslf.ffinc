!GFSLF
      SUBROUTINE GFSLF (H, K)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS IS THE USER CALLABLE ROUTINE FOR COMPUTING THE
!     GAIN FUNCTION OF A SYMMETRIC LINEAR FILTER (SHORT CALL).
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 26, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      INTEGER
     &   K
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   H(*)
!
!  SCALARS IN COMMON
      INTEGER
     &   IERR
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     &   DELTA,FMAX,FMIN,YPLTMN,YPLTMX
      INTEGER
     &   IGFERR,IPRT,NF,NORD,NPRT
      LOGICAL
     &   ERR01,ERR02,ERR03,HEAD,SYM
!
!  LOCAL ARRAYS
      REAL(KIND=WP) ::
     &   FREQ(101),GAIN(101),XORD(101),YORD(101)
      INTEGER
     &   ISORT(101)
      CHARACTER
     &   LH(8)*1,LK(8)*1,NMSUB(6)*1
!
!  EXTERNAL SUBROUTINES
!      EXTERNAL EISGE,ERIODD,ERSLF,GFORD,GFOUT,GFSEST,IPRINT,SETFRQ
!
!  COMMON BLOCKS
      COMMON /ERRCHK/IERR
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL DELTA
!        THE SAMPLING INTERVAL.
!     LOGICAL ERR01, ERR02, ERR03
!        VALUES INDICATING WHETHER AN ERROR WAS DETECTED (TRUE) OR NOT
!        (FALSE).
!     REAL FMAX, FMIN
!        THE MINIMUM AND MAXIMUM FREQUENCY FOR WHICH THE GAIN
!        FUNCTION IS TO BE ESTIMATED.
!     REAL FREQ(101)
!        THE VECTOR OF FREQUENCIES AT WHICH THE GAIN FUNCTION
!        HAS BEEN ESTIMATED.
!     REAL GAIN(101)
!        THE VECTOR IN WHICH THE GAIN FUNCTION ESTIMATES ARE
!        STORED.
!     REAL H(K)
!        THE ARRAY OF SYMMETRIC LINEAR FILTER COEFFICIENTS.
!     LOGICAL HEAD
!        A FLAG INDICATING WHETHER THE HEADING SHOULD BE PRINTED
!        (TRUE) OR NOT (FALSE).  IF A HEADING IS PRINTED, THE VALUE
!        OF HEAD WILL BE CHANGED TO FALSE.
!     INTEGER IERR
!        THE INTEGER VALUE RETURNED BY THIS ROUTINE DESIGNATING
!        WHETHER ANY ERRORS WERE DETECTED IN THE PARAMETER LIST.
!        IF IERR .EQ. 0, NO ERRORS WERE DETECTED.
!        IF IERR .EQ. 1, ERRORS HAVE BEEN DETECTED.
!     INTEGER IGFERR
!        AN ERROR FLAG INDICATING WHETHER COMPUTATIONS SEEM
!        TO HAVE PRODUCED REASONABLE RESULTS.
!     INTEGER IPRT
!        THE UNIT NUMBER USED FOR OUTPUT.
!     INTEGER ISORT(101)
!        THE ARRAY USED FOR SORTING.
!     INTEGER K
!        THE NUMBER OF FILTER TERMS.
!     CHARACTER*1 LH(8), LK(8)
!        THE ARRAY CONTAINING THE NAME OF THE VARIABLES H AND K.
!     INTEGER NF
!        THE NUMBER OF FREQUENCIES AT WHICH THE GAIN FUNCTION
!        IS TO BE ESTIMATED.
!     CHARACTER*1 NMSUB(6)
!        THE ARRAY CONTAINING THE NAME OF THIS SUBROUTINE.
!     INTEGER NORD
!        THE NUMBER OF POINTS TO BE PLOTTED.
!     INTEGER NPRT
!        A CODE USED TO SPECIFY THE TYPE OF PLOT, WHERE IF
!        NPRT < 0 THE PLOT IS DECIBELS/LINEAR
!        NPRT = 0 THE PLOT IS SUPPRESSED
!        NPRT > 0 THE PLOT IS LOG/LINEAR
!     LOGICAL SYM
!        THE VARIABLE USED TO INDICATE WHETHER THE GAIN FUNCTION
!        COMPUTED WAS FOR A SYMMETRIC OR AUTOREGRESSIVE FILTER.
!     REAL XORD(101)
!        THE X COORDINATES FOR THE GAIN FUNCTION PLOT
!     REAL YORD(101)
!        THE Y COORDINATES FOR THE GAIN FUNCTION PLOT
!     REAL YPLTMN, YPLTMX
!        THE MINIMUM AND MAXIMUM VALUES TO BE PLOTTED FOR THE Y AXIS.
!
!     SET UP NAME ARRAYS
!
      DATA
     &  NMSUB(1),  NMSUB(2),  NMSUB(3),  NMSUB(4),  NMSUB(5),  NMSUB(6)
     & /     'G',       'F',       'S',       'L',       'F',       ' '/
      DATA
     &  LH(1), LH(2), LH(3), LH(4), LH(5), LH(6), LH(7), LH(8)
     & /  'H',   ' ',   ' ',   ' ',   ' ',   ' ',   ' ',   ' '/
      DATA
     &  LK(1), LK(2), LK(3), LK(4), LK(5), LK(6), LK(7), LK(8)
     & /  'K',   ' ',   ' ',   ' ',   ' ',   ' ',   ' ',   ' '/
!
!     SET UP FOR ERROR CHECKING
!
      IERR = 0
      HEAD = .TRUE.
!
!     CALL ERROR CHECKING ROUTINES
!
      CALL EISGE(NMSUB, LK, K, 1, 1, HEAD, ERR01, LK)
!
      CALL ERIODD(NMSUB, LK, K, 1, HEAD, ERR02)
!
      IF ((.NOT. ERR01) .AND. (.NOT. ERR02)) THEN
         CALL ERSLF(NMSUB, LH, K, H, HEAD, ERR03)
      ELSE
         ERR03 = .FALSE.
      END IF
!
      IF (ERR01 .OR. ERR02 .OR. ERR03) GO TO 10
      GO TO 20
!
   10 CONTINUE
      IERR = 1
      CALL IPRINT (IPRT)
      WRITE (IPRT, 1000)
      RETURN
!
   20 CONTINUE
!
!     SET VARIOUS PROGRAM PARAMETERS
!
      SYM = .TRUE.
      NF = 101
!
      FMIN = 0.0E0
      FMAX = 0.5E0
!
      DELTA = 1.0E0
!
      NPRT = -1
!
!     SET FREQUENCIES AT WHICH THE GAIN FUNCTION IS TO BE ESTIMATED
!
      CALL SETFRQ (FREQ, NF, NPRT, FMIN, FMAX, DELTA)
!
!     COMPUTE THE GAIN FUNCTION
!
      CALL GFSEST (H, K, NF, FREQ, GAIN, DELTA)
!
!     PLOT THE RESULTS
!
      CALL GFORD (FREQ, GAIN, ISORT, NF, XORD, YORD, NORD,
     &   YPLTMN, YPLTMX, NPRT, IGFERR)
!
      CALL GFOUT (XORD, YORD, NORD, FREQ, GAIN, NF, K,
     &   SYM, FMIN, FMAX, YPLTMN, YPLTMX, NPRT, IGFERR, NMSUB)
!
      RETURN
!
!     FORMAT STATEMENTS
!
 1000 FORMAT (/42H THE CORRECT FORM OF THE CALL STATEMENT IS//
     &   24H       CALL GFSLF (H, K))
      END
