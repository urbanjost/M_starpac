!DIFSER
      subroutine difser (y, n, ndf, nd, iod, ydiff, ndiff)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS SUBROUTINE PERFORMS THE DIFFERENCING OPPERATION
!     DEFINED BY ND, IOD AND NDF ON THE SERIES Y, RESULTING IN
!     THE SERIES YDIFF.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVIAION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 21, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     integer&
     &   n,ndf,ndiff
!
!  ARRAY ARGUMENTS
     real(kind=wp) ::&
     &   y(*),ydiff(*)
     integer&
     &   iod(*),nd(*)
!
!  LOCAL SCALARS
     integer&
     &   i,j,k,l,ntimes
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     INTEGER I
!        AN INDEX VARIABLE.
!     INTEGER IOD(NDF)
!        THE VECTOR CONTAINING THE ORDERS OF EACH DIFFERENCE FACTOR.
!     INTEGER J, K, L
!        INDEX VARIABLES.
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS.
!     INTEGER ND(NDF)
!        THE VECTOR CONTAINING THE NUMBER OF TIMES EACH DIFFERENCING
!        FACTOR IS APPLIED.
!     INTEGER NDF
!        THE NUMBER OF DIFFERENCE FACTORS.
!     INTEGER NDIFF
!        THE NUMBER OF OBSERVATIONS REMAINING IN THE DIFFERENCED SERIES.
!     INTEGER NTIMES
!        THE NUMBER OF TIMES A GIVEN DIFFERENCE FACTOR IS TO BE APPLIED.
!     REAL(KIND=WP) :: Y(N), YDIFF(N)
!        THE VECTOR CONTAINING THE SERIES OBSERVATIONS AND THE VECTOR
!        IN WHICH THE DIFFERENCED SERIES IS RETURNED.
!
      do 10 i = 1, n
         ydiff(i) = y(i)
   10 continue
!
      ndiff = n
!
      if (ndf .eq. 0) return
!
      do 40 i = 1, ndf
         ntimes = nd(i)
         do 30 l = 1, ntimes
            ndiff = ndiff - iod(i)
            do 20 j = 1, ndiff
               k = j + iod(i)
               ydiff(j) = ydiff(k) - ydiff(j)
   20       continue
   30    continue
   40 continue
      return
      end
