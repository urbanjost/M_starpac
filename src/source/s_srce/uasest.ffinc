!UASEST
     subroutine uasest (iar, var, phi, nf, freq, delta, spca, lphi,&
     &   ispc)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE CALCULATES THE AUTOREGRESSIVE SPECTRUM.  IT IS
!        MODELED AFTER SUBROUTINE UASEC BY DICK JONES.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  DECEMBER 7, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     real(kind=wp) ::&
     &   delta,var
     integer&
     &   iar,ispc,lphi,nf
!
!  ARRAY ARGUMENTS
     real(kind=wp) ::&
     &   freq(nf),phi(lphi),spca(ispc)
!
!  LOCAL SCALARS
     real(kind=wp) ::&
     &   arg,ei,er,exi,exr,exrtmp,pi,ti,tr
     integer&
     &   i,j
!
!  EXTERNAL SUBROUTINES
!       EXTERNAL GETPI
!
!  INTRINSIC FUNCTIONS
      intrinsic cos,sin
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL(KIND=WP) :: ARG
!        THE ARGUMENT FOR THE SINE AND COSINE FUNCTIONS USED IN
!        CALCULATIONS OF THE SPECTRUM.
!     REAL(KIND=WP) :: DELTA
!        THE SAMPLING INTERVAL.
!     REAL(KIND=WP) :: EI, ER, EXI, EXR, EXRTMP
!        'COMPLEX' VARIABLES USED IN THE COMPUTATIONS.
!     REAL(KIND=WP) :: FREQ(NF)
!        THE ARRAY CONTAINING THE FREQUENCIES AT WHICH THE SPECTRUM
!        IS TO BE COMPUTED.
!     INTEGER I
!        AN INDEX VARIABLE.
!     INTEGER IAR
!        THE ORDER OF THE AUTOREGRESSIVE PROCESS CHOSEN.
!     INTEGER ISPC
!        THE LENGTH OF THE ARRAY SPCA.
!     INTEGER J
!        AN INDEX VARIABLE.
!     INTEGER LPHI
!        THE LENGTH OF THE ARRAY PHI.
!     INTEGER NF
!        THE NUMBER OF FREQUENCIES AT WHICH THE SPECTRUM IS TO BE
!        ESTIMATED.
!     REAL(KIND=WP) :: PHI(LPHI)
!        THE ARRAY OF AUTOREGRESSIVE COEFFICIENTS FOR THE
!        SELECTED ORDER.
!     REAL(KIND=WP) :: PI
!        THE VALUE OF PI.
!     REAL(KIND=WP) :: SPCA(ISPC)
!        THE ARRAY IN WHICH THE AUTOREGRESSIVE SPECTRUM IS STORED.
!     REAL(KIND=WP) :: TI, TR
!        A VARIABLE USED IN THE COMPUTATIONS.
!     REAL(KIND=WP) :: VAR
!        THE ONE STEP PREDICTION VARIANCE FOR THE SELECTED ORDER (IAR).
!
      call getpi(pi)
!
      do 20 j=1,nf
         spca(j) = delta * var
         if (iar.ge.1) then
            if (delta.eq.1.0e0) then
               if (freq(j).eq.0.0e0) then
                  er = 1.0e0
                  ei = 0.0e0
               else if (freq(j).eq.0.25e0) then
                  er = 0.0e0
                  ei = 1.0e0
               else if (freq(j).eq.0.5e0) then
                  er = -1.0e0
                  ei = 0.0e0
               else
                  arg = 2.0e0 * pi * delta * freq(j)
                  er = cos(arg)
                  ei = sin(arg)
               end if
            else
               arg = 2.0e0 * pi * delta * freq(j)
               er = cos(arg)
               ei = sin(arg)
            end if
            exr = 1.0e0
            exi = 0.0e0
            tr = 1.0e0
            ti = 0.0e0
            do 10 i=1,iar
               exrtmp = exr*er - exi*ei
               exi = exr*ei + exi*er
               exr = exrtmp
               tr = tr - phi(i) * exr
               ti = ti - phi(i) * exi
   10       continue
            spca(j) = spca(j)/(tr*tr + ti*ti)
         end if
   20 continue
      return
      end
