!UASEST
      SUBROUTINE UASEST (IAR, VAR, PHI, NF, FREQ, DELTA, SPCA, LPHI,
     +   ISPC)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE CALCULATES THE AUTOREGRESSIVE SPECTRUM.  IT IS
!        MODELED AFTER SUBROUTINE UASEC BY DICK JONES.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  DECEMBER 7, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP)
     +   DELTA,VAR
      INTEGER
     +   IAR,ISPC,LPHI,NF
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP)
     +   FREQ(NF),PHI(LPHI),SPCA(ISPC)
!
!  LOCAL SCALARS
      REAL(KIND=WP)
     +   ARG,EI,ER,EXI,EXR,EXRTMP,PI,TI,TR
      INTEGER
     +   I,J
!
!  EXTERNAL SUBROUTINES
      EXTERNAL GETPI
!
!  INTRINSIC FUNCTIONS
      INTRINSIC COS,SIN
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL ARG
!        THE ARGUMENT FOR THE SINE AND COSINE FUNCTIONS USED IN
!        CALCULATIONS OF THE SPECTRUM.
!     REAL DELTA
!        THE SAMPLING INTERVAL.
!     REAL EI, ER, EXI, EXR, EXRTMP
!        'COMPLEX' VARIABLES USED IN THE COMPUTATIONS.
!     REAL FREQ(NF)
!        THE ARRAY CONTAINING THE FREQUENCIES AT WHICH THE SPECTRUM
!        IS TO BE COMPUTED.
!     INTEGER I
!        AN INDEX VARIABLE.
!     INTEGER IAR
!        THE ORDER OF THE AUTOREGRESSIVE PROCESS CHOSEN.
!     INTEGER ISPC
!        THE LENGTH OF THE ARRAY SPCA.
!     INTEGER J
!        AN INDEX VARIABLE.
!     INTEGER LPHI
!        THE LENGTH OF THE ARRAY PHI.
!     INTEGER NF
!        THE NUMBER OF FREQUENCIES AT WHICH THE SPECTRUM IS TO BE
!        ESTIMATED.
!     REAL PHI(LPHI)
!        THE ARRAY OF AUTOREGRESSIVE COEFFICIENTS FOR THE
!        SELECTED ORDER.
!     REAL PI
!        THE VALUE OF PI.
!     REAL SPCA(ISPC)
!        THE ARRAY IN WHICH THE AUTOREGRESSIVE SPECTRUM IS STORED.
!     REAL TI, TR
!        A VARIABLE USED IN THE COMPUTATIONS.
!     REAL VAR
!        THE ONE STEP PREDICTION VARIANCE FOR THE SELECTED ORDER (IAR).
!
      CALL GETPI(PI)
!
      DO 20 J=1,NF
         SPCA(J) = DELTA * VAR
         IF (IAR.GE.1) THEN
            IF (DELTA.EQ.1.0E0) THEN
               IF (FREQ(J).EQ.0.0E0) THEN
                  ER = 1.0E0
                  EI = 0.0E0
               ELSE IF (FREQ(J).EQ.0.25E0) THEN
                  ER = 0.0E0
                  EI = 1.0E0
               ELSE IF (FREQ(J).EQ.0.5E0) THEN
                  ER = -1.0E0
                  EI = 0.0E0
               ELSE
                  ARG = 2.0E0 * PI * DELTA * FREQ(J)
                  ER = COS(ARG)
                  EI = SIN(ARG)
               END IF
            ELSE
               ARG = 2.0E0 * PI * DELTA * FREQ(J)
               ER = COS(ARG)
               EI = SIN(ARG)
            END IF
            EXR = 1.0E0
            EXI = 0.0E0
            TR = 1.0E0
            TI = 0.0E0
            DO 10 I=1,IAR
               EXRTMP = EXR*ER - EXI*EI
               EXI = EXR*EI + EXI*ER
               EXR = EXRTMP
               TR = TR - PHI(I) * EXR
               TI = TI - PHI(I) * EXI
   10       CONTINUE
            SPCA(J) = SPCA(J)/(TR*TR + TI*TI)
         END IF
   20 CONTINUE
      RETURN
      END
