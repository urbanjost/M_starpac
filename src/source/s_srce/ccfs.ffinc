!CCFS
     subroutine ccfs (ym, n, m, iym, lagmax, ccov, iccov, jccov,&
     &   nprt, ldstak)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS IS THE USER CALLABLE ROUTINE FOR COMPUTING THE CROSS
!     CORRELATIONS AND COVARIANCES OF A MULTIVARIATE SERIES (LONG CALL).
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 21, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     integer&
     &   iccov,iym,jccov,lagmax,ldstak,m,n,nprt
!
!  ARRAY ARGUMENTS
     real(kind=wp) ::&
     &   ccov(*),ym(*)
!
!  SCALARS IN COMMON
     integer&
     &   ierr
!
!  ARRAYS IN COMMON
      double precision dstak(12)
!
!  LOCAL SCALARS
     integer&
    &   i,ifp,inlppc,io,iprt,iymfft,j,jnlppc,k,ldsmin,lyfft,nall0,&
     &   nfft,rhoc,sdrhoc,ymean,ymeanj,ymeank,ysd,ysdj,ysdk
     logical&
     &   isfft,islong,newpg
!
!  LOCAL ARRAYS
     real(kind=wp) ::&
     &   rstak(12)
     integer&
     &   ndum(1)
     character&
     &   nmsub(6)*1
!
!  EXTERNAL FUNCTIONS
!      INTEGER
!     +   STKST
!      EXTERNAL STKST
!
!  EXTERNAL SUBROUTINES
!      EXTERNAL ACVF,CCFER,CCFMN,CCFOUT,IPRINT,LDSCMP,STKCLR,STKSET
!
!  INTRINSIC FUNCTIONS
      intrinsic sqrt
!
!  COMMON BLOCKS
      common /cstak/dstak
      common /errchk/ierr
!
!  STATEMENT FUNCTIONS
     integer&
     &   i2,i3
!
!  EQUIVALENCES
      equivalence (dstak(1),rstak(1))
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL CCOV(ICCOV, JCCOV, M)
!        THE CROSS COVARIANCE MATRIX.
!     DOUBLE PRECISION DSTAK(12)
!        THE DOUBLE PRECISION VERSION OF THE /CSTAK/ WORK AREA.
!     INTEGER ICCOV
!        THE ACTUAL FIRST DIMENSION OF THE ARRAY CCOV, AS
!        SPECIFIED IN THE USERS PROGRAM.
!     INTEGER IERR
!        THE INTEGER VALUE RETURNED BY THIS ROUTINE DESIGNATING
!        WHETHER ANY ERRORS WERE DETECTED IN THE PARAMETER LIST
!        IF IERR .EQ. 0, NO ERRORS WERE DETECTED
!        IF IERR .EQ. 1, ERRORS HAVE BEEN DETECTED
!     INTEGER IFP
!        AN INDICATOR FOR STACK ALLOCATION TYPE, WHERE IFP=3 INDICATES
!        SINGLE PRECISION AND IFP=4 INDICATES DOUBLE PRECISION.
!     INTEGER INLPPC
!        THE ACTUAL FIRST DIMENSION OF THE ARRAY NLPPC AS SPECIFIEC
!        IN THE USERS PROGRAM.
!     INTEGER IO
!        A VARIABLE USED TO DETERMINE THE AMOUNT OF STORAGE REQUIRED,
!        BASED ON WHETHER PRINTED OUTPUT IS DESIRED.
!     INTEGER IPRT
!        THE UNIT NUMBER USED FOR OUTPUT.
!     LOGICAL ISFFT
!        THE INDICATOR VARIABLE USED TO DESIGNATE WHETHER THE CALLING
!        ROUTINE HAS SUFFIX F (ISFFT = TRUE) OR NOT (ISFFT = FALSE)
!     LOGICAL ISLONG
!        THE INDICATOR VARIABLE USED TO DESIGNATE WHETHER THE CALLING
!        ROUTINE HAS SUFFIX S (ISLONG = TRUE) OR NOT (ISLONG = FALSE)
!     INTEGER IYM
!        THE ACTUAL FIRST DIMENSION OF THE MATRIX YM AS
!        SPECIFIED IN THE USERS PROGRAM.
!     INTEGER IYMFFT
!        THE ACTUAL FIRST DIMENSION OF THE MATRIX YMFFT AS
!        SPECIFIED IN THE USERS PROGRAM.
!     INTEGER I2
!        STATEMENT FUNCTION SPECIFYING THE DESIRED LOCATION WITHIN YM
!     INTEGER I3
!        STATEMENT FUNCTION SPECIFYING THE DESIRED LOCATION WITHIN CCOV
!     INTEGER J
!        THE INDEX OF -SERIES 1- IN THE ARRAY YM.
!     INTEGER JCCOV
!        THE ACTUAL SECOND DIMENSION OF THE ARRAY CCOV, AS
!        SPECIFIED IN THE USERS PROGRAM.
!     INTEGER JNLPPC
!        THE SECOND DIMENSION OF THE ARRAY NLPPC AS SPECIFIED
!        IN THE USERS PROGRAM.
!     INTEGER K
!        THE INDEX OF -SERIES 2- IN THE ARRAY YM.
!     INTEGER LAGMAX
!        THE MAXIMUM LAG VALUE REQUESTED.
!     INTEGER LDSMIN
!        THE MINIMUM LENGTH ALLOWED FOR THE ARRAY DSTAK.
!     INTEGER LDSTAK
!        THE LENGTH OF THE ARRAY DSTAK.
!     INTEGER LYFFT
!        THE LENGTH OF THE VECTOR YFFT.
!     INTEGER M
!        THE NUMBER OF SERIES BEING COMPARED, IE THE
!        NUMBER OF COLUMNS OF DATA IN YM.
!     INTEGER N
!        THE INTEGER NUMBER OF OBSERVATIONS IN EACH SERIES
!     INTEGER NALL0
!        THE NUMBER OF OUTSTANDING STACK ALLOCATIONS
!     INTEGER NDUM(1)
!        A DUMMY DIMENSIONED VARIABLE.
!     LOGICAL NEWPG
!        AN INDICATOR VARIABLE USED TO DETERMINE WHEN A NEW PAGE
!        IS APPROPRIATE FOR THE OUTPUT.
!     INTEGER NFFT
!        THE NUMBER OF OBSERVATIONS IN THE EXTENDED SERIES.
!     CHARACTER*1 NMSUB(6)
!        THE NAME OF THE SUBROUTINE CALLING THE ERROR CHECKING
!        SUBROUTINE.
!     INTEGER NPRT
!        THE INDICATOR VARIABLE USED TO SPEDIFY WHETHER OR NOT
!        PRINTED OUTPUT IS TO BE GIVEN, WHERE IF THE VALUE OF
!        NPRT IS ZERO, NO OUTPUT IS GIVEN.
!     INTEGER RHOC
!        THE STARTING LOCATION IN DSTAK OF THE ARRAY RHOC.
!     REAL RSTAK(12)
!        THE REAL VERSION OF THE /CSTAK/ WORK AREA.
!     INTEGER SDRHOC
!        THE STARTING LOCATION IN THE WORK AREA FOR SDRHOC.
!     REAL YM(IYM, M)
!        THE MATRIX CONTAINING THE OBSERVED TIME SERIES
!     INTEGER YMEAN, YMEANJ, YMEANK
!        THE STARTING LOCATION FOR THE ARRAY CONTAINING THE
!        MEANS OF EACH OF THE SERIES, AND THE LOCATION IN
!        THE ARRAY FOR THE MEAN OF THE JTH AND KTH SERIES,
!        RESPECTIVELY.
!     INTEGER YSD, YSDJ, YSDK
!        THE STARTING LOCATION FOR THE ARRAY CONTAINING THE
!        STANDARD DEVIATIONS OF EACH OF THE SERIES, AND THE
!        LOCATION IN THE ARRAY FOR THE STANDARD DEVIATION OF
!        THE JTH AND KTH SERIES, RESPECTIVELY.
!
!     SET UP NAME ARRAYS
!
     data&
    &  nmsub(1),  nmsub(2),  nmsub(3),  nmsub(4),  nmsub(5),  nmsub(6)&
     & /     'C',       'C',       'F',       'S',       ' ',       ' '/
!
      i2(i,j) = i + (j-1)*iym
      i3(i,j,k) = i + (j-1)*iccov + (k-1)*jccov*iccov
!
!     SET UP FOR ERROR CHECKING
!
      ierr = 0
      inlppc = iccov
      jnlppc = jccov
      lyfft = n + lagmax
      iymfft = iym
      nfft = n
      isfft = .false.
      islong = .true.
!
      io = 1
      if (nprt .eq. 0) io = 0
!
     call ldscmp(2+2*io, 0, 0, 0, 0, 0, 'S',&
     &            2*m+io*(4*lagmax+2), ldsmin)
!
     call ccfer(nmsub, n, lagmax, ldstak, ldsmin, iccov, jccov,&
     &  inlppc, jnlppc, m, lyfft, nfft, iym, iymfft, isfft, islong)
!
!     CHECK WHETHER AN ERROR HAS BEEN DETECTED
!
      if (ierr.eq.0) then
!
!       SET UP THE WORK AREA.
!
        call stkset (ldstak, 4)
        nall0 = stkst(1)
!
        ifp = 3
!
        ymean = stkget(m, ifp)
        ysd = stkget(m, ifp)
        if (nprt.eq.0) then
          rhoc = ysd
          sdrhoc = ysd
        else
          rhoc = stkget(2*lagmax+1, ifp)
          sdrhoc = stkget(2*lagmax+1, ifp)
        end if
!
        if (ierr.eq.0) then
!
!         BEGIN LOOP FOR COMPUTATIONS
!
          newpg = .false.
!
          do 40 k = 1, m
!
             ymeank = ymean + k - 1
             ysdk = ysd + k - 1
!
            call acvf (ym(i2(1,k)), n, rstak(ymeank),&
     &                  ccov(i3(1,k,k)), lagmax, iccov)
             rstak(ysdk) = sqrt(ccov(i3(1,k,k)) * n / (n - 1))
!
             if ((k-1).le.0) go to 40
!
             do 30 j = 1, (k-1)
!
                ymeanj = ymean + j - 1
                ysdj = ysd + j - 1
!
               call ccfmn (ym(i2(1,j)), ym(i2(1,k)),&
    &                      n, lagmax, 2*lagmax+1,&
    &                      ccov(i3(1,j,j)), ccov(i3(1,k,k)),&
    &                      ccov(i3(1,j,k)), ccov(i3(1,k,j)),&
    &                      iccov, rstak(ymeanj), rstak(ymeank),&
     &                      rstak(rhoc), rstak(sdrhoc), nprt)
!
                if (nprt .eq. 0) go to 30
!
!         CALL ROUTINE TO PRINT OUT CORRELATIONS
!
               call ccfout (j, rstak(ymeanj), rstak(ysdj), n, n, k,&
    &             rstak(ymeank), rstak(ysdk), n, n, lagmax, 2*lagmax+1,&
    &             rstak(rhoc), rstak(sdrhoc), .false., ndum, ndum, 1,&
     &             0.0e0, 0.0e0, newpg)
!
                newpg = .true.
!
   30        continue
   40     continue
        end if
!
        call stkclr(nall0)
      end if
!
      if (ierr.ne.0) then
!
!     PRINT PROPER CALL SEQUENCE AND RETURN
!
        ierr = 1
        call iprint (iprt)
        write (iprt, 1000)
      end if
!
      return
!
!     FORMAT STATEMENTS
!
1000 format(/42h the correct form of the call statement is//&
    &   '       CALL CCFS (YM, N, M, IYM,'/&
     &   '      +           LAGMAX, CCOV, ICCOV, JCCOV, NPRT, LDSTAK)')
      end
