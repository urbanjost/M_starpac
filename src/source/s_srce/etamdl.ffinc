!ETAMDL
      SUBROUTINE ETAMDL(MDL, PAR, NPAR, XM, N, M, IXM, ETA, NETA,
     &   PARTMP, PV, NROWIN)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     ROUTINE TO COMPUTE NOISE AND NUMBER OF GOOD DIGITS IN
!     RESULTS OF MODEL ROUTINE AT ROW <NROW>.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  OCTOBER 3, 1983
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) ::
     &   ETA
      INTEGER
     &   IXM,M,N,NETA,NPAR,NROWIN
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   PAR(NPAR),PARTMP(NPAR),PV(N),XM(IXM,M)
!
!  SUBROUTINE ARGUMENTS
       EXTERNAL MDL
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     &   A,B,FAC,FPLRS,J,RSSSM,RSSSMJ,SQRTMP
      INTEGER
     &   I,K,NROW
!
!  LOCAL ARRAYS
      REAL(KIND=WP) ::
     &   RSS(5)
!
!  EXTERNAL FUNCTIONS
      REAL(KIND=WP) ::
     &   R1MACH
!       EXTERNAL R1MACH
!
!  EXTERNAL SUBROUTINES
!       EXTERNAL SETROW
!
!  INTRINSIC FUNCTIONS
      INTRINSIC ABS,LOG10,MAX,SQRT
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL A, B
!        PARAMETERS OF THE FIT.
!     REAL ETA
!        THE NOISE IN THE MODEL RESULTS.
!     REAL FAC
!        A FACTOR USED IN THE COMPUTATIONS.
!     REAL FPLRS
!        THE FLOATING POINT LARGEST RELATIVE SPACING.
!     INTEGER I
!        AN INDEXING VARIABLE.
!     INTEGER IXM
!        ACTUAL FIRST DIMENSION OF XM
!     REAL J
!        THE VALUE FLOAT(I-3).
!     INTEGER K
!        AN INDEX VARIABLE.
!     INTEGER M
!        NUMBER OF VARIABLES
!     EXTERNAL MDL
!        THE NAME OF THE USER SUPPLIED SUBROUTINE WHICH COMPUTES THE
!        PREDICTED VALUES BASED ON THE CURRENT PARAMETER ESTIMATES.
!     INTEGER N
!        NUMBER OF OBSERVATIONS
!     INTEGER NETA
!        THE NUMBER OF ACCURATE DIGITS IN THE MODEL RESULTS.
!     INTEGER NPAR
!        NUMBER OF PARAMETERS
!     INTEGER NROW
!        THE ROW NUMBER ACTUALLY USED.
!     INTEGER NROWIN
!        THE INPUT NUMBER OF THE ROW BEING CHECKED.
!     REAL PAR(NPAR)
!        MODEL PARAMETERS
!     REAL PARTMP(NPAR)
!        MODIFIED MODEL PARAMETERS
!     REAL PV(N)
!        PREDICTED VALUES
!     REAL RSS(5)
!        THE RESIDUAL SUM OF SQUARES FOR EACH VALUE OF J.
!     REAL RSSSM
!        THE SUM OF THE RESIDUAL SUM OF SQUARES FOR EACH SET OF
!        PARAMETER VALUES.
!     REAL RSSSMJ
!        THE SUM OF THE RESIDUAL SUM OF SQUARES TIMES J FOR EACH
!        SET OF PARAMETER VALUES.
!     REAL SQRTMP
!        THE SQUARE ROOT OF MACHINE PRECISION (FPLRS).
!     REAL XM(IXM,M)
!        INDEPENDENT VARIABLES
!
      FPLRS = R1MACH(4)
!
!     SELECT FIRST ROW OF INDEPENDENT VARIABLES WHICH CONTAINS NO ZEROS
!
      CALL SETROW(NROWIN, XM, N, M, IXM, NROW)
!
      SQRTMP = SQRT(FPLRS)
      RSSSM = 0.0E0
      RSSSMJ = 0.0E0
      DO 20 I=1,5
         J = I-3
         DO 10 K=1,NPAR
            PARTMP(K) = PAR(K)*(1.0E0+J*SQRTMP)
   10    CONTINUE
         CALL MDL(PARTMP, NPAR, XM, N, M, IXM, PV)
!
         RSS(I) = PV(NROW)
!
         RSSSM = RSSSM + RSS(I)
         RSSSMJ = RSSSMJ + J*RSS(I)
   20 CONTINUE
      A = 0.2E00*RSSSM
      B = 0.1E00*RSSSMJ
      FAC = 1.0E0
      IF (RSS(3).NE.0.0E0) FAC = FAC/RSS(3)
      DO 30 I=1,5
         J = I-3
         RSS(I) = ABS((RSS(I)-(A+J*B))*FAC)
   30 CONTINUE
      ETA = MAX(RSS(1),RSS(2),RSS(3),RSS(4),RSS(5),FPLRS)
      NETA = -LOG10(ETA)
      ETA = 10.0E0**(-NETA)
      RETURN
      END
