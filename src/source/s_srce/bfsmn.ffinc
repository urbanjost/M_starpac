!BFSMN
      SUBROUTINE BFSMN(SPCF1, SPCF2, CEVEN, CODD, W, LW, LAG, DF, NPRT,
     &   NF, CSPC2, PHAS, FREQ, NPTS, XAXIS, YAXIS, ISYM, LPCV, ALPHA,
     &   LAGMX1, DELTA)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE COMPUTES THE SQUARED COHERENCY AND PHASE COMPONENTS
!     OF A BIVARIATE SPECTRUM.
!
!     REFERENCE - JENKINS AND WATTS
!                 SPECTRAL ANALYSIS AND ITS APPLICATIONS
!
!     WRITTEN BY - STEPHEN M. KEEFER AND JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  DECEMBER 2, 1985
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) ::
     &   ALPHA,DELTA,DF
      INTEGER
     &   LAG,LAGMX1,LPCV,LW,NF,NPRT,NPTS
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   CEVEN(*),CODD(*),CSPC2(*),FREQ(*),PHAS(*),SPCF1(*),SPCF2(*),
     &   W(*),XAXIS(*),YAXIS(*)
      INTEGER
     &   ISYM(*)
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     &   ARG,BARL,BARQ,BARY,C,CI,FAC,FPLM,FPLRS,FPSPM,G,PI,PIT2,SN,V0,
     &   V1,V2,Z0,Z1,Z2
      INTEGER
     &   I,K
!
!  EXTERNAL FUNCTIONS
      REAL(KIND=WP) ::
     &   PPFNML,R1MACH
!       EXTERNAL PPFNML,R1MACH
!
!  EXTERNAL SUBROUTINES
!       EXTERNAL GETPI
!
!  INTRINSIC FUNCTIONS
      INTRINSIC ATAN2,COS,LOG,SIGN,SIN,SQRT,TANH
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL ALPHA
!        THE DESIRED CONFIDENCE LEVEL.
!     REAL ARG
!        AN ARGUMENT USED IN THE SPECTRUM COMPUTATIONS.
!     REAL BARL
!        THE SMOOTHED COSPECTRAL ESTIMATES.
!     REAL BARQ
!        THE SMOOTHED QUADRATURE SPECTRAL ESTIMATES.
!     REAL BARY
!        A TRANSFORMATION OF THE SQUARED COHERENCY COMPONENT.
!     REAL C
!        AN ARGUMENT USED IN THE SPECTRUM COMPUTATIONS.
!     REAL CEVEN(LAGMX1)
!        THE SUMS OF THE COVARIANCES FOR EACH LAG.
!     REAL CI
!        THE CONFIDENCE INTERVAL FOR THE SQUARED COHERENCY COMPONENT.
!     REAL CODD(LAGMX1)
!        THE DIFFERENCES OF THE AUTOCOVARIANCES FOR EACH LAG.
!     REAL CSPC2(NF)
!        THE SQUARED COHERENCY COMPONENT OF THE BIVARIATE SPECTRA.
!     REAL DELTA
!        THE SAMPLING INTERVAL.
!     REAL DF
!        THE EFFECTIVE DEGREES OF FREEDOM.
!     REAL FAC
!        THE CONVERSION FACTOR FROM RADIANS TO DEGREES.
!     REAL FPLM
!        THE FLOATING POINT LARGEST MAGNITUDE.
!     REAL FPLRS
!        THE FLOATING POINT LARGEST RELATIVE SPACING.
!     REAL FPSPM
!        THE FLOATING POINT SMALLEST POSITIVE MAGNITUDE.
!     REAL FREQ(NF)
!        THE FREQUENCIES AT WHICH THE SPECTRUM IS COMPUTED.
!     REAL G
!        AN ARGUMENT USED IN THE COMPUTATION OF THE ALPHA PERCENT
!        SIGNIFICANCE LEVEL.
!     INTEGER I
!        AN INDEX VALUE.
!     INTEGER ISYM(LPCV)
!        THE VECTOR CONTAINING THE CODES FOR THE PLOT SYMBOLS.
!     INTEGER K
!         AN INDEX VALUE.
!     INTEGER LAG
!        THE LAG WINDOW TRUNCATION POINT USED FOR A SPECIFIC WINDOW.
!     INTEGER LAGMX1
!        THE VALUE LAGMAX+1.
!     INTEGER LPCV
!        THE LENGTH OF THE PLOT CO-ORDINATE VECTORS.
!     INTEGER LW
!         THE LENGTH OF VECTOR W.
!     INTEGER NF
!        THE NUMBER OF FREQUENCIES AT WHICH THE SPECTRUM IS
!        TO BE COMPUTED.
!     INTEGER NPRT
!        A CODE USED TO SPECIFY THE TYPE OF PLOT.
!        IF NPRT = 0 THE PLOT IS SUPPRESSED.
!        IF NPRT = 2 THE PLOT IS PROVIDED.
!     INTEGER NPTS
!        THE NUMBER OF X, Y CO-ORDINATES TO BE PLOTTED.
!     REAL PHAS(NF)
!        THE PHASE COMPONENT OF THE BIVARIATE SPECTRA.
!     REAL PI, PIT2
!        THE VALUE OF PI AND PI*2.
!     REAL SN
!        AN ARGUMENT USED IN THE COMPUTATION OF THE SPECTRUM.
!     REAL SPCF1(NF), SPCF2(NF)
!        THE UNIVARIATE SPECTRUM FOR EACH SERIES.
!     REAL V0, V1, V2
!        ARGUMENTS USED IN THE COMPUTATION OF THE SPECTRUM.
!     REAL W(LW)
!        THE WINDOW.
!     REAL XAXIS(LPCV)
!        THE X AXIS VALUES FOR THE SPECTRUM PLOTS.
!     REAL YAXIS(LPCV)
!        THE Y AXIS VALUES FOR THE SPECTRUM PLOTS.
!     REAL Z0, Z1, Z2
!        ARGUMENTS USED IN THE COMPUTATION OF THE SPECTRUM.
!
!
      CALL GETPI(PI)
      PIT2 = PI*2.0E0
!
      FPSPM = R1MACH(1)
      FPLM = R1MACH(2)
      FPLRS = R1MACH(4)
!
      FAC = 180.0E0/PI
!
!
!     COMPUTE SMOOTHED CO-SPECTRAL ESTIMATE
!
      DO 40 I=1,NF
!
!           COMPUTE SMOOTHED CO- AND QUADRATURE SPECTRA USING
!           THE ALGORITHM SHOWN ON PAGE 420 OF JENKINS AND WATTS
!
         IF (FREQ(I).EQ.0.0E0) THEN
            C = 1.0E0
            SN = 0.0E0
         ELSE IF (FREQ(I).EQ.0.25E0) THEN
            C = 0.0E0
            SN = 1.0E0
         ELSE IF (FREQ(I).EQ.0.5E0) THEN
            C = -1.0E0
            SN = 0.0
         ELSE
            ARG = PIT2*FREQ(I)
            C = COS(ARG)
            SN = SIN(ARG)
         END IF
         V0 = 0.0E0
         V1 = 0.0E0
         Z0 = 0.0E0
         Z1 = 0.0E0
         DO 10 K=LAG-1,1,-1
            V2 = 2.0E0*C*V1 - V0 + W(K+1)*CEVEN(K+1)
            Z2 = 2.0E0*C*Z1 - Z0 + W(K+1)*CODD(K+1)
            V0 = V1
            V1 = V2
            Z0 = Z1
            Z1 = Z2
   10    CONTINUE
         BARL = DELTA*(CEVEN(1)+2.0E0*(V1*C-V0))
         BARQ = 2.0E0*DELTA*Z1*SN
!
!     COMPUTE THE SMOOTHED SQUARED COHERENCY SPECTRA
!
         IF (SPCF1(I)*SPCF2(I).GT.0.0E0) THEN
            CSPC2(I) = (BARL*BARL+BARQ*BARQ)
            CSPC2(I) = CSPC2(I)/(SPCF1(I)*SPCF2(I))
         ELSE
            CSPC2(I) = FPLM
         END IF
!
!     COMPUTE PHASE (IN RADIANS)
!
         IF ((BARQ.NE.0.0E0) .OR. (BARL.NE.0.0E0)) THEN
            PHAS(I) = ATAN2(-BARQ,BARL)
         ELSE
            IF (I.EQ.1) THEN
               PHAS(I) = 0.0E0
            ELSE
               PHAS(I) = SIGN(PI,PHAS(I-1))
            END IF
         END IF
   40 CONTINUE
!
      IF (NPRT.EQ.0) RETURN
!
!     COMPUTE SMOOTHED SQUARED COHERENCY PLOT VECTORS
!
      CI = PPFNML(ALPHA)*SQRT(1.0E0/DF)
      G = 2.0E0/DF
      G = 1.0E0 - (1.0E0-ALPHA)**(G/(1.0E0-G))
      NPTS = 0
      DO 60 I=1,NF
         NPTS = NPTS + 1
!
!     COMPUTE 95 PER CENT SIGNIFICANCE LEVEL
!
         YAXIS(NPTS) = G
         XAXIS(NPTS) = FREQ(I)
         ISYM(NPTS) = 4
         IF (SPCF1(I)*SPCF2(I).LE.0.0E0) GO TO 60
!
!     COMPUTE COHERENCE SPECTRAL ESTIMATE
!
         IF (CSPC2(I).GT.1.0E0) GO TO 60
         NPTS = NPTS + 1
         YAXIS(NPTS) = CSPC2(I)
         XAXIS(NPTS) = FREQ(I)
         ISYM(NPTS) = 1
         IF (CSPC2(I).LT.G) GO TO 60
!
!     COMPUTE CONFIDENCE INTERVAL
!
         BARY = SQRT(CSPC2(I))
         BARY = 0.5E0*(LOG((1.0E0+BARY)/(1.0E0-BARY)))
         NPTS = NPTS + 1
         YAXIS(NPTS) = (TANH(BARY+CI))*(TANH(BARY+CI))
         XAXIS(NPTS) = FREQ(I)
         ISYM(NPTS) = 2
         NPTS = NPTS + 1
         YAXIS(NPTS) = (TANH(BARY-CI))*(TANH(BARY-CI))
         XAXIS(NPTS) = FREQ(I)
         ISYM(NPTS) = 2
   60 CONTINUE
!
      RETURN
!
      END
