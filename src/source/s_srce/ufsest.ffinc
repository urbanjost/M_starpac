!UFSEST
     subroutine ufsest(acov, w, lag, spcf, ispcf, lacov, lw, nf, freq,&
     &   delta)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE COMPUTES THE SPECTRUM, SPCF, AND
!     THEIR LOWER AND UPPER CONFIDENCE LIMITS, SPCLCL AND SPCUCL,
!     RESPECTIVELY.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  DECEMBER 7, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     real(kind=wp) ::&
     &   delta
     integer&
     &   ispcf,lacov,lag,lw,nf
!
!  ARRAY ARGUMENTS
     real(kind=wp) ::&
     &   acov(lacov),freq(nf),spcf(ispcf),w(lw)
!
!  LOCAL SCALARS
     real(kind=wp) ::&
     &   c,pi,v0,v1,v2
     integer&
     &   i,k,kk
!
!  EXTERNAL SUBROUTINES
!       EXTERNAL GETPI
!
!  INTRINSIC FUNCTIONS
      intrinsic cos
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL(KIND=WP) :: ACOV(LACOV)
!        THE AUTOCOVARIANCES OF THE SERIES.
!     REAL(KIND=WP) :: C
!        A VALUE USED TO COMPUTE THE SPECTRUM VALUES.
!     REAL(KIND=WP) :: DELTA
!        THE SAMPLING INTERVAL.
!     REAL(KIND=WP) :: FREQ(NF)
!        THE VECTOR OF FREQUENCIES AT WHICH THE SPECTRUM IS TO BE
!        COMPUTED.
!     INTEGER I
!        AN INDEX VARIABLE
!     INTEGER ISPCF
!         THE ACTUAL FIRST DIMENSION OF THE SPECTRUM ARRAYS.
!     INTEGER K, KK
!        INDEXING VARIABLES.
!     INTEGER LACOV
!        THE LENGTH OF VECTOR ACOV.
!     INTEGER LAG
!        THE LAG WINDOW TRUCCATION POINT.
!     INTEGER LW
!        THE LENGTH OF THE VECTOR W.
!     INTEGER NF
!        THE NUMBER OF FREQUENCIES AT WHICH THE SPECTRUM IS
!        TO BE COMPUTED.
!     REAL(KIND=WP) :: PI
!        THE VALUE OF PI.
!     REAL(KIND=WP) :: SPCF(ISPCF)
!        THE ARRAYS IN WHICH THE SPECTRUM IS STORED.
!     REAL(KIND=WP) :: V0, V1, V2
!        CONSTANTS USED FOR COMPUTING THE SPECTRUM VALUES.
!     REAL(KIND=WP) :: W(LW)
!        THE VECTOR OF LAG WINDOWS.
!
      call getpi(pi)
!
!        COMPUTE THE SPECTRUM AND ITS CONFIDENCE LIMITS.
!
      do 20 i=1,nf
         c = cos(2.0e0*pi*freq(i))
         v0 = 0.0e0
         v1 = 0.0e0
         do 10 k=1,lag
            kk = lag + 1 - k
            v2 = 2.0e0*c*v1 - v0 + w(kk+1)*acov(kk+1)
            v0 = v1
            v1 = v2
   10    continue
         spcf(i) = delta*(acov(1)*w(1)+2.0e0*(v1*c-v0))
         if (spcf(i).lt.0.0e0) spcf(i) = 0.0e0
   20 continue
      return
      end
