!AMEPT2
      SUBROUTINE AMEPT2 (RES, SDREST, N, RSS)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS SUBROUTINE, ADAPTED FROM OMNITAB II, PRINTS
!     THE FOUR STANDARDIZED RESIDUAL PLOTS.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  DECEMBER 2, 1985
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP)
     +   RSS
      INTEGER
     +   N
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP)
     +   RES(*),SDREST(*)
!
!  SCALARS IN COMMON
      INTEGER
     +   IERR
!
!  LOCAL SCALARS
      REAL(KIND=WP)
     +   AN,DOT,FAC1,FAC2,FPLM,GAMMA,PI,RATIO,ROWDIV,ROWMAX,ROWMID,
     +   ROWMIN,XDIV,XMAX,XMIN,YLABEL,YMAX,YMIN
      INTEGER
     +   I,I1,I2,IMID,IPLOT,IPRB,IPRT,IROW,IX,K,L,NCOL,NCOLP1,
     +   NCOLPL,NCOLT2,NDOT,NROW
      CHARACTER
     +   IBLANK*1,IMINUS*1,IPLUS*1,ISTAR*1
!
!  LOCAL ARRAYS
      CHARACTER
     +   LINE(113)*1
!
!  EXTERNAL FUNCTIONS
      REAL(KIND=WP)
     +   R1MACH
      LOGICAL
     +   MVCHK
      EXTERNAL R1MACH,MVCHK
!
!  EXTERNAL SUBROUTINES
      EXTERNAL DOTC,GETPI,IPRINT
!
!  INTRINSIC FUNCTIONS
      INTRINSIC INT,MAX,MIN,MOD
!
!  COMMON BLOCKS
      COMMON /ERRCHK/IERR
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL AN
!        THE NUMBER OF OBSERVATIONS, USED IN COMPUTING
!        THE NORMAL PROBABILITY PLOT.
!     REAL DOT
!        THE DOT PRODUCT USED TO COMPUTE THE CORRELATION COEFFICIENT.
!     REAL FAC1, FAC2
!        FACTORS USED IN COMPUTING THE NORMAL PROBABILITY PLOT.
!     REAL FPLM
!        THE FLOATING POINT LARGEST MAGNITUDE.
!     REAL GAMMA
!        A VALUE USED IN COMPUTING THE NORMAL PROBABILITY PLOT.
!     INTEGER I
!        AN INDEX VARIABLE.
!     CHARACTER*1 IBLANK
!        THE VALUE OF THE CHARACTER -BLANK-.
!     INTEGER IERR
!        THE VALUE RETURNED BY THIS ROUTINE DESIGNATING
!        WHETHER ANY ERRORS WERE DETECTED IN THE PARAMETER LIST.
!        IF IERR .EQ. 0, NO ERRORS WERE DETECTED.
!        IF IERR .GE. 1, ERRORS WERE DETECTED.
!     INTEGER IMID
!        THE MIDPOINT OF THE AUTOCORRELATION PLOT.
!     CHARACTER*1 IMINUS
!        THE CHARACTER MINUS.
!     INTEGER IPLOT
!        AN INDICATOR VARIABLE DESIGNATING WHETHER THE FIRST OR
!        SECOND SET OF TWO PLOTS ARE BEING PRINTED.
!     CHARACTER*1 IPLUS
!        THE CHARACTER PLUS.
!     INTEGER IPRB
!        THE LOCATION IN THE PLOT STRING OF THE SYMBOL FOR THE
!        PROBABILITY PLOT.
!     INTEGER IPRT
!        THE UNIT NUMBER FOR PRINTED OUTPUT.
!     INTEGER IROW
!        THE ROW OF THE VARIABLES BEING PLOTTED.
!     CHARACTER*1 ISTAR
!        THE CHARACTER STAR.
!     INTEGER IX
!        THE LOCATION IN THE PLOT STRING OF THE SYMBOL FOR THE PLOTS
!        VERSUS THE INDEPENDENT VARIABLE.
!     INTEGER I1, I2
!        INDEX VALUES.
!     INTEGER K
!        AN INDEX VARIABLE.
!     INTEGER L
!        AN INDEX VARIABLE.
!     CHARACTER*1 LINE(113)
!        THE SYMBOLS (BLANKS AND CHARACTERS) FOR A GIVEN LINE
!        OF THE PLOT.
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS.
!     INTEGER NCOL, NCOLPL, NCOLP1, NCOLT2
!        THE NUMBER OF COLUMNS IN THE PLOT, NCOL+L, NCOL+1,
!        AND NCOL * 2.
!     INTEGER NDOT
!        THE NUMBER OF POINTS MAKING UP DOT.
!     INTEGER NROW
!        THE NUMBER OF COLUMNS IN THE PLOT.
!     REAL PI
!        THE VALUE OF PI.
!     REAL RATIO
!        A VALUE USED TO PRODUCE THE NORMAL PROBABILITY PLOT.
!     REAL RES(N)
!        THE RESIDUALS FROM THE FIT.
!     REAL ROWDIV
!        THE VALUE OF A DIVISION ALONG THE -ROW- AXIS.
!     REAL ROWMAX
!        THE LARGEST ROW VALUE.
!     REAL ROWMID
!        THE MIDPOINT OF THE RANGE OF THE ROWS PLOTTED.
!     REAL ROWMIN
!        THE SMALLEST ROW VALUE PLOTTED.
!     REAL RSS
!        THE RESIDUAL SUM OF SQUARES.
!     REAL SDREST(N)
!        THE STANDARDIZED RESIDUALS.
!     REAL XDIV
!        THE VALUE OF A DIVISION ALONG THE X AXIS.
!     REAL XMAX
!        THE LARGEST VALUE ALONG THE X AXIS.
!     REAL XMIN
!        THE SMALLEST VALUE ALONG THE X AXIS.
!     REAL YLABEL
!        THE LABEL TO BE PRINTED ALONG THE Y AXIS.
!     REAL YMAX
!        THE LARGEST VALUE ALONG THE Y AXIS
!     REAL YMIN
!        THE SMALLEST VALUE ALONG THE Y AXIS.
!
      DATA IPLUS/'+'/, IMINUS/'-'/, ISTAR/'*'/, IBLANK/' '/
!
      CALL IPRINT(IPRT)
!
      FPLM = R1MACH(2)
!
!     CHECK FOR INSUFFICIENT POINTS TO PLOT
!
      IF (IERR.NE.4) GO TO 20
      DO 10 I = 1, N
         IF (SDREST(I).NE.FPLM) GO TO 20
   10 CONTINUE
      WRITE (IPRT, 1090)
      RETURN
!
   20 CONTINUE
!
!     INITIALIZE VALUES FOR PROBABILITY PLOT
!
      CALL GETPI(PI)
      GAMMA = PI/8.0E0
      AN = N
      FAC1 = 1.0E0 / (AN - 2.0E0*GAMMA + 1.0E0)
      FAC2 = 10.0E0
!
!     INITIALIZE THE PLOT SIZE (IN PLOT UNITS)
!
      NROW = 26
!
!     BEGIN COMPUTATIONS FOR FIRST SET OF PLOTS
!
      IPLOT = 1
      NCOL = 111
!
!     SET X AXIS LIMITS FOR STANDARDIZED RESIDUAL VS ROW PLOT,
!
      ROWMIN = 1
      ROWMAX = N
!
      ROWMID = (ROWMAX+ROWMIN)/2.0E0
      ROWDIV = (ROWMAX-ROWMIN)/(NCOL-1)
!
!     PRINT TITLES FOR FIRST PLOTS
!
      WRITE (IPRT,1000)
      GO TO 90
!
!     BEGIN COMPUTATIONS FOR SECOND SET OF PLOTS
!
   40 IPLOT = 2
      NCOL = 51
!
!     SET AXIS LIMITS FOR THE STANDARDIZED RESIDUALS VS
!     STANDARDIZED RESIDUALS LAGGED BY ONE AND FOR PROBABILITY PLOT
!
      XMIN = -3.75E0
      XMAX = 3.75E0
      XDIV = (XMAX-XMIN)/(NCOL-1)
!
!     PRINT TITLES FOR SECOND PLOTS
!
      WRITE (IPRT,1050)
!
!     WRITE FIRST LINE OF PLOTS
!
   90 CONTINUE
!
!     PRINT PLOTS, ONE LINE AT A TIME
!
      NCOLP1 = NCOL + 1
      NCOLT2 = 2*NCOL
      YLABEL = 3.75E0
      YMAX = FPLM
      YMIN = 4.05E0
      DO 160 K=1,NROW
         YMIN = YMIN - 0.3E0
         IF (-3.70E0.GE.YMIN) YMIN = -FPLM
         DO 100 L=1,NCOL
            NCOLPL = L + NCOL
            LINE(L) = IBLANK
            IF (IPLOT.EQ.2) LINE(NCOLPL) = IBLANK
            IF ((K.NE.1) .AND. (K.NE.NROW)) GO TO 100
               LINE(L) = IMINUS
               IF (IPLOT.EQ.2) LINE(NCOLPL) = IMINUS
               IF ((MOD(L,10).NE.1) .AND. (L.NE.1+NCOL/2)) GO TO 100
                  LINE(L) = IPLUS
                  IF (IPLOT.EQ.2) LINE(NCOLPL) = IPLUS
  100    CONTINUE
         DO 130 I=1,N
            IF (.NOT.MVCHK(SDREST(I),FPLM)) THEN
               IF ((SDREST(I).GT.YMIN) .AND. (SDREST(I).LE.YMAX)) THEN
                  IF (IPLOT.EQ.1) THEN
                      IROW = INT(((I-ROWMIN)/ROWDIV)+1.5E0)
                      LINE(IROW) = ISTAR
                   ELSE
                      RATIO = (AN-GAMMA) * FAC1
                      IPRB = INT(4.91E0*(RATIO**0.14E0-
     +                          (1.0E0-RATIO)**0.14E0)*FAC2) + 77
                      IF (IPRB.LE.NCOL) IPRB = NCOL+1
                      IF (IPRB.GE.103) IPRB = 102
                      LINE(IPRB) = ISTAR
                      AN = AN - 1.0E0
                      IF ((AN.LT.2.0E0) .AND. (N.LE.10)) THEN
                         GAMMA = 1.0E0/3.0E0
                      END IF
                   END IF
                END IF
             END IF
  130    CONTINUE
!
!     SET PLOT LINE FOR CORRELATION PLOT OF SECOND SET OF PLOTS
!
         IF (IPLOT.EQ.2) THEN
            IMID = (NCOL-1)/2
            IF (K.LE.N-1) THEN
               DOT = 0.0E0
               CALL DOTC(RES, 0.0E0, N, RES(K+1), 0.0E0,
     +                   N-K, DOT, NDOT)
               IX = INT(IMID*DOT/RSS) + IMID + 1
               I1 = MIN(IX,IMID+1)
               I2 = MAX(IX,IMID+1)
               DO 135 IX=I1,I2
                  LINE(IX) = ISTAR
  135          CONTINUE
            END IF
         END IF
         IF (MOD(K,5).EQ.1) THEN
            IF (IPLOT.EQ.1) THEN
               WRITE (IPRT,2020) YLABEL, (LINE(L),L=1,NCOL)
            ELSE
               WRITE (IPRT,1020) K, (LINE(L),L=1,NCOL), YLABEL,
     +                           (LINE(L),L=NCOLP1,NCOLT2)
            END IF
            YLABEL = YLABEL - 1.5E0
         ELSE
            IF (IPLOT.EQ.1) THEN
               WRITE (IPRT,2030) (LINE(L),L=1,111)
            ELSE
               WRITE (IPRT,1030) (LINE(L),L=1,102)
            END IF
         END IF
         YMAX = YMIN
  160 CONTINUE
!
!     PRINT BOTTOM LINE OF GRAPHS
!
      IF (IPLOT.EQ.1) THEN
!
!     PRINT X AXIS LABELS FOR FIRST SET OF PLOTS
!
         WRITE (IPRT,1040) ROWMIN, ROWMID, ROWMAX
         GO TO 40
!
!     PRINT X AXIS LABELS FOR SECOND SET OF PLOTS
!
      ELSE
         WRITE (IPRT,1070)
      END IF
!
      RETURN
!
!     FORMAT STATEMENTS
!
 1000 FORMAT (/51X, 23H STD RES VS ROW NUMBER )
 1020 FORMAT (1X, I5, '+', 51A1, '+', 2X, F5.2, '+', 51A1, '+')
 1030 FORMAT (6X, '-', 51A1, '-', 7X, '-', 51A1, '-')
 1040 FORMAT (1X, F8.1, 47X, F8.1, 47X, F8.1)
 1050 FORMAT (/13X, 'AUTOCORRELATION FUNCTION OF RESIDUALS',
     +   23X, 36H NORMAL PROBABILITY PLOT OF STD RES )
 1070 FORMAT (4X, 5H-1.00, 22X, 3H0.0, 21X, 4H1.00, 5X, 4H-2.5, 23X,
     +   3H0.0, 22X, 3H2.5)
 1090 FORMAT (// 1X, 13(1H*)/ 1X, 13H*  WARNING  */ 1X, 13(1H*)//
     +   54H THE STANDARDIZED RESIDUAL PLOTS HAVE BEEN SUPPRESSED.,
     +   45H  NONE OF THE STANDARDIZED RESIDUALS COULD BE,
     +   10H COMPUTED,/
     +   50H BECAUSE FOR EACH OBSERVATION EITHER THE WEIGHT OR,
     +   48H THE STANDARD DEVIATION OF THE RESIDUAL IS ZERO.)
 2020 FORMAT (1X, F5.2, '+', 111A1, '+')
 2030 FORMAT (6X, '-', 111A1, '-')
      END
