!PGMMN
     subroutine pgmmn (yfft, n, nfft, iextnd, nf, per, lper, yaxis,&
     &   freq, lfreq, nprt, nmsub)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS THE MAIN ROUTINE FOR COMPUTING THE RAW PERIODOGRAM.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  DECEMBER 7, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     integer&
     &   iextnd,lfreq,lper,n,nf,nfft,nprt
!
!  ARRAY ARGUMENTS
     real(kind=wp) ::&
     &   freq(lfreq),per(lper),yaxis(lfreq),yfft(nfft)
     character&
     &   nmsub(6)*1
!
!  LOCAL SCALARS
     real(kind=wp) ::&
     &   yextnd
     integer&
     &   i,n1
!
!  EXTERNAL SUBROUTINES
!       EXTERNAL AMEAN,PGMEST,PGORD,PGOUT,SETFRQ
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL(KIND=WP) :: FREQ(LFREQ)
!        THE ARRAY IN WHICH THE FREQUENCIES CORRESPONDING TO THE
!        INTEGRATED SPECTRUM VALUES ARE STORED.
!     INTEGER I
!        AN INDEX VARIABLE.
!     INTEGER IEXTND
!        THE INDICATOR VARIABLE USED TO DESIGNATE WHETHER ZERO
!        (IEXTND .EQ. 0) OR THE SERIES MEAN (IEXTND .NE. 0) IS TO BE
!        USED TO EXTEND THE SERIES.
!     INTEGER LFREQ
!        THE LENGTH OF THE ARRAY FREQ.
!     INTEGER LPER
!        THE LENGTH OF THE ARRAY PER.
!     INTEGER N
!        THE INTEGER NUMBER OF OBSERVATIONS IN EACH SERIES
!     INTEGER NF
!        THE NUMBER OF FREQUENCIES AT WHICH THE PERIODOGRAM IS
!        TO BE COMPUTED.
!     INTEGER NFFT
!        THE EFFECTIVE LENGTH OF THE SERIES TO BE TRANSFORMED.
!     CHARACTER*1 NMSUB(6)
!        THE NAME OF THE CALLING SUBROUTINE
!     INTEGER NPRT
!        THE VARIABLE CONTROLING PRINTED OUTPUT, WHERE
!        IF NPRT .LE. -2, THE OUTPUT CONSISTS OF A PAGE PLOT OF THE
!                         PERIODOGRAM ON A LOG-LINEAR SCALE,
!        IF NPRT .EQ. -1, THE OUTPUT CONSISTS OF A PAGE PLOT OF THE
!                         PERIODOGRAM IN DECIBELS ON A LINEAR SCALE,
!        IF NPRT .EQ.  0, THE OUTPUT IS SUPPRESSED,
!        IF NPRT .EQ.  1, THE OUTPUT CONSISTS OF A VERTICAL PLOT OF THE
!                         PERIODOGRAM IN DECIBELS ON A LINEAR SCALE.
!        IF NPRT .GE.  2, THE OUTPUT CONSISTS OF A VERTICAL PLOT OF THE
!                         PERIODOGRAM ON A LOG-LINEAR SCALE,
!     REAL(KIND=WP) :: PER(LPER)
!        THE ARRAY IN WHICH THE PERIODOGRAM IS STORED.
!     REAL(KIND=WP) :: YAXIS(LFREQ)
!        THE ARRAY IN WHICH THE Y AXIS VALUES TO BE PLOTTED ARE STORED.
!     REAL(KIND=WP) :: YEXTND
!        THE VALUE USED TO EXTEND THE SERIES.
!     REAL(KIND=WP) :: YFFT(NFFT)
!        THE ARRAY CONTAINING THE OBSERVED TIME SERIES.
!
      yextnd = 0.0e0
      if (iextnd .ne. 0) call amean (yfft, n, yextnd)
!
!     EXTEND THE PERIODOGRAM ARRAY BY ITS MEAN  OR ZERO TO THE
!     EXTENDED LENGTH NFFT.
!
      n1 = n + 1
!
      do 40 i = n1, nfft
         yfft(i) = yextnd
   40 continue
!
!     COMPUTE THE PERIODOGRAM.
!
      call pgmest (yfft, nfft, nf, 1.0_wp, per, lper)
!
!     SET FREQUENCIES FOR PERIODOGRAM VALUES
!
      call setfrq (freq, nf, 1, 0.0_wp, 0.5_wp, 1.0e0)
!
      if (nprt .eq. 0) return
!
!     SET Y CO-ORDINATES FOR PERIODOGRAM PLOT.
!
      call pgord (per, nf, yaxis, nprt)
!
!     PLOT PERIODOGRAM IF OUTPUT NOT SUPPRESSED
!
      call pgout (yaxis, freq, nf, nprt, nmsub)
!
      return
!
      end
