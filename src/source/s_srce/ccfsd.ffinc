!CCFSD
      subroutine ccfsd (ccov11, ccov22, sdrhoc, lagmax, ncc, n, iccov)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS IS THE MAIN SUBROUTINE FOR COMPUTING THE STANDARD ERROR
!     OF THE CROSS CORRELATIONS.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 21, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     integer&
     &   iccov,lagmax,n,ncc
!
!  ARRAY ARGUMENTS
     real(kind=wp) ::&
     &   ccov11(iccov),ccov22(iccov),sdrhoc(ncc)
!
!  LOCAL SCALARS
     real(kind=wp) ::&
     &   div,sum
     integer&
     &   i,ilast,k,k0,km,kp
!
!  INTRINSIC FUNCTIONS
      intrinsic min,sqrt
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL CCOV11(ICCOV), CCOV22(ICCOV)
!        THE ARRAYS IN WHICH THE AUTOCOVARIANCES ARE STORED
!     REAL DIV
!        THE SQUARE ROOT OF THE PRODUCT OF THE AUTOCOVARIANCE
!        FUNCTION VALUES AT LAG ZERO.
!     INTEGER I
!        AN INDEX VARIABLE.
!     INTEGER ICCOV
!        THE DIMENSION OF THE ACVF ARRAYS.
!     INTEGER ILAST
!        THE LAST LAG AT WHICH THE STANDARD ERROR IS TO BE COMPUTED.
!     INTEGER K
!        AN INDEX VARIALBE.
!     INTEGER KM, KP, K0
!        THE LOCATIONS IN THE ARRAYS -RHOC- AND -SDRHOC-
!        OF THE LAG -K, K AND 0, RESPECTIVELY.
!     INTEGER LAGMAX
!        THE MAXIMUM LAG VALUE USED.
!     INTEGER N
!        THE INTEGER NUMBER OF OBSERVATIONS IN THE SERIES
!     INTEGER NCC
!        THE NUMBER OF CCF COMPUTED.
!     REAL SDRHOC(NCC)
!        THE ARRAY IN WHICH THE STANDARD ERRORS OF THE CROSS CORRELATION
!        ARE STORED
!     REAL SUM
!         A SUMMING VARIABLE.
!
      k0 = lagmax + 1
!
      div = ccov11(1) * ccov22(1)
!
      sum = 0.0e0
      do 10 i = 1, lagmax
         sum = sum + ccov11(i+1) * ccov22(i+1)
   10 continue
      sum = sum / div
      sdrhoc(k0) = sqrt(n + 2.0e0 * sum) / n
!
      do 30 k = 1, lagmax
         sum = 0.0e0
         ilast = min(lagmax, n-lagmax)
         do 20 i = 1, ilast
            sum = sum + (n-k-i)*ccov11(i+1)*ccov22(i+1)
   20    continue
         sum = sum / div
         km = k0 - k
         sdrhoc(km) = sqrt((n - k) + 2.0e0 * sum) / n
!
         kp = k0 + k
         sdrhoc(kp) = sdrhoc(km)
!
   30 continue
!
      return
      end
