!CCVFF
     subroutine ccvff (yfft1, yfft2, n, nfft, lagmax, ccov12, ccov21,&
     &   iccov, lyfft, work, lwork)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE COMPUTES THE CROSS COVARIANCE FUNCTION
!     BETWEEN TWO SERIES.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 21, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     integer&
     &   iccov,lagmax,lwork,lyfft,n,nfft
!
!  ARRAY ARGUMENTS
     real(kind=wp) ::&
    &   ccov12(iccov),ccov21(iccov),work(lwork),yfft1(lyfft),&
     &   yfft2(lyfft)
!
!  LOCAL SCALARS
     real(kind=wp) ::&
     &   fac
     integer&
     &   i,isn,j,lag,nf,nf2,nfft2
!
!  EXTERNAL SUBROUTINES
!       EXTERNAL FFT,REALTR
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL CCOV12(ICCOV), CCOV21(ICCOV)
!        THE ARRAYS IN WHICH THE CCVF FOR SERIES 1 LAGGED
!        BEHIND SERIES 2 AND VISA VERSA, RESPECTIVELY, ARE STORED.
!     REAL FAC
!        THE APPROPRIATE FACTOR USED TO SCALE THE CCVF.
!     INTEGER I
!        AN INDEX VARIABLE.
!     INTEGER ICCOV
!        THE DIMENSION OF THE ARRAYS CCOV12 AND CCOV21.
!     INTEGER LAG, LAGMAX
!        THE INDEXING VARIABLE INDICATING THE LAG VALUE OF THE
!        CROSS CORRELATION BEING COMPUTED, AND THE MAXIMUM LAG
!        VALUE TO BE USED.
!     INTEGER LWORK
!        THE DIMENSION OF THE VECTOR WORK.
!     INTEGER LYFFT
!        THE ACTUAL LENGTH OF THE ARRAYS YFFT1 AND YFFT2.
!     INTEGER N
!        THE INTEGER NUMBER OF OBSERVATIONS IN THE SERIES
!     INTEGER NF
!        THE NUMBER OF FOURIER FREQUENCIES.
!     INTEGER NFFT
!        THE NUMBER OF OBSERVATIONS IN THE EXTENDED SERIES.
!     INTEGER NFFT2
!        THE EFFECTIVE NUMBER OF OBSERVATIONS FOR THE FFT TRANSFORM.
!     INTEGER NF2
!        TWICE THE NUMBER OF FOURIER FREQUENCIES.
!     REAL WORK(LWORK)
!        THE WORK ARRAY NEEDED FOR THE COMPUTATIONS.
!     REAL YFFT1(LYFFT), YFFT2(LYFFT)
!        THE VECTORS CONTAINING THE OBSERVED SERIES ALREADY PROCESSED
!        BY ONE PASS OF A FFT.
!
      nfft2 = (nfft-2) / 2
      nf = nfft2 + 1
      nf2 = nf * 2
!
!     COMPUTE THE CROSS COVARIANCES
!
      do 10 i = 2, nf2, 2
         work(i-1) = yfft1(i-1)*yfft2(i-1) + yfft1(i)*yfft2(i)
         work(i) = yfft1(i)*yfft2(i-1) - yfft1(i-1)*yfft2(i)
   10 continue
!
      fac = 1.0e0 / (4 * (nfft-2) * n)
!
      isn = -2
!
      call realtr (work, work(2), nfft2, isn)
!
      call fft (work, work(2), nfft2, nfft2, nfft2, isn)
!
      ccov12(1) = work(1) * fac
      ccov21(1) = ccov12(1)
!
      do 20 lag = 1, lagmax
         ccov21(lag+1) = work(lag+1) * fac
!
         j = nfft - 1 - lag
         ccov12(lag+1) = work(j) * fac
   20 continue
!
      return
      end
