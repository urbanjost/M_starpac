!HSTMN
      subroutine hstmn(y, n, ncells, ylb, yub, lsort, ydist)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS IS THE MAIN ROUTINE FOR PRODUCING A HISTOGRAM
!
!     ORIGINAL VERSION ADAPTED FROM AN EARLY VERSION OF MINITAB.
!
!     ADAPTED BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  MAY 17, 1982
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     real(kind=wp) ::&
     &   ylb,yub
     integer&
     &   n,ncells
!
!  ARRAY ARGUMENTS
     real(kind=wp) ::&
     &   y(n),ydist(ncells)
     integer&
     &   lsort(n)
!
!  LOCAL SCALARS
     real(kind=wp) ::&
    &   alpha,b1sqrt,b2,cfract,cfrctm,cntmx,fract,p,scale,sum1,sum2,&
    &   sum3,sumd2,sumd3,sumd4,sumda,sumt1,temp,width,xn,xnn,yintmp,&
     &   ymax,ymddsd,ymean,ymeant,ymed,ymidrg,ymin,yrange,ysd,yvar
     integer&
     &   i,iflag,iprt,j,mid,nhigh,nlow,nobs,num,nums
     character&
     &   iplus*1
!
!  LOCAL ARRAYS
     real(kind=wp) ::&
     &   a(6)
!
!  EXTERNAL SUBROUTINES
!      EXTERNAL GENI,IPRINT,SRTIR,SRTRI,STAT1,SUMBS,SUMDS,SUMSS,SUMTS,
!     +   VERSP
!
!  INTRINSIC FUNCTIONS
      intrinsic abs,int,max,mod,nint,sqrt
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL A(6)
!        A VECTOR USED FOR PRINTING THE HISTOGRAM SCALE.
!     REAL ALPHA
!        THE PERCENTAGE TO BE TRIMMED OFF EACH END OF Y FOR THE
!        TRIMMED MEANS CALCULATIONS.
!     REAL B1SQRT
!        BETA ONE  -  A MEASURE OF SKEWNESS
!     REAL B2
!        BETA TWO  -  A MEASURE OF KURTOSIS
!     REAL CFRACT
!        THE CUMULATIVE DISTRIBUTION
!     REAL CFRCTM
!        THE REVERSE CUMULATIVE DISTRIBUTION
!     REAL CNTMX
!        THE SIZE OF THE LARGEST CELL COUNT
!     REAL FRACT
!        THE FRACTION OF THE OBSERVATIONS IN A GIVEN CELL
!     INTEGER I
!        AN INDEX
!     INTEGER IFLAG
!        IF 1, THEN MORE THAN 50 OBS. FELL IN A SINGLE CELL,
!        AND A SCALED HISTOGRAM WILL BE PROVIDED.
!     CHARACTER*1 IPLUS
!        THE CHARACTER +
!     INTEGER IPRT
!        THE NUMBER OF THE STANDARD OUTPUT UNIT.
!     INTEGER J
!        AN INDEX.
!     INTEGER LSORT(N)
!        THE PERMUTATION VECTOR.
!     INTEGER MID
!        THE INDEX OF THE (AN) ELEMENT OF Y CLOSEST TO ZERO, WHEN
!        Y HAS BEEN SORTED.
!     INTEGER N
!        INPUT PARAMETER.  THE LENGTH OF Y.
!     INTEGER NCELLS
!        THE NUMBER OF CELLS IN THE FREQUENCY DISTRIBUTION.
!     INTEGER NHIGH
!        THE INDEX OF THE LARGEST VALUE IN THE SORTED ARRAY
!        TO BE USED IN THE HISTOGRAM
!     INTEGER NLOW
!        THE INDEX OF THE SMALLEST VALUE IN THE SORTED ARRAY
!        TO BE USED IN THE HISTOGRAM
!     INTEGER NOBS
!        THE NUMBER OF OBSERVATIONS ACTUALLY USED IN THE HISTOGRAM
!     INTEGER NUM
!        THE CELL COUNT
!     INTEGER NUMS
!        THE SCALED CELL COUNT
!     REAL P
!        A VARIABLE USED TO DETERMINE THE SCALE
!     REAL SCALE
!        THE PRINTED INCREMENT ON THE HISTOGRAM SCALE
!     REAL SUMDA
!        THE SUM OF THE ABSOLUTE DIFFERENCES FROM THE MEAN.
!     REAL SUMD2
!        THE SUM OF THE SQUARES OF THE DIFFERENCES.
!     REAL SUMD3
!        THE SUM OF THE CUBES OF THE DIFFERENCES.
!     REAL SUMD4
!        THE SUM OF THE 4TH POWERS OF THE DIFFERENCES.
!     REAL SUMT1
!        THE SUM OF THE ALPHA TRIMMED ARRAY Y.
!     REAL SUM1, SUM2, SUM3
!        VARIOUS SUMS OF THE DATA.
!     REAL TEMP
!        A TEMPORARY STORAGE VARIABLE
!     REAL WIDTH
!        THE WIDTH OF A CELL
!     REAL XN
!        THE FOATING POINT REPRESENTATION OF N
!     REAL XNN
!        THE UNROUNDED NUMBER OF PLOTTING POSISTIONS ON A SCALES
!        HISTOGRAM
!     REAL Y(N)
!        INPUT PARAMETER.  THE VECTOR OF DATA POINTS ON WHICH
!        THE STATISTICS ARE COMPUTED.  Y IS SORTED, BUT RESTORED
!        TO ITS ORIGINAL ORDER AFTERWARDS.
!     REAL YDIST(NCELLS)
!        THE FREQUENCY DISTRIBUTION USED TO CREATE THE HISTOGRAM.
!     REAL YINTMP
!        THE MIDPOINT OF THE ITH CELL
!     REAL YLB
!        THE LOWER BOUND FOR SELECTING DATA FROM Y FOR THE HISTOGRAM.
!     REAL YMAX
!        THE HISTOGRAM UPPER BOUND USED
!     REAL YMDDSD
!        THE MEAN ABSOLUTE DEVIATION / THE STANDARD DEVIATION
!     REAL YMEAN, YMEANT
!        THE MEAN OF THE OBSERVATIONS USED IN THE HISTOGRAM
!     REAL YMED
!        THE MEDIAN OF THE OBSERVATIONS USED IN THE HISTOGRAM
!     REAL YMIDRG
!        THE MID RANGE OF THE OBSERVATIONS USED IN THE HISTOGRAM
!     REAL YMIN
!        THE HISTOGRAM LOWER BOUND USED.
!     REAL YRANGE
!        THE RANGE OF THE OBSERVATIONS USED IN THE HISTOGRAM
!     REAL YSD
!        THE STANDARD DEVIATION OF THE OBSERVATIONS USED IN THE
!        HISTOGRAM.
!     REAL YUB
!        THE UPPER BOUND FOR SELECTING DATA FROM Y FOR THE HISTOGRAM.
!     REAL YVAR
!        THE VARIANCE OF THE OBSERVATIONS.
!
      data iplus /'+'/
      data alpha/0.25_wp/
!
      call iprint(iprt)
!
!     SORT DATA
!
      call geni(lsort, n, 1, 1)
      call srtir(lsort, n, y)
!
!     FIX UPPER AND LOWER BOUNDS.
!
      nlow = 1
      nhigh = n
      if (ylb.eq.yub) go to 50
!
!     FIND INDEX OF THE FIRST VALUE OF Y .GE. YLB
!
      do 20 i=1,n
         if (y(i).lt.ylb) go to 20
         nlow = i
         go to 30
   20 continue
!
!     FIND INDEX OF THE LAST VALUE OF Y .LE. YUB
!
   30 do 40 i=1,n
         j = n - i + 1
         if (y(j).gt.yub) go to 40
         nhigh = j
         go to 50
   40 continue
   50 continue
      xn = nhigh-nlow+1
      nobs = nhigh - nlow + 1
!
!     COMPUTE MEDIAN, EXTREMA, MID-RANGE, RANGE AND FREQUENCY
!     DISTRIBUTION FOR NCELLS CELLS
!
     call stat1(y(nlow), nobs, ymed, ymin, ymax, ymidrg, yrange,&
     &   ncells, ylb, yub, ydist)
!
      if (ylb.ge.yub) go to 55
!
      ymin = ylb
      ymax = yub
!
   55 continue
!
!     COMPUTE MEAN, TRIMMED MEAN, STANDARD DEVIATION,
!     MEAN DEVIATION/STANDARD DEVIATION, BETA ONE, AND BETA TWO
!
      call sumbs(y, n, nlow, mid, nhigh)
      call sumss(y, n, nlow, mid, nhigh, sum1, sum2, sum3, ymean)
      call sumts(y(nlow), nobs, alpha, sumt1, ymeant)
     call sumds(y, n, nlow, mid, nhigh, ymean, sumda, sumd2, sumd3,&
     &   sumd4)
!
      yvar = 0.0_wp
      ysd = 0.0_wp
      b1sqrt = 0.0_wp
      b2 = 0.0_wp
      ymddsd = 0.0_wp
!
      if ((sumd2.le.0.0_wp) .or. (nobs.le.1)) go to 60
!
      yvar = sumd2/(nobs-1)
      ysd = sqrt(yvar)
      b1sqrt = abs((sumd3/xn)/((sumd2/xn)**1.5_wp))
      b2 = (sumd4/xn)/((sumd2/xn)**2)
      ymddsd = sumda/(ysd*nobs)
!
   60 continue
!
!     OUTPUT STATISTICS
!
      call versp(.true.)
      write (iprt,1070) n, y(nlow), y(nhigh), ymin, ymax
     write (iprt,1000)&
    &   ncells, nobs, ymeant, y(nlow), ysd, y(nhigh), ymddsd,&
     &   ymean, b1sqrt, ymed, b2
      write (iprt,1010)
!
!     CHECK FOR MORE THAN 50 VALUES IN INTERVAL AND FIND MAX. VALUE.
!
      iflag = 0
      cntmx = 0.0_wp
      do 80 i=1,ncells
         if (ydist(i).gt.cntmx) cntmx = ydist(i)
   80 continue
      if (nint(cntmx).gt.50) iflag = 1
!
!     DETERMINE SCALE.
!
      if (iflag.eq.0) then
         scale = 1.0_wp
      else
         p = cntmx/xn
         scale = 0.05_wp
         if (p.gt.0.25_wp) scale = 0.1_wp
         if (p.gt.0.5_wp) scale = 0.2_wp
      end if
!
!     PRINT COLUMN HEADINGS AND HISTOGRAM SCALE.
!
      if (iflag.eq.0) write (iprt,1020)
      if (iflag.ne.0) write (iprt,1080)
      write (iprt,1090)
      if (iflag.ne.0) go to 100
      write (iprt,1030) (i,i=10,50,10)
      go to 120
  100 a(1) = 0.0_wp
      do 110 i=1,5
         a(i+1) = a(i) + scale
  110 continue
      write (iprt,1040) (a(i),i=1,6)
  120 write (iprt,1050)
      cfract = 0.0_wp
      cfrctm = 1.0_wp
      temp = 0.0_wp
      width = (ymax-ymin)/ncells
      yintmp = ymin
      yintmp = yintmp - width/2.0_wp
      do 150 i=1,ncells
         num = int(ydist(i)+0.5_wp)
        if (mod(ncells,2).eq.1 .and. i.eq.ncells/2+1&
     &       .and. ymin.eq.(-ymax)) then
            yintmp = 0.0_wp
         else
            yintmp = yintmp + width
         end if
         fract = ydist(i)/xn
         cfract = cfract + fract
         cfrctm = 1.0_wp - temp
         temp = cfract
         if (num.le.0) then
           write (iprt,1060) yintmp, cfract, cfrctm, fract, num
         else
           if (iflag.eq.0) then
             nums = num
           else
             xnn = fract*10.0_wp/scale
             nums = int(xnn)
             nums = max(1, nums + int(xnn-nums+0.5_wp))
           end if
          write (iprt,1060) yintmp, cfract, cfrctm, fract,&
     &        num, (iplus,j=1,nums)
         end if
  150 continue
!
!     RESTORE DATA TO ORIGINAL ORDER
!
      call srtri(y, n, lsort)
!
      return
!
!     FORMAT STATEMENTS
!
1000 format(/26h number of cells        = , i15/&
    &        26h observations used      = , i15, 11x,&
    &              22h25 pct trimmed mean = , 1pe15.8/&
    &        26h min. observation used  = , e15.8, 11x,&
    &              22hstandard deviation  = , e15.8/&
    &        26h max. observation used  = , e15.8, 11x,&
    &              22hmean dev./std. dev. = , e15.8/&
    &        26h mean value             = , e15.8, 11x,&
    &              22hsqrt(beta one)      = , e15.8/&
    &        26h median value           = , e15.8, 11x,&
     &              22hbeta two            = , e15.8)
1010 format(//44h for a normal distribution, the values (mean,&
    &   56h deviation/standard deviation), sqrt(beta one), and beta,&
    &   22h two are approximately/&
    &   ' 0.8, 0.0 AND 3.0, RESPECTIVELY.  TO TEST THE ',&
    &   59hnull hypothesis of normality, see tables of critical values,&
    &   13h pp. 207-208,/  22h biometrika tables for,&
    &   58h statisticians, vol. 1.  see pp. 67-68 for a discussion of,&
     &   13h these tests.)
1020 format(///5x,39hinterval     cum.   1-cum.   cell   no.,19x,&
     & 22hnumber of observations)
 1030 format('+',47x,1h0,8x,5(i2,8x))
 1040 format('+',46x,6(f4.2,6x))
 1050 format(4x,42('-'),2x,'+',5(10h---------+))
 1060 format(3x,1pe13.6,2x,2(0pf5.3,3x),f5.3,1x,i5,4x,50a1)
1070 format (10h histogram//&
    &        26h number of observations = , i15/&
    &        26h minimum observation    = , 1pe15.8/&
    &        26h maximum observation    = , e15.8//&
    &        26h histogram lower bound  = , e15.8/&
     &        26h histogram upper bound  = , e15.8)
1080 format(///5x,39hinterval     cum.   1-cum.   cell   no.,23x,&
     & 13hcell fraction)
 1090 format(5x,41hmid point   fract.  fract.  fract.  obs. )
      end
