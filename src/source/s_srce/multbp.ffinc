!MULTBP
      subroutine multbp(t, lt, c, lc, temp, ltemp, mbo)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE MULTIPLIES TOGETHER TWO DIFFERENCE FACTORS FROM A
!     (BOX-JENKINS) TIME SERIES MODEL.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  AUGUST 1, 1985
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     integer&
     &   lc,lt,ltemp,mbo
!
!  ARRAY ARGUMENTS
     real(kind=wp) ::&
     &   c(mbo),t(2*mbo),temp(mbo)
!
!  LOCAL SCALARS
     integer&
     &   i,j,ji,k
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL(KIND=WP) :: C(MBO)
!        THE SECOND FACTOR ON INPUT AND THE EXPANDED FACTOR ON OUTPUT.
!     INTEGER I
!        AN INDEX VARIABLE.
!     INTEGER J
!        AN INDEX VARIABLE.
!     INTEGER JI
!        AN INDEX VARIABLE
!     INTEGER K
!        AN INDEX VARIABLE.
!     INTEGER LC
!        THE LARGEST ORDER OF THE SECOND FACTOR ON INPUT, AND
!        THE LARGEST ORDER OF THE EXPANDED FACTOR ON OUTPUT.
!     INTEGER LT
!        THE LARGEST ORDER OF THE FIRST FACTOR.
!     INTEGER LTEMP
!        THE LENGTH OF THE VECTOR TEMP.
!     INTEGER MBO
!        THE MAXIMUM BACK ORDER OPERATOR.
!     REAL(KIND=WP) :: T(2*MBO)
!        A TEMPORARY WORK VECTOR.
!     REAL(KIND=WP) :: TEMP(MBO)
!        A TEMPORARY WORK VECTOR
!
      if (lc .eq. 0) go to 15
      do 10 j = 1, lc
         temp(j) = c(j)
   10 continue
   15 k = lc + 1
      do 20 j=k,ltemp
         temp(j) = 0.0e0
   20 continue
      if (lt .eq. 0) go to 50
      do 40 j=1,lt
         temp(j) = temp(j) + t(j)
         if (lc .eq. 0) go to 40
         do 30 i=1,lc
            ji = j + i
            temp(ji) = temp(ji) - c(i)*t(j)
   30    continue
   40 continue
!
   50 do 60 j=1,ltemp
         c(j) = temp(j)
   60 continue
      lc = ltemp
      return
      end
