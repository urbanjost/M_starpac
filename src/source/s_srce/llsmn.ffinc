!LLSMN
      SUBROUTINE LLSMN(Y, X, WT, N, M, NPAR, IX, RES, PAR, NNZW,
     &   RSD, PV, SDPV, SDRES, IPTOUT, WY, XW, VCV, FC, RED,
     &   T, PAR1, ACC, IFIT, WEIGHT, C, LLHDR, PAGE, WIDE)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS IS THE MAIN PROGRAM FOR THE LINEAR LEAST SQUARES FITTING
!     PROGRAMS
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  DECEMBER 29, 1982
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) ::
     &   RSD
      INTEGER
     &   IFIT,IX,M,N,NNZW,NPAR
      LOGICAL
     &   PAGE,WEIGHT,WIDE
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   ACC(NPAR),C(NPAR),FC(N,NPAR),PAR(NPAR),PAR1(NPAR),PV(N),
     &   RED(NPAR),RES(N),SDPV(N),SDRES(N),T(NPAR,NPAR),
     &   VCV(NPAR,NPAR),WT(N),WY(N),X(IX,M),XW(N,NPAR),Y(N)
      INTEGER
     &   IPTOUT(4)
!
!  SUBROUTINE ARGUMENTS
       EXTERNAL LLHDR
!
!  SCALARS IN COMMON
      INTEGER
     &   IERR
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     &   COND,DF,FPLM,FPLRS,FPSPM,PROBT,PROBT1,PX,R2,RATIO,RATIO1,
     &   RSD1,RSS,RSS1,RVAR,RVAR1,SDC,SDC1,SM,TD,WTSQRT,WTSUM,WTYM,YSUM
      INTEGER
     &   I,IDF,IDF1,IER,IPRT,IREFIT,ISUBHD,J,K,NC
      LOGICAL
     &   CONST
!
!  LOCAL ARRAYS
      REAL(KIND=WP) ::
     &   DET(2)
!
!  EXTERNAL FUNCTIONS
!      REAL(KIND=WP) ::
!     +   CDFT,R1MACH
!      EXTERNAL CDFT,R1MACH
!
!  EXTERNAL SUBROUTINES
!      EXTERNAL ACCDIG,FITPT1,FITPT2,IPRINT,MGS,OANOVA,VCVOUT
!      EXTERNAL STRCO,STRDI
!
!  INTRINSIC FUNCTIONS
      INTRINSIC ABS,MAX,SQRT
!
!  COMMON BLOCKS
      COMMON /ERRCHK/IERR
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL ACC(NPAR)
!        THE NUMBER OF ACCURATE DIGITS.
!     REAL C(NPAR)
!        A WORK VECTOR.
!     REAL COND
!        THE CONDITION NUMBER OF THE DESIGN MATRIX.
!     LOGICAL CONST
!        AN INDICATOR VALUE DESIGNATING WHETHER THE FIRST COLUMN OF
!        THE DESIGN MATRIX IS ALL ONES FOR A CONSTANT.
!     REAL DET(2)
!        THE DETERMINENT.
!     REAL DF
!        THE DEGREES OF FREEDOM.
!     REAL FC(N,NPAR)
!        THE ORTHONORMALIZATION MATRIX.
!     REAL FPLM
!        THE FLOATING POINT LARGEST MAGNITUDE.
!     REAL FPLRS
!        THE FLOATING POINT LARGEST RELATIVE SPACING.
!     REAL FPSPM
!        THE FLOATING POINT SMALLEST POSITIVE MAGNITUDE.
!     INTEGER I
!        AN INDEX.
!     INTEGER IDF
!        THE DEGREES OF FREEDOM
!     INTEGER IDF1
!        THE DEGREES OF FREEDOM FOR THE FIT WITHOUT THE LAST INDEPENDENT
!        VARIABLE.
!     INTEGER IER
!        THE ERROR FLAG RETURNED BY THE INVERSION ROUTINES.
!     INTEGER IERR
!        THE INTEGER VALUE DESIGNATING WHETHER ANY ERRORS WERE
!        DETECTED IN THE PARAMETER LIST.
!        IF IERR .EQ. 0, NO ERRORS WERE DETECTED.
!        IF IERR .EQ. 1, ERRORS WERE DETECTED.
!     INTEGER IFIT
!        THE INDICATOR VALUE DESIGNATING WHETHER THE FIT IS OF A
!        GENERAL MODEL (IFIT=3) OR A POLYNOMIAL MODEL (IFIT=1).
!     INTEGER IPRT
!        THE UNIT NUMBER FOR PRINTED OUTPUT.
!     INTEGER IPTOUT(4)
!        THE VARIABLE USED TO CONTROL PRINTED OUTPUT FOR EACH SECTION.
!     INTEGER IREFIT
!        AN INDICATOR USED TO DESIGNATE WHETHER THE FIT IS OF THE
!        FULL DESIGN MATRIX (IREFIT=0) OR IS OMITTING THE LAST
!        INDEPENDENT VARIABLE (IREFIT=1).
!     INTEGER ISUBHD
!        AN INDICATOR VALUE SPECIFYING SUBHEADINGS TO BE PRINTED.
!     INTEGER IX
!        THE FIRST DIMENSION OF THE MATRIX X.
!     INTEGER J
!        AN INDEX.
!     INTEGER K
!        AN INDEX.
!     EXTERNAL LLHDR
!        THE NAME OF THE ROUTINE THAT PRODUCED THE HEADING.
!     INTEGER M
!        THE NUMBER OF INDEPENDENT VARIABLES.
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS.
!     INTEGER NC
!        THE NUMBER OF PARAMETERS BEING FIT.
!     INTEGER NNZW
!        THE NUMBER OF NON ZERO WEIGHTS.
!     INTEGER NPAR
!        THE NUMBER OF PARAMETERS.
!     LOGICAL PAGE
!        THE VARIABLE USED TO INDICATE WHETHER A GIVEN SECTION OF
!        THE OUTPUT IS TO BEGIN ON A NEW PAGE (TRUE) OR NOT (FALSE).
!     REAL PAR(NPAR)
!        THE PARAMETERS  TO BE ESTIMATED.
!     REAL PAR1(NPAR)
!        THE PARAMETERS TO BE ESTIMATED OMMITTING THE LAST
!        INDEPENDENT VARIABLE.
!     REAL PROBT
!        ...
!     REAL PROBT1
!        ...
!     REAL PV(N)
!        THE PREDICTED VALUES.
!     REAL PX
!        A WORK VARIABLE.
!     REAL RATIO
!        THE RATIO OF THE PARAMETERS TO THEIR STANDARD DEVIATIONS.
!     REAL RATIO1
!        THE RATIO OF THE PARAMETERS COMPUTED OMITTING THE LAST
!        INDEPENDENT VARIABLE TO THEIR STANDARD DEVIATIONS.
!     REAL RED(NPAR)
!        THE REDUCTION TO THE SUM OF SQUARES DUE TO EACH PARAMETER.
!     REAL RES(N)
!        THE RESIDUALS.
!     REAL RSD
!        THE RESIDUAL STANDARD DEVIATION.
!     REAL RSD1
!        THE RESIDUAL STANDARD DEVIATION FROM THE FIT OMITTING
!        THE LAST INDEPENDENT VARIABLE.
!     REAL RSS
!        THE RESIDUAL SUM OF SQUARES.
!     REAL RSS1
!        THE RESIDUAL SUM OF SQUARES FROM THE FIT OMITTING
!        THE LAST INDEPENDENT VARIABLE.
!     REAL RVAR
!        THE RESIDUAL VARIANCE.
!     REAL RVAR1
!        THE RESIDUAL VARIANCE FROM THE FIT OMITTING
!        THE LAST INDEPENDENT VARIABLE.
!     REAL R2
!        THE MULTIPLE CORRELATION PARAMETER.
!     REAL SDC
!        THE ESTIMATED STANDARD DEVIATIONS OF THE PARAMETERS.
!     REAL SDC1
!        THE ESTIMATED STANDARD DEVIATIONS OF THE PARAMETERS OMITTING
!        THE LAST INDEPENDENT VARIABLE.
!     REAL SDPV(N)
!        THE STANDARD DEVIATIONS OF THE PREDICTED VALUES.
!     REAL SDRES(N)
!        THE STANDARDIZED RESIDUALS.
!     REAL SM
!        A WORK VARIABLE.
!     REAL T(NPAR,NPAR)
!        THE TRIANGULAR MATRIX FROM THE DECOMPOSITION.
!     REAL TD
!        A WORK VARIABLE.
!     REAL VCV(NPAR,NPAR)
!        THE VARIANCE COVARIANCE MATRIX.
!     LOGICAL WEIGHT
!        THE VARIABLE USED TO INDICATE WHETHER WEIGHTED ANALYSIS IS TO
!        BE PERFORMED (TRUE) OR NOT (FALSE).
!     LOGICAL WIDE
!        THE VARIABLE USED TO INDICATE WHETHER THE HEADING SHOULD
!        BE FULL WIDTH (TRUE) OR NOT (FALSE).
!     REAL WT(N)
!        THE WEIGHTS (A DUMMY VECTOR IN THE UNWEIGHTED CASE).
!     REAL WTSQRT
!        THE SQUARE ROOT OF THE WEIGHT.
!     REAL WTSUM
!        THE SUM OF THE WEIGHTS.
!     REAL WTYM
!        THE SUM OF THE WEIGHTED DEPENDENT VARIABLES.
!     REAL WY(N)
!        THE VECTOR CONTAINING SQRT(WT)*Y.
!     REAL X(IX,M)
!        THE INDEPENDENT VARIABLE.
!     REAL XW(N,NPAR)
!        THE MATRIX CONTAINING X * SQRT(WT).
!     REAL Y(N)
!        THE DEPENDENT VARIABLE.
!     REAL YSUM
!        THE SUM OF THE WEIGHTED DEPENDENT VARIABLES SQUARED.
!
      CALL IPRINT(IPRT)
!
!     INITIALIZE VARIABLES
!
      FPLM = R1MACH(2)
      FPLRS = R1MACH(4)
      FPSPM = R1MACH(1)
      NC = NPAR
      IDF = NNZW - NC
      DF = IDF
!
      R2 = 0.0
      RVAR1 = 0.0
      RSD1 = 0.0
      J = 0
      IDF1 = 0
      CONST = .FALSE.
!
      YSUM = 0.0E0
      WTSUM = 0.0E0
      DO 10 I=1,N
         IF (WT(I).LE.0.0E0) GO TO 10
         YSUM = YSUM + WT(I)*Y(I)*Y(I)
         WTSUM = WTSUM + WT(I)
   10 CONTINUE
!
!     BEGIN FIT
!
      IREFIT = -1
!
!     IF IREFIT IS EQUAL TO 1 FIT IS OMITTING THE LAST VARIABLE
!
   50 IREFIT = IREFIT + 1
!
!     COMPUTE WORKING VECTORS
!
      WTYM = 0.0E0
      I = 0
      DO 70 K=1,N
         IF (WT(K).LE.0.0E0) GO TO 70
         WTSQRT = SQRT(WT(K))
         I = I + 1
         PX = WTSQRT
         DO 60 J=1,NC
            IF (IFIT.EQ.1 .AND. J.GE.2) PX = PX*X(K,1)
            IF (IFIT.EQ.3) PX = X(K,J)*WTSQRT
            XW(I,J) = PX
   60    CONTINUE
!
!     COMPUTE WY ( WEIGHTS * Y VECTOR )
!
         WY(I) = Y(K)*WTSQRT
         WTYM = WTYM + WT(K)*Y(K)
   70 CONTINUE
      WTYM = WTYM/WTSUM
!
!     COMPUTE ESTIMATED VALUES OF PARAMETERS
!
      IF (IREFIT.EQ.1) GO TO 80
      IER = 0
      CALL MGS(XW, WY, NNZW, NC, PAR, C, T(1,1), T, NPAR, N, IER)
      IF (IER.EQ.0) GO TO 90
      IERR = 3
      ISUBHD = 0
      CALL LLHDR(PAGE, WIDE, ISUBHD)
      WRITE (IPRT,1160)
      WRITE (IPRT,1150)
      RETURN
   80 CONTINUE
      IER = 0
      CALL MGS(XW, WY, NNZW, NC, PAR1, C, T(1,1), T, NPAR, N, IER)
!
!     COMPUTE PREDICTED VALUES (PV) AND RESIDUALS (RES)
!             RESIDUAL SUM OF SQUARES (RSS) AND
!             RESIDUAL STANDARD DEVIATION (RSD) AND
!             SUM OF WEIGHTS (WTSUM) AND
!             RESIDUAL VARAINCE (RVAR)
!
   90 RSS = 0.0E0
      RSS1 = 0.0E0
      DO 120 I=1,N
         SM = 0.0E0
         PX = 1.0E0
         DO 100 J=1,NC
            IF (IFIT.EQ.1 .AND. J.GE.2) PX = PX*X(I,1)
            IF (IFIT.EQ.3) PX = X(I,J)
            IF (IREFIT.EQ.0) SM = SM + PAR(J)*PX
            IF (IREFIT.EQ.1) SM = SM + PAR1(J)*PX
  100    CONTINUE
         IF (IREFIT.EQ.0) THEN
            PV(I) = SM
            RES(I) = Y(I) - PV(I)
            RSS = RSS + RES(I)*WT(I)*RES(I)
         ELSE
            RSS1 = RSS1 + (Y(I)-SM)*WT(I)*(Y(I)-SM)
         END IF
  120 CONTINUE
!
      IF (IREFIT.EQ.0) GO TO 130
      RVAR1 = RSS1/(DF+1.0E0)
      RSD1 = SQRT(RVAR1)
      GO TO 380
  130 RVAR = 0.0E0
      IF (DF.GT.0.0E0) RVAR = RSS/DF
      RSD = SQRT(RVAR)
!
!     COMPUTE MULTIPLE CORRELATION PARAMETER SQUARED (R2)
!
      SM = 0.0E0
      CONST = .TRUE.
      DO 140 I=1,N
         IF ((X(I,1).NE.1.0E0) .AND. (IFIT.EQ.3)) CONST = .FALSE.
         SM = SM + (Y(I)-WTYM)*WT(I)*(Y(I)-WTYM)
  140 CONTINUE
      R2 = 1.0E0
      IF (SM.GT.0.0E0) R2 = 1.0E0-RSS/SM
!
!     CHECK FOR -EXACT- FIT
!
      TD = 0.0E0
      IF (DF.GT.0.0E0) TD = SQRT(RSS)
      IF (YSUM.GT.0.0E0) TD = TD/SQRT(YSUM)
      IF (TD.GT.10.0E0*FPLRS) GO TO 180
!
      IERR = 0
      IF ((IPTOUT(1).EQ.0) .AND. (IPTOUT(2).EQ.0) .AND. (IPTOUT(3).EQ.0)
     &   .AND. (IPTOUT(4).EQ.0)) GO TO 145
      ISUBHD = 0
      CALL LLHDR(PAGE, WIDE, ISUBHD)
      WRITE (IPRT,1180)
      WRITE (IPRT,1020) (I,PAR(I),I=1,NPAR)
!
!     ZERO OUT VCV MATRIX AND SDRES AND SDPV VECTORS
!
  145 CONTINUE
      DO 150 I=1,N
         SDPV(I) = 0.0E0
         SDRES(I) = 0.0E0
  150 CONTINUE
      DO 170 I=1,NPAR
         DO 160 J=1,NPAR
            VCV(I,J) = 0.0E0
  160    CONTINUE
  170 CONTINUE
      RETURN
!
  180 CONTINUE
!
!     COMPUTE ORTHO NORMALIZATION
!
      DO 200 I=1,NC
         T(I,I) = SQRT(T(I,1))
         DO 190 J=I,NC
            IF (I.EQ.J) GO TO 190
            T(I,J) = T(I,J)*T(I,I)
  190    CONTINUE
  200 CONTINUE
!
      CALL STRCO(T, NPAR, NC, COND, C, 1)
      IF (COND.NE.0.0E0) THEN
         COND = 1.0E0/COND
      ELSE
         COND = FPLM
      END IF
      CALL STRDI(T, NPAR, NC, DET, 011, IER)
!
      IF (IER.EQ.0) GO TO 210
      IERR = 3
      ISUBHD = 0
      CALL LLHDR(PAGE, WIDE, ISUBHD)
      WRITE (IPRT,1160)
      WRITE (IPRT,1120)
      RETURN
  210 CONTINUE
      DO 240 I=1,N
         DO 230 J=1,NC
            SM = 0.0E0
            PX = 1.0E0
            DO 220 K=1,J
               IF (IFIT.EQ.1 .AND. K.GE.2) PX = PX*X(I,1)
               IF (IFIT.EQ.3) PX = X(I,K)
               SM = SM + PX*T(K,J)
  220       CONTINUE
            FC(I,J) = SM
  230    CONTINUE
  240 CONTINUE
!
!     COMPUTE REDUCTION TO RSS DUE TO FITTING
!
      DO 260 J=1,NC
         SM = 0.0E0
         DO 250 I=1,N
            SM = SM + (FC(I,J))*WT(I)*Y(I)
  250    CONTINUE
         RED(J) = (SM*SM)
  260 CONTINUE
!
!     COMPUTE ESTIMATED VARIANCE COVARIANCE MATRIX
!
      DO 290 I=1,NC
         DO 280 J=I,NC
            SM = 0.0E0
            DO 270 K=J,NC
               SM = SM + T(I,K)*T(J,K)
  270       CONTINUE
            IF (SQRT(ABS(SM))*SQRT(RVAR).GE.SQRT(FPSPM)) THEN
               VCV(I,J) = SM*RVAR
            ELSE
               VCV(I,J) = 0.0E0
            END IF
            VCV(J,I) = VCV(I,J)
  280    CONTINUE
  290 CONTINUE
!
!     REFIT TO PREDICTED VALUES
!
      I = 0
      DO 310 K=1,N
         IF (WT(K).LE.0.0E0) GO TO 310
         WTSQRT = SQRT(WT(K))
         I = I + 1
         PX = WTSQRT
         DO 300 J=1,NC
            IF (IFIT.EQ.1 .AND. J.GE.2) PX = PX*X(K,1)
            IF (IFIT.EQ.3) PX = X(K,J)*WTSQRT
            XW(I,J) = PX
  300    CONTINUE
         WY(I) = PV(K)*WTSQRT
  310 CONTINUE
!
      IER = 0
      CALL MGS(XW, WY, NNZW, NC, PAR1, C, T(1,1), T, NPAR, N, IER)
!
      CALL ACCDIG(PAR, PAR1, ACC, NC)
!
!     COMPUTE STANDARD DEVIATION OF PREDICTED VALUES (SDPV)
!     AND STANDARDIZED RESIDUALS (SDRES)
!
      DO 360 I=1,N
         SM = 0.0E0
         DO 330 J=1,NC
            SM = 0.0E0
            PX = 1.0E0
            DO 320 K=1,NC
               IF (IFIT.EQ.1 .AND. K.GE.2) PX = PX*X(I,1)
               IF (IFIT.EQ.3) PX = X(I,K)
               SM = SM + PX*VCV(J,K)
  320       CONTINUE
            XW(I,J) = SM
  330    CONTINUE
         SM = 0.0E0
         PX = 1.0E0
         DO 340 K=1,NC
            IF (IFIT.EQ.1 .AND. K.GE.2) PX = PX*X(I,1)
            IF (IFIT.EQ.3) PX = X(I,K)
            SM = SM + XW(I,K)*PX
  340    CONTINUE
         SM = MAX(0.0E0, SM)
         SDPV(I) = SQRT(SM)
         SDRES(I) = FPLM
         IF (WT(I).LE.0.0E0) GO TO 360
         IF ((RVAR/WT(I)-SM).LE.0.0E0) IERR = 4
         IF (RVAR/WT(I)-SM.GT.0.0E0)
     &      SDRES(I) = (RES(I)/SQRT(RVAR/WT(I)-SM))
  360 CONTINUE
!
!     CHECK FOR PRINTED OUTPUT
!
      IF ((IPTOUT(1).EQ.0) .AND. (IPTOUT(2).EQ.0) .AND.
     &   (IPTOUT(3).EQ.0) .AND. (IPTOUT(4).EQ.0)) RETURN
!
!     BEGIN PRINTED OUTPUT
!
      IF (IPTOUT(1).EQ.0) GO TO 370
!
!     PRINT FIRST PAGE OF OUTPUT
!
      ISUBHD = 0
      CALL LLHDR(PAGE, WIDE, ISUBHD)
      CALL FITPT1(N, M, X, Y, PV, SDPV, RES, SDRES, WT, IX, NNZW,
     &   WEIGHT, IPTOUT(1))
!
!     PRINT PLOTS
!
  370 CONTINUE
      IF (IPTOUT(2).EQ.0) GO TO 375
      ISUBHD = 0
      CALL LLHDR(PAGE, WIDE, ISUBHD)
      CALL FITPT2 (SDRES, PV, WT, N, NNZW, WEIGHT, RES, RSS)
!
  375 CONTINUE
      IF (IPTOUT(3).EQ.0) GO TO 376
!
!     PRINT ANALYSIS OF VARIANCE
!
      ISUBHD = 0
      CALL LLHDR(PAGE, WIDE, ISUBHD)
      CALL OANOVA(YSUM, RED, NPAR, RVAR, NNZW, PAR1, IPRT)
!
  376 CONTINUE
      IF (IPTOUT(4).EQ.0) RETURN
!
      IF (NPAR.EQ.1) GO TO 450
      NC = NC - 1
      IDF1 = NNZW - NC
      GO TO 50
  380 CONTINUE
      DO 400 I=1,NC
         T(I,I) = SQRT(T(I,1))
         DO 390 J=I,NC
            IF (I.EQ.J) GO TO 390
            T(I,J) = T(I,J)*T(I,I)
  390    CONTINUE
  400 CONTINUE
!
      CALL STRDI(T, NPAR, NC, DET, 011, IER)
!
      IF (IER.EQ.0) GO TO 420
      WRITE (IPRT,1140)
      DO 410 I=1,NC
         PAR1(I) = 0.0E0
         T(I,I) = 0.0E0
  410 CONTINUE
      GO TO 450
  420 CONTINUE
      DO 440 I=1,NC
         SM = 0.0E0
         DO 430 K=I,NC
            SM = SM + T(I,K)*T(I,K)
  430    CONTINUE
         T(I,I) = SM
  440 CONTINUE
!
!     PRINT PAGE HEADINGS
!
  450 CONTINUE
      ISUBHD = 0
      CALL LLHDR(PAGE, WIDE, ISUBHD)
!
!     PRINT VARIANCE COVARIANCE MATRIX
!
      CALL VCVOUT(NPAR, VCV, NPAR, .TRUE.)
!
      WRITE (IPRT,1030)
      IF (NPAR.GT.1) WRITE (IPRT,1040)
      WRITE (IPRT,1050)
      IF (NPAR.GT.1) WRITE (IPRT,1060)
      WRITE (IPRT, 1170)
      IF (NPAR.GE.2) THEN
         DO 460 I=1,NC
            SDC = SQRT(VCV(I,I))
            RATIO = FPLM
            PROBT = 0.0E0
            IF (SDC.GT.0.0E0) RATIO = PAR(I)/SDC
            IF (SDC.GT.0.0E0)
     &         PROBT = (1.0E0-CDFT(ABS(RATIO), NNZW-NPAR)) * 2.0E0
            SDC1 = SQRT(T(I,I))*SQRT(RVAR1)
            RATIO1 = FPLM
            PROBT1 = 0.0E0
            IF (SDC1.GT.0.0E0) RATIO1 = PAR1(I)/SDC1
            IF (SDC1.GT.0.0E0)
     &         PROBT1 = (1.0E0 - CDFT(ABS(RATIO1), NNZW-NPAR+1)) * 2.0E0
            WRITE (IPRT,1070) I, PAR(I), SDC, RATIO, PROBT, ACC(I),
     &         PAR1(I), SDC1, RATIO1, PROBT1
  460    CONTINUE
      END IF
      SDC = SQRT(VCV(NPAR,NPAR))
      RATIO = FPLM
      PROBT = 0.0E0
      IF (SDC.GT.0.0E0) RATIO = PAR(NPAR)/SDC
      IF (SDC.GT.0.0E0)
     &   PROBT = (1.0E0 - CDFT(ABS(RATIO), NNZW-NPAR)) * 2.0E0
      WRITE (IPRT,1070) NPAR, PAR(NPAR), SDC, RATIO, PROBT, ACC(NPAR)
      IF (NPAR.EQ.1) WRITE (IPRT,1080) RSD
      IF (NPAR.GT.1) WRITE (IPRT,1080) RSD, RSD1
      WRITE (IPRT,1090) NNZW, NPAR, IDF
      IF (NPAR.GT.1) WRITE (IPRT,1100) NNZW, NC, IDF1
      IF (CONST) WRITE (IPRT,1010) R2
      WRITE (IPRT,1000) COND
      WRITE (IPRT,1110)
      RETURN
!
!     FORMAT STATEMENTS
!
 1000 FORMAT (/29H APPROXIMATE CONDITION NUMBER, 10X, G15.7)
 1010 FORMAT (/44H MULTIPLE CORRELATION COEFFICIENT SQUARED   , F7.4)
 1020 FORMAT (//46H THE VALUES COMPUTED FOR THE PARAMETERS ARE - /(5X,
     &   4HPAR(, I3, 4H) = , G15.7))
 1030 FORMAT (////1X, 25('-'), 1X, 18HESTIMATES FROM FIT, 1X, 24('-'))
 1040 FORMAT ('+', 72X, 4('-'), 1X,
     &   48HESTIMATES FROM FIT OMITTING LAST PREDICTOR VALUE, 1X,
     &   4('-'))
 1050 FORMAT (/2X, 19HESTIMATED PARAMETER, 7X, 9HSD OF PAR, 5X,
     &   8HT(PAR=0), 3X, 7HPROB(T), 2X, 8HACC DIG*)
 1060 FORMAT ('+', 72X, 19HESTIMATED PARAMETER, 7X, 9HSD OF PAR, 5X,
     &   8HT(PAR=0), 3X, 7HPROB(T))
 1070 FORMAT (1X, I3, 2X, G16.9, 3X, G16.9, 2X, G10.4, 1X, F5.3, 2X,
     &   F7.1, 9X, G16.9, 3X, G16.9, 2X, G10.4, 1X, F5.3)
 1080 FORMAT (//1X, 30HRESIDUAL STANDARD DEVIATION   , 9X, G15.7, 56X,
     &   G15.7)
 1090 FORMAT (1X, 27HBASED ON DEGREES OF FREEDOM, 7X, I4, 3H - , I2,
     &   3H = , I4)
 1100 FORMAT ('+', 105X, I4, 3H - , I2, 3H = , I4)
 1110 FORMAT (//52H * THE NUMBER OF CORRECTLY COMPUTED DIGITS IN EACH P,
     &   56HARAMETER USUALLY DIFFERS BY LESS THAN 1 FROM THE VALUE G,
     &   10HIVEN HERE.)
 1120 FORMAT (/47H THE PROGRAM WAS UNABLE TO COMPUTE THE VARIANCE,
     &   19H-COVARIANCE MATRIX./
     &   52H THE DESIGN MATRIX IS EITHER NEARLY SINGULAR OR VERY,
     &   17H ILL CONDITIONED./
     &   29H CHECK YOUR INPUT FOR ERRORS.)
 1140 FORMAT (54H0ESTIMATES FOR THE STANDARD DEVIATION OF THE ESTIMATED,
     &   55HPARAMETERS OMITTING THE LAST INDEPENDENT VARIABLE COULD/
     &   59H NOT BE COMPUTED.  THE ZEROS PRINTED FOR THE ESTIMATES OF T,
     &   50HHE PARAMETERS, THEIR STANDARD DEVIATIONS AND THEIR/
     &   59H RATIOS FOR THE FIT OMITTING THE LAST VARIABLE ARE MEANINGL,
     &   4HESS.)
 1150 FORMAT (/30H THE DESIGN MATRIX IS SINGULAR,
     &   29H TO WITHIN MACHINE PRECISION./
     &   50H CHECK THE DESIGN MATRIX FOR A LINEAR RELATIONSHIP,
     &   29H BETWEEN SOME OF THE COLUMNS.)
 1160 FORMAT (//1X, 11('*')/ 1X, 11H*  ERROR  */ 1X, 11('*'))
 1170 FORMAT (' ')
 1180 FORMAT (/50H THE LEAST SQUARES FIT OF THE DATA TO THE MODEL IS,
     &   35H EXACT TO WITHIN MACHINE PRECISION./
     &   38H STATISTICAL ANALYSIS IS NOT POSSIBLE.)
      END
