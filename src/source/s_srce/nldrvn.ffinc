!NLDRVN
      SUBROUTINE NLDRVN (MDL, DRV, DONE, IFIXD, PAR, NPAR, XM, N, M,
     &   IXM, PVT, D, WEIGHT, WT, LWT, STPT, LSTPT, SCL, LSCL)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE COMPUTES THE NUMERICAL APPROXIMATIONS TO THE
!     DERIVATIVE MATRIX (JACOBIAN).
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  OCTOBER 3, 1983
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      INTEGER
     &   IXM,LSCL,LSTPT,LWT,M,N,NPAR
      LOGICAL
     &   DONE,WEIGHT
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   D(N,NPAR),PAR(NPAR),PVT(N),SCL(LSCL),STPT(LSTPT),WT(LWT),
     &   XM(IXM,M)
      INTEGER
     &   IFIXD(NPAR)
!
!  SUBROUTINE ARGUMENTS
       EXTERNAL DRV,MDL
!
!  SCALARS IN COMMON
      INTEGER
     &   IERR
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     &   PJ,STPJ,WTSQRT
      INTEGER
     &   I,J,JPK
!
!  INTRINSIC FUNCTIONS
      INTRINSIC ABS,MAX,SIGN,SQRT
!
!  COMMON BLOCKS
      COMMON /ERRCHK/IERR
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL D(N,NPAR)
!        THE FIRST DERIVATIVE OF THE MODEL (JACOBIAN).
!     EXTERNAL DRV
!        THE NAME OF THE USER SUPPLIED SUBROUTINE WHICH COMPUTES THE
!        DERIVATIVE (JACOBIAN) MATRIX OF THE MODEL.
!     LOGICAL DONE
!        THE VARIABLE USED TO INDICATE WHETHER THIS IS THE FINAL
!        COMPUTATION OF THE JACOBIAN OR NOT.
!     INTEGER I
!        AN INDEX VARIABLE.
!     INTEGER IERR
!        THE VALUE RETURNED BY THIS ROUTINE DESIGNATING
!        WHETHER ANY ERRORS WERE DETECTED IN THE PARAMETER LIST.
!        IF IERR .EQ. 0, NO ERRORS WERE DETECTED.
!        IF IERR .GE. 1, ERRORS WERE DETECTED.
!     INTEGER IFIXD(NPAR)
!        THE INDICATOR VALUES USED TO DESIGNATE WHETHER THE
!        PARAMETERS ARE TO BE OPTIMIZED OR ARE TO BE HELD FIXED.
!        IF IFIXED(I).NE.0, THEN PAR(I) WILL BE HELD FIXED.
!        IF IFIXED(I).EQ.0, THEN PAR(I) WILL BE OPTIMIZED.
!     INTEGER IXM
!        THE FIRST DIMENSION OF MATRIX XM.
!     INTEGER J
!        AN INDEX VARIABLE.
!     INTEGER JPK
!        AN INDEX VARIABLE.
!     INTEGER LSCL
!        THE DIMENSION OF VECTOR SCL.
!     INTEGER LSTPT
!        THE DIMENSION OF VECTOR STPT.
!     INTEGER LWT
!        THE DIMENSION OF VECTOR WT.
!     INTEGER M
!        THE NUMBER OF INDEPENDENT VARIABLES.
!     EXTERNAL MDL
!        THE NAME OF THE USER SUPPLIED SUBROUTINE WHICH COMPUTES THE
!        PREDICTED VALUES BASED ON THE CURRENT PARAMETER ESTIMATES.
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS.
!     INTEGER NPAR
!        THE NUMBER OF PARAMETERS IN THE MODEL.
!     REAL PAR(NPAR)
!        THE CURRENT ESTIMATES OF THE PARAMETERS.
!     REAL PJ
!        A TEMPORARY LOCATION FOR STORAGE OF THE JTH PARAMETER.
!     REAL PVT(N)
!        THE PREDICTED VALUE BASED ON THE CURRENT PARAMETER ESTIMATES.
!     REAL SCL(LSCL)
!        THE SCALE VALUES.
!     REAL STPT(LSTPT)
!        THE STEP SIZE ARRAY.
!     REAL STPJ
!        THE JTH STEP SIZE.
!     LOGICAL WEIGHT
!        THE VARIABLE USED TO INDICATE WHETHER WEIGHTED ANALYSIS IS TO
!        BE PERFORMED (TRUE) OR NOT (FALSE).
!     REAL WT(LWT)
!        THE USER SUPPLIED WEIGHTS.
!     REAL WTSQRT
!        THE SQUARE ROOT OF THE USER SUPPLIED WEIGHTS.
!     REAL XM(IXM,M)
!        THE INDEPENDENT VARIABLE.
!
!     COMPUTE FINITE-DIFFERENCE JACOBIAN OF THE OPTIMIZED PARAMETERS
!
      JPK = 0
!
      DO 20 J=1,NPAR
         IF (IFIXD(J).EQ.0) THEN
            JPK = JPK + 1
            PJ = PAR(J)
            IF (SCL(JPK).EQ.0.0E0) THEN
               IF (PAR(J).NE.0.0E0) THEN
                  STPJ = STPT(J)*SIGN(1.0E0,PAR(J))*ABS(PAR(J))
               ELSE
                  STPJ = STPT(J)
               END IF
            ELSE
               STPJ = STPT(J)*
     &                SIGN(1.0E0,PAR(J))*MAX(ABS(PAR(J)),1.0E0/
     &                ABS(SCL(JPK)))
            END IF
!
            STPJ = STPJ + PAR(J)
            STPJ = STPJ - PAR(J)
!
            PAR(J) = PJ + STPJ
            CALL MDL(PAR, NPAR, XM, N, M, IXM, D(1,J))
!
            DO 10 I=1,N
               WTSQRT = 1.0E0
               IF (WEIGHT .AND. (.NOT.DONE)) WTSQRT = SQRT(WT(I))
               D(I,JPK) = WTSQRT*(PVT(I)-D(I,J))/STPJ
   10       CONTINUE
!
            PAR(J) = PJ
         END IF
   20 CONTINUE
!
      RETURN
!
      END
