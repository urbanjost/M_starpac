!SUMWSS
      SUBROUTINE SUMWSS(X, W, N, LO, MID, HI, NNZW, SUM1, SUMW1, SUMW2,
     +   SUMWA, SUMW, XMEAN, XMEANW)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     A ROUTINE TO CALCULATE WEIGHTED AND UNWEIGHTED SUMS OF
!     POWERS AND THE MEAN FOR A SORTED VECTOR IN WHICH THE MID TH
!     ELEMENT IS THE ELEMENT CLOSEST TO ZERO.  USED BY THE STAT
!     FAMILY.
!
!     WRITTEN BY - JOHN E. KOONTZ
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  MAY 17, 1982
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP)
     +   SUM1,SUMW,SUMW1,SUMW2,SUMWA,XMEAN,XMEANW
      INTEGER
     +   HI,LO,MID,N,NNZW
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP)
     +   W(N),X(N)
!
!  LOCAL SCALARS
      INTEGER
     +   CURHI,CURLO,I,IREV
!
!  INTRINSIC FUNCTIONS
      INTRINSIC ABS
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     INTEGER CURHI
!        THE UPPER BOUND OF THE CURRENT INTERVAL.
!     INTEGER CURLO
!        THE LOWER BOUND OF THE CURRENT INTERVAL.
!     INTEGER HI
!        INPUT PARAMETER.  THE UPPER BOUND OF THE INITIAL
!        INTERVAL.
!     INTEGER I
!        LOOP PARAMETER.
!     INTEGER IREV
!        A VARIABLE WHICH RUNS IN THE SAME INTERVAL AS I, BUT
!        IN THE REVERSE ORDER.
!     INTEGER LO
!        INPUT PARAMETER.  THE LOWER BOUND OF THE INITIAL
!        INTERVAL.
!     INTEGER MID
!        INPUT PARAMETER.  THE INDEX OF THE ELEMENT IN X CLOSEST TO
!        ZERO IN VALUE.  THE POINT OUT FROM WHICH THE SUMMING IS
!        DONE.
!     INTEGER N
!        INPUT PARAMETER.  THE LENGTH OF THE ARRAY X.
!     INTEGER NNZW
!        INPUT PARAMETER.  THE NUMBER OF POSITIVELY WEIGHTED VALUES
!        IN X.
!     REAL SUMW
!        OUTPUT PARAMETER.  THE SUM OF THE WEIGHTS WT(I).
!     REAL SUMWA
!        OUTPUT PARAMETER.  THE WEIGHTED SUM OF THE ABSOLUTE
!        VALUES OF THE ELEMENTS OF X.
!     REAL SUMW1
!        OUTPUT PARAMETER.  THE WEIGHTED SUM OF THE ELEMENTS OF
!        X.
!     REAL SUMW2
!        OUTPUT PARAMETER.  THE WEIGHTED SUM OF THE SQUARES OF
!        THE ELEMENTS OF X.
!     REAL SUM1
!        OUTPUT PARAMETER.  THE UNWEIGHTED SUM OF THE ELEMENTS OF X.
!     REAL W(N)
!        INPUT PARAMETER.  THE ARRAY OF WEIGHTS.
!     REAL X(N)
!        INPUT PARAMETER.  THE DATA ARRAY X IN WHICH THE SUMS ARE TAKEN.
!     REAL XMEAN
!        OUTPUT PARAMETER.  THE UNWEIGHTED MEAN OF X.
!     REAL XMEANW
!        OUTPUT PARAMETER.  THE WEIGHTED MEAN OF X.
!
!     INITIALIZE SUMMATION VARIABLES.
!
      SUM1 = 0.0E0
      SUMW1 = 0.0E0
      SUMW2 = 0.0E0
      SUMWA = 0.0E0
      SUMW = 0.0E0
      IF (W(MID).EQ.0.0E0) GO TO 10
      SUM1 = X(MID)
      SUMW1 = W(MID)*X(MID)
      SUMW2 = W(MID)*X(MID)*X(MID)
      SUMWA = W(MID)*ABS(X(MID))
      SUMW = W(MID)
   10 CURLO = MID - 1
      CURHI = MID + 1
!
!     SUM OUTWARDS, FROM THE VALUE NEAREST ZERO.  THAT IS, FROM
!     THE LEAST IN MAGNITUDE TO THE GREATEST.
!
   20 IF (CURHI.GT.HI .OR. CURLO.LT.LO) GO TO 60
      IF (ABS(X(CURHI)).LT.ABS(X(CURLO))) GO TO 40
      IF (W(CURLO).EQ.0.0E0) GO TO 30
      SUM1 = SUM1 + X(CURLO)
      SUMW1 = SUMW1 + W(CURLO)*X(CURLO)
      SUMW2 = SUMW2 + W(CURLO)*X(CURLO)*X(CURLO)
      SUMWA = SUMWA + W(CURLO)*ABS(X(CURLO))
      SUMW = SUMW + W(CURLO)
   30 CURLO = CURLO - 1
      GO TO 20
   40 IF (W(CURHI).EQ.0.0E0) GO TO 50
      SUM1 = SUM1 + X(CURHI)
      SUMW1 = SUMW1 + W(CURHI)*X(CURHI)
      SUMW2 = SUMW2 + W(CURHI)*X(CURHI)*X(CURHI)
      SUMWA = SUMWA + W(CURHI)*ABS(X(CURHI))
      SUMW = SUMW + W(CURHI)
   50 CURHI = CURHI + 1
      GO TO 20
!
!     SUM UP ANY VALUES WHICH MAY REMAIN BECAUSE OF AN OFF
!     CENTER ZERO POINT.
!
   60 IF (CURHI.GT.HI) GO TO 80
      DO 70 I=CURHI,HI
         IF (W(I).EQ.0.0E0) GO TO 70
         SUM1 = SUM1 + X(I)
         SUMW1 = SUMW1 + W(I)*X(I)
         SUMW2 = SUMW2 + W(I)*X(I)*X(I)
         SUMWA = SUMWA + W(I)*ABS(X(I))
         SUMW = SUMW + W(I)
   70 CONTINUE
   80 IF (CURLO.LT.LO) GO TO 100
      DO 90 IREV=LO,CURLO
         I = LO - IREV + CURLO
         IF (W(I).EQ.0.0E0) GO TO 90
         SUM1 = SUM1 + X(I)
         SUMW1 = SUMW1 + W(I)*X(I)
         SUMW2 = SUMW2 + W(I)*X(I)*X(I)
         SUMWA = SUMWA + W(I)*ABS(X(I))
         SUMW = SUMW + W(I)
   90 CONTINUE
  100 XMEAN = SUM1/NNZW
      XMEANW = SUMW1/SUMW
      RETURN
      END
