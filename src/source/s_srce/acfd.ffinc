!ACFD
      SUBROUTINE ACFD (Y, N, LAGMAX, NFAC, ND, IOD, LDSTAK)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS IS THE USER CALLABLE ROUTINE FOR COMPUTING THE AUTO-
!     CORRELATIONS AND PARTIAL AUTOCORRELATIONS OF A TIME SERIES
!     WITH DIFFERENCING.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 21, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      INTEGER
     +   LAGMAX,LDSTAK,N,NFAC
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP)
     +   Y(*)
      INTEGER
     +   IOD(*),ND(*)
!
!  SCALARS IN COMMON
      INTEGER
     +   IERR
!
!  ARRAYS IN COMMON
      DOUBLE PRECISION DSTAK(12)
!
!  LOCAL SCALARS
      REAL(KIND=WP)
     +   CHIA,CHIAP,OSPVAR,YMEAN,YSD
      INTEGER
     +   ACOV,AIC,FTEST,I,IAR,IFP,IPRT,LACOV,LDSMIN,LYFFT,NALL0,
     +   NFFT,NTIMES,NYD,NYF,PHI,PRHO,SDRHO,WORK,YF
      LOGICAL
     +   DIFFER,ISFFT,NEWPG
!
!  LOCAL ARRAYS
      REAL(KIND=WP)
     +   RSTAK(12)
      INTEGER
     +   NDUM(1)
      CHARACTER
     +   NMSUB(6)*1
!
!  EXTERNAL FUNCTIONS
      INTEGER
     +   STKGET,STKST
      EXTERNAL STKGET,STKST
!
!  EXTERNAL SUBROUTINES
      EXTERNAL ACFER,ACFMN,ACFOUT,DIFSER,IPRINT,LDSCMP,SCOPY,STKCLR,
     +   STKSET
!
!  INTRINSIC FUNCTIONS
      INTRINSIC MIN,SQRT
!
!  COMMON BLOCKS
      COMMON /CSTAK/DSTAK
      COMMON /ERRCHK/IERR
!
!  EQUIVALENCES
      EQUIVALENCE (DSTAK(1),RSTAK(1))
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     INTEGER ACOV
!        THE STARTING LOCATION IN DSTAK FOR
!        THE AUTOCOVARIANCE VECTOR.
!     INTEGER AIC
!        THE STARTING LOCATION IN DSTAK FOR
!        THE ARRAY CONTAINING THE AKAIKES CRITERION FOR EACH ORDER.
!     REAL CHIA, CHIAP
!        THE VARIABLES IN WHICH THE CHI SQUARE STATISTIC AND
!        CHI SQUARED STATISTIC PROBABILITY FOR THE AUTOCORRELATIONS
!        ARE STORED.
!     LOGICAL DIFFER
!        THE VARIABLE USED TO DESIGNATE WHETHER OR NOT THE SERIES
!        BEING ANALYZED WAS DIFFERENCED OR NOT.
!     DOUBLE PRECISION DSTAK(12)
!        THE DOUBLE PRECISION VERSION OF THE /CSTAK/ WORK AREA.
!     INTEGER FTEST
!        THE STARTING LOCATION IN DSTAK FOR
!        THE ARRAY CONTAINING THE PARTIAL F RATIO AND PROBABILITIES
!     INTEGER I
!        AN INDEX VARIABLE.
!     INTEGER IAR
!        THE ORDER OF THE AUTOREGRESSIVE PROCESS CHOSEN.
!     INTEGER IERR
!        THE INTEGER VALUE RETURNED BY THIS ROUTINE DESIGNATING
!        WHETHER ANY ERRORS WERE DETECTED IN THE PARAMETER LIST
!        IF IERR .EQ. 0, NO ERRORS WERE DETECTED
!     INTEGER IFP
!        THE INDICATOR VARIABLE USED TO DESIGNATE WHETHER THE FLOATING
!        POINT VARIABLES ARE SINGLE (IFP=3) OR DOUBLE (IFP=4) PRECISION.
!     INTEGER IOD(NFAC)
!        THE ORDER OF EACH OF THE DIFFERENCE FACTORS.
!     INTEGER IPRT
!        THE UNIT NUMBER USED FOR OUTPUT.
!     LOGICAL ISFFT
!        THE INDICATOR VARIABLE USED TO DESIGNATE WHETHER THE CALLING
!        ROUTINE HAS SUFFIX F (ISFFT = TRUE) OR NOT (ISFFT = FALSE)
!     INTEGER LACOV
!        THE LENGTH OF THE VECTOR ACOV.
!     INTEGER LAGMAX
!        THE MAXIMUM LAG VALUE REQUESTED.
!     INTEGER LDSMIN
!        THE MINIMUM LENGTH ALLOWED FOR THE ARRAY DSTAK.
!     INTEGER LDSTAK
!        THE LENGTH OF THE ARRAY DSTAK.
!     INTEGER LYFFT
!        A DUMMY VARIABLE
!     INTEGER N
!        THE INTEGER NUMBER OF OBSERVATIONS IN EACH SERIES
!     INTEGER NALL0
!        THE NUMBER OF OUTSTANDING STACK ALLOCATIONS
!     INTEGER ND(NFAC)
!        THE ARRAY CONTAINING THE NUMBER OF TIMES THE DIFFERENCE
!        FACTORS ARE TO BE APPLIED.
!     INTEGER NDUM(1)
!        A DUMMY DIMENSIONED VARIABLE.
!     LOGICAL NEWPG
!        THE VARIABLE USED TO DESIGNATE WHETHER OR NOT THE OUTPUT
!        IS TO BEGIN ON A NEW PAGE.
!     INTEGER NFAC
!        THE NUMBER OF DIFFERENCE FACTORS.
!     INTEGER NFFT
!        THE NUMBER OF OBSERVATIONS IN THE EXTENDED SERIES.
!     CHARACTER*1 NMSUB(6)
!        THE NAME OF THE SUBROUTINE CALLING THE ERROR CHECKING
!        SUBROUTINE.
!     INTEGER NTIMES
!        THE NUMBER OF TIMES THE FIRST DIFFERENCE FACTOR HAS BEEN
!        APPLIED.
!     INTEGER NYD, NYF
!        THE NUMBER OF OBSERVATIONS AFTER THE DIFFERENCE FILTER IS
!        APPLIED.
!     REAL OSPVAR
!        THE ONE STEP PREDICTION VARIANCE FOR THE ORDER SELECTED (IAR).
!     INTEGER PHI
!        THE STARTING LOCATION IN DSTAK FOR
!        THE ARRAY OF AUTOREGRESSIVE COEFFICIENTS FOR THE SELECTED
!        ORDER.
!     INTEGER PRHO
!        THE STARTING LOCATION IN THE WORK AREA FOR PRHO.
!     REAL RSTAK(12)
!        THE REAL VERSION OF THE /CSTAK/ WORK AREA.
!     INTEGER SDRHO
!        THE STARTING LOCATION IN THE WORK AREA FOR SDRHO.
!     INTEGER WORK
!        THE STARTING LOCATION IN THE WORK AREA FOR WORK.
!     REAL Y(N)
!        THE VECTOR CONTAINING THE OBSERVED TIME SERIES
!     INTEGER YF
!        THE STARTING LOCATION IN DSTAK FOR
!        THE VECTOR CONTAINING THE DIFFERENCE FILTERED SERIES.
!     REAL YMEAN
!        THE MEAN OF THE OBSERVED TIME SERIES
!     REAL YSD
!        THE STANDARD DEVIATION OF THE OBSERVED TIME SERIES
!
!
!     SET UP NAME ARRAYS
!
      DATA
     +  NMSUB(1),  NMSUB(2),  NMSUB(3),  NMSUB(4),  NMSUB(5),  NMSUB(6)
     + /     'A',       'C',       'F',       'D',       ' ',       ' '/
!
!
!     SET UP FOR ERROR CHECKING
!
      IERR = 0
      LACOV = LAGMAX+1
      DIFFER = .TRUE.
      ISFFT = .FALSE.
      LYFFT = 0
      NFFT = 0
!
      CALL LDSCMP(7, 0, 0, 0, 0, 0, 'S', 7*LAGMAX+2+N, LDSMIN)
!
!     CALL ERROR CHECKING ROUTINES
!
      CALL ACFER(NMSUB, N, LAGMAX, LACOV, LDSTAK, LDSMIN,
     +  DIFFER, NFAC, ND, IOD, ISFFT, LYFFT, NFFT)
!
!     CHECK WHETHER AN ERROR HAS BEEN DETECTED
!
      IF (IERR.EQ.0) THEN
!
!       SET UP THE WORK AREA.
!
        CALL STKSET(LDSTAK, 4)
        NALL0 = STKST(1)
!
        IFP = 3
!
        ACOV = STKGET(LAGMAX+1, IFP)
        PRHO = STKGET(LAGMAX, IFP)
        AIC = STKGET(LAGMAX+1, IFP)
        FTEST = STKGET(2*LAGMAX, IFP)
        PHI = STKGET(LAGMAX, IFP)
        WORK = STKGET(LAGMAX, IFP)
        YF = STKGET(N, IFP)
        SDRHO = WORK
!
        IF (IERR.EQ.0) THEN
!
          NEWPG = .FALSE.
          NTIMES = 0
!
!         CHECK WHETHER THERE IS MORE THAN ONE DIFFERENCE FACTOR.
!
          IF (NFAC.LE.1) THEN
            CALL SCOPY(N, Y, 1, RSTAK(YF), 1)
          ELSE
!
!           CREATE NEW DATA BY APPLYING DIFFERENCE FACTORS BEYOND THE
!           FIRST.
!
            CALL DIFSER(Y, N, NFAC-1, ND(2), IOD(2), RSTAK(YF), NYF)
          END IF
          NYD = N
!
!         CALL ROUTINE FOR MAIN AUTOCORRELATION COMPUTATIONS.
!
          CALL ACFMN (RSTAK(YF), NYD, MIN(LAGMAX, NYD-1),
     +       RSTAK(ACOV+1), RSTAK(SDRHO), YMEAN, RSTAK(PRHO),
     +       RSTAK(AIC), RSTAK(FTEST), RSTAK(PHI), IAR, OSPVAR,
     +       RSTAK(ACOV), LAGMAX+1, LAGMAX+1, CHIA, CHIAP, RSTAK(WORK),
     +       1)
!
          YSD = SQRT(RSTAK(ACOV) * N / (N-1))
!
!         CALL ROUTINE TO PRINT OUT AUTOCORRELATIONS
!
          CALL ACFOUT (YMEAN, YSD, NYF, NYF, MIN(LAGMAX, NYD-1),
     +       RSTAK(ACOV+1), RSTAK(SDRHO), RSTAK(PRHO), NDUM, RSTAK(AIC),
     +       LAGMAX+1, RSTAK(FTEST), IAR, RSTAK(PHI), OSPVAR, CHIA,
     +       CHIAP, LAGMAX, .FALSE., 0.0E0, DIFFER, NEWPG, NFAC, ND,
     +       IOD, NTIMES)
!
          NEWPG = .TRUE.
!
!         COMPUTE CORRELATION ANALYSIS FOR FIRST DIFFERENCE FACTOR
!         APPLIED 1 TO ND(1) TIMES
!
          IF ((NFAC.GE.1) .AND. (ND(1).GE.1)) THEN
            DO 50 I = 1, ND(1)
!
               CALL DIFSER(RSTAK(YF), NYD, NFAC-1, ND(2), IOD(2),
     +           RSTAK(YF), NYF)
!
!              CALL ROUTINE FOR MAIN AUTOCORRELATION COMPUTATIONS.
!
             CALL ACFMN (RSTAK(YF), NYD, MIN(LAGMAX, NYD-1),
     +          RSTAK(ACOV+1), RSTAK(SDRHO), YMEAN, RSTAK(PRHO),
     +          RSTAK(AIC), RSTAK(FTEST), RSTAK(PHI), IAR, OSPVAR,
     +          RSTAK(ACOV), LAGMAX+1, LAGMAX+1, CHIA, CHIAP,
     +          RSTAK(WORK), 1)
!
               YSD = SQRT(RSTAK(ACOV) * N / (N-1))
!
!           CALL ROUTINE TO PRINT OUT AUTOCORRELATIONS
!
             CALL ACFOUT (YMEAN, YSD, NYF, NYF, MIN(LAGMAX, NYD-1),
     +          RSTAK(ACOV+1), RSTAK(SDRHO), RSTAK(PRHO), NDUM,
     +          RSTAK(AIC), LAGMAX+1, RSTAK(FTEST), IAR, RSTAK(PHI),
     +          OSPVAR, CHIA, CHIAP, LAGMAX, .FALSE., 0.0E0, DIFFER,
     +          NEWPG, NFAC, ND, IOD, I)
!
   50       CONTINUE
          END IF
        END IF
!
        CALL STKCLR(NALL0)
      END IF
!
      IF (IERR.NE.0) THEN
!
!     PRINT PROPER CALL SEQUENCE AND RETURN
!
        IERR = 1
        CALL IPRINT (IPRT)
        WRITE (IPRT, 1000)
      END IF
!
      RETURN
!
!     FORMAT STATEMENTS
!
 1000 FORMAT(/42H THE CORRECT FORM OF THE CALL STATEMENT IS//
     +  '       CALL ACFD (Y, N, LAGMAX, NFAC, ND, IOD, LDSTAK)')
      END
