!GFAEST
      SUBROUTINE GFAEST (PHI, IAR, NF, FREQ, GAIN, PHAS, DELTA)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE COMPUTES THE GAIN FUNCTION OF AN AUTOREGRESSIVE
!     LINEAR FILTER USING THE ALGORITHM GIVEN ON PAGES 419 AND 420
!     JENKINS AND WATTS.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 26, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) ::
     &   DELTA
      INTEGER
     &   IAR,NF
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   FREQ(NF),GAIN(NF),PHAS(NF),PHI(IAR)
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     &   ARG,C,PI,S,V0,V1,V2,Z0,Z1,Z2
      INTEGER
     &   I,J,JJ
!
!  EXTERNAL SUBROUTINES
      EXTERNAL GETPI
!
!  INTRINSIC FUNCTIONS
      INTRINSIC ATAN2,COS,SIN,SQRT
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL ARG
!        A VALUE USED TO COMPUTE THE GAIN FUNCTION ESTIMATES.
!     REAL C
!        A VALUE USED TO COMPUTE THE GAIN FUNCTION ESTIMATES.
!     REAL DELTA
!        THE SAMPLING INTERVAL OF THE ORIGINAL SERIES.
!     REAL FREQ(NF)
!        THE VECTOR OF FREQUENCIES AT WHICH THE GAIN FUNCTION IS TO
!        BE ESTIMATED.
!     REAL GAIN(NF)
!        THE ARRAY IN WHICH THE GAIN FUNCTION ESTIMATES ARE RETURNED.
!     INTEGER I
!        AN INDEX VARIABLE.
!     INTEGER IAR
!        THE NUMBER OF FILTER COEFFICIENTS.
!     INTEGER J, JJ
!        INDEX VARIABLES.
!     INTEGER NF
!        THE NUMBER OF FREQUENCIES FOR WHICH THE GAIN FUNCTION ESTIMATES
!        ARE TO BE COMPUTED.
!     REAL PHAS(NF)
!        THE ARRAY IN WHICH THE PHASE OF THE FILTER IS RETURNED.
!     REAL PHI(IAR)
!        THE VECTOR CONTAINING THE AUTOREGRESSIVE FILTER COEFFICIENTS.
!     REAL PI
!        THE VALUE OF PI.
!     REAL S
!        A VALUE USED TO COMPUTE THE GAIN FUNCTION ESTIMATES.
!     REAL V0, V1, V2
!        CONSTANTS USED FOR COMPUTING THE GAIN FUNCTION ESTIMATES.
!     REAL Z0, Z1, Z2
!        CONSTANTS USED FOR COMPUTING THE GAIN FUNCTION ESTIMATES.
!
      CALL GETPI(PI)
!
!     COMPUTE GAIN FUNCTION ESTIMATES AND THEIR CONFIDENCE LIMITS.
!
      DO 30 I = 1, NF
         IF (FREQ(I).EQ.0.0E0) THEN
            ARG = 0.0E0
            C = 1.0E0
            S = 0.0E0
         ELSE IF (FREQ(I).EQ.0.25E0) THEN
            ARG = PI/2.0E0
            C = 0.0E0
            S = 1.0E0
         ELSE IF (FREQ(I).EQ.0.5E0) THEN
            ARG = PI
            C = -1.0E0
            S = 0.0E0
         ELSE
            ARG = 2.0E0 * PI * FREQ(I)
            C = COS(ARG)
            S = SIN(ARG)
         END IF
         V0 = 0.0E0
         V1 = 0.0E0
         Z0 = 0.0E0
         Z1 = 0.0E0
!
         JJ = IAR + 1
!
         DO 20 J = 1, IAR
            JJ = JJ -1
!
            V2 = C * V1 - V0 - PHI(JJ)
            V0 = V1
            V1 = V2
!
            Z2 = S * Z1 - Z0 - PHI(JJ)
            Z0 = Z1
            Z1 = Z2
   20    CONTINUE
!
         V2 = DELTA * (1.0E0 + (V1 * C - V0))
!
         Z2 = DELTA * Z1 * S
!
         GAIN(I) = SQRT(V2*V2 + Z2*Z2)
!
         PHAS(I) = -0.5E0 * PI
         IF (Z2.NE.0.0E0 .AND. V2.NE.0.0E0) PHAS(I) = ATAN2(Z2, V2)
!
   30 CONTINUE
!
      RETURN
      END
