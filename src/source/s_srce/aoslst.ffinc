!AOSLST
     subroutine aoslst (prho, aic, ftest, lagmax, laic, iar, phi,&
     &   ospvar, ifprho, n)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE LISTS THE AUTOREGRESSIVE MODEL ORDER SELECTION
!     STATISTICS.
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 21, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     real(kind=wp) ::&
     &   ospvar
     integer&
     &   iar,lagmax,laic,n
     logical&
     &   ifprho
!
!  ARRAY ARGUMENTS
     real(kind=wp) ::&
     &   aic(*),ftest(2,*),phi(*),prho(*)
!
!  LOCAL SCALARS
     integer&
     &   i,imax,imin,iprt,lag,m,nperl
     character&
     &   fmt*160
!
!  EXTERNAL SUBROUTINES
!      EXTERNAL IPRINT
!
!  INTRINSIC FUNCTIONS
      intrinsic min
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL AIC(LAIC)
!        THE ARRAY CONTAINING THE AKAIKES INFORMATION CRITERION.
!     CHARACTER*160 FMT
!        THE FORMAT USED.
!     REAL FTEST(2, LAGMAX)
!        THE ARRAY CONTAINING THE PARTIAL F RATIO AND ITS PROBABILITY
!        OF BEING EXCEEDED.
!     INTEGER I
!        AN INDEX VARIABLE.
!     INTEGER IAR
!        THE ORDER OF THE AUTOREGRESSIVE MODEL SELECTED.
!     LOGICAL IFPRHO
!        THE LOGICAL VARIABLE USED TO INDICATE IF THE PARTIAL
!        AUTOCORRELATIONS ARE BEING PRINTED.  IF -IFPRHO- IS
!        .FALSE. THE AUTOCORRELATIONA AND THEIR STANDARD ERRORS
!        ARE PRINTED,  IF .TRUE., THE PARTIALS.
!     INTEGER IMAX, IMIN
!        THE INDEX VALUES OF THE FIRST AND LAST OBSERVATION
!        TO BE PRINTED PER LINE
!     INTEGER IPRT
!        THE UNIT NUMBER FOR PRINTED OUTPUT
!     INTEGER LAG
!        THE LAG VALUE OF THE AUTOREGRESSIVE ORDER SELECTION STATISTIC
!        BEING PRINTED.
!     INTEGER LAGMAX
!        THE MAXIMUM LAG AT WHICH THE AUTOREGRESSIVE ORDER SELECTION
!        STATISTICS WERE COMPUTED.
!     INTEGER LAIC
!        THE LENGTH OF THE VECTOR AIC.
!     INTEGER M
!        AN INDEX VARIABLE.
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS IN THE SERIES.
!     INTEGER NPERL
!        THE NUMBER OF VALUES TO BE PRINTED PER LINE.
!     REAL OSPVAR
!        THE ONE STEP PREDICTION VARIANCE FOR THE ORDER SELECTED.
!     REAL PHI(LAGMAX)
!        THE AUTOREGRESSIVE COEFFICIENTS FOR THE SELECTED ORDER.
!     REAL PRHO(LAGMAX)
!        THE ARRAY CONTAINING THE PARTIAL AUTOCORRELATION
!        COEFFICIENTS.
!
      call iprint(iprt)
!
      nperl = 12
      do 30 i = 1, lagmax, nperl
         imin = i
         imax = min(i + nperl - 1, lagmax)
         write(iprt, 1000) (lag, lag = imin, imax)
         if (ifprho) write (iprt, 1001) (prho(lag), lag = imin, imax)
!
         if ((imax.eq.lagmax) .and. (lagmax.eq.(n-1))) then
            if (imax-imin.ge.1) then
               write(fmt, 1002) imax-imin
            else
               write(fmt, 1003)
            end if
           write(iprt, fmt) ' AIC               ',&
     &                       (aic(lag+1), lag = imin, imax-1)
           write(iprt, fmt) ' F RATIO           ',&
     &                       (ftest(1, lag), lag = imin, imax-1)
           write(iprt, fmt) ' F PROBABILITY     ',&
     &                       (ftest(2, lag), lag = imin, imax-1)
         else
            write(fmt, 1004) nperl
           write(iprt, fmt) ' AIC               ',&
     &                       (aic(lag+1), lag = imin, imax)
           write(iprt, fmt) ' F RATIO           ',&
     &                       (ftest(1, lag), lag = imin, imax)
           write(iprt, fmt) ' F PROBABILITY     ',&
     &                       (ftest(2, lag), lag = imin, imax)
         end if
   30 continue
!
      if (lagmax.eq.n-1) write(iprt, 1009)
!
!     PRINT INFORMATION ON ORDER SELECTED
!
      write (iprt, 1005) iar, ospvar
!
      if (iar .eq. 0) return
!
      write(iprt, 1008)
!
      do 40 i = 1, iar, nperl
         imin = i
         imax = min(i + nperl - 1, iar)
         write (iprt, 1006) (m, m = imin, imax)
         write (iprt, 1007) (phi(m), m = imin, imax)
   40 continue
!
      return
!
!     FORMAT STATEMENTS
!
 1000 format(/19h lag               , 12(1x, i6))
 1001 format( 19h pacf              , 12(2x, f5.2))
 1002 format('(A19,',i2,'(1X, F6.2), 7H ******)')
 1003 format('(A19, 7H ******)')
 1004 format('(A19,',i2,'(1X, F6.2))')
1005 format (//&
    &   49h order autoregressive process selected =         , i6/&
     &   51h one step prediction variance of process selected =, g15.8/)
 1006 format (/19h coefficient number, 12(1x, i6))
 1007 format ( 19h coefficient value , 12(1x, f6.4))
1008 format (45h yule-walker estimates of the coefficients of,&
     &   36h the autoregressive process selected)
1009 format (/'****** THIS VALUE CANNOT BE COMPUTED',&
     &         ' BECAUSE LAG = N-1'//)
!
      end
