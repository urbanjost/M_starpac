!UASVAR
      SUBROUTINE UASVAR (Y, YMEAN, N, IAR, PHI, VAR)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE COMPUTES THE VARIANCE FOR A GIVEN SERIES
!     AND AUTOREGRESSIVE MODEL.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  DECEMBER 7, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) ::
     &   VAR,YMEAN
      INTEGER
     &   IAR,N
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   PHI(IAR),Y(N)
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     &   RES,RSS
      INTEGER
     &   I,IAR1,J,K
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     INTEGER I
!        AN INDEX VARIABLE.
!     INTEGER IAR
!        THE ORDER OF THE AUTOREGRESSIVE PROCESS CHOSEN.
!     INTEGER IAR1
!        THE VALUE IAR + 1.
!     INTEGER J, K
!        INDEX VALUES.
!     INTEGER N
!        THE INTEGER NUMBER OF OBSERVATIONS IN EACH SERIES
!     REAL PHI(IAR)
!        THE ARRAY OF AUTOREGRESSIVE COEFFICIENTS FOR THE
!        SELECTED ORDER.
!     REAL RES
!        *
!     REAL RSS
!        THE ONE STEP PREDICTION RESIDUAL SUM OF SQUARES.
!     REAL VAR
!        THE ONE STEP PREDICTION VARIANCE.
!     REAL Y(N)
!         THE ARRAY CONTAINING THE OBSERVED TIME SERIES.
!     REAL YMEAN
!        THE MEAN OF THE OBSERVED TIME SERIES
!
      RSS = 0.0E0
      IAR1 = IAR+1
      DO 20 I = IAR1, N
         RES = Y(I) - YMEAN
         DO 10 J = 1, IAR
            K = I-J
            RES = RES - PHI(J) * (Y(K)-YMEAN)
   10    CONTINUE
         RSS = RSS + RES*RES
   20 CONTINUE
!
      VAR = RSS / (N-IAR1)
!
      RETURN
!
      END
