!SETROW
      subroutine setrow (nrow, xm, n, m, ixm, nrowu)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE SELECTS THE ROW USED BY THE DERIVATIVE CHECKING
!     PROCEDURE.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  OCTOBER 3, 1983
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     integer&
     &   ixm,m,n,nrow,nrowu
!
!  ARRAY ARGUMENTS
     real(kind=wp) ::&
     &   xm(ixm,m)
!
!  LOCAL SCALARS
     integer&
     &   i,j
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     INTEGER I
!        AN INDEX VARIABLE.
!     INTEGER IXM
!        THE FIRST DIMENSION OF THE INDEPENDENT VARIABLE ARRAY XM.
!     INTEGER J
!        AN INDEX VARIABLE.
!     INTEGER M
!        THE NUMBER OF INDEPENDENT VARIABLES.
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS OF DATA.
!     INTEGER NROW, NROWU
!        THE USER-SUPPLIED NUMBER OF THE ROW OF THE INDEPENDENT
!        VARIABLE ARRAY AT WHICH THE DERIVATIVE IS TO BE CHECKED,
!        AND THE NUMBER OF THE ROW ACTUALLY USED.
!     REAL XM(IXM,M)
!        THE INDEPENDENT VARIABLE MATRIX.
!
      nrowu = nrow
!
      if ((nrowu.ge.1) .and. (nrowu.le.n)) return
!
!     SELECT FIRST ROW OF INDEPENDENT VARIABLES WHICH CONTAINS NO ZEROS
!     IF THERE IS ONE, OTHERWISE FIRST ROW IS USED.
!
      do 20 i = 1, n
         do 10 j = 1, m
            if (xm(i,j) .eq. 0.0e0) go to 20
   10    continue
         nrowu = i
         return
   20 continue
!
      nrowu = 1
!
      return
      end
