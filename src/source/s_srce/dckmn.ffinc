!DCKMN
      SUBROUTINE DCKMN(J, D, PAR, SCALE, NPAR, ETA, TAU, MDL, XM,
     &   N, NROW, M, IXM, PV, PVTEMP, MSG, LMSG)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS IS THE MAIN SUBROUTINE FOR CHECKING USER SUPPLIED
!     ANALYTIC DERIVATIVES AGAINST NUMERICAL DERIVATIVES
!
!     WRITTEN BY  -  ROBERT B. SCHNABEL (CODED BY JANET R. DONALDSON)
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  APRIL 2, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) ::
     &   D,ETA,PV,SCALE,TAU
      INTEGER
     &   IXM,J,LMSG,M,N,NPAR,NROW
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   PAR(NPAR),PVTEMP(N),XM(IXM,M)
      INTEGER
     &   MSG(LMSG)
!
!  SUBROUTINE ARGUMENTS
      EXTERNAL MDL
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     &   FD,PARMX,PVPSTP,STP,TEMP
!
!  EXTERNAL SUBROUTINES
      EXTERNAL DCKCRV,DCKZRO
!
!  INTRINSIC FUNCTIONS
      INTRINSIC ABS,MAX,SIGN,SQRT
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL D
!        THE SCALAR IN WHICH ROW   NROW   OF THE DERIVATIVE
!        MATRIX WITH RESPECT TO THE JTH UNKNOWN PARAMETER
!        IS STORED.
!     REAL ETA
!        THE RELATIVE NOISE IN THE MODEL
!     REAL FD
!        THE FORWARD DIFFERENCE QUOTIENT DERIVATIVE WITH RESPECT TO THE
!        JTH PARAMETER
!     INTEGER IXM
!        THE FIRST DIMENSION OF THE INDEPENDENT VARIABLE ARRAY.
!     INTEGER J
!        THE INDEX OF THE PARTIAL DERIVATIVE BEING EXAMINED.
!     INTEGER LMSG
!        THE LENGTH OF THE VECTOR MSG.
!     INTEGER M
!        THE NUMBER OF INDEPENDENT VARIABLES.
!     EXTERNAL MDL
!        THE NAME OF THE USER SUPPLIED SUBROUTINE WHICH COMPUTES THE
!        PREDICTED VALUES BASED ON THE CURRENT PARAMETER ESTIMATES.
!     INTEGER MSG(LMSG)
!        AN ARRAY USED TO STORE MESSAGE PARAMETERS.
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS
!     INTEGER NPAR
!        THE NUMBER OF UNKNOWN PARAMETERS IN THE MODEL.
!     INTEGER NROW
!        THE NUMBER OF THE ROW OF THE INDEPENDENT VARIABLE ARRAY AT
!        WHICH THE DERIVATIVE IS TO BE CHECKED.
!     REAL PAR(NPAR)
!        THE ARRAY IN WHICH THE CURRENT ESTIMATES OF THE UNKNOWN
!        PARAMETERS ARE STORED.
!     REAL PARMX
!        THE MAXIMUM OF THE CURRENT PARAMETER ESTIMATE AND THE
!        TYPICAL VALUE OF THAT PARAMETER
!     REAL PV
!        THE SCALAR IN WHICH THE PREDICTED VALUE FROM THE MODEL FOR
!        ROW   NROW   IS STORED.
!     REAL PVPSTP
!        THE PREDICTED VALUE FOR ROW    NROW   OF THE MODEL
!        BASED ON THE CURRENT PARAMETER ESTIMATES
!        FOR ALL BUT THE JTH PARAMETER VALUE, WHICH IS PAR(J) + STP.
!     REAL PVTEMP(N)
!        THE VECTOR OF PREDICTED VALUE FROM THE MODEL.
!     REAL SCALE
!        THE TYPICAL SIZE OF THE JTH PARAMETER.
!     REAL STP
!        THE STEP SIZE CURRENTLY BEING EXAMINED FOR THE FINITE DIFFERENC
!        DERIVATIVE
!     REAL TAU
!        THE AGREEMENT TOLERANCE.
!     REAL TEMP
!        A TEMPORARY LOCATION IN WHICH THE CURRENT ESTIMATE OF THE JTH
!        PARAMETER IS STORED.
!     REAL XM(IXM,M)
!        THE ARRAY IN WHICH ONE ROW OF THE INDEPENDENT VARIABLE ARRAY
!        IS STORED.
!
!     CALCULATE THE JTH PARTIAL DERIVATIVE USING FORWARD DIFFERENCE
!     QUOTIENTS AND DECIDE IF IT AGREES WITH USER SUPPLIED VALUES
!
      MSG(J+1) = 0
!
      PARMX = MAX(ABS(PAR(J)),ABS(SCALE))
      IF (PARMX .EQ. 0.0E0) PARMX = 1.0E0
!
!     COMPUTE INITIAL STEP SIZE
!
      STP = (SQRT(ETA)*PARMX*SIGN(1.0E0,PAR(J))+PAR(J)) - PAR(J)
!
!     COMPUTE PREDICTED VALUES
!
      TEMP = PAR(J)
      PAR(J) = PAR(J) + STP
      CALL MDL(PAR, NPAR, XM, N, M, IXM, PVTEMP)
      PAR(J) = TEMP
!
      PVPSTP = PVTEMP(NROW)
!
      FD = (PVPSTP-PV)/STP
!
!     CHECK FOR DISAGREEMENT
!
      IF (ABS(FD-D) .GT. TAU*ABS(D)) GO TO 10
!
!     NUMERICAL AND ANALYTIC DERIVATIVES AGREE
!
!     CHECK IF ANALYTIC DERIVATIVE IS IDENTICALLY ZERO, INDICATING
!     THE POSSIBILITY THAT THE DERIVATIVE SHOULD BE RECHECKED AT
!     ANOTHER POINT.
!
      IF (D.NE.0.0E0) RETURN
!
!     JTH ANALYTIC AND NUMERICAL DERIVATIVES BOTH ARE ZERO.
!
      IF (MSG(1).EQ.0) MSG(1) = 1
      MSG(J+1) = 3
      RETURN
!
   10 CONTINUE
!
!     NUMERICAL AND ANALYTIC DERIVATIVES DISAGREE
!
!     CHECK WHY
!
      IF (D.EQ.0.0E0) THEN
         CALL DCKZRO(J, PAR, NPAR, MDL, XM, N,
     &      NROW, M, IXM, PV, PVTEMP, MSG, LMSG, FD, PARMX, PVPSTP,
     &      STP)
      ELSE
         CALL DCKCRV(J, D, PAR, NPAR, ETA, TAU, MDL, XM,
     &      N, NROW, M, IXM, PV, PVTEMP, MSG, LMSG, FD, PARMX,
     &      PVPSTP, STP)
      END IF
!
      RETURN
      END
