!LPFLT
      SUBROUTINE LPFLT (FC, K, HLP)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE COMPUTES THE LOPASS FILTER COEFFICIENTS.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 26, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP)
     +   FC
      INTEGER
     +   K
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP)
     +   HLP(K)
!
!  LOCAL SCALARS
      REAL(KIND=WP)
     +   ARG,CON,PI,SUM
      INTEGER
     +   I,IHM,IHP,KHALF,KMID
!
!  EXTERNAL SUBROUTINES
      EXTERNAL GETPI
!
!  INTRINSIC FUNCTIONS
      INTRINSIC SIN
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL ARG, CON
!        VARIABLES USED IN THE COMPUTATION OF THE LOW PASS FILTER
!        COEFFICIENTS.
!     REAL FC
!        THE CUTOFF FREQUENCY USED FOR THE LOW PASS FILTER.
!     REAL HLP(K)
!        THE ARRAY IN WHICH THE INPUT LOW PASS FILTER COEFFICIENTS
!        ARE STORED.
!     INTEGER I
!        AN INDEX VARIABLE.
!     INTEGER IHM, IHP
!        INDEX VARIABLES FOR SYMMETRIC LOCATIONS AROUND THE MIDPOINT
!        OF THE FILTER.
!     INTEGER K
!        THE NUMBER OF TERMS IN THE FILTER.
!     INTEGER KHALF
!        THE VALUE OF THE MIDPOINT OF K MINUS 1.
!     INTEGER KMID
!        THE MIDPOINT OF THE FILTER.
!     REAL PI
!        THE VALUE OF PI.
!     REAL SUM
!        A VALUE USED FOR SUMMING.
!
      CALL GETPI(PI)
!
      KMID = (K + 1) / 2
!
      HLP(KMID) = 1.0E0
!
      IF (K .EQ. 1) RETURN
!
      HLP(KMID) = 2.0E0 * FC
      CON = 2.0E0 * PI / K
      SUM = HLP(KMID)
!
      KHALF = (K - 1) / 2
!
      DO 10 I = 1, KHALF
         ARG = I * CON
         IHP = KMID + I
         HLP(IHP) = SIN(I * FC * 2.0E0 * PI) * SIN(ARG) /
     +      (I * PI * ARG)
         IHM = KMID - I
         HLP(IHM) = HLP(IHP)
         SUM = SUM + HLP(IHM) + HLP(IHP)
   10 CONTINUE
      DO 20 I = 1, K
         HLP(I) = HLP(I) / SUM
   20 CONTINUE
      RETURN
      END
