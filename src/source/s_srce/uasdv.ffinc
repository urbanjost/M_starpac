!UASDV
     subroutine uasdv(acov, spca, spcf, lspc, iar, phi, nf, fmin, fmax,&
    &   freq, n, lagmax, ftest, aic, work, lacov, lwork, delta, isort,&
    &   isym, xaxis, yaxis, lpcv, alpha, lag, laic, lphi, nprt, var,&
     &   window, nmsub)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS IS THE MAIN DRIVER FOR COMPUTING THE AUTOREGRESSIVE
!     (AND FOURIER) SPECTRUMS.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  DECEMBER 7, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     real(kind=wp) ::&
     &   alpha,delta,fmax,fmin,var
     integer&
     &   iar,lacov,lag,lagmax,laic,lpcv,lphi,lspc,lwork,n,nf,nprt
!
!  ARRAY ARGUMENTS
     real(kind=wp) ::&
    &   acov(lacov),aic(laic),freq(nf),ftest(2,lagmax),phi(lphi),&
     &   spca(lspc),spcf(lspc),work(lwork),xaxis(lpcv),yaxis(lpcv)
     integer&
     &   isort(nf),isym(lpcv)
     character&
     &   nmsub(6)*1
!
!  SUBROUTINE ARGUMENTS
       external window
!
!  LOCAL SCALARS
     real(kind=wp) ::&
    &   alow,aup,bw,df,spcamn,spcamx,spcfmn,spcfmx,xpltmn,xpltmx,&
     &   ypltmn,ypltmx
     integer&
     &   ispcer,npts,nspca,nspcf,nw
     logical&
     &   aicprt
!
!  LOCAL ARRAYS
     integer&
     &   lags(1),nlppa(1)
!
!  EXTERNAL SUBROUTINES
!       EXTERNAL AOS,SETFRQ,SPCCK,UASCFT,UASEST,UASORD,UASOUT,UFSLAG,UFSMN
!
!  INTRINSIC FUNCTIONS
      intrinsic iabs,int
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL ACOV(LACOV)
!        THE AUTOCOVARIANCE COMPUTED FROM THE LAG PRODUCT PAIRS.
!     REAL AIC(LAIC)
!        THE ARRAY CONTAINING THE AKAIKES CRITERIA FOR EACH ORDER(?).
!     LOGICAL AICPRT
!        AN INDICATOR VARIABLE USED TO DETERMINE IF THE AKIAKE
!        INFORMATION CRITERIA AND CHI SQUARED STATISTICS SHOULD
!        BE PRINTED.
!     REAL ALOW
!        A FACTOR USED TO COMPUTE THE LOWER CONFIDENCE LIMITS.
!     REAL ALPHA
!        THE DESIRED CONFIDENCE LEVEL.
!     REAL AUP
!        A FACTOR USED TO COMPUTE THE UPPER CONFIDENCE LIMITS.
!     REAL BW
!        THE BANDWIDTH.
!     REAL DELTA
!        THE SAMPLING INTERVAL.
!     REAL DF
!        THE EFFECTIVE DEGREES OF FREEDOM.
!     REAL FMAX, FMIN
!        THE MAXIMUM AND MINIMUM FREQUENCY FOR WHICH THE
!        SPECTRUM ESTIMATES ARE TO BE COMPUTED.
!     REAL FREQ(NF)
!        THE VECTOR OF FREQUENCIES AT WHICH THE SPECTRUM IS TO BE
!        ESTIMATED.
!     REAL FTEST(2,LAGMAX)
!        THE ARRAY IN WHICH THE F RATIO AND PROBABILITY ARE STORED.
!        THE ORDER OF THE AUTOREGRESSIVE PROCESS CHOSEN.
!     INTEGER ISORT(NF)
!        AN ARRAY USED FOR SORTING.
!     INTEGER LSPC
!         THE ACTUAL FIRST DIMENSION FOR THE SPECTRUM ARRAYS.
!     INTEGER ISPCER
!        AN ERROR FLAG USED FOR THE SPECTRUM PLOTS.
!     INTEGER ISYM(LPCV)
!        THE ARRAY CONTAINING THE CODE FOR THE PLOT SYMBOLS.
!     INTEGER LACOV
!        THE LENGTH OF THE COVARIANCE ARRAYS.
!     INTEGER LAG
!        THE LAG WINDOW TRUNCATION POINT USED FOR A SPECIFIC WINDOW.
!     INTEGER LAGMAX
!        THE MAXIMUM LAG VALUE TO BE USED.
!     INTEGER LAGS(1)
!        THE LAG WINDOW TRUNCATION POINT RETURNED FROM UFSLAG.
!     INTEGER LAIC
!        THE LENGTH OF THE ARRAY AIC.
!     INTEGER LPCV
!        THE LENGTH OF THE PLOT CO-ORDINATE VECTORS.
!     INTEGER LPHI
!        THE LENGTH OF THE VECTOR PHI.
!     INTEGER LWORK
!        THE ACTUAL LENGTH OF THE WORK ARRAY.
!     INTEGER N
!        THE INTEGER NUMBER OF OBSERVATIONS IN THE SERIES
!     INTEGER NF
!        THE NUMBER OF FREQUENCIES FOR WHICH THE SPECTRUM ESTIMATES
!        ARE TO BE ESTIMATED.
!     INTEGER NLPPA(1)
!        A DUMMY ARRAY
!     CHARACTER*1 NMSUB(6)
!        THE NAME OF THE CALLING SUBROUTINE.
!     INTEGER NPRT
!        A CODE USED TO SPECIFY THE TYPE OF PLOT, WHERE IF
!        NPRT < 0 THE PLOT IS DECIBLES/LINEAR
!        NPRT = 0 THE PLOT IS SUPPRESSED
!        NPRT > 0 THE PLOT IS LOG/LINEAR
!     INTEGER NPTS
!        THE NUMBER OF X, Y CO-ORDINATES TO BE PLOTTED.
!     INTEGER NSPCA, NSPCF
!        THE NUMBER OF VALID SPECTRUM ESTIMATES FOR THE AUTOREGRESSIVE
!        AND FOURIER SPECTRUMS, RESPECTIVELY.
!     INTEGER NW
!        THE NUMBER OF LAG WINDOW TRUNCATION POINTS SELCTED.
!     REAL PHI(LPHI)
!        THE ARRAY OF AUTOREGRESSIVE COEFFICIENTS FOR THE
!        SELECTED ORDER.
!     REAL SPCA(LSPC)
!        THE ARAY CONTAINING THE AUTOREGRESSIVE SPECTRUM ESTIMATES.
!     REAL SPCAMN, SPCAMX
!        THE MINIMUM AND MAXIMUM AUTOREGRESSIVE SPECTRUM VALUE TO BE
!        PLOTTED.
!     REAL SPCF(LSPC)
!        THE ARRAY CONTAINING THE FOURIER SPECTRUM ESTIMATES.
!     REAL SPCFMN, SPCFMX
!        THE MINIMUM AND MAXIMUM FOURIER SPECTRUM VALUE TO BE PLOTTED.
!     REAL VAR
!        THE ONE STEP PREDICTION VARIANCE.
!     EXTERNAL WINDOW
!        THE TYPE OF WINDOW TO BE USED.
!     REAL WORK(LWORK)
!        THE WORK ARRAY.
!     REAL XAXIS(LPCV)
!        THE X AXIS VALUES FOR THE SPECTRUM PLOT.
!     REAL XPLTMN, XPLTMX
!        THE MINIMUM AND MAXIMUM VALUES TO BE PLOTTED FOR THE X AXIS.
!     REAL YAXIS(LPCV)
!        THE Y AXIS VALUES FOR THE SPECTRUM PLOT.
!     REAL YPLTMN, YPLTMX
!        THE MINIMUM AND MAXIMUM VALUES TO BE PLOTTED FOR THE Y AXIS.
!
      nw = 1
!
      if (lag.le.0) then
!
!     SET THE LAG WINDOW TRUNCATION POINT TO BE USED FOR THE
!     FOURIER SPECTRUM ESTIMATES.
!
         call ufslag(acov, lagmax, lags, n, nw, nw, lacov)
         lag = lags(1)/2
      end if
!
!     SET FREQUENCIES FOR THE SPECTRUM.
!
      call setfrq(freq, nf, 1, fmin, fmax, delta)
!
!     COMPUTE THE FOURIER SPECTRUM ESTIMATES
!
     call ufsmn(acov, nlppa, lag, df, nf, freq, alpha, bw, spcf,&
    &  alow, aup, lacov, lspc, window, work, lwork, n, delta,&
     &  .false., 1)
!
      aicprt = .false.
!
      if (iar.lt.0) then
!
!     USER HAS CHOSEN ORDER.
!     COMPUTE COEFFICIENTS AND VARIANCE USING DURBINS RECURSIVE METHOD.
!
         call uascft(acov, lagmax, lacov, iabs(iar), phi, n, var)
!
      else if (iar.eq.0) then
!
!     SELECT MODEL ORDER AND COMPUTE COEFFICIENTS AND VARIANCE.
!
         aicprt = .true.
        call aos(n, lagmax, acov, work, iar, var, phi,&
     &            work, aic, ftest, lacov, laic)
      end if
!
!     COMPUTE THE AUTOREGRESSIVE SPECTRUM ESTIMATES.
!
     call uasest(iabs(iar), var, phi, nf, freq, delta, spca, lphi,&
     &   lspc)
!
      if (nprt.eq.0) return
!
!     SET PLOTTING VECTORS.
!
      xpltmn = fmin
      xpltmx = fmax
!
      ypltmn = 0.0e0
      ypltmx = 0.0e0
!
      call spcck(spcf, isort, nf, spcfmn, spcfmx, nspcf, ispcer)
      if (ispcer.ne.0) go to 40
      call spcck(spca, isort, nf, spcamn, spcamx, nspca, ispcer)
      if (ispcer.ne.0) go to 40
!
     call uasord(spcf, spca, spcfmn, spcfmx, spcamn, spcamx, freq, nf,&
    &   xaxis, yaxis, isym, npts, lspc, lpcv, nspcf, nspca, bw, alow,&
     &   aup, xpltmn, xpltmx, ypltmn, ypltmx, nprt)
!
!     PRINT RESULTS
!
  40 call uasout(xaxis, yaxis, isym, npts, bw, int(df+0.5e0), lag,&
    &   iabs(iar), phi, ispcer, lpcv, xpltmn, xpltmx, ypltmn, ypltmx,&
     &   ftest, aic, laic, var, nprt, lagmax, aicprt, n, nmsub)
!
      return
      end
