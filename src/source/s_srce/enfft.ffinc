!ENFFT
      SUBROUTINE ENFFT(NMSUB, NFFT, NDIV, N, LYFFT, NFFT2, HEAD, ERROR)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE CHECKS WHETHER THE VALUE NFFT IS SUCH THAT NFFT-2 IS
!     DIVISIBLE BY NDIV AND HAS NO PRIME FACTORS GREATER THAN 23, AND
!     THE PRODUCT OF THE SQUARE FREE PRIME FACTORS OF NFFT - 2 DO NOT
!     EXCEED 209, I.E., THE VALUE OF NFFT MEETS THE REQUIREMENTS OF
!     THE EXTENDED LENGTH OF THE SERIES REQUIRED FOR ANY ROUTINE
!     USING THE SINGLETON FFT PROVIDING THE PROPER VALUE OF NDIV
!     IS CHOSEN.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  DECEMBER 7, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      INTEGER
     +   LYFFT,N,NDIV,NFFT,NFFT2
      LOGICAL
     +   ERROR,HEAD
!
!  ARRAY ARGUMENTS
      CHARACTER
     +   NMSUB(6)*1
!
!  LOCAL SCALARS
      INTEGER
     +   IPRT,NFFT1
!
!  EXTERNAL SUBROUTINES
      EXTERNAL EHDR,IPRINT,SETESL
!
!  INTRINSIC FUNCTIONS
      INTRINSIC MAX
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     LOGICAL ERROR
!        VALUE(S) INDICATING WHETHER AN ERROR WAS DETECTED (TRUE) OR NOT
!        (FALSE).
!     LOGICAL HEAD
!        A FLAG INDICATING WHETHER THE HEADING SHOULD BE PRINTED
!        (TRUE) OR NOT (FALSE).  IF A HEADING IS PRINTED, THE VALUE
!        OF HEAD WILL BE CHANGED TO FALSE.
!     INTEGER IPRT
!        THE UNIT NUMBER FOR PRINTED OUTPUT.
!     INTEGER LYFFT
!        THE LENGTH OF THE VECTOR CONTAINING THE SERIES TO BE EXTENDED.
!     INTEGER N
!        THE ACTUAL NUMBER OF OBSERVATIONS IN THE SERIES.
!     CHARACTER*1 NMSUB(6)
!        THE CHARACTERS OF THE CALLING SUBROUTINES NAME.
!     INTEGER NFFT
!        THE USER SUPPLIED EXTENDED SERIES LENGTH.
!     INTEGER NFFT1
!        THE MAXIMUM OF NFFT AND N+2.
!     INTEGER NFFT2
!        THE SMALLEST EXTENDED SERIES LENGTH WHICH EQUALS OR
!        EXCEEDS NFFT AND WHICH MEETS THE REQUIREMENTS OF
!        SINGLETONS FFT CODE.
!
      ERROR = .FALSE.
      CALL IPRINT (IPRT)
!
      IF (NFFT .GE. N+2) GO TO 20
!
!     PRINT WARNING
!
      CALL EHDR(NMSUB, HEAD)
!
      WRITE (IPRT, 1050) N
!
   20 CONTINUE
      NFFT1 = MAX(NFFT, N+2)
      CALL SETESL(NFFT1-2, NDIV, NFFT2)
!
      IF (NFFT .EQ. NFFT2) RETURN
!
!     PRINT WARNING
!
      CALL EHDR(NMSUB, HEAD)
!
      WRITE (IPRT, 1020) NFFT, NFFT2
!
      IF (NFFT .GT. LYFFT) GO TO 40
!
      WRITE (IPRT, 1030) NFFT2
      RETURN
!
   40 CONTINUE
!
      ERROR = .TRUE.
!
      WRITE (IPRT, 1040) NFFT2, LYFFT
      RETURN
!
!     FORMAT STATEMENTS
!
 1020 FORMAT (/
     +   40H THE INPUT VALUE OF THE PARAMETER NFFT (, I5,
     +   15H) DOES NOT MEET/
     +   51H THE REQUIREMENTS OF SINGLETONS FFT CODE.  THE NEXT,
     +   13H LARGER VALUE/
     +   15H WHICH DOES IS , I5, '.')
 1030 FORMAT (/
     +   11H THE VALUE , I5, 37H WILL BE USED FOR THE EXTENDED SERIES,
     +    8H LENGTH.)
 1040 FORMAT (/
     +   20H HOWEVER, THE VALUE , I5, 27H EXCEEDS THE LENGTH LYFFT (,
     +   I5, 8H) OF THE/
     +   58H VECTOR YFFT, AND THEREFORE CANNOT BE USED AS THE EXTENDED/
     +   43H SERIES LENGTH WITHOUT REDIMENSIONING YFFT.)
 1050 FORMAT (/
     +   56H THE EXTENDED SERIES LENGTH (NFFT) MUST EQUAL OR EXCEED,/
     +   45H THE NUMBER OF OBSERVATIONS IN THE SERIES (N=, I5,
     +    9H) PLUS 2.)
!
      END
