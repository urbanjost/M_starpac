!LLCNT
     subroutine llcnt(y, wt, lwt, xm, n, m, ixm, npar, res, ldstak,&
    &   nprt, par, lpar, rsd, pv, lpv, sdpv, lsdpv, sdres, lsdres, vcv,&
     &   ivcv, llhdr, ifit, nmsub, weight, save)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS IS THE CONTROLLING SUBROUTINE FOR LINEAR LEAST
!     SQUARES.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  DECEMBER 29, 1982
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     real(kind=wp) ::&
     &   rsd
     integer&
    &   ifit,ivcv,ixm,ldstak,lpar,lpv,lsdpv,lsdres,lwt,m,n,npar,&
     &   nprt
     logical&
     &   save,weight
!
!  ARRAY ARGUMENTS
     real(kind=wp) ::&
     &   par(*),pv(*),res(*),sdpv(*),sdres(*),vcv(*),wt(*),xm(*),y(*)
     character&
     &   nmsub(6)*1
!
!  SUBROUTINE ARGUMENTS
       external llhdr
!
!  SCALARS IN COMMON
     integer&
     &   ierr
!
!  ARRAYS IN COMMON
      double precision dstak(12)
!
!  LOCAL SCALARS
     integer&
    &   acc,c,fc,ifp,nall0,ndigit,nnzw,par1,pari,pvi,red,resi,&
     &   rsdi,sdpvi,sdresi,t,vcvi,wti,wy,xmw
     logical&
     &   page,wide
!
!  LOCAL ARRAYS
     real(kind=wp) ::&
     &   rstak(12)
     integer&
     &   iptout(4),istak(12)
!
!  EXTERNAL FUNCTIONS
     integer&
     &   stkst
!       EXTERNAL STKST
!
!  EXTERNAL SUBROUTINES
!       EXTERNAL CPYMSS,LLER,LLSMN,PRTCNT,SETRV,STKCLR,STKSET
!
!  COMMON BLOCKS
      common /cstak/dstak
      common /errchk/ierr
!
!  EQUIVALENCES
      equivalence (dstak(1),rstak(1))
      equivalence (dstak(1),istak(1))
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     INTEGER ACC
!        THE STARTING LOCATION IN THE WORK AREA FOR
!        THE NUMBER OF ACCURATE DIGITS.
!     INTEGER C
!        *
!     DOUBLE PRECISION DSTAK(12)
!        THE DOUBLE PRECISION VERSION OF THE /CSTAK/ WORK AREA.
!     INTEGER FC
!        THE STARTING LOCATION IN THE WORK AREA FOR
!        THE ORTHONORMALIZATION MATRIX.
!     INTEGER IERR
!        THE INTEGER VALUE DESIGNATING WHETHER ANY ERRORS WERE
!        DETECTED IN THE PARAMETER LIST.
!        IF IERR .EQ. 0, NO ERRORS WERE DETECTED.
!        IF IERR .EQ. 1, ERRORS WERE DETECTED.
!     INTEGER IFIT
!        THE INDICATOR VALUE DESIGNATING WHETHER THE FIT IS OF A
!        GENERAL MODEL (IFIT=3) OR A POLYNOMIAL MODEL (IFIT=1).
!     INTEGER IFP
!        AN INDICATOR FOR STACK ALLOCATION TYPE, WHERE IFP=3 INDICATES
!        SINGLE PRECISION AND IFP=4 INDICATES DOUBLE PRECISION.
!     INTEGER IPTOUT(4)
!        THE VARIABLE USED TO CONTROL PRINTED OUTPUT FOR EACH SECTION.
!     INTEGER ISTAK(12)
!        THE INTEGER VERSION OF THE /CSTAK/ WORK AREA.
!     INTEGER IVCV
!        THE FIRST DIMENSION OF THE MATRIX VCV.
!     INTEGER IXM
!        THE FIRST DIMENSION OF THE MATRIX XM.
!     INTEGER LDSTAK
!        THE LENGTH OF THE VECTOR DSTAK IN COMMON CSTAK.
!     EXTERNAL LLHDR
!        THE NAME OF THE ROUTINE THAT PRODUCED THE HEADING.
!     INTEGER LPAR
!        THE ACTUAL LENGTH OF THE VECTOR P.
!     INTEGER LPV
!        THE ACTUAL LENGTH OF THE VECTOR PV.
!     INTEGER LSDPV
!        THE ACTUAL LENGTH OF THE VECTOR SDPV.
!     INTEGER LSDRES
!        THE ACTUAL LENGTH OF THE VECTOR SDRES.
!     INTEGER LWT
!        THE ACTUAL LENGTH OF THE VECTOR WT.
!     INTEGER M
!        THE NUMBER OF INDEPENDENT VARIABLES.
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS.
!     INTEGER NALL0
!        NUMBER OF ALLOCATIONS ON ENTRY.
!     INTEGER NDIGIT
!        THE NUMBER OF DIGITS IN THE PRINT CONTROL VALUE.
!     CHARACTER*1 NMSUB(6)
!        THE NAME OF THE SUBROUTINE CALLING THE ERROR CHECKING
!        SUBROUTINES.
!     INTEGER NNZW
!        THE NUMBER OF NON ZERO WEIGHTS.
!     INTEGER NPAR
!        THE NUMBER OF PARAMETERS.
!     INTEGER NPRT
!        THE INDICATOR VARIABLE USED TO DESIGNATE THE AMOUNT OF
!        PRINTED OUTPUT.
!     LOGICAL PAGE
!        THE VARIABLE USED TO INDICATE WHETHER A GIVEN SECTION OF
!        THE OUTPUT IS TO BEGIN ON A NEW PAGE (TRUE) OR NOT (FALSE).
!     REAL PAR(LPAR)
!        THE PARAMETERS TO BE ESTIMATED.
!     INTEGER PARI
!        THE STARTING LOCATION IN THE WORK AREA OF
!        THE PARAMETERS TO BE ESTIMATED.
!     INTEGER PAR1
!        THE STARTING LOCATION IN THE WORK AREA FOR
!        THE PARAMETERS TO BE ESTIMATED OMMITTING THE LAST
!        INDEPENDENT VARIABLE.
!     REAL PV(LPV)
!        THE PREDICTED VALUES.
!     INTEGER PVI
!        THE STARTING LOCATION IN THE WORK AREA FOR
!        THE PREDICTED VALUES.
!     INTEGER RED
!        THE STARTING LOCATION IN THE WORK AREA FOR
!        THE REDUCTION TO THE SUM OF SQUARES DUE TO EACH PARAMETER.
!     REAL RES(N)
!        THE RESIDUALS.
!     INTEGER RESI
!        THE STARTING LOCATION IN THE WORK AREA FOR
!        THE RESIDUALS.
!     REAL RSD
!        THE RESIDUAL STANDARD DEVIATION.
!     INTEGER RSDI
!        THE STARTING LOCATION IN THE WORK AREA FOR
!        THE RESIDUAL STANDARD DEVIATION.
!     REAL RSTAK(12)
!        THE REAL VERSION OF THE /CSTAK/ WORK AREA.
!     LOGICAL SAVE
!        THE VARIABLE USED TO INDICATE WHETHER ANY RESULTS OTHER THAN
!        THE RESIDUALS ARE TO VE SAVED (TRUE) OR NOT (FALSE).
!     REAL SDPV(LSDPV)
!        THE STANDARD DEVIATIONS OF THE PREDICTED VALUES.
!     INTEGER SDPVI
!        THE STARTING LOCATION IN THE WORK AREA FOR
!        THE STANDARD DEVIATIONS OF THE PREDICTED VALUES.
!     REAL SDRES(LSDRES)
!        THE STANDARDIZED RESIDUALS.
!     INTEGER SDRESI
!        THE STARTING LOCATION IN THE WORK AREA FOR
!        THE STANDARDIZED RESIDUALS.
!     INTEGER T
!        THE STARTING LOCATION IN THE WORK AREA FOR
!        THE TRIANGULAR MATRIX FROM THE DECOMPOSITION.
!     REAL VCV(IVCV,NPAR)
!        THE VARIANCE COVARIANCE MATRIX.
!     INTEGER VCVI
!        THE STARTING LOCATION IN THE WORK AREA FOR
!        THE VARIANCE COVARIANCE MATRIX.
!     LOGICAL WEIGHT
!        THE VARIABLE USED TO INDICATE WHETHER WEIGHTED ANALYSIS IS TO
!        BE PERFORMED (TRUE) OR NOT (FALSE).
!     LOGICAL WIDE
!        THE VARIABLE USED TO INDICATE WHETHER THE HEADING SHOULD
!        BE FULL WIDTH (TRUE) OR NOT (FALSE).
!     REAL WT(LWT)
!        THE WEIGHTS.
!     INTEGER WTI
!        THE STARTING LOCATION IN THE WORK AREA FOR
!        THE WEIGHTS.
!     INTEGER WY
!        THE STARTING LOCATION IN THE WORK AREA FOR
!        THE VECTOR CONTAINING SQRT(WT)*Y.
!     REAL XM(IXM,M)
!        THE INDEPENDENT VARIABLE.
!     INTEGER XMW
!        THE STARTING LOCATION IN THE WORK AREA FOR
!        THE MATRIX CONTAINING XM * SQRT(WT).
!     REAL Y(N)
!        THE DEPENDENT VARIABLE.
!
!
      wide = .true.
      page = .false.
      ndigit = 4
!
      ifp = 3
!
!     SET PRINT CONTROL VALUES
!
      call prtcnt(nprt, ndigit, iptout)
!
!     CHECK FOR ERRORS
!
     call ller(nmsub, ixm, ivcv, n, npar, lpar, ldstak, wt, lwt,&
     &   weight, nnzw, ifit, save)
      if (ierr.ne.0) return
!
      call stkset(ldstak, 4)
      nall0 = stkst(1)
!
!     SET UP SUBDIVISION OF WORK AREAS
!
      wti = stkget(n,ifp)
      resi = stkget(n,ifp)
      rsdi = stkget(1,ifp)
      pari = stkget(npar,ifp)
      pvi = stkget(n,ifp)
      sdpvi = stkget(n,ifp)
      sdresi = stkget(n,ifp)
      vcvi = stkget(npar*npar,ifp)
!
      wy = stkget(n,ifp)
      xmw = stkget(n*npar,ifp)
      red = stkget(npar,ifp)
      t = stkget(npar*npar,ifp)
      par1 = stkget(npar,ifp)
      acc = stkget(npar,ifp)
      c = stkget(npar,ifp)
!
!     EQUIVALENCED LOCATIONS WITHIN SCRAT
!
      fc = xmw
!
!     SET UP WEIGHTS VECTOR
!
      if (weight) then
         call scopy(n, wt, 1, rstak(wti), 1)
      else
         call setrv(rstak(wti), n, 1.0e0)
      end if
!
     call llsmn(y, xm, rstak(wti), n, m, npar, ixm, rstak(resi),&
    &   rstak(pari), nnzw, rstak(rsdi), rstak(pvi), rstak(sdpvi),&
    &   rstak(sdresi), iptout, rstak(wy), rstak(xmw), rstak(vcvi),&
    &   rstak(fc), rstak(red), rstak(t), rstak(par1), rstak(acc), ifit,&
     &   weight, rstak(c), llhdr, page, wide)
!
      call scopy(n, rstak(resi), 1, res, 1)
!
      if (save) then
         rsd = rstak(rsdi)
         call scopy(npar, rstak(pari), 1, par, 1)
         call scopy(n, rstak(pvi), 1, pv, 1)
         call scopy(n, rstak(sdpvi), 1, sdpv, 1)
         call scopy(n, rstak(sdresi), 1, sdres, 1)
         call cpymss(npar, npar, rstak(vcvi), npar, vcv, ivcv)
      end if
      call stkclr(nall0)
!
      if (ierr.eq.3) ierr = 2
      if (ierr.eq.4) ierr = 3
!
      return
!
      end
