!OBSSUM
     subroutine obssum(n, m, xm, y, pv, sdpv, res, sdres, wt, ixm,&
     &   weight, k, ifirst, ilast, jcol1, jcolm)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS SUBOUTINE LISTS THE DATA SUMMARY FOR THE
!     LEAST SQUARES SUBROUTINES.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  DECEMBER 29,  1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     integer&
     &   ifirst,ilast,ixm,jcol1,jcolm,k,m,n
     logical&
     &   weight
!
!  ARRAY ARGUMENTS
     real(kind=wp) ::&
     &   pv(n),res(n),sdpv(n),sdres(n),wt(n),xm(ixm,m),y(n)
!
!  SCALARS IN COMMON
     integer&
     &   ierr
!
!  LOCAL SCALARS
     real(kind=wp) ::&
     &   fplm
     integer&
     &   i,iprt,j
     character&
     &   string*20,fmt1*160,fmt2*160,fmt3*160
!
!  EXTERNAL FUNCTIONS
!      REAL(KIND=WP) ::
!     +   R1MACH
!      LOGICAL
!     +   MVCHK
!      EXTERNAL R1MACH,MVCHK
!
!  EXTERNAL SUBROUTINES
!      EXTERNAL IPRINT
!
!  COMMON BLOCKS
      common /errchk/ierr
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     CHARACTER*160 FMT1,FMT2,FMT3
!        THE FORMATS USED TO PRINT THE INFORMATION.
!     REAL FPLM
!        THE FLOATING POINT LARGEST MAGNITUDE.
!     INTEGER I
!        AN INDEXING VARIABLE.
!     INTEGER IERR
!        THE INTEGER VALUE DESIGNATING WHETHER ANY ERRORS HAVE
!        BEEN DETECTED.
!        IF IERR .EQ. 0, NO ERRORS WERE DETECTED
!        IF IERR .NE. 0, ERRORS HAVE BEEN DETECTED.
!     INTEGER IFIRST, ILAST
!        THE FIRST AND LAST INDICES TO BE LISTED.
!     INTEGER IXM
!        THE FIRST DIMENSION OF THE INDEPENDENT VARIABLE ARRAY.
!     INTEGER J
!        THE INDEX OF THE PARAMETER BEING EXAMINED.
!     INTEGER JCOLM
!        THE LAST COLUMN OF THE INDEPENDENT VARIABLE TO BE PRINTED.
!     INTEGER JCOL1
!        THE FIRST COLUMN OF THE INDEPENDENT VARIABLE TO BE PRINTED.
!     INTEGER K
!        AN INDEX VARIABLE.
!     INTEGER M
!        THE NUMBER OF INDEPENDENT VARIABLES.
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS.
!     REAL PV(N)
!        THE PREDICTED VALUE BASED ON THE CURRENT PARAMETER ESTIMATES
!     REAL RES(N)
!        THE RESIDUALS FROM THE FIT.
!     REAL SDPV(N)
!        THE STANDARD DEVIATION OF THE PREDICTED VALUE.
!     REAL SDRES(N)
!        THE STANDARD DEVIATIONS OF THE RESIDUALS.
!     CHARACTER*20 STRING
!        CHARACTER STRING USED TO BUILD THE FORMATS.
!     LOGICAL WEIGHT
!        THE VARIABLE USED TO INDICATE WHETHER WEIGHTED ANALYSIS IS TO
!        BE PERFORMED (TRUE) OR NOT (FALSE).
!     REAL WT(N)
!        THE USER SUPPLIED WEIGHTS.
!     REAL XM(IXM,M)
!        THE ARRAY IN WHICH ONE ROW OF THE INDEPENDENT VARIABLE ARRAY
!        IS STORED.
!     REAL Y(N)
!        THE ARRAY OF THE DEPENDENT VARIABLE.
!
      fplm = r1mach(2)
!
      call iprint(iprt)
!
!     CONSTRUCT FORMAT
!
      if (k.eq.1) then
         string = '1X,I4,15X,G15.8,15X,'
      else if (k.eq.2) then
         string = '1X,I4,7X,2G15.8,8X, '
      else
         string = '1X,I4,3G15.8,       '
      end if
      write (fmt1,1020) string
      write (fmt2,1030) string
      write (fmt3,1040) string
!
      do 140 i=ifirst, ilast
         if (mvchk(sdpv(i),fplm)) then
            if (weight) then
              write (iprt, fmt1) i, (xm(i,j),j=jcol1,jcolm),&
     &                            y(i), pv(i), res(i), wt(i)
            else
              write (iprt, fmt1) i, (xm(i,j),j=jcol1,jcolm),&
     &                            y(i), pv(i), res(i)
            end if
         else
            if (mvchk(sdres(i),fplm)) then
               if (weight) then
                 write (iprt, fmt2) i, (xm(i,j),j=jcol1,jcolm),&
     &                               y(i), pv(i), sdpv(i), res(i), wt(i)
               else
                 write (iprt, fmt2) i, (xm(i,j),j=jcol1,jcolm),&
     &                               y(i), pv(i), sdpv(i), res(i)
               end if
            else
               if (weight) then
                 write (iprt, fmt3) i, (xm(i,j),j=jcol1,jcolm),&
    &                               y(i), pv(i), sdpv(i), res(i),&
     &                               sdres(i), wt(i)
               else
                 write (iprt, fmt3) i, (xm(i,j),j=jcol1,jcolm),&
    &                               y(i), pv(i), sdpv(i), res(i),&
     &                               sdres(i)
               end if
            end if
         end if
  140 continue
!
      return
!
!     FORMAT STATEMENTS
!
 1020 format ('(',a20,'2G16.8,8X,4HNC *,4X,G16.8,4X,4HNC *,1X,E9.3)')
 1030 format ('(',a20,'4G16.8,4X,4HNC *,1X,E9.3)')
 1040 format ('(',a20,'4G16.8,1X,F7.2,1X,E9.3)')
      end
