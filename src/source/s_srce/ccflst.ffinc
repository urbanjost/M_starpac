!CCFLST
      SUBROUTINE CCFLST (RHOC, SDRHOC, NLPP12, NLPP21, LAGMAX, LCCOV,
     &   NCC, IFMISS)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE ACTUALLY LISTS THE CROSS CORRELATIONS AND THEIR
!     STANDARD ERRORS, AND MISCELLANEOUS SUMMARY INFORMATION.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 21, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      INTEGER
     &   LAGMAX,LCCOV,NCC
      LOGICAL
     &   IFMISS
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   RHOC(*),SDRHOC(*)
      INTEGER
     &   NLPP12(*),NLPP21(*)
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     &   FPLM
      INTEGER
     &   I,I1,IMAX,IMIN,IPRT,K,K0,K1,LAGN,NPERL
!
!  LOCAL ARRAYS
      REAL(KIND=WP) ::
     &   RLST(12),SDRLST(12)
      INTEGER
     &   LAG(12),NLPLST(12)
!
!  EXTERNAL FUNCTIONS
!      REAL(KIND=WP) ::
!     +   R1MACH
!      EXTERNAL R1MACH
!
!  EXTERNAL SUBROUTINES
!      EXTERNAL IPRINT
!
!  INTRINSIC FUNCTIONS
      INTRINSIC MIN,MOD
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL FPLM
!        THE FLOATING POINT LARGEST MAGNITUDE.
!     INTEGER I
!        AN INDEX VARIABLE.
!     LOGICAL IFMISS
!        THE INDICATOR VARIABLE USED TO DETERMINE
!        WHETHER THE INPUT SERIES HAS MISSING DATA OR NOT.
!     INTEGER IMAX, IMIN
!        THE INDEX VALUES OF THE FIRST AND LAST OBSERVATION
!        TO BE PRINTED PER LINE
!     INTEGER IPRT
!        THE UNIT NUMBER FOR PRINTED OUTPUT
!     INTEGER I1
!        AN INDEX VARIABLE.
!     INTEGER K, K0, K1
!        INDEX VARIABLES.
!     INTEGER LAG(12)
!        THE LAG VALUE OF THE CORRELATION BEING PRINTED.
!     INTEGER LAGMAX
!        THE LARGEST LAG VALUE TO BE USED.
!     INTEGER LAGN
!        THE NUMBER OF LAG VALUES TO BE PRINTED PER LINE.
!     INTEGER LCCOV
!        THE NUMBER OF LOCATIONS ALLOWED FOR STORING THE NLPPC.
!     INTEGER NCC
!        THE NUMBER OF CROSS CORRELATIONS COMPUTED (FROM -LAGMAX
!        TO +LAGMAX).
!     INTEGER NLPLST(12)
!        THE ARRAY WHICH CONTAINS THE VALUES OF NLPPC TO BE PRINTED
!        ON EACH LINE, ORDERED PROPERLY.
!     INTEGER NLPP12(LCCOV), NLPP21(LCCOV)
!        THE NUMBER OF LAGGED PRODUCT PAIRS USED TO COMPUTE EACH
!        CCVF AT EACH LAG.
!     INTEGER NPERL
!        THE NUMBER OF VALUES TO BE PRINTED PER LINE.
!     REAL RHOC(NCC)
!        THE ARRAY IN WHICH THE AUTOCORRELATIONS OR PARTIAL
!        AUTOCORRELATIONS WILL BE PASSED TO THIS ROUTINE.
!     REAL RLST(12)
!        THE ARRAY WHICH CONTAINS THE VALUES OF RHO TO BE PRINTED
!        ON EACH LINE, ORDERED PROPERLY.
!     REAL SDRHOC(NCC)
!        THE ARRAY IN WHICH THE STANDARD ERRORS OF THE AUTOCORRELATIONS
!        ARE STORED
!     REAL SDRLST(12)
!        THE ARRAY WHICH CONTAINS THE VALUES OF SDRHO TO BE PRINTED
!        ON EACH LINE, ORDERED PROPERLY.
!
!
      CALL IPRINT(IPRT)
      NPERL = 12
!
      K0 = LAGMAX + 1
!
      LAGN = MOD(LAGMAX, NPERL)
      IF (LAGN .EQ. 0) LAGN = NPERL
      I1 = LAGN + 1
!
      DO 20 I = I1, K0, NPERL
         DO 10 K = 1, LAGN
            LAG(K) = I - K0 - K
            K1 = I - K
            RLST(K) = RHOC(K1)
            SDRLST(K) = SDRHOC(K1)
            IF (.NOT. IFMISS) GO TO 10
            K1 = K0 - K1
            NLPLST(K) = NLPP21(K1+1)
   10    CONTINUE
         WRITE(IPRT, 1000) (LAG(K), K = 1, LAGN)
         WRITE(IPRT, 1001) (RLST(K), K = 1, LAGN)
         WRITE(IPRT, 1002) (SDRLST(K), K = 1, LAGN)
         IF (IFMISS) WRITE(IPRT, 1003) (NLPLST(K), K = 1, LAGN)
         LAGN = NPERL
   20 CONTINUE
!
      LAG(1) = 0
      WRITE(IPRT, 1000) LAG(1)
      WRITE(IPRT, 1001) RHOC(K0)
      WRITE(IPRT, 1002) SDRHOC(K0)
      IF (IFMISS) WRITE(IPRT, 1003) NLPP12(1)
!
      DO 40 I = 1, LAGMAX, NPERL
         IMIN = I + K0
         IMAX = MIN(IMIN + NPERL - 1, 2*LAGMAX+1)
         LAGN = IMAX - IMIN + 1
         DO 30 K = 1, LAGN
            LAG(K) = I - 1 + K
   30    CONTINUE
         WRITE(IPRT, 1000) (LAG(K), K = 1, LAGN)
         WRITE(IPRT, 1001) (RHOC(K), K = IMIN, IMAX)
         WRITE(IPRT, 1002) (SDRHOC(K), K = IMIN, IMAX)
         IF (.NOT. IFMISS) GO TO 40
         IMIN = I
         IMAX = MIN(I + NPERL - 1, LAGMAX)
         WRITE (IPRT,1003) (NLPP12(K+1), K=IMIN,IMAX)
   40 CONTINUE
!
      FPLM = R1MACH(2)
!
      IF (SDRHOC(1).EQ.FPLM .OR. SDRHOC(2*LAGMAX+1).EQ.FPLM)
     &   WRITE(IPRT, 1004) FPLM
!
      RETURN
!
!     FORMAT STATEMENTS
!
 1000 FORMAT(/19H LAG               , 12(1X, I6))
 1001 FORMAT( 19H CCF               , 12(2X, F5.2))
 1002 FORMAT( 19H STANDARD ERROR    , 12(2X, F5.2))
 1003 FORMAT( 19H NO. OF OBS. USED  , 12(1X, I6))
 1004 FORMAT(///5X, F5.2, 38H INDICATES VALUE COULD NOT BE COMPUTED,
     &   21H DUE TO MISSING DATA.)
      END
