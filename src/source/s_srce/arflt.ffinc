!ARFLT
      SUBROUTINE ARFLT (Y, N, IAR, PHI, YF, NYF)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS SUBROUTINE PERFORMS THE AUTOREGRESSIVE FILTERING
!     OPERATION DEFINED BY PHI, RETURNING THE FILTERED SERIES
!     IN YF.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 26, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      INTEGER
     &   IAR,N,NYF
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   PHI(*),Y(*),YF(*)
!
!  SCALARS IN COMMON
      INTEGER
     &   IERR
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     &   YMEAN
      INTEGER
     &   I,IPRT
      LOGICAL
     &   ERR01,HEAD
!
!  LOCAL ARRAYS
      CHARACTER
     &   LN(8)*1,NMSUB(6)*1
!
!  EXTERNAL SUBROUTINES
!      EXTERNAL AMEAN,EISGE,FLTAR,IPRINT
!
!  COMMON BLOCKS
      COMMON /ERRCHK/IERR
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     LOGICAL ERR01
!        VALUE(S) INDICATING WHETHER AN ERROR WAS DETECTED (TRUE) OR NOT
!        (FALSE).
!     LOGICAL HEAD
!        A FLAG INDICATING WHETHER THE HEADING SHOULD BE PRINTED
!        (TRUE) OR NOT (FALSE).  IF A HEADING IS PRINTED, THE VALUE
!        OF HEAD WILL BE CHANGED TO FALSE.
!     INTEGER I
!        AN INDEX VARIABLE.
!     INTEGER IAR
!        THE NUMBER OF FILTER COEFFICIENTS.
!     INTEGER IERR
!        THE INTEGER VALUE RETURNED BY THIS ROUTINE DESIGNATING
!        WHETHER ANY ERRORS WERE DETECTED IN THE PARAMETER LIST.
!        IF IERR .EQ. 0, NO ERRORS WERE DETECTED.
!        IF IERR .EQ. 1, ERRORS HAVE BEEN DETECTED.
!     INTEGER IPRT
!        THE UNIT NUMBER USED FOR OUTPUT.
!     CHARACTER*1 LN(8)
!        THE ARRAY(S) CONTAINING THE NAME(S) OF INPUT PARAMETER(S)
!        CHECKED FOR ERRORS.
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS IN THE SERIES Y.
!     CHARACTER*1 NMSUB(6)
!        THE ARRAY CONTAINING THE NAME OF THIS SUBROUTINE.
!     INTEGER NYF
!        THE NUMBER OF OBSERVATIONS IN THE FILTERED SERIES YF.
!     REAL PHI(IAR)
!        THE VECTOR CONTAINING THE FILTER COEFFICIENTS.
!     REAL Y(N)
!        THE VECTOR CONTAINING THE OBSERVED TIME SERIES.
!     REAL YF(N)
!        THE VECTOR IN WHICH THE FILTERED SERIES IS RETURNED.
!     REAL YMEAN
!        THE MEAN OF THE INPUT SERIES Y.
!
!     SET UP NAME ARRAYS
!
      DATA
     &  NMSUB(1),  NMSUB(2),  NMSUB(3),  NMSUB(4),  NMSUB(5),  NMSUB(6)
     & /     'A',       'R',       'F',       'L',       'T',       ' '/
      DATA
     &  LN(1), LN(2), LN(3), LN(4), LN(5), LN(6), LN(7), LN(8)
     & /  'N',   ' ',   ' ',   ' ',   ' ',   ' ',   ' ',   ' '/
!
!     SET UP FOR ERROR CHECKING
!
      IERR = 0
      HEAD = .TRUE.
!
!     CALL ERROR CHECKING ROUTINES
!
      CALL EISGE(NMSUB, LN, N, 3, 1, HEAD, ERR01, LN)
!
!
      IF (.NOT. ERR01) GO TO 10
!
      IERR = 1
      CALL IPRINT (IPRT)
      WRITE (IPRT, 1000)
      RETURN
!
   10 CONTINUE
!
!     COMPUTE ARITHMETIC MEAN
!
      CALL AMEAN(Y, N, YMEAN)
!
      DO 20 I = 1, N
         YF(I) = Y(I) - YMEAN
   20 CONTINUE
!
      CALL FLTAR (YF, N, IAR, PHI, YF, NYF)
!
      RETURN
!
!     FORMAT STATEMENTS
!
 1000 FORMAT (/42H THE CORRECT FORM OF THE CALL STATEMENT IS//
     &   43H       CALL ARFLT (Y, N, IAR, PHI, YF, NYF))
      END
