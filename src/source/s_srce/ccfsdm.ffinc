!CCFSDM
     subroutine ccfsdm (ccov11, ccov22, sdrhoc, lagmax, ncc, laglst, n,&
     &   nlpp12, nlpp21, iccov, inlppc)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS IS THE MAIN SUBROUTINE FOR COMPUTING THE STANDARD ERROR
!     OF THE CROSS CORRELATIONS WHEN THERE ARE MISSING VALUES.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 21, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     integer&
     &   iccov,inlppc,laglst,lagmax,n,ncc
!
!  ARRAY ARGUMENTS
     real(kind=wp) ::&
     &   ccov11(iccov),ccov22(iccov),sdrhoc(ncc)
     integer&
     &   nlpp12(inlppc),nlpp21(inlppc)
!
!  LOCAL SCALARS
     real(kind=wp) ::&
     &   div,fplm,sum
     integer&
     &   i,ilast,k,k0,km,kp
!
!  EXTERNAL FUNCTIONS
     real(kind=wp) ::&
     &   r1mach
!       EXTERNAL R1MACH
!
!  INTRINSIC FUNCTIONS
      intrinsic min,sqrt
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL CCOV11(ICCOV), CCOV22(ICCOV)
!        THE ARRAYS IN WHICH THE AUTOCOVARIANCES ARE STORED.
!     REAL DIV
!        THE SQUARE ROOT OF THE PRODUCT OF THE AUTOCOVARIANCE
!        FUNCTION VALUES AT LAG ZERO.
!     REAL FPLM
!        THE FLOATING POINT LARGEST MAGNITUDE.
!     INTEGER I
!        INDEXING VARIABLES.
!     INTEGER ICCOV
!        THE DIMENSION OF THE COVARIANCE VECTORS.
!     INTEGER ILAST
!        THE LAST LAG AT WHICH THE STANDARD ERRORS ARE TO BE COMPUTED.
!     INTEGER INLPPC
!        THE DIMENSION OF THE LAGGED PRODUCT PAIR COUNT VECTORS.
!     INTEGER K
!        INDEXING VARIABLES.
!     INTEGER KM, KP, K0
!        THE LOCATIONS IN THE ARRAYS -RHOC- AND -SDRHOC-
!        OF THE LAG -K, K AND 0, RESPECTIVELY.
!     INTEGER LAGLST
!        THE LAST AUTOCORRELATION COMPUTED BEFORE A MISSING
!        AUTOCORRELATION WAS INCOUNTERED IN EITHER SERIES.
!     INTEGER LAGMAX
!        THE MAXIMUM LAG VALUE USED.
!     INTEGER N
!        THE INTEGER NUMBER OF OBSERVATIONS IN THE SERIES
!     INTEGER NCC
!       THE NUMBER OF CROSS CORRELATIONS COMPUTED.
!     INTEGER NLPP12(INLPPC), NLPP21(INLPPC)
!        THE ARRAYS IN WHICH THE NUMBER OF OBSERVATIONS USED TO
!        COMPUTE EACH CROSS CORRELATION ARE STORED.
!     REAL SDRHOC(NCC)
!        THE ARRAY IN WHICH THE STANDARD ERRORS OF THE CROSS CORRELATION
!        ARE STORED
!     REAL SUM
!         A SUMMING VARIABLE.
!
      fplm = r1mach(2)
!
      div = ccov11(1) * ccov22(1)
!
      k0 = lagmax + 1
!
      sum = 0.0e0
      do 10 i = 1, laglst
         sum = sum + ccov11(i+1) * ccov22(i+1)
   10 continue
      sum = sum / div
      sdrhoc(k0) = fplm
     if (nlpp12(1) .ge. 1) sdrhoc(k0) =&
     &   sqrt(n + 2.0e0 * sum) / nlpp12(1)
!
      do 30 k = 1, lagmax
         sum = 0.0e0
         ilast = min(laglst, n-laglst)
         do 20 i = 1, ilast
            sum = sum + (n-k-i) * ccov11(i+1) * ccov22(i+1)
   20    continue
         sum = sum / div
         km = k0 - k
         sdrhoc(km) = fplm
        if (nlpp21(k+1) .ge. 1) sdrhoc(km) =&
     &      sqrt((n - k) + 2.0e0 * sum) * (n - k)/ (n * nlpp21(k+1))
!
         kp = k0 + k
         sdrhoc(kp) = fplm
        if (nlpp12(k+1) .ge. 1) sdrhoc(kp) =&
     &      sqrt((n - k) + 2.0e0 * sum) * (n - k) / (n * nlpp12(k+1))
   30 continue
!
      return
      end
