!PPFNML
      real(kind=wp) function ppfnml(p)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS FUNCTION IS A VERSION OF DATAPAC SUBROUTINE
!     NORPPF, WITH MODIFICATIONS TO FACILITATE CONVERSION TO
!     DOUBLE PRECISION AUTOMATICALLY USING THE NAG, INC. CODE APT, AND
!     TO CORRESPOND TO STARPAC CONVENTIONS.
!
!     PURPOSE--THIS SUBROUTINE COMPUTES THE PERCENT POINT
!              FUNCTION VALUE FOR THE NORMAL (GAUSSIAN)
!              DISTRIBUTION WITH MEAN = 0 AND STANDARD DEVIATION = 1.
!              THIS DISTRIBUTION IS DEFINED FOR ALL X AND HAS
!              THE PROBABILITY DENSITY FUNCTION
!              F(X) = (1/SQRT(2*PI))*EXP(-X*X/2).
!              NOTE THAT THE PERCENT POINT FUNCTION OF A DISTRIBUTION
!              IS IDENTICALLY THE SAME AS THE INVERSE CUMULATIVE
!              DISTRIBUTION FUNCTION OF THE DISTRIBUTION.
!     ERROR CHECKING--NONE
!     RESTRICTIONS--P SHOULD BE BETWEEN 0.0E0 AND 1.0E0, EXCLUSIVELY.
!     REFERENCES--ODEH AND EVANS, THE PERCENTAGE POINTS
!                 OF THE NORMAL DISTRIBUTION, ALGORITHM 70,
!                 APPLIED STATISTICS, 1974, PAGES 96-97.
!               --EVANS, ALGORITHMS FOR MINIMAL DEGREE
!                 POLYNOMIAL AND RATIONAL APPROXIMATION,
!                 M. SC. THESIS, 1972, UNIVERSITY
!                 OF VICTORIA, B. C., CANADA.
!               --HASTINGS, APPROXIMATIONS FOR DIGITAL
!                 COMPUTERS, 1955, PAGES 113, 191, 192.
!               --NATIONAL BUREAU OF STANDARDS APPLIED MATHEMATICS
!                 SERIES 55, 1964, PAGE 933, FORMULA 26.2.23.
!               --FILLIBEN, SIMPLE AND ROBUST LINEAR ESTIMATION
!                 OF THE LOCATION PARAMETER OF A SYMMETRIC
!                 DISTRIBUTION (UNPUBLISHED PH.D. DISSERTATION,
!                 PRINCETON UNIVERSITY), 1969, PAGES 21-44, 229-231.
!               --FILLIBEN, "THE PERCENT POINT FUNCTION",
!                 (UNPUBLISHED MANUSCRIPT), 1970, PAGES 28-31.
!               --JOHNSON AND KOTZ, CONTINUOUS UNIVARIATE
!                 DISTRIBUTIONS--1, 1970, PAGES 40-111.
!               --THE KELLEY STATISTICAL TABLES, 1948.
!               --OWEN, HANDBOOK OF STATISTICAL TABLES,
!                 1962, PAGES 3-16.
!               --PEARSON AND HARTLEY, BIOMETRIKA TABLES
!                 FOR STATISTICIANS, VOLUME 1, 1954,
!                 PAGES 104-113.
!     COMMENTS--THE CODING AS PRESENTED BELOW
!               IS ESSENTIALLY IDENTICAL TO THAT
!               PRESENTED BY ODEH AND EVANS
!               AS ALGORITHM 70 OF APPLIED STATISTICS.
!               THE PRESENT AUTHOR HAS MODIFIED THE
!               ORIGINAL ODEH AND EVANS CODE WITH ONLY
!               MINOR STYLISTIC CHANGES.
!             --AS POINTED OUT BY ODEH AND EVANS
!               IN APPLIED STATISTICS,
!               THEIR ALGORITHM REPRESENTES A
!               SUBSTANTIAL IMPROVEMENT OVER THE
!               PREVIOUSLY EMPLOYED
!               HASTINGS APPROXIMATION FOR THE
!               NORMAL PERCENT POINT FUNCTION--
!               THE ACCURACY OF APPROXIMATION
!               BEING IMPROVED FROM 4.5*(10**-4)
!               TO 1.5*(10**-8).
!
!     WRITTEN BY--JAMES J. FILLIBEN
!                 STATISTICAL ENGINEERING LABORATORY
!                 NATIONAL BUREAU OF STANDARDS
!                 WASHINGTON, D. C. 20234
!     ORIGINAL VERSION--JUNE      1972.
!     UPDATED         --SEPTEMBER 1975.
!     UPDATED         --NOVEMBER  1975.
!     UPDATED         --OCTOBER   1976.
!
!     MODIFIED BY     --JANET R. DONALDSON, DECEMBER 7, 1981
!                       STATISTICAL ENGINEERING DIVISION
!                       NATIONAL BUREAU OF STANDARDS, BOULDER, COLORDAO
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     real(kind=wp) ::&
     &   p
!
!  LOCAL SCALARS
     real(kind=wp) ::&
     &   aden,anum,p0,p1,p2,p3,p4,q0,q1,q2,q3,q4,r,t
!
!  INTRINSIC FUNCTIONS
      intrinsic log,sqrt
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL(KIND=WP) :: ADEN, ANUM
!        *
!     REAL(KIND=WP) :: P
!        THE PROBABILITY AT WHICH THE PERCENT POINT IS TO BE EVALUATED
!     REAL(KIND=WP) :: P0, P1, P2, P3, P4
!        VARIOUS PARAMETERS USED IN THE APPROXIMATIONS.
!     REAL(KIND=WP) :: Q0, Q1, Q2, Q3, Q4
!        VARIOUS ADDITIONAL PARAMETERS USED IN THE APPROXIMATIONS.
!     REAL(KIND=WP) :: R
!        *
!     REAL(KIND=WP) :: T
!        *
!
     data p0, p1, p2, p3, p4&
    &  /-.322232431088_wp, -1.0_wp, -.342242088547_wp,&
     &   -.204231210245e-1,-.453642210148e-4/
     data q0, q1, q2, q3, q4&
    &  /.993484626060e-1, .588581570495_wp,&
     &   .531103462366_wp, .103537752850_wp, .38560700634e-2/
!
!
      if (p.ne.0.5e0) go to 30
      ppfnml = 0.0e0
      return
!
   30 r = p
      if (p.gt.0.5e0) r = 1.0e0 - r
      t = sqrt(-2.0e0*log(r))
      anum = ((((t*p4+p3)*t+p2)*t+p1)*t+p0)
      aden = ((((t*q4+q3)*t+q2)*t+q1)*t+q0)
      ppfnml = t + (anum/aden)
!
      if (p.lt.0.5e0) ppfnml = -ppfnml
!
      return
!
      end
