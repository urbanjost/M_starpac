!UFSDRV
      SUBROUTINE UFSDRV(Y, LY, YMISS, ACOV, NLPPA, SPCF, ISPCF, NF,
     &   FMIN, FMAX, FREQ, N, NW, LAGMAX, LAGS, WORK, LACOV, LWORK,
     &   DELTA, ISORT, ISYM, XAXIS, YAXIS, LPCV, ALPHA, NPRT, WINDOW,
     &   NMSUB, LDSMIN, LDSTAK, OPTION, LNLPPA, NFFT)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS IS THE CONTROLING ROUTINE FOR TIME SERIES FOURIER
!     SPECTRUM ANALYSIS .
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  DECEMBER 7, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) ::
     &   ALPHA,DELTA,FMAX,FMIN,YMISS
      INTEGER
     &   ISPCF,LACOV,LAGMAX,LDSMIN,LDSTAK,LNLPPA,LPCV,LWORK,LY,N,
     &   NF,NFFT,NPRT,NW
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   ACOV(*),FREQ(*),SPCF(*),WORK(*),XAXIS(*),Y(*),YAXIS(*)
      INTEGER
     &   ISORT(*),ISYM(*),LAGS(*),NLPPA(*)
      LOGICAL
     &   OPTION(4)
      CHARACTER
     &   NMSUB(6)*1
!
!  SUBROUTINE ARGUMENTS
      EXTERNAL WINDOW
!
!  SCALARS IN COMMON
      INTEGER
     &   IERR
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     &   ALOW,AUP,BW,DF,FMN,FMX,SPCFMN,SPCFMX,XPLTMN,XPLTMX,YMEAN,
     &   YPLTMN,YPLTMX
      INTEGER
     &   I,ILOG,ISPCER,LAG,LAGLST,NFUSED,NPTS,NSPC,NWUSED
      LOGICAL
     &   NEWPG,UNIVAR
!
!  EXTERNAL FUNCTIONS
      INTEGER
     &   LSTLAG
      EXTERNAL LSTLAG
!
!  EXTERNAL SUBROUTINES
      EXTERNAL ACVF,ACVFF,ACVFM,SETFRQ,SPCCK,UFSER,UFSLAG,UFSMN,UFSOUT,
     &   UFSPCV
!
!  INTRINSIC FUNCTIONS
      INTRINSIC MAX,MIN,NINT
!
!  COMMON BLOCKS
      COMMON /ERRCHK/IERR
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL ACOV(LACOV)
!        THE AUTOCOVARIANCE.
!     REAL ALOW
!        A FACTOR USED TO COMPUTE THE LOWER CONFIDENCE LIMITS.
!     REAL ALPHA
!        THE DESIRED CONFIDENCE LEVEL.
!     REAL AUP
!        A FACTOR USED TO COMPUTE THE UPPER CONFIDENCE LIMITS.
!     REAL BW
!        THE BANDWIDTH.
!     REAL DELTA
!        THE SAMPLING INTERVAL.
!     REAL DF
!        THE EFFECTIVE DEGREES OF FREEDOM.
!     REAL FMAX, FMIN
!        THE MAXIMUM AND MINIMUM FREQUENCES AT WHICH THE
!        SPECTRUM IS TO BE COMPUTED.
!     REAL FMN, FMX
!        THE MAXIMUM AND MINIMUM FREQUENCES ACTUALLY USED.
!     REAL FREQ(NF)
!        THE VECTOR OF FREQUENCIES AT WHICH THE SPECTRUM IS TO BE
!        COMPUTED.
!     INTEGER I
!        AN INDEX VARIABLE
!     INTEGER IERR
!        THE INTEGER VALUE RETURNED BY THIS ROUTINE DESIGNATING
!        WHETHER ANY ERRORS WERE DETECTED IN THE PARAMETER LIST
!        IF IERR .EQ. 0, NO ERRORS WERE DETECTED
!        IF IERR .EQ. 1, ERRORS HAVE BEEN DETECTED
!     INTEGER ILOG
!        A CODE USED TO SPECIFY THE TYPE OF PLOT, WHERE IF
!        ILOG = 0 THE PLOT IS LINEAR/LINEAR, IF
!        ILOG = 1 THE PLOT IS LOG/LINEAR, IF
!        ILOG = 2 THE PLOT IS LINEAR/LOG, AND IF
!        ILOG = 3 THE PLOT IS LOG/LOG.
!     INTEGER ISORT(NF)
!        THE VECTOR USED FOR SORTING.
!     INTEGER ISPCER
!        AN ERROR FLAG USED FOR THE SPECTRUM PLOTS.
!     INTEGER ISPCF
!         THE ACTUAL FIRST DIMENSION OF THE SPECTRUM ARRAYS.
!     INTEGER ISYM(LPCV)
!        THE ARRAY CONTAINING THE CODE FOR THE PLOT SYMBOLS.
!     INTEGER LACOV
!        THE LENGTH OF THE VECTOR ACOV.
!     INTEGER LAG
!        THE LAG WINDWO TRUNCATION POINT USED FOR A SPECIFIC WINDOW.
!     INTEGER LAGLST
!        THE LAST LAG BEFORE MISSING DATA CAUSED AN ACVF
!        TO BE UNABLE TO BE COMPUTED.
!     INTEGER LAGMAX
!        THE MAXIMUM LAG VALUE TO BE USED.
!     INTEGER LAGS(NW)
!        THE ARRAY USED TO STORE THE LAG WINDOW TRUCCATION
!        POINTS USED FOR EACH SET OF SPECTRUM VALUES.
!     INTEGER LDSMIN
!        THE MINIMUM LENGTH ALLOWED FOR DSTAK.
!     INTEGER LDSTAK
!        THE LENGTH OF THE VECTOR DSTAK IN COMMON CSTAK.
!     INTEGER LNLPPA
!        THE LENGTH OF THE VECTOR NLPPA.
!     INTEGER LPCV
!        THE LENGTH OF THE VECTORS USED FOR PLOTTING.
!     INTEGER LWORK
!        THE LENGTH OF THE VECTOR W.
!     INTEGER LY
!        THE LENGTH OF THE VECTOR Y.
!     INTEGER N
!        THE INTEGER NUMBER OF OBSERVATIONS IN EACH SERIES
!     LOGICAL NEWPG
!        THE LOGICAL VARIABLE USED TO DETERMINE IF OUTPUT
!        WILL BEGIN ON A NEW PAGE (TRUE) OR NOT (FALSE).
!     INTEGER NF
!        THE NUMBER OF FREQUENCIES AT WHICH THE SPECTRUM IS
!        TO BE COMPUTED.
!     INTEGER NFFT
!        THE NUMBER OF OBSERVATIONS IN THE EXTENDED SERIES.
!     INTEGER NFUSED
!        THE NUMBER OF FREQUENCIES ACTUALLY USED.
!     INTEGER NLPPA(LNLPPA)
!        THE ARRAY CONTAINING THE NUMBER OF LAG PRODUCT PAIRS.
!     CHARACTER*1 NMSUB(6)
!        THE ARRAY CONTAINING THE NAME OF THIS SUBROUTINE.
!     INTEGER NPRT
!        A CODE USED TO SPECIFY THE TYPE OF PLOT, WHERE IF
!        NPRT < 0 THE PLOT IS DECIBLES/LINEAR
!        NPRT = 0 THE PLOT IS SUPPRESSED
!        NPRT > 0 THE PLOT IS LOG/LINEAR
!     INTEGER NPTS
!        THE NUMBER OF X, Y CO-ORDINATES TO BE PLOTTED.
!     INTEGER NSPC
!        THE NUMBER OF VALID (POSITIVE) SPECTRUM VALUES.
!     INTEGER NW
!        THE VARIABLE USED TO DETERMINE THE NUMBER OF DIFFERENT
!        BANDWIDTHS TO BE USED.
!     INTEGER NWUSED
!        THE NUMBER OF DIFFERENT BANDWIDTHS ACTUALLY USED.
!     LOGICAL OPTION(4)
!        AN INDICATOR ARRAY USED TO DESIGNATE WHETHER ANY OF THE
!        FOUR POSSIBLE OPTIONS (F, M, V, OR S) HAVE BEEN USED (TRUE)
!        OR NOT (FALSE).
!     REAL SPCF(ISPCF,NW)
!        THE ARRAYS IN WHICH THE SPECTRUM IS STORED.
!     REAL SPCFMN, SPCFMX
!        THE MINIMUM AND MAXIMUM SPECTRUM VALUE TO BE PLOTTED.
!     LOGICAL UNIVAR
!        THE LOGICAL VARIABLE USED TO DETERMINE IF THE OUTPUT
!        IS FOR UNIVARIATE (TRUE) OR BIVARIATE (FALSE) SPECTRA.
!     EXTERNAL WINDOW
!        THE SUBROUTINE USED TO COMPUTE THE WINDOW.
!     REAL WORK(LWORK)
!        THE VECTOR OF LAG WINDOWS.
!     REAL XAXIS(LPCV)
!        THE X AXIS VALUES FOR THE SPECTRUM PLOT.
!     REAL XPLTMN, XPLTMX
!        THE MINIMUM AND MAXIMUM VALUES TO BE PLOTTED FOR THE X AXIS.
!     REAL Y(LY)
!        THE ARRAY CONTAINING THE OBSERVED TIME SERIES.
!     REAL YAXIS(LPCV)
!        THE Y AXIS VALUES FOR THE SPECTRUM PLOTS.
!     REAL YMEAN
!        THE MEAN OF THE OBSERVED TIME SERIES
!     REAL YMISS
!        THE USER SUPPLIED CODE WHICH IS USED TO DETERMINE WHETHER OR
!        NOT AN OBSERVATION IN THE SERIES IS MISSING.  IF Y(I) = YMISS,
!        THE VALUE IS ASSUMED MISSING, OTHERWISE IT IS NOT.
!     REAL YPLTMN, YPLTMX
!        THE MINIMUM AND MAXIMUM VALUES TO BE PLOTTED FOR THE Y AXIS.
!
      NFUSED = NF
      IF (OPTION(4)) THEN
        FMN = MAX(FMIN, 0.0E0)
        FMX = MIN(FMAX, 0.5E0)
        IF (FMN.GE.FMX) THEN
          FMN = 0.0E0
          FMX = 0.5E0
        END IF
      ELSE
!
!       SET VARIOUS VALUES FOR SHORT FORMS OF CALL STATEMENT
!
        NPRT = -1
        FMN = 0.0E0
        FMX = 0.5E0
      END IF
!
!     CHECK FOR ERRORS
!
      CALL UFSER(NMSUB, N, LAGMAX, LACOV, NFUSED, ISPCF, NW, LAGS,
     &  LDSTAK, LDSMIN, LY, NFFT, OPTION)
!
      IF (IERR.EQ.1) RETURN
!
!     SET VARIOUS PROGRAM PARAMETERS.
!
      ALPHA = 0.95E0
      DELTA = 1.0E0
!
!     COMPUTE COVARIANCES
!
      LAGLST = LAGMAX
      IF (OPTION(1)) THEN
        CALL ACVFF(Y, N, NFFT, YMEAN, ACOV, LAGMAX, LACOV,
     &   LY, WORK, NFFT)
      ELSE
        IF (.NOT.OPTION(3)) THEN
          IF (OPTION(2)) THEN
            CALL ACVFM(Y, YMISS, N, YMEAN, ACOV, LAGMAX, LAGLST,
     &        NLPPA, LACOV)
          ELSE
            CALL ACVF(Y, N, YMEAN, ACOV, LAGMAX, LACOV)
          END IF
        END IF
      END IF
      IF (OPTION(2) .AND. OPTION(3)) LAGLST = LSTLAG(NLPPA,LAGMAX,LACOV)
!
      IF (LAGLST.GE.1) GO TO 20
!
!     AN ERROR HAS BEEN DETECTED
!
      IERR = 2
      RETURN
!
   20 CONTINUE
!
!     COMPUTE THE VECTOR OF LAG WINDOW TRUNCATION POINTS, ORDERED
!     SMALLEST TO LARGEST.
!
      NWUSED = NW
      IF (.NOT.OPTION(4)) CALL UFSLAG(ACOV, LAGLST, LAGS, N, NW,
     &   NWUSED, LACOV)
!
!     BEGIN COMPUTING FOURIER SPECTRUM FOR SERIES
!
      UNIVAR = .TRUE.
!
      IF (NPRT.GE.1) THEN
        ILOG = 1
      ELSE
        ILOG = 0
      END IF
!
      XPLTMN = FMN
      XPLTMX = FMX
!
!     SET FREQUENCIES FOR THE SPECTRUM.
!
      CALL SETFRQ(FREQ, NFUSED, 2, FMN, FMX, DELTA)
!
!     COMPUTE AND PLOT SPECTRUM VALUES.
!
      NEWPG = .FALSE.
!
      DO 50 I=1,NWUSED
         LAG = LAGS(I)
         ISPCER = 0
         IF (LAG.LE.LAGLST) GO TO 30
         ISPCER = 2
         DF = 0.0E0
         GO TO 40
!
   30    CALL UFSMN(ACOV, NLPPA, LAG, DF, NFUSED, FREQ, ALPHA, BW,
     &              SPCF(1+(I-1)*ISPCF), ALOW, AUP, LACOV, ISPCF,
     &              WINDOW, WORK, LAG, N, DELTA, OPTION(2), LNLPPA)
!
         IF (NPRT.EQ.0) GO TO 50
!
         ISPCER = 0
         CALL SPCCK(SPCF(1+(I-1)*ISPCF), ISORT, NFUSED,
     &              SPCFMN, SPCFMX, NSPC, ISPCER)
!
         IF (ISPCER.NE.0) GO TO 40
!
         CALL UFSPCV(SPCF(1+(I-1)*ISPCF), SPCFMN, SPCFMX,
     &               FREQ, NFUSED, XAXIS, YAXIS, ISYM, NPTS, ISPCF,
     &               NFUSED+5, NSPC, BW, ALOW, AUP,
     &               XPLTMN, XPLTMX, YPLTMN, YPLTMX, NPRT)
!
   40    CALL UFSOUT(XAXIS, YAXIS, ISYM, NPTS, BW, NINT(DF), LAG,
     &      LAGLST, NEWPG, ISPCER, NFUSED+5, XPLTMN, XPLTMX, YPLTMN,
     &      YPLTMX, ILOG, YAXIS, XAXIS, NPTS, UNIVAR, NMSUB)
!
         NEWPG = .TRUE.
!
   50 CONTINUE
!
      RETURN
!
      END
