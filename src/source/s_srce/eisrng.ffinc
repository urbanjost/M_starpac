!EISRNG
      SUBROUTINE EISRNG (NMSUB, ISEED, ISEEDU, HEAD)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE CHECKS WHETHER THE INPUT VARIABLE ISEED IS
!     WITHIN [0, 2**MDIG], AND, IF NONZERO, IS ODD.
!
!     IF ISEED IS WITHIN [0, 2**MDIG] THEN
!        ISEEDU = ISEED-MOD(ISEED,2)+1
!     ELSE
!        ISEEDU = MIN[ ABS(ISEED)-MOD(ABS(ISEED),2)+1, 2**(MDIG-1)-1]
!                 AND AN ERROR MESSAGE IS PRINTED.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    CENTER FOR COMPUTING AND APPLIED MATHEMATICS
!                    NATIONAL INSTITUTE OF STANDARDS AND TECHNOLOGY
!                    BOULDER, COLORADO
!
!     CREATION DATE  -  JANUARY 17, 1990
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      INTEGER
     +   ISEED,ISEEDU
      LOGICAL
     +   HEAD
!
!  ARRAY ARGUMENTS
      CHARACTER
     +   NMSUB(6)*1
!
!  LOCAL SCALARS
      INTEGER
     +   IPRT,MDIG
!
!  EXTERNAL FUNCTIONS
      INTEGER
     +   I1MACH
      EXTERNAL I1MACH
!
!  EXTERNAL SUBROUTINES
      EXTERNAL EHDR,IPRINT
!
!  INTRINSIC FUNCTIONS
      INTRINSIC ABS,MIN,MOD
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     LOGICAL HEAD
!        A FLAG INDICATING WHETHER THE HEADING SHOULD BE PRINTED
!        (TRUE) OR NOT (FALSE).  IF A HEADING IS PRINTED, THE VALUE
!        OF HEAD WILL BE CHANGED TO FALSE.
!     INTEGER IPRT
!        THE UNIT NUMBER FOR PRINTED OUTPUT.
!     INTEGER ISEED
!        THE VALUE OF THE SEED BEING TESTED.
!     INTEGER ISEEDU
!        THE VALUE OF THE SEED ACTUALLY USED BY NRAND AND NRANDC.
!     INTEGER MDIG
!        A LOWER BOUND ON THE NUMBER OF BINARY DIGITS AVAILABLE
!        FOR REPRESENTING INTEGERS, INCLUDING THE SIGN BIT.
!     CHARACTER*1 NMSUB(6)
!        THE CHARACTERS OF THE CALLING SUBROUTINES NAME.
!
      MDIG = MIN(I1MACH(8)+1,32)
!
!     CHECK FOR VIOLATIONS
!
      IF ((ISEED.EQ.0) .OR.
     +    ((ISEED.GE.1) .AND.
     +     (ISEED.LE.2**(MDIG-1)-1) .AND.
     +     (MOD(ISEED,2).EQ.1))) THEN
!
!     SUPPLIED SEED WILL BE USED
!
         ISEEDU = ISEED
      ELSE
!
!     VIOLATIONS FOUND
!
         ISEEDU = MIN( ABS(ISEED)+MOD(ABS(ISEED),2)-1, 2**(MDIG-1)-1)
         CALL IPRINT(IPRT)
         CALL EHDR(NMSUB, HEAD)
         WRITE (IPRT, 1010) MDIG-1,ISEEDU
      END IF
!
      RETURN
!
!     FORMAT STATEMENTS
!
 1010 FORMAT(/
     +   ' THE VALUE OF ISEED MUST BE BETWEEN 0 AND 2**',I2,' - 1,'/
     +   ' INCLUSIVE, AND, IF ISEED IS NOT 0, ISEED MUST BE ODD.  THE'/
     +   ' SEED ACTUALLY USED BY THE RANDOM NUMBER GENERATOR HAS BEEN'/
     +   ' SET TO', I10,'.')
!
      END
