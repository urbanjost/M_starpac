!PARZEN
      subroutine parzen (lag, w, lw)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS SUBROUTINE COMPUTES AND STORES THE PARZEN LAG WINDOW
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  DECEMBER 7, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     integer&
     &   lag,lw
!
!  ARRAY ARGUMENTS
     real(kind=wp) ::&
     &   w(lw)
!
!  LOCAL SCALARS
     integer&
     &   k,l
!
!  INTRINSIC FUNCTIONS
      intrinsic real
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     INTEGER K
!        AN INDEX VARIABLE
!     INTEGER L
!        THE VALUE LAG/2.
!     INTEGER LAG
!        THE LAG WINDOW TRUNCATION POINT USED FOR A SPECIFIC WINDOW.
!     INTEGER LW
!        THE LENGTH OF THE VECTOR W.
!     REAL(KIND=WP) :: W(LW)
!        THE VECTOR OF LAG WINDOWS.
!
      l = lag/2
      w(1) = 1.0e0
      if (l.le.0) go to 15
      do 10 k = 1, l
         w(k+1) = real(k) / real(lag)
         w(k+1) = 1.0e0 + 6.0e0 * w(k+1) * w(k+1) * (w(k+1) - 1.0e0)
   10 continue
!
   15 continue
      l = l + 1
      do 20 k = l, lag
         w(k+1) = 1.0e0 - real(k) / real(lag)
         w(k+1) = 2.0e0 * w(k+1) * w(k+1) * w(k+1)
   20 continue
!
      return
      end
