!EISGE
      SUBROUTINE EISGE(NMSUB, NMVAR1, NVAL, NMIN, MSGTYP, HEAD, ERROR,
     &   NMVAR2)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE CHECKS WHETHER THE VALUE   NVAL   IS GREATER THAN
!     OR EQUAL TO   NMIN   AND PRINTS A DIAGNOSTIC IF IT IS NOT.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  DECEMBER 29, 1982
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      INTEGER
     &   MSGTYP,NMIN,NVAL
      LOGICAL
     &   ERROR,HEAD
!
!  ARRAY ARGUMENTS
      CHARACTER
     &   NMSUB(6)*1,NMVAR1(8)*1,NMVAR2(8)*1
!
!  LOCAL SCALARS
      INTEGER
     &   I,IPRT
!
!  EXTERNAL SUBROUTINES
!      EXTERNAL EHDR,IPRINT
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     LOGICAL ERROR
!        VALUE(S) INDICATING WHETHER AN ERROR WAS DETECTED (TRUE) OR NOT
!        (FALSE).
!     LOGICAL HEAD
!        A FLAG INDICATING WHETHER THE HEADING SHOULD BE PRINTED
!        (TRUE) OR NOT (FALSE).  IF A HEADING IS PRINTED, THE VALUE
!        OF HEAD WILL BE CHANGED TO FALSE.
!     INTEGER I
!        AN INDEX ARGUMENT.
!     INTEGER IPRT
!        THE UNIT NUMBER FOR PRINTED OUTPUT.
!     INTEGER MSGTYP
!        AN ARGUMENT USED TO INDICATE THE TYPE OF MESSAGE TO BE
!        PRINTED, WHERE IF ERROR IS TRUE AND
!        MSGTYP = 1 THE INPUT VALUE WAS TOO SMALL BASED
!                   ON LIMITS IMPOSED BY STARPAC
!        MSGTYP = 2 THE INPUT VALUE WAS TOO SMALL BASED ON OTHER INPUT
!                   ARGUMENTS.
!        MSGTYP = 3 THE INPUT VALUE WAS TOO SMALL BASED ON OTHER INPUT
!                   ARGUMENTS, WHERE THE VALUE INDICATES THE FIRST
!                   DIMENSION OF A DIMENSIONED ARRAY
!                   N.B.  IT IS ASSUMED THAT THE DIMENSION NAME IS THE
!                         ARRAY NAME PRECEDED BY THE LETTER I.  IF THE
!                         ARRAY NAME IS 6 LETTERS, THE DIMENSION NAME
!                         SHOULD OMIT THE LAST LETTER.  THE DIMENSION
!                         NAME WILL BE PRINTED USING (NMVAR(I),I=1,6),
!                         AND THE ARRAY NAME USING (NMVAR(I),I=2,7).
!        MSGTYP = 4 THE INPUT VALUE WAS TOO SMALL BASED ON OTHER INPUT
!                   ARGUMENTS, WHERE THE VALUE INDICATES THE SECOND
!                   DIMENSION OF A DIMENSIONED ARRAY
!                   N.B.  IT IS ASSUMED THAT THE DIMENSION NAME IS THE
!                         ARRAY NAME PRECEDED BY THE LETTER J.  IF THE
!                         ARRAY NAME IS 6 LETTERS, THE DIMENSION NAME
!                         SHOULD OMIT THE LAST LETTER.  THE DIMENSION
!                         NAME WILL BE PRINTED USING (NMVAR(I),I=1,6),
!                         AND THE ARRAY NAME USING (NMVAR(I),I=2,7).
!        MSGTYP = 5 THE ARGUMENT BEING CHECKED IS LDSTAK.
!                   NO LONGER USED.
!        MSGTYP = 6 THE ARGUMENT INDICATES THE FIRST DIMENSION OF
!                   AN ARRAY BEING CHECKED AGAINST THE NUMBER OF
!                   UNFIXED PARAMETERS.
!        MSGTYP = 7 THE INPUT VALUE WAS TOO SMALL BASED ON OTHER INPUT
!                   ARGUMENTS, WHERE THE VALUE INDICATES THE
!                   DIMENSION OF A VECTOR.
!                   N.B.  IT IS ASSUMED THAT THE DIMENSION NAME IS THE
!                         ARRAY NAME PRECEDED BY THE LETTER L.  IF THE
!                         ARRAY NAME IS 6 LETTERS, THE DIMENSION NAME
!                         SHOULD OMIT THE LAST LETTER.  THE DIMENSION
!                         NAME WILL BE PRINTED USING (NMVAR(I),I=1,6),
!                         AND THE ARRAY NAME USING (NMVAR(I),I=2,7).
!        MSGTYP = 8 THE INPUT VALUE WAS TOO SMALL BASED ON OTHER INPUT
!                   ARGUMENTS, WHERE THE VALUE INDICATES THE
!                   DIMENSION OF THE VECTORS ACOV AND NLPPA.
!        MSGTYP = 9 THE INPUT VALUE WAS TOO SMALL BASED ON LIMITS
!                   IMPOSED BY STARPAC, WHERE THE VALUE INDICATES THE
!                   DIMENSION OF A VECTOR.
!                   N.B.  IT IS ASSUMED THAT THE DIMENSION NAME IS THE
!                         ARRAY NAME PRECEDED BY THE LETTER L.  IF THE
!                         ARRAY NAME IS 6 LETTERS, THE DIMENSION NAME
!                         SHOULD OMIT THE LAST LETTER.  THE DIMENSION
!                         NAME WILL BE PRINTED USING (NMVAR(I),I=1,6),
!                         AND THE ARRAY NAME USING (NMVAR(I),I=2,7).
!     INTEGER NMIN
!        THE MINIMUM ACCEPTABLE VALUE FOR THE ARGUMENT BEING TESTED.
!     CHARACTER*1 NMSUB(6)
!        THE CHARACTERS OF THE CALLING SUBROUTINES NAME.
!     CHARACTER*1 NMVAR1(8)
!        THE CHARACTERS OF THE NAME OF THE ARGUMENT BEING CHECKED.
!     CHARACTER*1 NMVAR2(8)
!        THE CHARACTERS OF THE NAME OF THE ARGUMENT BEING CHECKED
!        AGAINST.
!     INTEGER NVAL
!        THE INPUT VALUE OF THE ARGUMENT BEING CHECKED.
!
      ERROR = .FALSE.
!
      IF (NVAL .GE. NMIN) RETURN
!
      ERROR = .TRUE.
!
      CALL IPRINT (IPRT)
!
      CALL EHDR(NMSUB, HEAD)
!
      WRITE (IPRT, 1000) (NMVAR1(I), I=1,6), NVAL
!
      GO TO (20, 30, 40, 50, 60, 70, 80, 90, 100), MSGTYP
!
!     PRINT MESSAGE FOR VALUE TOO SMALL BASED ON LIMITS IMPOSED
!     BY STARPAC.
!
   20 WRITE (IPRT, 1010) (NMVAR1(I), I=1,6), NMIN
      RETURN
!
!     PRINT MESSAGE FOR VALUE TOO SMALL BASED ON OTHER INPUT
!     ARGUMENTS.
!
   30 WRITE (IPRT, 1020) (NMVAR1(I), I=1,6), (NMVAR2(I), I=1,8)
      RETURN
!
!     PRINT MESSAGE FOR VALUE TOO SMALL, WHERE VALUE INDICATED THE
!     FIRST DIMENSION OF A DIMENSIONED ARRAY.
!
   40 WRITE (IPRT, 1030) (NMVAR1(I), I=2,7), (NMVAR1(I), I=1,6),
     &   (NMVAR2(I), I=1,8)
      RETURN
!
!     PRINT MESSAGE FOR VALUE TOO SMALL, WHERE VALUE INDICATED THE
!     SECOND DIMENSION OF A DIMENSIONED ARRAY.
!
   50 WRITE (IPRT, 1040) (NMVAR1(I), I=2,7), (NMVAR1(I), I=1,6),
     &   (NMVAR2(I), I=1,8)
      RETURN
!
!     PRINT MESSAGE FOR VALUE TOO SMALL, WHEN ARGUMENT IS LDSTAK.
!
   60 WRITE(IPRT, 1050) NMIN
      RETURN
!
!     PRINT MESSAGE FOR VALUE TOO SMALL, WHERE VALUE INDICATED THE
!     FIRST DIMENSION OF A DIMENSIONED ARRAY CHECK AGAINST THE NUMBER OF
!     UNFIXED PARAMETERS.
!
   70 WRITE (IPRT, 1060) (NMVAR1(I), I=2,7), (NMVAR1(I), I=1,6)
      RETURN
!
!     PRINT MESSAGE FOR VALUE TOO SMALL, WHERE VALUE INDICATED THE
!     DIMENSION OF A VECTOR.
!
   80 WRITE (IPRT, 1070) (NMVAR1(I), I=2,7), (NMVAR1(I), I=1,6),
     &   (NMVAR2(I), I=1,8)
      RETURN
!
!     PRINT MESSAGE FOR VALUE TOO SMALL, WHERE VALUE INDICATED THE
!     DIMENSION OF THE VECTORS ACOV AND NLPPA.
!
   90 WRITE (IPRT, 1080) (NMVAR1(I), I=1,6), (NMVAR2(I), I=1,8)
      RETURN
!
!     PRINT MESSAGE FOR VALUE TOO SMALL, WHERE VALUE INDICATED THE
!     DIMENSION OF A VECTOR.
!
  100 WRITE (IPRT, 1090) (NMVAR1(I), I=2,7), (NMVAR1(I), I=1,6),
     &   NMIN
      RETURN
!
!     FORMAT STATEMENTS
!
 1000 FORMAT (/20H THE INPUT VALUE OF , 6A1, 4H IS , I5, '.')
 1010 FORMAT(
     &   27H THE VALUE OF THE ARGUMENT , 6A1,
     &   34H MUST BE GREATER THAN OR EQUAL TO , I5, '.')
 1020 FORMAT(
     &   27H THE VALUE OF THE ARGUMENT , 6A1,
     &   34H MUST BE GREATER THAN OR EQUAL TO , 8A1, '.')
 1030 FORMAT(
     &   24H THE FIRST DIMENSION OF , 6A1,
     &   30H, AS INDICATED BY THE ARGUMENT/
     &    1X, 6A1, 35H, MUST BE GREATER THAN OR EQUAL TO , 8A1, '.')
 1040 FORMAT(
     &   25H THE SECOND DIMENSION OF , 6A1,
     &   30H, AS INDICATED BY THE ARGUMENT/
     &    1X, 6A1, 35H, MUST BE GREATER THAN OR EQUAL TO , 8A1, '.')
 1050 FORMAT(
     &   55H THE DIMENSION OF THE DOUBLE PRECISION VECTOR DSTAK, AS,
     &   13H INDICATED BY/
     &   54H THE ARGUMENT LDSTAK, MUST BE GREATER THAN OR EQUAL TO,
     &   I5, '.')
 1060 FORMAT(
     &   24H THE FIRST DIMENSION OF , 6A1,
     &   30H, AS INDICATED BY THE ARGUMENT/
     &    1X, 6A1, 34H, MUST BE GREATER THAN OR EQUAL TO,
     &   34H THE NUMBER OF UNFIXED PARAMETERS.)
 1070 FORMAT(
     &   15H THE LENGTH OF , 6A1,
     &   30H, AS INDICATED BY THE ARGUMENT/
     &    1X, 6A1, 35H, MUST BE GREATER THAN OR EQUAL TO , 8A1, '.')
 1080 FORMAT(
     &   29H THE LENGTH OF ACOV AND NLPPA,
     &   30H, AS INDICATED BY THE ARGUMENT/
     &    1X, 6A1, 35H, MUST BE GREATER THAN OR EQUAL TO , 8A1, '.')
 1090 FORMAT(
     &   15H THE LENGTH OF , 6A1,
     &   30H, AS INDICATED BY THE ARGUMENT/
     &    1X, 6A1, 35H, MUST BE GREATER THAN OR EQUAL TO , I6, '.')
!
      END
