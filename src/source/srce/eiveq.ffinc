!EIVEQ
      SUBROUTINE EIVEQ (NMSUB, NMVAR1, IVEC, N, IVAL, NEQMN, HEAD, NEQ,
     &                  NNE, MSGTYP, ERROR, NMVAR2, NMVAR3)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE CHECKS WHETHER THE NUMBER OF ELEMENTS OF IVEC EQUAL
!     TO IVAL IS AT LEAST NEQMN.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  AUGUST 3, 1987
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      INTEGER
     &   IVAL,MSGTYP,N,NEQ,NEQMN,NNE
      LOGICAL
     &   ERROR,HEAD
!
!  ARRAY ARGUMENTS
      INTEGER
     &   IVEC(*)
      CHARACTER
     &   NMSUB(6)*1,NMVAR1(8)*1,NMVAR2(8)*1,NMVAR3(8)*1
!
!  LOCAL SCALARS
      INTEGER
     &   I,IPRT
!
!  EXTERNAL SUBROUTINES
!      EXTERNAL EHDR,IPRINT
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     LOGICAL ERROR
!        VALUE(S) INDICATING WHETHER AN ERROR WAS DETECTED (TRUE) OR NOT
!        (FALSE).
!     LOGICAL HEAD
!        A FLAG INDICATING WHETHER THE HEADING SHOULD BE PRINTED
!        (TRUE) OR NOT (FALSE).  IF A HEADING IS PRINTED, THE VALUE
!        OF HEAD WILL BE CHANGED TO FALSE.
!     INTEGER I
!        AN INDEX ARGUMENT.
!     INTEGER IPRT
!        THE UNIT NUMBER FOR PRINTED OUTPUT.
!     INTEGER IVEC(N)
!        THE VECTOR BEING CHECKED.
!     INTEGER MSGTYP
!        AN ARGUMENT USED TO INDICATE THE TYPE OF MESSAGE TO BE
!        PRINTED, WHERE IF ERROR IS TRUE AND
!        MSGTYP = 1, THE INPUT VALUE WAS TOO SMALL BASED ON LIMITS
!                    IMPOSED BY STARPAC.
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS.
!     CHARACTER*1 NMSUB(6)
!        THE CHARACTERS OF THE CALLING SUBROUTINES NAME.
!     CHARACTER*1 NMVAR1(8)
!        THE CHARACTERS OF THE NAME OF THE ARGUMENT BEING CHECKED.
!     CHARACTER*1 NMVAR2(8)
!        THE CHARACTERS OF THE NAME OF THE ARGUMENT BEING CHECKED
!        AGAINST.
!     CHARACTER*1 NMVAR3(8)
!        THE CHARACTERS OF THE NAME OF THE ARGUMENT THAT THE ELEMENTS
!        MUST BE EQUAL TO.
!     INTEGER NEQ
!        THE NUMBER OF ELEMENTS EQUAL TO IVAL.
!     INTEGER NEQMN
!        THE MINIMUM NUMBER OF ELEMENTS EQUAL TO IVAL WHICH IS OK.
!     INTEGER NNE
!        THE NUMBER OF ELEMENTS NOT EQUAL TO IVAL.
!
      ERROR = .FALSE.
!
      IF (N.LE.0) RETURN
!
!     CHECK FOR VALUES EQUAL TO IVAL
!
      NEQ = 0
      DO 10 I = 1, N
         IF (IVEC(I) .EQ. IVAL) NEQ = NEQ + 1
   10 CONTINUE
!
      NNE = N - NEQ
      IF (NEQ .GE. NEQMN) RETURN
!
!     INSUFFICIENT NUMBER OF ELEMENTS EQUAL TO IVAL.
!
      ERROR = .TRUE.
!
      CALL IPRINT(IPRT)
!
      CALL EHDR(NMSUB, HEAD)
!
      IF (MSGTYP.EQ.1) WRITE(IPRT, 1000)
     &   (NMVAR1(I),I=1,8), (NMVAR2(I),I=1,8), NEQ,
     &   (NMVAR2(I),I=1,8), (NMVAR3(I),I=1,8)
!
      RETURN
!
!     FORMAT STATEMENTS
!
 1000 FORMAT(
     &   ' THE NUMBER OF ELEMENTS IN ', 8A1,
     &   ' EQUAL TO ', 8A1, ' IS ', I6, '.'/
     &   ' THE NUMBER OF ELEMENTS EQUAL TO ', 8A1,
     &   ' MUST BE GREATER THAN OR EQUAL TO ', 8A1, '.')
!
      END
