!SETESL
      SUBROUTINE SETESL(N, NDIV, NFFT)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE COMPUTES THE SMALLEST VALUE OF NFFT WHICH
!     EQUALS OR EXCEEDS N + 2, SUCH THAT NFFT - 2 IS
!     1. DIVISIBLE BY NDIV,
!     2. HAS NO MORE THAN 11 PRIME FACTORS,
!     3. HAS NO PRIME FACTOR GREATER THAN 23, AND
!     4. THE PRODUCT OF THE SQUARE FREE PRIME FACTORS OF
!        (NFFT-2)/NDIV DO NOT EXCEED 210 IF NDIV = 2, AND
!                                    105 IF NDIV = 4.
!     THE VALUE OF NFFT THUS MEET THE REQUIREMENTS OF
!     THE EXTENDED LENGTH OF THE SERIES REQUIRED FOR ANY ROUTINE
!     USING THE SINGLETON FFT PROVIDING THE PROPER VALUE OF NDIV
!     IS CHOSEN.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  DECEMBER 7, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      INTEGER
     &   N,NDIV,NFFT
!
!  LOCAL SCALARS
      INTEGER
     &   I,NPF,NSFP
!
!  LOCAL ARRAYS
      INTEGER
     &   IPF(50),IPFEXP(50)
!
!  EXTERNAL SUBROUTINES
      EXTERNAL FACTOR
!
!  INTRINSIC FUNCTIONS
      INTRINSIC MOD
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     INTEGER I
!       AN INDEX VARIABLE.
!     INTEGER IPF(50), IPFEXP(50)
!        THE VECTORS OF PRIME FACTORS OF NFFT AND THEIR EXPONENTS,
!        RESPECTIVELY, WHERE THE LENGTH OF THESE VECTORS IS
!        SUFFICIENT TO ACCOMODATE THE PRIME FACTORS OF AN INTEGER
!        UP TO 2 ** 128 (APPROXIMATELY 10 ** 40).
!     INTEGER N
!        THE NUMBER UPON WHICH NFFT IS BASED.
!     INTEGER NDIV
!        A REQUIRED FACTOR OF NFFT - 2.
!     INTEGER NFFT
!        THE RETURNED VALUE WHICH MEETS THE ABOVE DESCRIPTION.
!     INTEGER NPF
!        THE NUMBER OF PRIME FACTORS IN NFFT.
!     INTEGER NSFP
!        THE PRODUCT OF THE NON SQUARE FACTORS.
!
      NFFT = N
      IF (NFFT.LE.0) RETURN
      IF (MOD(NFFT, NDIV) .NE. 0) NFFT = NFFT + NDIV - MOD(NFFT, NDIV)
      NFFT = NFFT - NDIV
   20 NFFT = NFFT + NDIV
      CALL FACTOR(NFFT/NDIV, NPF, IPF, IPFEXP)
      IF ((NPF.GE.11) .OR. (IPF(NPF).GT.23)) GO TO 20
      NSFP = 1
      IF (NDIV.EQ.4) NSFP = 2
      DO 30 I = 1, NPF
         IF (MOD(IPFEXP(I), 2).EQ.1) NSFP = NSFP * IPF(I)
   30 CONTINUE
      IF (NSFP .GE. 210) GO TO 20
!
      NFFT = NFFT + 2
!
      RETURN
!
      END
