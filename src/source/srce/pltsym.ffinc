!PLTSYM
      SUBROUTINE PLTSYM(IPTSYM, I, J, ISYM, N, IPOINT, LINE, ICOUNT)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE SUPPLIES THE APPROPRIATE PLOT SYMBOL FOR
!     THE PLOT LINE.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  JANUARY 21, 1982
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      INTEGER
     &   I,IPOINT,IPTSYM,J,N
!
!  ARRAY ARGUMENTS
      INTEGER
     &   ICOUNT(103),ISYM(N)
      CHARACTER
     &   LINE(103)*1
!
!  LOCAL SCALARS
      INTEGER
     &   ISYMBL
!
!  LOCAL ARRAYS
      CHARACTER
     &   SYM(30)*1,SYM1(10)*1
!
!  INTRINSIC FUNCTIONS
      INTRINSIC MAX,MIN
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     INTEGER I
!        AN INDEXING VARIABLE.
!     INTEGER ICOUNT(103)
!        THE NUMBER OF PLOT SYMBOLS AT EACH LOCATION.
!     INTEGER IPOINT
!        THE LOCATION IN THE PLOT STRING OF THE VALUE BEING PLOTTED.
!     INTEGER IPTSYM
!        AN INDICATOR VARIABLE USED TO DESIGNATE THE TYPE
!        OF PLOT.  IF IPTSYM = 1, THE PLOT IS A SYMPLE PAGE
!        OR VERTICAL PLOT.  IF IPTSYM = 2, THE PLOT IS A SYMBOL
!        PLOT.  IF IPTSYM = 3, THE PLOT IS A MULTIVARIATE PLOT.
!     INTEGER ISYM(N)
!        VECTOR CONTAINING SYMBOL DESIGNATIONS FOR PLOTTING
!     INTEGER ISYMBL
!        THE INDEX OF THE PLOT SYMBOL TO BE USED.
!     INTEGER J
!        AN INDEX VARIABLE.
!     CHARACTER*1 LINE(103)
!        THE VECTOR USED FOR THE PLOT STRING.
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS.
!     CHARACTER*1 SYM(30), SYM1(10)
!        THE PLOT SYMBOLS.
!
      DATA SYM( 1)/'+'/,SYM( 2)/'.'/,SYM( 3)/'*'/,SYM( 4)/'-'/,
     &     SYM( 5)/'A'/,SYM( 6)/'B'/,SYM( 7)/'C'/,SYM( 8)/'D'/,
     &     SYM( 9)/'E'/,SYM(10)/'F'/,SYM(11)/'G'/,SYM(12)/'H'/,
     &     SYM(13)/'I'/,SYM(14)/'J'/,SYM(15)/'K'/,SYM(16)/'L'/,
     &     SYM(17)/'M'/,SYM(18)/'N'/,SYM(19)/'O'/,SYM(20)/'P'/,
     &     SYM(21)/'Q'/,SYM(22)/'R'/,SYM(23)/'S'/,SYM(24)/'T'/,
     &     SYM(25)/'U'/,SYM(26)/'V'/,SYM(27)/'W'/,SYM(28)/'Y'/,
     &     SYM(29)/'Z'/,SYM(30)/'Z'/
      DATA SYM1(1)/'1'/,SYM1(2)/'2'/,SYM1(3)/'3'/,SYM1(4)/'4'/,
     &     SYM1(5)/'5'/,SYM1(6)/'6'/,SYM1(7)/'7'/,SYM1(8)/'8'/,
     &     SYM1(9)/'9'/,SYM1(10)/'X'/
!
      ICOUNT(IPOINT) = ICOUNT(IPOINT) + 1
      IF (ICOUNT(IPOINT) .EQ. 1) GO TO 5
!
      ISYMBL = MIN(ICOUNT(IPOINT), 10)
      LINE(IPOINT) = SYM1(ISYMBL)
      RETURN
!
    5 CONTINUE
      GO TO (10, 20, 30), IPTSYM
!
   10 LINE(IPOINT) = SYM(1)
      RETURN
!
   20 ISYMBL = MIN(29, MAX(1, ISYM(I)))
      LINE(IPOINT) = SYM(ISYMBL)
      RETURN
!
   30 ISYMBL = MIN(29, MAX(1, J+4))
      LINE(IPOINT) = SYM(ISYMBL)
!
      RETURN
      END
