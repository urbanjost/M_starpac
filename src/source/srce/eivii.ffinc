!EIVII
     subroutine eivii (nmsub, nmvar, ivec, n, iveclb, ivecub, nvmx,&
     &   head, msgtyp, nv, error, nmmin, nmmax)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE CHECKS FOR VALUES IN THE INPUT VECTOR IVEC
!     WHICH ARE OUTSIDE THE (INCLUSIVE) LIMITS IVECLB TO IVECUB, PRINTS
!     AN ERROR MESSAGE IF THE NUMBER OF VIOLATIONS EXCEEDS THE LARGEST
!     NUMBER OF VIOLATIONS ALLOWED, AND RETURNS THE NUMBER OF
!     VIOLATIONS AND AN ERROR FLAG INDICATING THE RESULTS.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  JUNE 10, 1982
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     integer&
     &   iveclb,ivecub,msgtyp,n,nv,nvmx
     logical&
     &   error,head
!
!  ARRAY ARGUMENTS
     integer&
     &   ivec(*)
     character&
     &   nmmax(8)*1,nmmin(8)*1,nmsub(6)*1,nmvar(8)*1
!
!  LOCAL SCALARS
     integer&
     &   i,iprt
!
!  EXTERNAL SUBROUTINES
!      EXTERNAL EHDR,IPRINT
!
!  INTRINSIC FUNCTIONS
      intrinsic mod
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     LOGICAL ERROR
!        THE VALUE RETURNED FROM THE ERROR CHECKING ROUTINES TO INDICATE
!        WHETHER AN ERROR WAS DETECTED (TRUE) OR NOT (FALSE).
!     LOGICAL HEAD
!        A FLAG INDICATING WHETHER THE HEADING SHOULD BE PRINTED (TRUE)
!        OR NOT (FALSE).
!     INTEGER I
!        AN INDEX ARGUMENT.
!     INTEGER IPRT
!        THE UNIT NUMBER FOR PRINTED OUTPUT.
!     INTEGER IVEC(N)
!        THE VECTOR BEING TESTED.
!     INTEGER IVECLB, IVECUB
!        THE (INCLUSIVE) RANGE THAT THE VECTOR IS BEING TESTED
!        AGAINST.
!     INTEGER MSGTYP
!        THE INDICATOR ARGUMENT FOR THE TYPE OF MESSAGE.
!        IF (MSGTYP.GE.4) THE MESSAGE PRINTED WILL USE NMMIN AND
!        NMMAX, OTHERWISE IT WILL USE IVECLB AND IVECUB.
!        IF (MSGTYP = 1 OR 4) NO VIOLATIONS ARE ALLOWED.
!        IF (MSGTYP = 2 OR 5) THE NUMBER OF VIOLATIONS MUST
!                             BE LESS THAN   NVMX   .
!        IF (MSGTYP = 3 OR 6) VIOLATIONS ARE COUNTED ONLY IF THE
!                             THE FIRST ELEMENT IS NOT IN VIOLATION.
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS.
!     CHARACTER*1 NMMAX(8)
!        THE NAME OF THE ARGUMENT SPECIFYING THE MAXIMUM.
!     CHARACTER*1 NMMIN(8)
!        THE NAME OF THE ARGUMENT SPECIFYING THE MINIMUM.
!     CHARACTER*1 NMSUB(6)
!        THE CHARACTERS OF THE CALLING SUBROUTINES NAME.
!     CHARACTER*1 NMVAR(8)
!        THE CHARACTERS OF THE ARGUMENTS NAME.
!     INTEGER NV
!        THE NUMBER OF VIOLATIONS FOUND.
!     INTEGER NVMX
!        THE LARGEST NUMBER OF VIOLATIONS ALLOWED.
!
      error = .false.
!
      if (n.le.0) return
      if (ivecub.lt.iveclb) return
!
!     TEST WHETHER TESTING IS NECESSRY
!
     if ((mod(msgtyp,3) .eq. 0) .and.&
     &    ((ivec(1) .lt. iveclb) .or. (ivec(1) .gt. ivecub))) return
!
!     CHECK FOR VIOLATIONS
!
      nv = 0
      do 5 i = 1, n
         if ((ivec(i).lt.iveclb) .or. (ivec(i).gt.ivecub)) nv = nv + 1
    5 continue
!
      if (nv .le. nvmx) return
!
!     VIOLATIONS FOUND
!
      error = .true.
      call iprint(iprt)
      call ehdr(nmsub, head)
!
     if (msgtyp.le.3)&
     &   write (iprt, 1000) (nmvar(i),i=1,6), iveclb, ivecub, nv
     if (msgtyp.ge.4)&
    &   write (iprt, 1005) (nmvar(i),i=1,6), (nmmin(i),i=1,8),&
     &   (nmmax(i),i=1,8), nv
!
      go to (10, 20, 30, 10, 20, 30), msgtyp
!
   10 write(iprt, 1010) (nmvar(i),i=1,6)
      return
!
   20 write(iprt, 1020) (nmvar(i),i=1,6), nvmx
      return
!
   30 write(iprt, 1030) (nmvar(i),i=1,6)
      return
!
!     FORMAT STATEMENTS
!
1000 format (/&
    &   32h the number of values in vector , 6a1,&
    &   19h outside the range , i6, 3h to/&
     &   1x, i6, 16h, inclusive, is , i6, '.')
1005 format (/&
    &   32h the number of values in vector , 6a1,&
    &   19h outside the range , 8a1, 3h to/&
     &   1x, 8a1, 16h, inclusive, is , i6, '.')
1010 format(&
    &   26h the values in the vector , 6a1,&
     &   31h must all be within this range.)
1020 format(&
    &   36h the number of values in the vector , 6a1,&
    &   19h outside this range/&
     &   19h must be less than , i5, '.')
1030 format(&
    &   34h if the first value of the vector , 6a1,&
    &   21h is within this range/&
     &   45h all of the values must be within this range.)
!
      end
