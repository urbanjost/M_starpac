!DGAMIT
      double precision function dgamit (a, x)
! JULY 1977 EDITION.  W. FULLERTON, C3, LOS ALAMOS SCIENTIFIC LAB.
!
! EVALUATE TRICOMI-S INCOMPLETE GAMMA FUNCTION DEFINED BY
!
! GAMIT = X**(-A)/GAMMA(A) * INTEGRAL T = 0 TO X OF EXP(-T) * T**(A-1.)
!
! AND ANALYTIC CONTINUATION FOR A .LE. 0.0.  GAMMA(X) IS THE COMPLETE
! GAMMA FUNCTION OF X.  GAMIT IS EVALUATED FOR ARBITRARY REAL VALUES OF
! A AND FOR NON-NEGATIVE VALUES OF X (EVEN THOUGH GAMIT IS DEFINED FOR
! X .LT. 0.0), EXCEPT THAT FOR X = 0 AND A .LE. 0.0, GAMIT IS INFINITE,
! A FATAL ERROR.
!
!      A SLIGHT DETERIORATION OF 2 OR 3 DIGITS ACCURACY WILL OCCUR WHEN
! GAMIT IS VERY LARGE OR VERY SMALL IN ABSOLUTE VALUE, BECAUSE LOG-
! ARITHMIC VARIABLES ARE USED.  ALSO, IF THE PARAMETER A IS VERY CLOSE
! TO A NEGATIVE INTEGER (BUT NOT A NEGATIVE INTEGER), THERE IS A LOSS
! OF ACCURACY, WHICH IS REPORTED IF THE RESULT IS LESS THAN HALF
! MACHINE PRECISION.
!
! REF. -- W. GAUTSCHI, AN EVALUATION PROCEDURE FOR INCOMPLETE GAMMA
! FUNCTIONS, ACM TRANS. MATH. SOFTWARE.
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      double precision a,x
!
!  LOCAL SCALARS
     double precision aeps,ainta,algap1,alneps,alng,alx,bot,h,sga,&
     &   sgngam,sqeps,t
!
!  EXTERNAL FUNCTIONS
!      DOUBLE PRECISION D1MACH,D9GMIT,D9LGIC,D9LGIT,DGAMR,DLNGAM
!       EXTERNAL D1MACH,D9GMIT,D9LGIC,D9LGIT,DGAMR,DLNGAM
!
!  EXTERNAL SUBROUTINES
!       EXTERNAL DLGAMS,XERCLR,XERROR
!
!  INTRINSIC FUNCTIONS
      intrinsic abs,dsign,dsqrt,exp,int,log
!
!
      data alneps, sqeps, bot / 3*0.d0 /
!
      if (alneps.ne.0.d0) go to 10
      alneps = -log (d1mach(3))
      sqeps = dsqrt (d1mach(4))
      bot = log (d1mach(1))
!
 10   if (x.lt.0.d0) call xerror ('DGAMIT  X IS NEGATIVE', 21, 2, 2)
!
      if (x.ne.0.d0) alx = log (x)
      sga = 1.0d0
      if (a.ne.0.d0) sga = dsign (1.0d0, a)
      ainta = int (a + 0.5d0*sga)
      aeps = a - ainta
!
      if (x.gt.0.d0) go to 20
      dgamit = 0.0d0
      if (ainta.gt.0.d0 .or. aeps.ne.0.d0) dgamit = dgamr(a+1.0d0)
      return
!
 20   if (x.gt.1.d0) go to 30
     if (a.ge.(-0.5d0) .or. aeps.ne.0.d0) call dlgams (a+1.0d0, algap1,&
     &  sgngam)
      dgamit = d9gmit (a, x, algap1, sgngam, alx)
      return
!
 30   if (a.lt.x) go to 40
      t = d9lgit (a, x, dlngam(a+1.0d0))
      if (t.lt.bot) call xerclr
      dgamit = exp (t)
      return
!
 40   alng = d9lgic (a, x, alx)
!
! EVALUATE DGAMIT IN TERMS OF LOG (DGAMIC (A, X))
!
      h = 1.0d0
      if (aeps.eq.0.d0 .and. ainta.le.0.d0) go to 50
!
      call dlgams (a+1.0d0, algap1, sgngam)
      t = log (abs(a)) + alng - algap1
      if (t.gt.alneps) go to 60
!
      if (t.gt.(-alneps)) h = 1.0d0 - sga * sgngam * exp(t)
      if (abs(h).gt.sqeps) go to 50
!
      call xerclr
      call xerror ('DGAMIT  RESULT LT HALF PRECISION', 32, 1, 1)
!
 50   t = -a*alx + log(abs(h))
      if (t.lt.bot) call xerclr
      dgamit = dsign (exp(t), h)
      return
!
 60   t = t - a*alx
      if (t.lt.bot) call xerclr
      dgamit = -sga * sgngam * exp(t)
      return
!
      end
