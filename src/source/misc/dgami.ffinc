!DGAMI
      DOUBLE PRECISION FUNCTION DGAMI (A, X)
! JULY 1977 EDITION.  W. FULLERTON, C3, LOS ALAMOS SCIENTIFIC LAB.
!
! EVALUATE THE INCOMPLETE GAMMA FUNCTION DEFINED BY
!
! GAMI = INTEGRAL FROM T = 0 TO X OF EXP(-T) * T**(A-1.0) .
!
! GAMI IS EVALUATED FOR POSITIVE VALUES OF A AND NON-NEGATIVE VALUES
! OF X.  A SLIGHT DETERIORATION OF 2 OR 3 DIGITS ACCURACY WILL OCCUR
! WHEN GAMI IS VERY LARGE OR VERY SMALL, BECAUSE LOGARITHMIC VARIABLES
! ARE USED.
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      DOUBLE PRECISION A,X
!
!  LOCAL SCALARS
      DOUBLE PRECISION FACTOR
!
!  EXTERNAL FUNCTIONS
      DOUBLE PRECISION D1MACH,DGAMIT,DLNGAM
      EXTERNAL D1MACH,DGAMIT,DLNGAM
!
!  EXTERNAL SUBROUTINES
      EXTERNAL XERROR
!
!  INTRINSIC FUNCTIONS
      INTRINSIC EXP,LOG
!
!
      IF (A.LE.0.D0) CALL XERROR ('DGAMI   A MUST BE GT ZERO', 25, 1,2)
      IF (X.LT.0.D0) CALL XERROR ('DGAMI   X MUST BE GE ZERO', 25, 2,2)
!
      DGAMI = 0.D0
      IF (X.EQ.0.0D0) RETURN
!
! THE ONLY ERROR POSSIBLE IN THE EXPRESSION BELOW IS A FATAL OVERFLOW.
!
      FACTOR = DLNGAM(A) + A*LOG(X)
      IF (FACTOR.GT.LOG(D1MACH(2))) THEN
         DGAMI = D1MACH(2)
      ELSE
         DGAMI = EXP(FACTOR) * DGAMIT(A,X)
      END IF
!
      RETURN
      END
