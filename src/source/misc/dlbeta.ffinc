!DLBETA
      DOUBLE PRECISION FUNCTION DLBETA (A, B)
! JULY 1977 EDITION.   W. FULLERTON, C3, LOS ALAMOS SCIENTIFIC LAB.
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      DOUBLE PRECISION A,B
!
!  LOCAL SCALARS
      DOUBLE PRECISION CORR,P,Q,SQ2PIL
!
!  EXTERNAL FUNCTIONS
!      DOUBLE PRECISION D9LGMC,DGAMMA,DLNGAM,DLNREL
!       EXTERNAL D9LGMC,DGAMMA,DLNGAM,DLNREL
!
!  EXTERNAL SUBROUTINES
!       EXTERNAL XERROR
!
!  INTRINSIC FUNCTIONS
      INTRINSIC LOG,MAX,MIN
!
      DATA SQ2PIL / 0.9189385332 0467274178 0329736405 62 D0 /
!
      P = MIN (A, B)
      Q = MAX (A, B)
!
      IF (P.LE.0.D0) CALL XERROR (
     &  'DLBETA  BOTH ARGUMENTS MUST BE GT ZERO', 38, 1, 2)
!
      IF (P.GE.10.D0) GO TO 30
      IF (Q.GE.10.D0) GO TO 20
!
! P AND Q ARE SMALL.
!
      DLBETA = LOG (DGAMMA(P) * (DGAMMA(Q)/DGAMMA(P+Q)) )
      RETURN
!
! P IS SMALL, BUT Q IS BIG.
!
 20   CORR = D9LGMC(Q) - D9LGMC(P+Q)
      DLBETA = DLNGAM(P) + CORR + P - P*LOG(P+Q)
     &  + (Q-0.5D0)*DLNREL(-P/(P+Q))
      RETURN
!
! P AND Q ARE BIG.
!
 30   CORR = D9LGMC(P) + D9LGMC(Q) - D9LGMC(P+Q)
      DLBETA = -0.5D0*LOG(Q) + SQ2PIL + CORR + (P-0.5D0)*LOG(P/(P+Q))
     &  + Q*DLNREL(-P/(P+Q))
      RETURN
!
      END
