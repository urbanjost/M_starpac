!INITS
      INTEGER FUNCTION INITS (OS, NOS, ETA)
! APRIL 1977 VERSION.  W. FULLERTON, C3, LOS ALAMOS SCIENTIFIC LAB.
!
! INITIALIZE THE ORTHOGONAL SERIES SO THAT INITS IS THE NUMBER OF TERMS
! NEEDED TO INSURE THE ERROR IS NO LARGER THAN ETA.  ORDINARILY, ETA
! WILL BE CHOSEN TO BE ONE-TENTH MACHINE PRECISION.
!
!             INPUT ARGUMENTS --
! OS     ARRAY OF NOS COEFFICIENTS IN AN ORTHOGONAL SERIES.
! NOS    NUMBER OF COEFFICIENTS IN OS.
! ETA    REQUESTED ACCURACY OF SERIES.
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL ETA
      INTEGER NOS
!
!  ARRAY ARGUMENTS
      REAL OS(NOS)
!
!  LOCAL SCALARS
      REAL ERR
      INTEGER I,II
!
!  EXTERNAL SUBROUTINES
!       EXTERNAL XERROR
!
!  INTRINSIC FUNCTIONS
      INTRINSIC ABS
!
!
      IF (NOS.LT.1) CALL XERROR (
     &  'INITS   NUMBER OF COEFFICIENTS LT 1', 35, 2, 2)
!
      ERR = 0.
      DO 10 II=1,NOS
        I = NOS + 1 - II
        ERR = ERR + ABS(OS(I))
        IF (ERR.GT.ETA) GO TO 20
 10   CONTINUE
!
 20   IF (I.EQ.NOS) CALL XERROR ('INITS   ETA MAY BE TOO SMALL', 28,
     &  1, 2)
      INITS = I
!
      RETURN
      END
