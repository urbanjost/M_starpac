!GAMI
      real function gami (a, x)
! JULY 1977 EDITION.  W. FULLERTON, C3, LOS ALAMOS SCIENTIFIC LAB.
!
! EVALUATE THE INCOMPLETE GAMMA FUNCTION DEFINED BY
!
! GAMI = INTEGRAL FROM T = 0 TO X OF EXP(-T) * T**(A-1.0) .
!
! GAMI IS EVALUATED FOR POSITIVE VALUES OF A AND NON-NEGATIVE VALUES
! OF X.  A SLIGHT DETERIORATION OF 2 OR 3 DIGITS ACCURACY WILL OCCUR
! WHEN GAMI IS VERY LARGE OR VERY SMALL, BECAUSE LOGARITHMIC VARIABLES
! ARE USED.
!
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      real a,x
!
!  LOCAL SCALARS
      real factor
!
!  EXTERNAL FUNCTIONS
!      REAL ALNGAM,GAMIT,R1MACH
!       EXTERNAL ALNGAM,GAMIT,R1MACH
!
!  EXTERNAL SUBROUTINES
!       EXTERNAL XERROR
!
!  INTRINSIC FUNCTIONS
      intrinsic exp,log
!
      if (a.le.0.0) call xerror ('GAMI    A MUST BE GT ZERO', 25, 1, 2)
      if (x.lt.0.0) call xerror ('GAMI    X MUST BE GE ZERO', 25, 2, 2)
!
      gami = 0.0
      if (x.eq.0.0) return
!
! THE ONLY ERROR POSSIBLE IN THE EXPRESSION BELOW IS A FATAL OVERFLOW.
!
      factor = alngam(a) + a*log(x)
      if (factor.gt.log(r1mach(2))) then
         gami = r1mach(2)
      else
         gami = exp(factor) * gamit(a,x)
      end if
!
      return
      end
