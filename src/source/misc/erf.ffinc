!ERF
      REAL FUNCTION ERF (X)
! APRIL 1977 VERSION.  W. FULLERTON, C3, LOS ALAMOS SCIENTIFIC LAB.
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL X
!
!  LOCAL SCALARS
      REAL SQEPS,SQRTPI,XBIG,Y
      INTEGER NTERF
!
!  LOCAL ARRAYS
      REAL ERFCS(13)
!
!  EXTERNAL FUNCTIONS
!      REAL CSEVL,ERFC,R1MACH
!      INTEGER INITS
!       EXTERNAL CSEVL,ERFC,R1MACH,INITS
!
!  INTRINSIC FUNCTIONS
      INTRINSIC ABS,LOG,SIGN,SQRT
!
!
! SERIES FOR ERF        ON THE INTERVAL  0.          TO  1.00000D+00
!                                        WITH WEIGHTED ERROR   7.10E-18
!                                         LOG WEIGHTED ERROR  17.15
!                               SIGNIFICANT FIGURES REQUIRED  16.31
!                                    DECIMAL PLACES REQUIRED  17.71
!
      DATA ERF CS( 1) /   -.0490461212 34691808E0 /
      DATA ERF CS( 2) /   -.1422612051 0371364E0 /
      DATA ERF CS( 3) /    .0100355821 87599796E0 /
      DATA ERF CS( 4) /   -.0005768764 69976748E0 /
      DATA ERF CS( 5) /    .0000274199 31252196E0 /
      DATA ERF CS( 6) /   -.0000011043 17550734E0 /
      DATA ERF CS( 7) /    .0000000384 88755420E0 /
      DATA ERF CS( 8) /   -.0000000011 80858253E0 /
      DATA ERF CS( 9) /    .0000000000 32334215E0 /
      DATA ERF CS(10) /   -.0000000000 00799101E0 /
      DATA ERF CS(11) /    .0000000000 00017990E0 /
      DATA ERF CS(12) /   -.0000000000 00000371E0 /
      DATA ERF CS(13) /    .0000000000 00000007E0 /
!
      DATA SQRTPI /1.772453850 9055160E0/
      DATA NTERF, XBIG, SQEPS / 0, 0., 0./
!
      IF (NTERF.NE.0) GO TO 10
      NTERF = INITS (ERFCS, 13, 0.1*R1MACH(3))
      XBIG = SQRT(-LOG(SQRTPI*R1MACH(3)))
      SQEPS = SQRT(2.0*R1MACH(3))
!
 10   Y = ABS(X)
      IF (Y.GT.1.) GO TO 20
!
! ERF(X) = 1. - ERFC(X) FOR -1. .LE. X .LE. 1.
!
      IF (Y.LE.SQEPS) THEN
         ERF = 2.0*X/SQRTPI
      ELSE
         ERF = X*(1.0 + CSEVL(2.*X**2-1., ERFCS, NTERF))
      END IF
!
      RETURN
!
! ERF(X) = 1. - ERFC(X) FOR  ABS(X) .GT. 1.
!
 20   IF (Y.LE.XBIG) THEN
         ERF = SIGN (1.0-ERFC(Y), X)
      ELSE
         ERF = SIGN (1.0, X)
      END IF
!
      RETURN
      END
