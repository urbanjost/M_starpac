!GAMMA
      real function gamma (x)
! JUNE 1977 EDITION.   W. FULLERTON, C3, LOS ALAMOS SCIENTIFIC LAB.
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      real x
!
!  LOCAL SCALARS
      real dxrel,pi,sinpiy,sq2pil,xmax,xmin,y
      integer i,n,ngcs
!
!  LOCAL ARRAYS
      real gcs(23)
!
!  EXTERNAL FUNCTIONS
!      REAL CSEVL,R1MACH,R9LGMC
!      INTEGER INITS
!       EXTERNAL CSEVL,R1MACH,R9LGMC,INITS
!
!  EXTERNAL SUBROUTINES
!       EXTERNAL GAMLIM,XERROR
!
!  INTRINSIC FUNCTIONS
      intrinsic abs,aint,exp,float,log,sin,sqrt
!
!
      data gcs   ( 1) / .008571195590989331_wp/
      data gcs   ( 2) / .004415381324841007_wp/
      data gcs   ( 3) / .05685043681599363_wp/
      data gcs   ( 4) /-.004219835396418561_wp/
      data gcs   ( 5) / .001326808181212460_wp/
      data gcs   ( 6) /-.0001893024529798880_wp/
      data gcs   ( 7) / .0000360692532744124_wp/
      data gcs   ( 8) /-.0000060567619044608_wp/
      data gcs   ( 9) / .0000010558295463022_wp/
      data gcs   (10) /-.0000001811967365542_wp/
      data gcs   (11) / .0000000311772496471_wp/
      data gcs   (12) /-.0000000053542196390_wp/
      data gcs   (13) / .0000000009193275519_wp/
      data gcs   (14) /-.0000000001577941280_wp/
      data gcs   (15) / .0000000000270798062_wp/
      data gcs   (16) /-.0000000000046468186_wp/
      data gcs   (17) / .0000000000007973350_wp/
      data gcs   (18) /-.0000000000001368078_wp/
      data gcs   (19) / .0000000000000234731_wp/
      data gcs   (20) /-.0000000000000040274_wp/
      data gcs   (21) / .0000000000000006910_wp/
      data gcs   (22) /-.0000000000000001185_wp/
      data gcs   (23) / .0000000000000000203_wp/
!
      data pi /3.14159265358979324_wp/
! SQ2PIL IS LOG (SQRT (2.*PI) )
      data sq2pil /0.91893853320467274_wp/
      data ngcs, xmin, xmax, dxrel /0, 3*0.0 /
!
      if (ngcs.ne.0) go to 10
!
! ---------------------------------------------------------------------
! INITIALIZE.  FIND LEGAL BOUNDS FOR X, AND DETERMINE THE NUMBER OF
! TERMS IN THE SERIES REQUIRED TO ATTAIN AN ACCURACY TEN TIMES BETTER
! THAN MACHINE PRECISION.
!
      ngcs = inits (gcs, 23, 0.1*r1mach(3))
!
      call gamlim (xmin, xmax)
      dxrel = sqrt (r1mach(4))
!
! ---------------------------------------------------------------------
! FINISH INITIALIZATION.  START EVALUATING GAMMA(X).
!
 10   y = abs(x)
      if (y.gt.10.0) go to 50
!
! COMPUTE GAMMA(X) FOR ABS(X) .LE. 10.0.  REDUCE INTERVAL AND
! FIND GAMMA(1+Y) FOR 0. .LE. Y .LT. 1. FIRST OF ALL.
!
      n = x
      if (x.lt.0.) n = n - 1
      y = x - float(n)
      n = n - 1
      gamma = 0.9375 + csevl(2.*y-1., gcs, ngcs)
      if (n.eq.0) return
!
      if (n.gt.0) go to 30
!
! COMPUTE GAMMA(X) FOR X .LT. 1.
!
      n = -n
      if (x.eq.0.) call xerror ('GAMMA   X IS 0', 14, 4, 2)
      if (x.lt.0. .and. x+float(n-2).eq.0.) call xerror ( 'GAMMA   X IS A NEGATIVE INTEGER', 31, 4, 2)
      if (x.lt.(-0.5) .and. abs((x-aint(x-0.5))/x).lt.dxrel) &
      & call xerror (  'GAMMA   ANSWER LT HALF PRECISION BECAUSE X TOO NEAR NEGATIVE INTEGER', 68, 1, 1)
!
      do i=1,n
        gamma = gamma / (x+float(i-1))
      enddo   
      return
!
! GAMMA(X) FOR X .GE. 2.
!
 30   do 40 i=1,n
        gamma = (y+float(i))*gamma
 40   continue
      return
!
! COMPUTE GAMMA(X) FOR ABS(X) .GT. 10.0.  RECALL Y = ABS(X).
!
 50   if (x.gt.xmax) call xerror ('GAMMA   X SO BIG GAMMA OVERFLOWS', 32, 3, 2)
!
      gamma = 0.
      if (x.lt.xmin) call xerror ('GAMMA   X SO SMALL GAMMA UNDERFLOWS', 35, 2, 1)
      if (x.lt.xmin) return
!
      gamma = exp((y-0.5)*log(y) - y + sq2pil + r9lgmc(y) )
      if (x.gt.0.) return
!
      if (abs((x-aint(x-0.5))/x).lt.dxrel) call xerror ( 'GAMMA   ANSWER LT HALF PRECISION, X TOO NEAR NEGATIVE INTEGER', 61, 1, 1)
!
      sinpiy = sin (pi*y)
      if (sinpiy.eq.0.) call xerror ( 'GAMMA   X IS A NEGATIVE INTEGER', 31, 4, 2)
!
      gamma = -pi / (y*sinpiy*gamma)
!
      return
      end
