!CSEVL
      real function csevl (x, cs, n)
! APRIL 1977 VERSION.  W. FULLERTON, C3, LOS ALAMOS SCIENTIFIC LAB.
!
! EVALUATE THE N-TERM CHEBYSHEV SERIES CS AT X.  ADAPTED FROM
! R. BROUCKE, ALGORITHM 446, C.A.C.M., 16, 254 (1973).  ALSO SEE FOX
! AND PARKER, CHEBYSHEV POLYS IN NUMERICAL ANALYSIS, OXFORD PRESS, P.56.
!
!             INPUT ARGUMENTS --
! X      VALUE AT WHICH THE SERIES IS TO BE EVALUATED.
! CS     ARRAY OF N TERMS OF A CHEBYSHEV SERIES.  IN EVAL-
!        UATING CS, ONLY HALF THE FIRST COEF IS SUMMED.
! N      NUMBER OF TERMS IN ARRAY CS.
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      real x
      integer n
!
!  ARRAY ARGUMENTS
      real cs(n)
!
!  LOCAL SCALARS
      real b0,b1,b2,twox
      integer i,ni
!
!  EXTERNAL SUBROUTINES
!       EXTERNAL XERROR
!
!
      if (n.lt.1) call xerror ('CSEVL   NUMBER OF TERMS LE 0', 28, 2,2)
     if (n.gt.1000) call xerror ('CSEVL   NUMBER OF TERMS GT 1000',&
     &  31, 3, 2)
     if (x.lt.(-1.0) .or. x.gt.1.0) call xerror (&
     &  'CSEVL   X OUTSIDE (-1,+1)', 25, 1, 1)
!
      b0 = 0.0
      b1 = 0.0
      b2 = 0.0
      twox = 2.0*x
      do 10 i=1,n
        b2 = b1
        b1 = b0
        ni = n + 1 - i
        b0 = twox*b1 - b2 + cs(ni)
 10   continue
!
      csevl = 0.5 * (b0-b2)
!
      return
      end
