!DCSEVL
      double precision function dcsevl (x, a, n)
!
!     LATEST REVISION  -  OCTOBER 3, 1983  (JRD)
!
! EVALUATE THE N-TERM CHEBYSHEV SERIES A AT X.  ADAPTED FROM
! R. BROUCKE, ALGORITHM 446, C.A.C.M., 16, 254 (1973).
!
!             INPUT ARGUMENTS --
! X      DBLE PREC VALUE AT WHICH THE SERIES IS TO BE EVALUATED.
! A      DBLE PREC ARRAY OF N TERMS OF A CHEBYSHEV SERIES.  IN EVAL-
!        UATING A, ONLY HALF THE FIRST COEF IS SUMMED.
! N      NUMBER OF TERMS IN ARRAY A.
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      double precision x
      integer n
!
!  ARRAY ARGUMENTS
      double precision a(n)
!
!  LOCAL SCALARS
      double precision b0,b1,b2,twox
      integer i,ni1
!
!  EXTERNAL SUBROUTINES
!       EXTERNAL XERROR
!
!
      if (n.lt.1) call xerror ('DCSEVL  NUMBER OF TERMS LE 0', 28, 2,2)
     if (n.gt.1000) call xerror ('DCSEVL  NUMBER OF TERMS GT 1000',&
     &  31, 3, 2)
     if (x.lt.(-1.d0) .or. x.gt.1.d0) call xerror (&
     &  'DCSEVL  X OUTSIDE (-1,+1)', 25, 1, 1)
!
      twox = 2.0d0*x
      b0 = 0.0d0
      b1 = 0.0d0
      b2 = 0.0d0
      do 10 i=1,n
        b2 = b1
        b1 = b0
        ni1 = n-i+1
        b0 = twox*b1 - b2 + a(ni1)
 10   continue
!
      dcsevl = 0.5d0 * (b0-b2)
!
      return
      end
