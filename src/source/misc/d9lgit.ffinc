!D9LGIT
      DOUBLE PRECISION FUNCTION D9LGIT(A,X,ALGAP1)
!***BEGIN PROLOGUE  D9LGIT
!***DATE WRITTEN   770701   (YYMMDD)
!***REVISION DATE  820801   (YYMMDD)
!***CATEGORY NO.  C7E
!***KEYWORDS  DOUBLE PRECISION,GAMMA,INCOMPLETE GAMMA FUNCTION,
!             LOGARITHM,SPECIAL FUNCTION,TRICOMI
!***AUTHOR  FULLERTON, W., (LANL)
!***PURPOSE  COMPUTES  THE LOG OF TRICOMI'S INCOMPLETE GAMMA FUNCTION
!            WITH PERRON'S CONTINUED FRACTION FOR LARGE X AND A .GE. X.
!***DESCRIPTION
!
! COMPUTE THE LOG OF TRICOMI'S INCOMPLETE GAMMA FUNCTION WITH PERRON'S
! CONTINUED FRACTION FOR LARGE X AND FOR A .GE. X.
!***REFERENCES  (NONE)
!***ROUTINES CALLED  D1MACH,XERROR
!***END PROLOGUE  D9LGIT
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      DOUBLE PRECISION A,ALGAP1,X
!
!  LOCAL SCALARS
      DOUBLE PRECISION A1X,AX,EPS,FK,HSTAR,P,R,S,SQEPS,T
      INTEGER K
!
!  EXTERNAL FUNCTIONS
!      DOUBLE PRECISION D1MACH
!       EXTERNAL D1MACH
!
!  EXTERNAL SUBROUTINES
!       EXTERNAL XERROR
!
!  INTRINSIC FUNCTIONS
      INTRINSIC ABS,DSQRT,LOG
!
      DATA EPS, SQEPS / 2*0.D0 /
!***FIRST EXECUTABLE STATEMENT  D9LGIT
      IF (EPS.NE.0.D0) GO TO 10
      EPS = 0.5D0*D1MACH(3)
      SQEPS = DSQRT (D1MACH(4))
!
 10   IF (X.LE.0.D0 .OR. A.LT.X) CALL XERROR ( 'D9LGIT  X SHOULD BE GT 0
     &.0 AND LE A', 35, 2, 2)
!
      AX = A + X
      A1X = AX + 1.0D0
      R = 0.D0
      P = 1.D0
      S = P
      DO 20 K=1,200
        FK = K
        T = (A+FK)*X*(1.D0+R)
        R = T/((AX+FK)*(A1X+FK)-T)
        P = R*P
        S = S + P
        IF (ABS(P).LT.EPS*S) GO TO 30
 20   CONTINUE
      CALL XERROR ( 'D9LGIT  NO CONVERGENCE IN 200 TERMS OF CONTINUED FR
     &ACTION', 57, 3, 2)
!
 30   HSTAR = 1.0D0 - X*S/A1X
      IF (HSTAR.LT.SQEPS) CALL XERROR ( 'D9LGIT  RESULT LESS THAN HALF P
     &RECISION', 39, 1, 1)
!
      D9LGIT = -X - ALGAP1 - LOG(HSTAR)
      RETURN
!
      END
