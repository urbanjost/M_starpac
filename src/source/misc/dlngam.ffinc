!DLNGAM
      double precision function dlngam (x)
! JUNE 1977 EDITION.   W. FULLERTON, C3, LOS ALAMOS SCIENTIFIC LAB.
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      double precision x
!
!  LOCAL SCALARS
      double precision dxrel,pi,sinpiy,sq2pil,sqpi2l,xmax,y
!
!  EXTERNAL FUNCTIONS
!      DOUBLE PRECISION D1MACH,D9LGMC,DGAMMA
!       EXTERNAL D1MACH,D9LGMC,DGAMMA
!
!  EXTERNAL SUBROUTINES
!       EXTERNAL XERROR
!
!  INTRINSIC FUNCTIONS
      intrinsic abs,dsqrt,int,log,sin
!
!
      data sq2pil / 0.91893853320467274178032973640562d0 /
! SQ2PIL = LOG (SQRT(2*PI)),  SQPI2L = LOG(SQRT(PI/2))
      data sqpi2l / +.225791352644727432363097614947441d+0    /
      data pi / 3.14159265358979323846264338327950d0 /
!
      data xmax, dxrel / 2*0.d0 /
!
      if (xmax.ne.0.d0) go to 10
      xmax = d1mach(2)/log(d1mach(2))
      dxrel = dsqrt (d1mach(4))
!
 10   y = abs (x)
      if (y.gt.10.d0) go to 20
!
! LOG (ABS (DGAMMA(X)) ) FOR ABS(X) .LE. 10.0
!
      dlngam = log (abs (dgamma(x)) )
      return
!
! LOG ( ABS (DGAMMA(X)) ) FOR ABS(X) .GT. 10.0
!
20   if (y.gt.xmax) call xerror (&
     &  'DLNGAM  ABS(X) SO BIG DLNGAM OVERFLOWS', 39, 2, 2)
!
      if (x.gt.0.d0) then
         dlngam = sq2pil + (x-0.5d0)*log(x) - x + d9lgmc(y)
         return
      end if
!
      sinpiy = abs (sin(pi*y))
     if (sinpiy.eq.0.d0) call xerror (&
     &  'DLNGAM  X IS A NEGATIVE INTEGER', 31, 3, 2)
!
     if (abs ((x-int(x-0.5d0))/x).lt.dxrel) call xerror (&
    &    'DLNGAM  ANSWER LT HALF PRECISION BECAUSE X TOO NEAR NEGATIVE&
     &INTEGER', 68, 1, 1)
!
      dlngam = sqpi2l + (x-0.5d0)*log(y) - x - log(sinpiy) - d9lgmc(y)
      return
!
      end
