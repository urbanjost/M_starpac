!D9GMIT
      double precision function d9gmit(a,x,algap1,sgngam,alx)
!***BEGIN PROLOGUE  D9GMIT
!***DATE WRITTEN   770701   (YYMMDD)
!***REVISION DATE  820801   (YYMMDD)
!***CATEGORY NO.  C7E
!***KEYWORDS  COMPLEMENTARY,COMPLEMENTARY INCOMPLETE GAMMA FUNCTION,
!             DOUBLE PRECISION,GAMMA,GAMMA FUNCTION,SPECIAL FUNCTION,
!             TRICOMI
!***AUTHOR  FULLERTON, W., (LANL)
!***PURPOSE  COMPUTES D.P. TRICOMI-S INCOMPLETE GAMMA FUNCTION FOR
!            SMALL X.
!***DESCRIPTION
!
! COMPUTE TRICOMI'S INCOMPLETE GAMMA FUNCTION FOR SMALL X.
!***REFERENCES  (NONE)
!***ROUTINES CALLED  D1MACH,DLNGAM,XERROR
!***END PROLOGUE  D9GMIT
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      double precision a,algap1,alx,sgngam,x
!
!  LOCAL SCALARS
      double precision ae,aeps,alg2,algs,bot,eps,fk,s,sgng2,t,te
      integer k,m,ma
!
!  EXTERNAL FUNCTIONS
!      DOUBLE PRECISION D1MACH,DLNGAM
!       EXTERNAL D1MACH,DLNGAM
!
!  EXTERNAL SUBROUTINES
!       EXTERNAL XERROR
!
!  INTRINSIC FUNCTIONS
      intrinsic abs,dble,dsign,exp,float,log
!
      data eps, bot / 2*0.d0 /
!***FIRST EXECUTABLE STATEMENT  D9GMIT
      if (eps.ne.0.d0) go to 10
      eps = 0.5d0*d1mach(3)
      bot = log (d1mach(1))
!
 10   if (x.le.0.d0) call xerror ( 'D9GMIT  X SHOULD BE GT 0', 24, 1, 2)
!
      ma = a + 0.5d0
      if (a.lt.0.d0) ma = a - 0.5d0
      aeps = a - dble(float(ma))
!
      ae = a
      if (a.lt.(-0.5d0)) ae = aeps
!
      t = 1.d0
      te = ae
      s = t
      do 20 k=1,200
        fk = k
        te = -x*te/fk
        t = te/(ae+fk)
        s = s + t
        if (abs(t).lt.eps*abs(s)) go to 30
 20   continue
     call xerror&
     &('D9GMIT  NO CONVERGENCE IN 200 TERMS OF TAYLOR-S SERIES',54,2,2)
!
 30   if (a.ge.(-0.5d0)) then
         algs = -algap1 + log(s)
      else
         algs = -dlngam(1.d0+aeps) + log(s)
         s = 1.0d0
         m = -ma - 1
         if (m.eq.0) go to 50
         t = 1.0d0
         do 40 k=1,m
            t = x*t/(aeps-dble(float(m+1-k)))
            s = s + t
            if (abs(t).lt.eps*abs(s)) go to 50
 40      continue
!
 50      d9gmit = 0.0d0
         algs = -dble(float(ma))*log(x) + algs
         if (s.ne.0.0d0 .and. aeps.ne.0.0d0) then
            sgng2 = sgngam * dsign (1.0d0, s)
            alg2 = -x - algap1 + log(abs(s))
!
            if (alg2.gt.bot) d9gmit = sgng2 * exp(alg2)
            if (algs.gt.bot) d9gmit = d9gmit + exp(algs)
            return
         end if
      end if
!
      d9gmit = exp (algs)
      return
!
      end
