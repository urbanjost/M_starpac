!R9LGMC
      REAL FUNCTION R9LGMC (X)
! AUGUST 1977 EDITION.  W. FULLERTON, C3, LOS ALAMOS SCIENTIFIC LAB.
!
! COMPUTE THE LOG GAMMA CORRECTION FACTOR FOR X .GE. 10.0 SO THAT
!  LOG (GAMMA(X)) = LOG(SQRT(2*PI)) + (X-.5)*LOG(X) - X + R9LGMC(X)
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL X
!
!  LOCAL SCALARS
      REAL XBIG,XMAX
      INTEGER NALGM
!
!  LOCAL ARRAYS
      REAL ALGMCS(6)
!
!  EXTERNAL FUNCTIONS
!      REAL CSEVL,R1MACH
!      INTEGER INITS
!       EXTERNAL CSEVL,R1MACH,INITS
!
!  EXTERNAL SUBROUTINES
!       EXTERNAL XERROR
!
!  INTRINSIC FUNCTIONS
      INTRINSIC EXP,LOG,MIN,SQRT
!
!
! SERIES FOR ALGM       ON THE INTERVAL  0.          TO  1.00000D-02
!                                        WITH WEIGHTED ERROR   3.40E-16
!                                         LOG WEIGHTED ERROR  15.47
!                               SIGNIFICANT FIGURES REQUIRED  14.39
!                                    DECIMAL PLACES REQUIRED  15.86
!
      DATA ALGMCS( 1) /    .1666389480 45186E0 /
      DATA ALGMCS( 2) /   -.0000138494 817606E0 /
      DATA ALGMCS( 3) /    .0000000098 108256E0 /
      DATA ALGMCS( 4) /   -.0000000000 180912E0 /
      DATA ALGMCS( 5) /    .0000000000 000622E0 /
      DATA ALGMCS( 6) /   -.0000000000 000003E0 /
!
      DATA NALGM, XBIG, XMAX / 0, 2*0.0 /
!
      IF (NALGM.NE.0) GO TO 10
      NALGM = INITS (ALGMCS, 6, R1MACH(3))
      XBIG = 1.0/SQRT(R1MACH(3))
      XMAX = EXP (MIN(LOG(R1MACH(2)/12.0), -LOG(12.0*R1MACH(1))) )
!
 10   IF (X.LT.10.0) CALL XERROR ('R9LGMC  X MUST BE GE 10', 23, 1, 2)
      IF (X.GE.XMAX) GO TO 20
!
      R9LGMC = 1.0/(12.0*X)
      IF (X.LT.XBIG) R9LGMC = CSEVL (2.0*(10./X)**2-1., ALGMCS, NALGM)/X
      RETURN
!
 20   R9LGMC = 0.0
      CALL XERROR ('R9LGMC  X SO BIG R9LGMC UNDERFLOWS', 34, 2, 1)
      RETURN
!
      END
