!D9LGIC
      double precision function d9lgic(a,x,alx)
!***BEGIN PROLOGUE  D9LGIC
!***DATE WRITTEN   770701   (YYMMDD)
!***REVISION DATE  820801   (YYMMDD)
!***CATEGORY NO.  C7E
!***KEYWORDS  DOUBLE PRECISION,GAMMA,INCOMPLETE GAMMA FUNCTION,
!             LOGARITHM INCOMPLETE GAMMA FUNCTION,SPECIAL FUNCTION
!***AUTHOR  FULLERTON, W., (LANL)
!***PURPOSE  COMPUTES THE D.P. LOG INCOMPLETE GAMMA FUNCTION FOR LARGE X
!            AND FOR A .LE. X.
!***DESCRIPTION
!
! COMPUTE THE LOG COMPLEMENTARY INCOMPLETE GAMMA FUNCTION FOR LARGE X
! AND FOR A .LE. X.
!***REFERENCES  (NONE)
!***ROUTINES CALLED  D1MACH,XERROR
!***END PROLOGUE  D9LGIC
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      double precision a,alx,x
!
!  LOCAL SCALARS
      double precision eps,fk,p,r,s,t,xma,xpa
      integer k
!
!  EXTERNAL FUNCTIONS
!      DOUBLE PRECISION D1MACH
!       EXTERNAL D1MACH
!
!  EXTERNAL SUBROUTINES
!       EXTERNAL XERROR
!
!  INTRINSIC FUNCTIONS
      intrinsic abs,log
!
      data eps / 0.d0 /
!***FIRST EXECUTABLE STATEMENT  D9LGIC
      if (eps.eq.0.d0) eps = 0.5d0*d1mach(3)
!
      xpa = x + 1.0d0 - a
      xma = x - 1.d0 - a
!
      r = 0.d0
      p = 1.d0
      s = p
      do 10 k=1,300
        fk = k
        t = fk*(a-fk)*(1.d0+r)
        r = -t/((xma+2.d0*fk)*(xpa+2.d0*fk)+t)
        p = r*p
        s = s + p
        if (abs(p).lt.eps*s) go to 20
 10   continue
     call xerror&
    &('D9LGIC  NO CONVERGENCE IN 300 TERMS OF CONTINUED FRACTION',&
     & 57,1,2)
!
 20   d9lgic = a*alx - x + log(s/xpa)
!
      return
      end
