!R9LGIC
      real function r9lgic (a, x, alx)
! JULY 1977 EDITION.  W. FULLERTON, C3, LOS ALAMOS SCIENTIFIC LAB.
!
! COMPUTE THE LOG COMPLEMENTARY INCOMPLETE GAMMA FUNCTION FOR LARGE X
! AND FOR A .LE. X.
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      real a,alx,x
!
!  LOCAL SCALARS
      real eps,fk,p,r,s,t,xma,xpa
      integer k
!
!  EXTERNAL FUNCTIONS
!      REAL R1MACH
!       EXTERNAL R1MACH
!
!  EXTERNAL SUBROUTINES
!       EXTERNAL XERROR
!
!  INTRINSIC FUNCTIONS
      intrinsic abs,log
!
      data eps / 0.0 /
!
      if (eps.eq.0.0) eps = 0.5*r1mach(3)
!
      xpa = x + 1.0 - a
      xma = x - 1.0 - a
!
      r = 0.0
      p = 1.0
      s = p
      do 10 k=1,200
        fk = k
        t = fk*(a-fk)*(1.0+r)
        r = -t/((xma+2.0*fk)*(xpa+2.0*fk)+t)
        p = r*p
        s = s + p
        if (abs(p).lt.eps*s) go to 20
 10   continue
     call xerror (  'R9LGIC  NO CONVERGENCE IN 200 TERMS OF CONTINUED F&
     &RACTION', 57, 1, 2)
!
 20   r9lgic = a*alx - x + log(s/xpa)
!
      return
      end
