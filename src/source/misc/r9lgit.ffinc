!R9LGIT
      real function r9lgit (a, x, algap1)
! JULY 1977 EDITION.  W. FULLERTON, C3, LOS ALAMOS SCIENTIFIC LAB.
!
! COMPUTE THE LOG OF TRICOMI-S INCOMPLETE GAMMA FUNCTION WITH PERRON-S
! CONTINUED FRACTION FOR LARGE X AND FOR A .GE. X.
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      real a,algap1,x
!
!  LOCAL SCALARS
      real a1x,ax,eps,fk,hstar,p,r,s,sqeps,t
      integer k
!
!  EXTERNAL FUNCTIONS
!      REAL R1MACH
!       EXTERNAL R1MACH
!
!  EXTERNAL SUBROUTINES
!       EXTERNAL XERROR
!
!  INTRINSIC FUNCTIONS
      intrinsic abs,log,sqrt
!
      data eps, sqeps / 2*0.0 /
!
      if (eps.eq.0.0) eps = 0.5*r1mach(3)
      if (sqeps.eq.0.0) sqeps = sqrt(r1mach(4))
!
     if (x.le.0.0 .or. a.lt.x) call xerror (&
     &  'R9LGIT  X SHOULD BE GT 0.0 AND LE A', 35, 2, 2)
!
      ax = a + x
      a1x = ax + 1.0
      r = 0.0
      p = 1.0
      s = p
      do 20 k=1,200
        fk = k
        t = (a+fk)*x*(1.0+r)
        r = t/((ax+fk)*(a1x+fk)-t)
        p = r*p
        s = s + p
        if (abs(p).lt.eps*s) go to 30
 20   continue
     call xerror (  'R9LGIT  NO CONVERGENCE IN 200 TERMS OF CONTINUED F&
     &RACTION', 57, 3, 2)
!
 30   hstar = 1.0 - x*s/a1x
     if (hstar.lt.sqeps) call xerror (&
     &  'R9LGIT  RESULT LESS THAN HALF PRECISION', 39, 1, 1)
!
      r9lgit = -x - algap1 - log(hstar)
!
      return
      end
