!CCFOUT
      SUBROUTINE CCFOUT (J, YMEANJ, YSDJ, NJ, NUSEDJ, K, YMEANK, YSDK,
     +   NK, NUSEDK, LAGMAX, NCC, RHOC, SDRHOC, IFMISS, NLPP12,
     +   NLPP21, LCCOV, YMISSJ, YMISSK, NEWPG)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE PRINTS THE CROSS CORRELATIONS AND THEIR
!     STANDARD ERRORS, AND MISCELLANEOUS SUMMARY INFORMATION.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 21, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) ::
     +   YMEANJ,YMEANK,YMISSJ,YMISSK,YSDJ,YSDK
      INTEGER
     +   J,K,LAGMAX,LCCOV,NCC,NJ,NK,NUSEDJ,NUSEDK
      LOGICAL
     +   IFMISS,NEWPG
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     +   RHOC(*),SDRHOC(*)
      INTEGER
     +   NLPP12(*),NLPP21(*)
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     +   FPLM,PMISSJ,PMISSK
      INTEGER
     +   IPRT,NMISSJ,NMISSK
!
!  LOCAL ARRAYS
      REAL(KIND=WP) ::
     +   YMMISS(1)
      INTEGER
     +   ISYM(1)
!
!  EXTERNAL FUNCTIONS
      REAL(KIND=WP) ::
     +   D1MACH
      EXTERNAL D1MACH
!
!  EXTERNAL SUBROUTINES
      EXTERNAL CCFLST,IPRINT,VERSP,VPMN
!
!  INTRINSIC FUNCTIONS
      INTRINSIC DBLE
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL(KIND=WP) :: FPLM
!        THE FLOATING POINT LARGEST MAGNITUDE.
!     LOGICAL IFMISS
!        THE INDICATOR VARIABLE USED TO DETERMINE
!        WHETHER THE INPUT SERIES HAS MISSING DATA OR NOT.
!     INTEGER IPRT
!        THE UNIT NUMBER OF THE DEVICE USED FOR PRINTED
!        OUTPUT.
!     INTEGER ISYM(1)
!        A DUMMY ARRAY.
!     INTEGER J, K
!        THE SUBSCRIPT VALUES OF THE TWO SERIES BEING COMPARED.
!     INTEGER LAGMAX
!        THE LARGEST LAG VALUE TO BE USED.
!     INTEGER LCCOV
!        THE NUMBER OF LOCATIONS ALLOWED FOR STORING THE NLPPC.
!     INTEGER NCC
!        THE NUMBER OF CROSS CORRELATIONS COMPUTED (FROM -LAGMAX
!        TO +LAGMAX).
!     LOGICAL NEWPG
!        AN INDICATOR VARIABLE USED TO DETERMINE IF THE OUTPUT SHOULD
!        START ON A NEW PAGE.
!     INTEGER NJ, NK
!        THE INTEGER NUMBER OF OBSERVATIONS IN EACH SERIES
!     INTEGER NLPP12(LCCOV), NLPP21(LCCOV)
!        THE NUMBER OF LAGGED PRODUCT PAIRS USED TO COMPUTE EACH
!        CCVF AT EACH LAG.
!     INTEGER NMISSJ, NMISSK
!        THE NUMBER OF MISSING VALUES IN EACH SERIES.
!     INTEGER NUSEDJ, NUSEDK
!        THE ACTIVE NUMBER OF OBSERVATIONS IN EACH SERIES.
!     REAL(KIND=WP) :: PMISSJ, PMISSK
!        THE PERCENT OF MISSING OBSERVATIONS.
!     REAL(KIND=WP) :: RHOC(NCC)
!        THE ARRAY IN WHICH THE CROSS CORRELATIONS ARE STORED
!     REAL(KIND=WP) :: SDRHOC(NCC)
!        THE ARRAY IN WHICH THE STANDARD ERRORS OF THE AUTOCORRELATIONS
!        ARE STORED
!     REAL(KIND=WP) :: YMEANJ, YMEANK
!        THE MEAN OF EACH OF THE SERIES.
!     REAL(KIND=WP) :: YMISSJ, YMISSK
!        THE MISSING VALUE CODE FOR EACH SERIES.
!     REAL(KIND=WP) :: YMMISS(1)
!        THE MISSING VALUE CODE FOR THE CROSS CORRELATIONS.
!     REAL(KIND=WP) :: YSDJ, YSDK
!        THE STANDARD DEVIAION OF EACH SERIES.
!
      FPLM = D1MACH(2)
!
      YMMISS(1) = FPLM
!
!     PRINT SUMMARY INFORMATION
!
      CALL IPRINT(IPRT)
!
      IF (NEWPG) WRITE (IPRT, 1004)
      CALL VERSP (.TRUE.)
      WRITE(IPRT, 1005)
!
      WRITE(IPRT, 1000) J, K, YMEANJ, YMEANK, YSDJ, YSDK, NJ, NK
      IF (.NOT. IFMISS) GO TO 10
      NMISSJ = NJ - NUSEDJ
      PMISSJ = 100.0E0_WP * NMISSJ / NJ
      NMISSK = NK - NUSEDK
      PMISSK = 100.0E0_WP * NMISSK / NK
      WRITE(IPRT, 1003) NMISSJ, NMISSK, PMISSJ, PMISSK
!
   10 WRITE(IPRT, 1006) LAGMAX
      IF (IFMISS) WRITE(IPRT, 1007) YMISSJ, YMISSK
!
      IF (YSDJ .GT. 0.0E0_WP .AND. YSDK .GT. 0.0E0_WP) GO TO 20
      WRITE (IPRT, 1008) J, K
      RETURN
!
!     PRINT CCF INFORMATION
!
   20 CONTINUE
      WRITE(IPRT, 1002)
      WRITE (IPRT, 1001) J, K
      CALL CCFLST (RHOC, SDRHOC, NLPP12, NLPP21, LAGMAX, LCCOV, NCC,
     +   IFMISS)
!
!     PLOT CCF INFORMATION
!
      WRITE(IPRT, 1004)
      CALL VERSP (.TRUE.)
      WRITE (IPRT, 1001) J, K
      CALL VPMN(RHOC, YMMISS, 2*LAGMAX+1, 1, 2*LAGMAX+1, 1, 0,
     +   ISYM, 1, 0, -1.0E0_WP, 1.0E0_WP, DBLE(-LAGMAX), 1.0E0_WP, IFMISS,
     +   0, 0, 1)
      RETURN
!
!     FORMAT STATEMENTS
!
 1000 FORMAT(/ 42X, 7HSERIES , I2, 5X, 7HSERIES , I2//
     +   41H AVERAGE OF THE SERIES                 = , 2G14.7/
     +   41H STANDARD DEVIATION OF THE SERIES      = , 2G14.7/
     +   41H NUMBER OF TIME POINTS                 = , 2(I10, 4X))
 1001 FORMAT (42H CROSS CORRELATION FUNCTION ESTIMATE (CCF)//
     +   22H CCF CORRELATES SERIES , I2, 22H AT TIME T WITH SERIES , I2,
     +   15H AT TIME T + K./
     +   5X, 55H(IF PEAK CORRELATION OCCURES AT POSITIVE (NEGATIVE) LAG/
     +   8X, 36HTHEN SERIES 1 LEADS (LAGS) SERIES 2))
 1002 FORMAT(//)
 1003 FORMAT (
     +   41H NUMBER OF MISSING OBSERVATIONS        = , 2(I10, 4X)/
     +   41H PERCENTAGE OF OBSERVATIONS MISSING    = , 2(F10.4, 4X))
 1004 FORMAT ('1')
 1005 FORMAT ( 27H CROSS CORRELATION ANALYSIS)
 1006 FORMAT(/
     +   41H LARGEST LAG VALUE TO BE USED          = , I10)
 1007 FORMAT(
     +   41H MISSING VALUE CODE                    = , 2G14.7)
 1008 FORMAT (//35H CROSS CORRELATIONS BETWEEN SERIES , I2, 5H AND ,
     +   I2, 22H COULD NOT BE COMPUTED/
     +   54H BECAUSE THE LAG ZERO AUTOCOVARIANCE OF ONE OR BOTH OF/
     +   20H THE SERIES IS ZERO.)
      END
