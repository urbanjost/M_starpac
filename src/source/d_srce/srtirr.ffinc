!SRTIRR
      SUBROUTINE SRTIRR(IR, RR, LA, A)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE SORTS THE LENGTH LA ARRAY A, THE LENGTH LA
!     INTEGER ARRAY IR, AND THE LENGTH LA ARRAY RR INTO
!     ASCENDING ORDER, BASED ON THE VALUES IN A.  THE ARRAY
!     A CONSTITUTES THE SORTING KEY.  THE OTHER TWO ARRAYS ARE
!     CARRIED ALONG.  ORDINARILY THE ARRAY IR CONTAINS THE
!     VALUES 1, ..., LA INITIALLY, SO THAT THE THREE ARRAYS CAN
!     LATER BE SORTED AGAIN WITH IR AS THE KEY, IN ORDER TO
!     RESTORE A AND RR TO THEIR ORIGINAL ORDER.
!
!     WRITTEN BY - JOHN E. KOONTZ
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  MAY 17, 1982
!        (BASED CLOSELY ON THE IMSL CDC LIBRARY 3 ROUTINE VSORTP)
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      INTEGER
     &   LA
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   A(LA),RR(LA)
      INTEGER
     &   IR(LA)
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     &   R,RT,RTT,T,TT
      INTEGER
     &   I,IJ,IT,ITT,J,K,L,M
!
!  LOCAL ARRAYS
      INTEGER
     &   IL(21),IU(21)
!
!  INTRINSIC FUNCTIONS
      INTRINSIC INT
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL(KIND=WP) :: A(LA)
!        INPUT/OUTPUT PARAMETER.  THE KEY ARRAY.
!     INTEGER I
!        *
!     INTEGER IJ
!        *
!     INTEGER IL(21)
!        *
!     INTEGER IR(LA)
!        INPUT/OUTPUT PARAMETER.  THE INTEGER ARRAY CARRIED ALONG
!        IN THE SORT.  INITIALLY IT SHOULD CONTAIN 1, ..., LA.
!        ON EXIT IT CONTAINS THE PERMUTATION VECTOR OF THE SORT.
!        SORTING ON THE PERMUTATION VECTOR WILL RESTORE THE KEY
!        ARRAY A AND THE ARRAY RR TO THEIR ORIGINAL ORDERS.
!     INTEGER IT
!        *
!     INTEGER ITT
!        *
!     INTEGER IU(21)
!        *
!     INTEGER J
!        *
!     INTEGER K
!        *
!     INTEGER L
!        *
!     INTEGER LA
!        INPUT PARAMETER.  THE LENGTH OF THE INPUT/OUTPUT PARAMETERS
!        A, IR, AND RR.
!     INTEGER M
!        *
!     REAL(KIND=WP) :: R
!        *
!     REAL(KIND=WP) :: RR(LA)
!        INPUT/OUTPUT PARAMETER.  THE ARRAY CARRIED ALONG IN
!        THE SORT.  IT MIGHT BE THE SET OF WEIGHTS FOR A.
!     REAL(KIND=WP) :: RT
!        *
!     REAL(KIND=WP) :: RTT
!        *
!     REAL(KIND=WP) :: T
!        *
!     REAL(KIND=WP) :: TT
!
      M = 1
      I = 1
      J = LA
      R = .375_WP
   10 IF (I.EQ.J) GO TO 90
      IF (R.GT.0.5898437_WP) GO TO 20
      R = R + 3.90625E-2_WP
      GO TO 30
   20 R = R - .21875_WP
   30 K = I
!                                  SELECT A CENTRAL ELEMENT OF THE
!                                  ARRAY AND SAVE IT IN LOCATION T
      IJ = I + INT((J-I)*R)
      T = A(IJ)
      IT = IR(IJ)
      RT = RR(IJ)
!                                  IF FIRST ELEMENT OF ARRAY IS GREATER
!                                  THAN T, INTERCHANGE WITH T
      IF (A(I).LE.T) GO TO 40
      A(IJ) = A(I)
      A(I) = T
      T = A(IJ)
      IR(IJ) = IR(I)
      IR(I) = IT
      IT = IR(IJ)
      RR(IJ) = RR(I)
      RR(I) = RT
      RT = RR(IJ)
   40 L = J
!                                  IF LAST ELEMENT OF ARRAY IS LESS THAN
!                                  T, INTERCHANGE WITH T
      IF (A(J).GE.T) GO TO 60
      A(IJ) = A(J)
      A(J) = T
      T = A(IJ)
      IR(IJ) = IR(J)
      IR(J) = IT
      IT = IR(IJ)
      RR(IJ) = RR(J)
      RR(J) = RT
      RT = RR(IJ)
!                                  IF FIRST ELEMENT OF ARRAY IS GREATER
!                                  THAN T, INTERCHANGE WITH T
      IF (A(I).LE.T) GO TO 60
      A(IJ) = A(I)
      A(I) = T
      T = A(IJ)
      IR(IJ) = IR(I)
      IR(I) = IT
      IT = IR(IJ)
      RR(IJ) = RR(I)
      RR(I) = RT
      RT = RR(IJ)
      GO TO 60
   50 TT = A(L)
      A(L) = A(K)
      A(K) = TT
      ITT = IR(L)
      IR(L) = IR(K)
      IR(K) = ITT
      RTT = RR(L)
      RR(L) = RR(K)
      RR(K) = RTT
!                                  FIND AN ELEMENT IN THE SECOND HALF OF
!                                  THE ARRAY WHICH IS SMALLER THAN T
   60 L = L - 1
      IF (A(L).GT.T) GO TO 60
!                                  FIND AN ELEMENT IN THE FIRST HALF OF
!                                  THE ARRAY WHICH IS GREATER THAN T
   70 K = K + 1
      IF (A(K).LT.T) GO TO 70
!                                  INTERCHANGE THESE ELEMENTS
      IF (K.LE.L) GO TO 50
!                                  SAVE UPPER AND LOWER SUBSCRIPTS OF
!                                  THE ARRAY YET TO BE SORTED
      IF (L-I.LE.J-K) GO TO 80
      IL(M) = I
      IU(M) = L
      I = K
      M = M + 1
      GO TO 100
   80 IL(M) = K
      IU(M) = J
      J = L
      M = M + 1
      GO TO 100
!                                  BEGIN AGAIN ON ANOTHER PORTION OF
!                                  THE UNSORTED ARRAY
   90 M = M - 1
      IF (M.EQ.0) RETURN
      I = IL(M)
      J = IU(M)
  100 IF (J-I.GE.1) GO TO 30
      IF (I.EQ.1) GO TO 10
      I = I - 1
  110 I = I + 1
      IF (I.EQ.J) GO TO 90
      T = A(I+1)
      IT = IR(I+1)
      RT = RR(I+1)
      IF (A(I).LE.T) GO TO 110
      K = I
  120 A(K+1) = A(K)
      IR(K+1) = IR(K)
      RR(K+1) = RR(K)
      K = K - 1
      IF (T.LT.A(K)) GO TO 120
      A(K+1) = T
      IR(K+1) = IT
      RR(K+1) = RT
      GO TO 110
      END
