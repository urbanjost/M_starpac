!PGORD
      SUBROUTINE PGORD (PER, NPTS, YAXIS, NPRT)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE PRODUCES CO-ORDINATES FOR THE PERIODOGRAM PLOT.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  DECEMBER 7, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      INTEGER
     +   NPRT,NPTS
!
!  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   PER(NPTS),YAXIS(NPTS)
!
!  LOCAL SCALARS
      DOUBLE PRECISION
     +   FPLM
      INTEGER
     +   I
!
!  EXTERNAL FUNCTIONS
      DOUBLE PRECISION
     +   D1MACH
      EXTERNAL D1MACH
!
!  INTRINSIC FUNCTIONS
      INTRINSIC IABS,LOG10
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     DOUBLE PRECISION FPLM
!        THE FLOATING POINT LARGEST MAGNITUDE.
!     INTEGER I
!        AN INDEX VARIABLE
!     INTEGER NPRT
!        THE VARIABLE CONTROLING PRINTED OUTPUT, WHERE
!        IF NPRT .LE. -2, THE OUTPUT CONSISTS OF A PAGE PLOT OF THE
!                         PERIODOGRAM ON A LOG-LINEAR SCALE,
!        IF NPRT .EQ. -1, THE OUTPUT CONSISTS OF A PAGE PLOT OF THE
!                         PERIODOGRAM IN DECIBELS ON A LINEAR SCALE,
!        IF NPRT .EQ.  0, THE OUTPUT IS SUPPRESSED,
!        IF NPRT .EQ.  1, THE OUTPUT CONSISTS OF A VERTICAL PLOT OF THE
!                         PERIODOGRAM IN DECIBELS ON A LINEAR SCALE.
!        IF NPRT .GE.  2, THE OUTPUT CONSISTS OF A VERTICAL PLOT OF THE
!                         PERIODOGRAM ON A LOG-LINEAR SCALE,
!     INTEGER NPTS
!        THE NUMBER OF FREQUENCIES FOR WHICH THE SPECTRAL ESTIMATES
!        ARE ESTIMATED.
!     DOUBLE PRECISION PER(NPTS)
!        THE ARRAY CONTAINING THE PERIODOGRAM VALUES.
!     DOUBLE PRECISION YAXIS(NPTS)
!        THE Y CO-ORDINATES FOR THE PERIODOGRAM PLOTS.
!
      FPLM = D1MACH(2)
!
!     THE FIRST VALUE SHOULD BE ZERO, SO NO ATTEMPT IS MADE TO PLOT IT.
!
      YAXIS(1) = FPLM
!
      DO 10 I = 2, NPTS
         YAXIS(I) = FPLM
         IF (PER(I) .LE. 0.0D0) GO TO 10
            YAXIS(I) = PER(I)
            IF (IABS(NPRT) .EQ. 1) YAXIS(I) = 10.0D0*LOG10(YAXIS(I))
   10 CONTINUE
!
      RETURN
!
      END
