!HSTMN
      SUBROUTINE HSTMN(Y, N, NCELLS, YLB, YUB, LSORT, YDIST)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS IS THE MAIN ROUTINE FOR PRODUCING A HISTOGRAM
!
!     ORIGINAL VERSION ADAPTED FROM AN EARLY VERSION OF MINITAB.
!
!     ADAPTED BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  MAY 17, 1982
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) ::
     &   YLB,YUB
      INTEGER
     &   N,NCELLS
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   Y(N),YDIST(NCELLS)
      INTEGER
     &   LSORT(N)
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     &   ALPHA,B1SQRT,B2,CFRACT,CFRCTM,CNTMX,FRACT,P,SCALE,SUM1,SUM2,
     &   SUM3,SUMD2,SUMD3,SUMD4,SUMDA,SUMT1,TEMP,WIDTH,XN,XNN,YINTMP,
     &   YMAX,YMDDSD,YMEAN,YMEANT,YMED,YMIDRG,YMIN,YRANGE,YSD,YVAR
      INTEGER
     &   I,IFLAG,IPRT,J,MID,NHIGH,NLOW,NOBS,NUM,NUMS
      CHARACTER
     &   IPLUS*1
!
!  LOCAL ARRAYS
      REAL(KIND=WP) ::
     &   A(6)
!
!  EXTERNAL SUBROUTINES
!      EXTERNAL GENI,IPRINT,SRTIR,SRTRI,STAT1,SUMBS,SUMDS,SUMSS,SUMTS,
!     +   VERSP
!
!  INTRINSIC FUNCTIONS
      INTRINSIC ABS,INT,MAX,MOD,NINT,SQRT
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL(KIND=WP) :: A(6)
!        A VECTOR USED FOR PRINTING THE HISTOGRAM SCALE.
!     REAL(KIND=WP) :: ALPHA
!        THE PERCENTAGE TO BE TRIMMED OFF EACH END OF Y FOR THE
!        TRIMMED MEANS CALCULATIONS.
!     REAL(KIND=WP) :: B1SQRT
!        BETA ONE  -  A MEASURE OF SKEWNESS
!     REAL(KIND=WP) :: B2
!        BETA TWO  -  A MEASURE OF KURTOSIS
!     REAL(KIND=WP) :: CFRACT
!        THE CUMULATIVE DISTRIBUTION
!     REAL(KIND=WP) :: CFRCTM
!        THE REVERSE CUMULATIVE DISTRIBUTION
!     REAL(KIND=WP) :: CNTMX
!        THE SIZE OF THE LARGEST CELL COUNT
!     REAL(KIND=WP) :: FRACT
!        THE FRACTION OF THE OBSERVATIONS IN A GIVEN CELL
!     INTEGER I
!        AN INDEX
!     INTEGER IFLAG
!        IF 1, THEN MORE THAN 50 OBS. FELL IN A SINGLE CELL,
!        AND A SCALED HISTOGRAM WILL BE PROVIDED.
!     CHARACTER*1 IPLUS
!        THE CHARACTER +
!     INTEGER IPRT
!        THE NUMBER OF THE STANDARD OUTPUT UNIT.
!     INTEGER J
!        AN INDEX.
!     INTEGER LSORT(N)
!        THE PERMUTATION VECTOR.
!     INTEGER MID
!        THE INDEX OF THE (AN) ELEMENT OF Y CLOSEST TO ZERO, WHEN
!        Y HAS BEEN SORTED.
!     INTEGER N
!        INPUT PARAMETER.  THE LENGTH OF Y.
!     INTEGER NCELLS
!        THE NUMBER OF CELLS IN THE FREQUENCY DISTRIBUTION.
!     INTEGER NHIGH
!        THE INDEX OF THE LARGEST VALUE IN THE SORTED ARRAY
!        TO BE USED IN THE HISTOGRAM
!     INTEGER NLOW
!        THE INDEX OF THE SMALLEST VALUE IN THE SORTED ARRAY
!        TO BE USED IN THE HISTOGRAM
!     INTEGER NOBS
!        THE NUMBER OF OBSERVATIONS ACTUALLY USED IN THE HISTOGRAM
!     INTEGER NUM
!        THE CELL COUNT
!     INTEGER NUMS
!        THE SCALED CELL COUNT
!     REAL(KIND=WP) :: P
!        A VARIABLE USED TO DETERMINE THE SCALE
!     REAL(KIND=WP) :: SCALE
!        THE PRINTED INCREMENT ON THE HISTOGRAM SCALE
!     REAL(KIND=WP) :: SUMDA
!        THE SUM OF THE ABSOLUTE DIFFERENCES FROM THE MEAN.
!     REAL(KIND=WP) :: SUMD2
!        THE SUM OF THE SQUARES OF THE DIFFERENCES.
!     REAL(KIND=WP) :: SUMD3
!        THE SUM OF THE CUBES OF THE DIFFERENCES.
!     REAL(KIND=WP) :: SUMD4
!        THE SUM OF THE 4TH POWERS OF THE DIFFERENCES.
!     REAL(KIND=WP) :: SUMT1
!        THE SUM OF THE ALPHA TRIMMED ARRAY Y.
!     REAL(KIND=WP) :: SUM1, SUM2, SUM3
!        VARIOUS SUMS OF THE DATA.
!     REAL(KIND=WP) :: TEMP
!        A TEMPORARY STORAGE VARIABLE
!     REAL(KIND=WP) :: WIDTH
!        THE WIDTH OF A CELL
!     REAL(KIND=WP) :: XN
!        THE FOATING POINT REPRESENTATION OF N
!     REAL(KIND=WP) :: XNN
!        THE UNROUNDED NUMBER OF PLOTTING POSISTIONS ON A SCALES
!        HISTOGRAM
!     REAL(KIND=WP) :: Y(N)
!        INPUT PARAMETER.  THE VECTOR OF DATA POINTS ON WHICH
!        THE STATISTICS ARE COMPUTED.  Y IS SORTED, BUT RESTORED
!        TO ITS ORIGINAL ORDER AFTERWARDS.
!     REAL(KIND=WP) :: YDIST(NCELLS)
!        THE FREQUENCY DISTRIBUTION USED TO CREATE THE HISTOGRAM.
!     REAL(KIND=WP) :: YINTMP
!        THE MIDPOINT OF THE ITH CELL
!     REAL(KIND=WP) :: YLB
!        THE LOWER BOUND FOR SELECTING DATA FROM Y FOR THE HISTOGRAM.
!     REAL(KIND=WP) :: YMAX
!        THE HISTOGRAM UPPER BOUND USED
!     REAL(KIND=WP) :: YMDDSD
!        THE MEAN ABSOLUTE DEVIATION / THE STANDARD DEVIATION
!     REAL(KIND=WP) :: YMEAN, YMEANT
!        THE MEAN OF THE OBSERVATIONS USED IN THE HISTOGRAM
!     REAL(KIND=WP) :: YMED
!        THE MEDIAN OF THE OBSERVATIONS USED IN THE HISTOGRAM
!     REAL(KIND=WP) :: YMIDRG
!        THE MID RANGE OF THE OBSERVATIONS USED IN THE HISTOGRAM
!     REAL(KIND=WP) :: YMIN
!        THE HISTOGRAM LOWER BOUND USED.
!     REAL(KIND=WP) :: YRANGE
!        THE RANGE OF THE OBSERVATIONS USED IN THE HISTOGRAM
!     REAL(KIND=WP) :: YSD
!        THE STANDARD DEVIATION OF THE OBSERVATIONS USED IN THE
!        HISTOGRAM.
!     REAL(KIND=WP) :: YUB
!        THE UPPER BOUND FOR SELECTING DATA FROM Y FOR THE HISTOGRAM.
!     REAL(KIND=WP) :: YVAR
!        THE VARIANCE OF THE OBSERVATIONS.
!
      DATA IPLUS /'+'/
      DATA ALPHA/0.25_WP/
!
      CALL IPRINT(IPRT)
!
!     SORT DATA
!
      CALL GENI(LSORT, N, 1, 1)
      CALL SRTIR(LSORT, N, Y)
!
!     FIX UPPER AND LOWER BOUNDS.
!
      NLOW = 1
      NHIGH = N
      IF (YLB.EQ.YUB) GO TO 50
!
!     FIND INDEX OF THE FIRST VALUE OF Y .GE. YLB
!
      DO 20 I=1,N
         IF (Y(I).LT.YLB) GO TO 20
         NLOW = I
         GO TO 30
   20 CONTINUE
!
!     FIND INDEX OF THE LAST VALUE OF Y .LE. YUB
!
   30 DO 40 I=1,N
         J = N - I + 1
         IF (Y(J).GT.YUB) GO TO 40
         NHIGH = J
         GO TO 50
   40 CONTINUE
   50 CONTINUE
      XN = NHIGH-NLOW+1
      NOBS = NHIGH - NLOW + 1
!
!     COMPUTE MEDIAN, EXTREMA, MID-RANGE, RANGE AND FREQUENCY
!     DISTRIBUTION FOR NCELLS CELLS
!
      CALL STAT1(Y(NLOW), NOBS, YMED, YMIN, YMAX, YMIDRG, YRANGE,
     &   NCELLS, YLB, YUB, YDIST)
!
      IF (YLB.GE.YUB) GO TO 55
!
      YMIN = YLB
      YMAX = YUB
!
   55 CONTINUE
!
!     COMPUTE MEAN, TRIMMED MEAN, STANDARD DEVIATION,
!     MEAN DEVIATION/STANDARD DEVIATION, BETA ONE, AND BETA TWO
!
      CALL SUMBS(Y, N, NLOW, MID, NHIGH)
      CALL SUMSS(Y, N, NLOW, MID, NHIGH, SUM1, SUM2, SUM3, YMEAN)
      CALL SUMTS(Y(NLOW), NOBS, ALPHA, SUMT1, YMEANT)
      CALL SUMDS(Y, N, NLOW, MID, NHIGH, YMEAN, SUMDA, SUMD2, SUMD3,
     &   SUMD4)
!
      YVAR = 0.0_WP
      YSD = 0.0_WP
      B1SQRT = 0.0_WP
      B2 = 0.0_WP
      YMDDSD = 0.0_WP
!
      IF ((SUMD2.LE.0.0_WP) .OR. (NOBS.LE.1)) GO TO 60
!
      YVAR = SUMD2/(NOBS-1)
      YSD = SQRT(YVAR)
      B1SQRT = ABS((SUMD3/XN)/((SUMD2/XN)**1.5_WP))
      B2 = (SUMD4/XN)/((SUMD2/XN)**2)
      YMDDSD = SUMDA/(YSD*NOBS)
!
   60 CONTINUE
!
!     OUTPUT STATISTICS
!
      CALL VERSP(.TRUE.)
      WRITE (IPRT,1070) N, Y(NLOW), Y(NHIGH), YMIN, YMAX
      WRITE (IPRT,1000)
     &   NCELLS, NOBS, YMEANT, Y(NLOW), YSD, Y(NHIGH), YMDDSD,
     &   YMEAN, B1SQRT, YMED, B2
      WRITE (IPRT,1010)
!
!     CHECK FOR MORE THAN 50 VALUES IN INTERVAL AND FIND MAX. VALUE.
!
      IFLAG = 0
      CNTMX = 0.0_WP
      DO 80 I=1,NCELLS
         IF (YDIST(I).GT.CNTMX) CNTMX = YDIST(I)
   80 CONTINUE
      IF (NINT(CNTMX).GT.50) IFLAG = 1
!
!     DETERMINE SCALE.
!
      IF (IFLAG.EQ.0) THEN
         SCALE = 1.0_WP
      ELSE
         P = CNTMX/XN
         SCALE = 0.05_WP
         IF (P.GT.0.25_WP) SCALE = 0.1_WP
         IF (P.GT.0.5_WP) SCALE = 0.2_WP
      END IF
!
!     PRINT COLUMN HEADINGS AND HISTOGRAM SCALE.
!
      IF (IFLAG.EQ.0) WRITE (IPRT,1020)
      IF (IFLAG.NE.0) WRITE (IPRT,1080)
      WRITE (IPRT,1090)
      IF (IFLAG.NE.0) GO TO 100
      WRITE (IPRT,1030) (I,I=10,50,10)
      GO TO 120
  100 A(1) = 0.0_WP
      DO 110 I=1,5
         A(I+1) = A(I) + SCALE
  110 CONTINUE
      WRITE (IPRT,1040) (A(I),I=1,6)
  120 WRITE (IPRT,1050)
      CFRACT = 0.0_WP
      CFRCTM = 1.0_WP
      TEMP = 0.0_WP
      WIDTH = (YMAX-YMIN)/NCELLS
      YINTMP = YMIN
      YINTMP = YINTMP - WIDTH/2.0_WP
      DO 150 I=1,NCELLS
         NUM = INT(YDIST(I)+0.5_WP)
         IF (MOD(NCELLS,2).EQ.1 .AND. I.EQ.NCELLS/2+1
     &       .AND. YMIN.EQ.(-YMAX)) THEN
            YINTMP = 0.0_WP
         ELSE
            YINTMP = YINTMP + WIDTH
         END IF
         FRACT = YDIST(I)/XN
         CFRACT = CFRACT + FRACT
         CFRCTM = 1.0_WP - TEMP
         TEMP = CFRACT
         IF (NUM.LE.0) THEN
           WRITE (IPRT,1060) YINTMP, CFRACT, CFRCTM, FRACT, NUM
         ELSE
           IF (IFLAG.EQ.0) THEN
             NUMS = NUM
           ELSE
             XNN = FRACT*10.0_WP/SCALE
             NUMS = INT(XNN)
             NUMS = MAX(1, NUMS + INT(XNN-NUMS+0.5_WP))
           END IF
           WRITE (IPRT,1060) YINTMP, CFRACT, CFRCTM, FRACT,
     &        NUM, (IPLUS,J=1,NUMS)
         END IF
  150 CONTINUE
!
!     RESTORE DATA TO ORIGINAL ORDER
!
      CALL SRTRI(Y, N, LSORT)
!
      RETURN
!
!     FORMAT STATEMENTS
!
 1000 FORMAT(/26H NUMBER OF CELLS        = , I15/
     &        26H OBSERVATIONS USED      = , I15, 11X,
     &              22H25 PCT TRIMMED MEAN = , 1PE15.8/
     &        26H MIN. OBSERVATION USED  = , E15.8, 11X,
     &              22HSTANDARD DEVIATION  = , E15.8/
     &        26H MAX. OBSERVATION USED  = , E15.8, 11X,
     &              22HMEAN DEV./STD. DEV. = , E15.8/
     &        26H MEAN VALUE             = , E15.8, 11X,
     &              22HSQRT(BETA ONE)      = , E15.8/
     &        26H MEDIAN VALUE           = , E15.8, 11X,
     &              22HBETA TWO            = , E15.8)
 1010 FORMAT(//44H FOR A NORMAL DISTRIBUTION, THE VALUES (MEAN,
     &   56H DEVIATION/STANDARD DEVIATION), SQRT(BETA ONE), AND BETA,
     &   22H TWO ARE APPROXIMATELY/
     &   ' 0.8, 0.0 AND 3.0, RESPECTIVELY.  TO TEST THE ',
     &   59HNULL HYPOTHESIS OF NORMALITY, SEE TABLES OF CRITICAL VALUES,
     &   13H PP. 207-208,/  22H BIOMETRIKA TABLES FOR,
     &   58H STATISTICIANS, VOL. 1.  SEE PP. 67-68 FOR A DISCUSSION OF,
     &   13H THESE TESTS.)
 1020 FORMAT(///5X,39HINTERVAL     CUM.   1-CUM.   CELL   NO.,19X,
     & 22HNUMBER OF OBSERVATIONS)
 1030 FORMAT('+',47X,1H0,8X,5(I2,8X))
 1040 FORMAT('+',46X,6(F4.2,6X))
 1050 FORMAT(4X,42('-'),2X,'+',5(10H---------+))
 1060 FORMAT(3X,1PE13.6,2X,2(0PF5.3,3X),F5.3,1X,I5,4X,50A1)
 1070 FORMAT (10H HISTOGRAM//
     &        26H NUMBER OF OBSERVATIONS = , I15/
     &        26H MINIMUM OBSERVATION    = , 1PE15.8/
     &        26H MAXIMUM OBSERVATION    = , E15.8//
     &        26H HISTOGRAM LOWER BOUND  = , E15.8/
     &        26H HISTOGRAM UPPER BOUND  = , E15.8)
 1080 FORMAT(///5X,39HINTERVAL     CUM.   1-CUM.   CELL   NO.,23X,
     & 13HCELL FRACTION)
 1090 FORMAT(5X,41HMID POINT   FRACT.  FRACT.  FRACT.  OBS. )
      END
