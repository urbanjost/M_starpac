!HSTMN
      SUBROUTINE HSTMN(Y, N, NCELLS, YLB, YUB, LSORT, YDIST)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS IS THE MAIN ROUTINE FOR PRODUCING A HISTOGRAM
!
!     ORIGINAL VERSION ADAPTED FROM AN EARLY VERSION OF MINITAB.
!
!     ADAPTED BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  MAY 17, 1982
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      DOUBLE PRECISION
     +   YLB,YUB
      INTEGER
     +   N,NCELLS
!
!  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   Y(N),YDIST(NCELLS)
      INTEGER
     +   LSORT(N)
!
!  LOCAL SCALARS
      DOUBLE PRECISION
     +   ALPHA,B1SQRT,B2,CFRACT,CFRCTM,CNTMX,FRACT,P,SCALE,SUM1,SUM2,
     +   SUM3,SUMD2,SUMD3,SUMD4,SUMDA,SUMT1,TEMP,WIDTH,XN,XNN,YINTMP,
     +   YMAX,YMDDSD,YMEAN,YMEANT,YMED,YMIDRG,YMIN,YRANGE,YSD,YVAR
      INTEGER
     +   I,IFLAG,IPRT,J,MID,NHIGH,NLOW,NOBS,NUM,NUMS
      CHARACTER
     +   IPLUS*1
!
!  LOCAL ARRAYS
      DOUBLE PRECISION
     +   A(6)
!
!  EXTERNAL SUBROUTINES
      EXTERNAL GENI,IPRINT,SRTIR,SRTRI,STAT1,SUMBS,SUMDS,SUMSS,SUMTS,
     +   VERSP
!
!  INTRINSIC FUNCTIONS
      INTRINSIC ABS,INT,MAX,MOD,NINT,SQRT
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     DOUBLE PRECISION A(6)
!        A VECTOR USED FOR PRINTING THE HISTOGRAM SCALE.
!     DOUBLE PRECISION ALPHA
!        THE PERCENTAGE TO BE TRIMMED OFF EACH END OF Y FOR THE
!        TRIMMED MEANS CALCULATIONS.
!     DOUBLE PRECISION B1SQRT
!        BETA ONE  -  A MEASURE OF SKEWNESS
!     DOUBLE PRECISION B2
!        BETA TWO  -  A MEASURE OF KURTOSIS
!     DOUBLE PRECISION CFRACT
!        THE CUMULATIVE DISTRIBUTION
!     DOUBLE PRECISION CFRCTM
!        THE REVERSE CUMULATIVE DISTRIBUTION
!     DOUBLE PRECISION CNTMX
!        THE SIZE OF THE LARGEST CELL COUNT
!     DOUBLE PRECISION FRACT
!        THE FRACTION OF THE OBSERVATIONS IN A GIVEN CELL
!     INTEGER I
!        AN INDEX
!     INTEGER IFLAG
!        IF 1, THEN MORE THAN 50 OBS. FELL IN A SINGLE CELL,
!        AND A SCALED HISTOGRAM WILL BE PROVIDED.
!     CHARACTER*1 IPLUS
!        THE CHARACTER +
!     INTEGER IPRT
!        THE NUMBER OF THE STANDARD OUTPUT UNIT.
!     INTEGER J
!        AN INDEX.
!     INTEGER LSORT(N)
!        THE PERMUTATION VECTOR.
!     INTEGER MID
!        THE INDEX OF THE (AN) ELEMENT OF Y CLOSEST TO ZERO, WHEN
!        Y HAS BEEN SORTED.
!     INTEGER N
!        INPUT PARAMETER.  THE LENGTH OF Y.
!     INTEGER NCELLS
!        THE NUMBER OF CELLS IN THE FREQUENCY DISTRIBUTION.
!     INTEGER NHIGH
!        THE INDEX OF THE LARGEST VALUE IN THE SORTED ARRAY
!        TO BE USED IN THE HISTOGRAM
!     INTEGER NLOW
!        THE INDEX OF THE SMALLEST VALUE IN THE SORTED ARRAY
!        TO BE USED IN THE HISTOGRAM
!     INTEGER NOBS
!        THE NUMBER OF OBSERVATIONS ACTUALLY USED IN THE HISTOGRAM
!     INTEGER NUM
!        THE CELL COUNT
!     INTEGER NUMS
!        THE SCALED CELL COUNT
!     DOUBLE PRECISION P
!        A VARIABLE USED TO DETERMINE THE SCALE
!     DOUBLE PRECISION SCALE
!        THE PRINTED INCREMENT ON THE HISTOGRAM SCALE
!     DOUBLE PRECISION SUMDA
!        THE SUM OF THE ABSOLUTE DIFFERENCES FROM THE MEAN.
!     DOUBLE PRECISION SUMD2
!        THE SUM OF THE SQUARES OF THE DIFFERENCES.
!     DOUBLE PRECISION SUMD3
!        THE SUM OF THE CUBES OF THE DIFFERENCES.
!     DOUBLE PRECISION SUMD4
!        THE SUM OF THE 4TH POWERS OF THE DIFFERENCES.
!     DOUBLE PRECISION SUMT1
!        THE SUM OF THE ALPHA TRIMMED ARRAY Y.
!     DOUBLE PRECISION SUM1, SUM2, SUM3
!        VARIOUS SUMS OF THE DATA.
!     DOUBLE PRECISION TEMP
!        A TEMPORARY STORAGE VARIABLE
!     DOUBLE PRECISION WIDTH
!        THE WIDTH OF A CELL
!     DOUBLE PRECISION XN
!        THE FOATING POINT REPRESENTATION OF N
!     DOUBLE PRECISION XNN
!        THE UNROUNDED NUMBER OF PLOTTING POSISTIONS ON A SCALES
!        HISTOGRAM
!     DOUBLE PRECISION Y(N)
!        INPUT PARAMETER.  THE VECTOR OF DATA POINTS ON WHICH
!        THE STATISTICS ARE COMPUTED.  Y IS SORTED, BUT RESTORED
!        TO ITS ORIGINAL ORDER AFTERWARDS.
!     DOUBLE PRECISION YDIST(NCELLS)
!        THE FREQUENCY DISTRIBUTION USED TO CREATE THE HISTOGRAM.
!     DOUBLE PRECISION YINTMP
!        THE MIDPOINT OF THE ITH CELL
!     DOUBLE PRECISION YLB
!        THE LOWER BOUND FOR SELECTING DATA FROM Y FOR THE HISTOGRAM.
!     DOUBLE PRECISION YMAX
!        THE HISTOGRAM UPPER BOUND USED
!     DOUBLE PRECISION YMDDSD
!        THE MEAN ABSOLUTE DEVIATION / THE STANDARD DEVIATION
!     DOUBLE PRECISION YMEAN, YMEANT
!        THE MEAN OF THE OBSERVATIONS USED IN THE HISTOGRAM
!     DOUBLE PRECISION YMED
!        THE MEDIAN OF THE OBSERVATIONS USED IN THE HISTOGRAM
!     DOUBLE PRECISION YMIDRG
!        THE MID RANGE OF THE OBSERVATIONS USED IN THE HISTOGRAM
!     DOUBLE PRECISION YMIN
!        THE HISTOGRAM LOWER BOUND USED.
!     DOUBLE PRECISION YRANGE
!        THE RANGE OF THE OBSERVATIONS USED IN THE HISTOGRAM
!     DOUBLE PRECISION YSD
!        THE STANDARD DEVIATION OF THE OBSERVATIONS USED IN THE
!        HISTOGRAM.
!     DOUBLE PRECISION YUB
!        THE UPPER BOUND FOR SELECTING DATA FROM Y FOR THE HISTOGRAM.
!     DOUBLE PRECISION YVAR
!        THE VARIANCE OF THE OBSERVATIONS.
!
      DATA IPLUS /'+'/
      DATA ALPHA/0.25D0/
!
      CALL IPRINT(IPRT)
!
!     SORT DATA
!
      CALL GENI(LSORT, N, 1, 1)
      CALL SRTIR(LSORT, N, Y)
!
!     FIX UPPER AND LOWER BOUNDS.
!
      NLOW = 1
      NHIGH = N
      IF (YLB.EQ.YUB) GO TO 50
!
!     FIND INDEX OF THE FIRST VALUE OF Y .GE. YLB
!
      DO 20 I=1,N
         IF (Y(I).LT.YLB) GO TO 20
         NLOW = I
         GO TO 30
   20 CONTINUE
!
!     FIND INDEX OF THE LAST VALUE OF Y .LE. YUB
!
   30 DO 40 I=1,N
         J = N - I + 1
         IF (Y(J).GT.YUB) GO TO 40
         NHIGH = J
         GO TO 50
   40 CONTINUE
   50 CONTINUE
      XN = NHIGH-NLOW+1
      NOBS = NHIGH - NLOW + 1
!
!     COMPUTE MEDIAN, EXTREMA, MID-RANGE, RANGE AND FREQUENCY
!     DISTRIBUTION FOR NCELLS CELLS
!
      CALL STAT1(Y(NLOW), NOBS, YMED, YMIN, YMAX, YMIDRG, YRANGE,
     +   NCELLS, YLB, YUB, YDIST)
!
      IF (YLB.GE.YUB) GO TO 55
!
      YMIN = YLB
      YMAX = YUB
!
   55 CONTINUE
!
!     COMPUTE MEAN, TRIMMED MEAN, STANDARD DEVIATION,
!     MEAN DEVIATION/STANDARD DEVIATION, BETA ONE, AND BETA TWO
!
      CALL SUMBS(Y, N, NLOW, MID, NHIGH)
      CALL SUMSS(Y, N, NLOW, MID, NHIGH, SUM1, SUM2, SUM3, YMEAN)
      CALL SUMTS(Y(NLOW), NOBS, ALPHA, SUMT1, YMEANT)
      CALL SUMDS(Y, N, NLOW, MID, NHIGH, YMEAN, SUMDA, SUMD2, SUMD3,
     +   SUMD4)
!
      YVAR = 0.0D0
      YSD = 0.0D0
      B1SQRT = 0.0D0
      B2 = 0.0D0
      YMDDSD = 0.0D0
!
      IF ((SUMD2.LE.0.0D0) .OR. (NOBS.LE.1)) GO TO 60
!
      YVAR = SUMD2/(NOBS-1)
      YSD = SQRT(YVAR)
      B1SQRT = ABS((SUMD3/XN)/((SUMD2/XN)**1.5D0))
      B2 = (SUMD4/XN)/((SUMD2/XN)**2)
      YMDDSD = SUMDA/(YSD*NOBS)
!
   60 CONTINUE
!
!     OUTPUT STATISTICS
!
      CALL VERSP(.TRUE.)
      WRITE (IPRT,1070) N, Y(NLOW), Y(NHIGH), YMIN, YMAX
      WRITE (IPRT,1000)
     +   NCELLS, NOBS, YMEANT, Y(NLOW), YSD, Y(NHIGH), YMDDSD,
     +   YMEAN, B1SQRT, YMED, B2
      WRITE (IPRT,1010)
!
!     CHECK FOR MORE THAN 50 VALUES IN INTERVAL AND FIND MAX. VALUE.
!
      IFLAG = 0
      CNTMX = 0.0D0
      DO 80 I=1,NCELLS
         IF (YDIST(I).GT.CNTMX) CNTMX = YDIST(I)
   80 CONTINUE
      IF (NINT(CNTMX).GT.50) IFLAG = 1
!
!     DETERMINE SCALE.
!
      IF (IFLAG.EQ.0) THEN
         SCALE = 1.0D0
      ELSE
         P = CNTMX/XN
         SCALE = 0.05D0
         IF (P.GT.0.25D0) SCALE = 0.1D0
         IF (P.GT.0.5D0) SCALE = 0.2D0
      END IF
!
!     PRINT COLUMN HEADINGS AND HISTOGRAM SCALE.
!
      IF (IFLAG.EQ.0) WRITE (IPRT,1020)
      IF (IFLAG.NE.0) WRITE (IPRT,1080)
      WRITE (IPRT,1090)
      IF (IFLAG.NE.0) GO TO 100
      WRITE (IPRT,1030) (I,I=10,50,10)
      GO TO 120
  100 A(1) = 0.0D0
      DO 110 I=1,5
         A(I+1) = A(I) + SCALE
  110 CONTINUE
      WRITE (IPRT,1040) (A(I),I=1,6)
  120 WRITE (IPRT,1050)
      CFRACT = 0.0D0
      CFRCTM = 1.0D0
      TEMP = 0.0D0
      WIDTH = (YMAX-YMIN)/NCELLS
      YINTMP = YMIN
      YINTMP = YINTMP - WIDTH/2.0D0
      DO 150 I=1,NCELLS
         NUM = INT(YDIST(I)+0.5D0)
         IF (MOD(NCELLS,2).EQ.1 .AND. I.EQ.NCELLS/2+1
     +       .AND. YMIN.EQ.(-YMAX)) THEN
            YINTMP = 0.0D0
         ELSE
            YINTMP = YINTMP + WIDTH
         END IF
         FRACT = YDIST(I)/XN
         CFRACT = CFRACT + FRACT
         CFRCTM = 1.0D0 - TEMP
         TEMP = CFRACT
         IF (NUM.LE.0) THEN
           WRITE (IPRT,1060) YINTMP, CFRACT, CFRCTM, FRACT, NUM
         ELSE
           IF (IFLAG.EQ.0) THEN
             NUMS = NUM
           ELSE
             XNN = FRACT*10.0D0/SCALE
             NUMS = INT(XNN)
             NUMS = MAX(1, NUMS + INT(XNN-NUMS+0.5D0))
           END IF
           WRITE (IPRT,1060) YINTMP, CFRACT, CFRCTM, FRACT,
     +        NUM, (IPLUS,J=1,NUMS)
         END IF
  150 CONTINUE
!
!     RESTORE DATA TO ORIGINAL ORDER
!
      CALL SRTRI(Y, N, LSORT)
!
      RETURN
!
!     FORMAT STATEMENTS
!
 1000 FORMAT(/26H NUMBER OF CELLS        = , I15/
     +        26H OBSERVATIONS USED      = , I15, 11X,
     +              22H25 PCT TRIMMED MEAN = , 1PE15.8/
     +        26H MIN. OBSERVATION USED  = , E15.8, 11X,
     +              22HSTANDARD DEVIATION  = , E15.8/
     +        26H MAX. OBSERVATION USED  = , E15.8, 11X,
     +              22HMEAN DEV./STD. DEV. = , E15.8/
     +        26H MEAN VALUE             = , E15.8, 11X,
     +              22HSQRT(BETA ONE)      = , E15.8/
     +        26H MEDIAN VALUE           = , E15.8, 11X,
     +              22HBETA TWO            = , E15.8)
 1010 FORMAT(//44H FOR A NORMAL DISTRIBUTION, THE VALUES (MEAN,
     +   56H DEVIATION/STANDARD DEVIATION), SQRT(BETA ONE), AND BETA,
     +   22H TWO ARE APPROXIMATELY/
     +   ' 0.8, 0.0 AND 3.0, RESPECTIVELY.  TO TEST THE ',
     +   59HNULL HYPOTHESIS OF NORMALITY, SEE TABLES OF CRITICAL VALUES,
     +   13H PP. 207-208,/  22H BIOMETRIKA TABLES FOR,
     +   58H STATISTICIANS, VOL. 1.  SEE PP. 67-68 FOR A DISCUSSION OF,
     +   13H THESE TESTS.)
 1020 FORMAT(///5X,39HINTERVAL     CUM.   1-CUM.   CELL   NO.,19X,
     + 22HNUMBER OF OBSERVATIONS)
 1030 FORMAT('+',47X,1H0,8X,5(I2,8X))
 1040 FORMAT('+',46X,6(F4.2,6X))
 1050 FORMAT(4X,42('-'),2X,'+',5(10H---------+))
 1060 FORMAT(3X,1PE13.6,2X,2(0PF5.3,3X),F5.3,1X,I5,4X,50A1)
 1070 FORMAT (10H HISTOGRAM//
     +        26H NUMBER OF OBSERVATIONS = , I15/
     +        26H MINIMUM OBSERVATION    = , 1PE15.8/
     +        26H MAXIMUM OBSERVATION    = , E15.8//
     +        26H HISTOGRAM LOWER BOUND  = , E15.8/
     +        26H HISTOGRAM UPPER BOUND  = , E15.8)
 1080 FORMAT(///5X,39HINTERVAL     CUM.   1-CUM.   CELL   NO.,23X,
     + 13HCELL FRACTION)
 1090 FORMAT(5X,41HMID POINT   FRACT.  FRACT.  FRACT.  OBS. )
      END
