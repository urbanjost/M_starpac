!CMPFD
      SUBROUTINE CMPFD(N,STP,PVSTP,PV,FD)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE COMPUTES A FINITE DIFFERENCE DERIVATIVE,
!     ASSUMING THAT IF THE DIFFERENCE BETWEEN PVSTP(I) AND PV(I) IS
!     SMALL ENOUGH THE DERIVATIVE IS ZERO.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  JUNE 30, 1987
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      DOUBLE PRECISION
     +   STP
      INTEGER
     +   N
!
!  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   FD(*),PV(*),PVSTP(*)
!
!  LOCAL SCALARS
      DOUBLE PRECISION
     +   FPLRS
      INTEGER
     +   I
!
!  EXTERNAL FUNCTIONS
      DOUBLE PRECISION
     +   D1MACH
      EXTERNAL D1MACH
!
!  INTRINSIC FUNCTIONS
      INTRINSIC ABS,MIN
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     INTEGER I
!        AN INDEX VARIABLE.
!     DOUBLE PRECISION FD(N)
!        THE FINITE-DIFFERENCE DERIVATIVE.
!     DOUBLE PRECISION FPLRS
!        THE FLOATING POINT LARGEST RELATIVE SPACING.
!     DOUBLE PRECISION PV(N)
!        THE PREDICTED VALUES AT THE CURRENT PARAMETER VALUE.
!     DOUBLE PRECISION PVSTP(N)
!        THE PREDICTED VALUES AT THE CURRENT PARAMETER VALUE PLUS STP.
!     DOUBLE PRECISION STP
!        THE STEP.
!
      FPLRS = D1MACH(4)
!
      DO 10 I=1,N
         FD(I) = PVSTP(I) - PV(I)
         IF (ABS(FD(I)).GE.5*FPLRS*MIN(ABS(PVSTP(I)),ABS(PV(I)))) THEN
            FD(I) = FD(I) / STP
         ELSE
            FD(I) = 0.0D0
         END IF
   10 CONTINUE
      RETURN
      END
