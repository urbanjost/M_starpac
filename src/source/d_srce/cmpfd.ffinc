!CMPFD
      subroutine cmpfd(n,stp,pvstp,pv,fd)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE COMPUTES A FINITE DIFFERENCE DERIVATIVE,
!     ASSUMING THAT IF THE DIFFERENCE BETWEEN PVSTP(I) AND PV(I) IS
!     SMALL ENOUGH THE DERIVATIVE IS ZERO.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  JUNE 30, 1987
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     real(kind=wp) ::&
     &   stp
     integer&
     &   n
!
!  ARRAY ARGUMENTS
     real(kind=wp) ::&
     &   fd(*),pv(*),pvstp(*)
!
!  LOCAL SCALARS
     real(kind=wp) ::&
     &   fplrs
     integer&
     &   i
!
!  EXTERNAL FUNCTIONS
     real(kind=wp) ::&
     &   d1mach
!       EXTERNAL D1MACH
!
!  INTRINSIC FUNCTIONS
      intrinsic abs,min
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     INTEGER I
!        AN INDEX VARIABLE.
!     REAL(KIND=WP) :: FD(N)
!        THE FINITE-DIFFERENCE DERIVATIVE.
!     REAL(KIND=WP) :: FPLRS
!        THE FLOATING POINT LARGEST RELATIVE SPACING.
!     REAL(KIND=WP) :: PV(N)
!        THE PREDICTED VALUES AT THE CURRENT PARAMETER VALUE.
!     REAL(KIND=WP) :: PVSTP(N)
!        THE PREDICTED VALUES AT THE CURRENT PARAMETER VALUE PLUS STP.
!     REAL(KIND=WP) :: STP
!        THE STEP.
!
      fplrs = d1mach(4)
!
      do 10 i=1,n
         fd(i) = pvstp(i) - pv(i)
         if (abs(fd(i)).ge.5*fplrs*min(abs(pvstp(i)),abs(pv(i)))) then
            fd(i) = fd(i) / stp
         else
            fd(i) = 0.0_wp
         end if
   10 continue
      return
      end
