!SUMBS
      SUBROUTINE SUMBS(X, N, LO, MID, HI)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     A ROUTINE TO FIND A ZERO OR VALUE CLOSEST TO ZERO IN
!     A SORTED VECTOR.
!
!     WRITTEN BY - JOHN E. KOONTZ
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS
!
!     CREATION DATE  -  MAY 17, 1982
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      INTEGER
     +   HI,LO,MID,N
!
!  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   X(N)
!
!  LOCAL SCALARS
      INTEGER
     +   CURHI,CURLO
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     INTEGER CURHI
!        THE UPPER BOUND OF THE CURRENT INTERVAL.
!     INTEGER CURLO
!        THE LOWER BOUND OF THE CURRENT INTERVAL.
!     INTEGER HI
!        INPUT PARAMETER.  THE UPPER BOUND OF THE INITIAL
!        INTERVAL.
!     INTEGER LO
!        INPUT PARAMETER.  THE LOWER BOUND OF THE INITIAL
!        INTERVAL.
!     INTEGER MID
!        OUTPUT PARAMETER.  THE MIDPOINT OF THE CURRENT
!        INTERVAL, AND ON EXIT THE POINT IN THE INITIAL
!        INTERVAL CLOSEST TO ZERO.
!     INTEGER N
!        INPUT PARAMETER.  THE LENGTH OF THE ARRAY X.
!     DOUBLE PRECISION X(N)
!        INPUT PARAMETER.  THE ARRAY X IN WHICH THE SEARCH FOR
!        A (NEAR) ZERO VALUE IS MADE.
!
!     BINARY SEARCH OF X FOR 0.0D0 OR CLOSEST TO IT.
!
      CURLO = LO
      CURHI = HI
!
!     SEE IF ZERO IS OUT OF THE RANGE X(LO) TO X(HI).
!
      IF (X(CURLO).LT.0.0D0) GO TO 10
      MID = CURLO
      GO TO 50
   10 IF (X(CURHI).GT.0.0D0) GO TO 20
      MID = CURHI
      GO TO 50
!
!     LOCATE EITHER A ZERO OR A PAIR OF ADJACENT VALUES BETWEEN
!     WHICH THERE LIES A ZERO.
!
   20 IF (CURLO+1.EQ.CURHI) GO TO 30
      MID = (CURLO+CURHI)/2
      IF (0.0D0.LT.X(MID)) CURHI = MID
      IF (0.0D0.GT.X(MID)) CURLO = MID
      IF (0.0D0.EQ.X(MID)) GO TO 50
      GO TO 20
!
!     PICK AS MIDPOINT THE CURRENT ENDPOINT CLOSEST TO ZERO.
!
   30 IF (X(CURHI)+X(CURLO).GT.0.0D0) GO TO 40
      MID = CURHI
      GO TO 50
   40 MID = CURLO
   50 RETURN
      END
