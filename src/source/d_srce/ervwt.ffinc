!ERVWT
      SUBROUTINE ERVWT (NMSUB, NMVAR1, WT, N, NNZWMN, HEAD, NNZW,
     +   NZW, MSGTYP, ERROR, NMVAR2)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE CHECKS USER SUPPLIED WEIGHTS TO ASSURE THERE
!     ARE NO NEGATIVE WEIGHTS, AND THAT THERE ARE SUFFICIENT POSITIVE
!     WEIGHTS FOR THE TASK.  IT RETURNS ERROR SET TO TRUE
!     IF NO ERRORS ARE FOUND, AND SET TO FALSE OTHERWISE, AND IN
!     ADDITION, RETURNS THE NUMBER OF NONZERO WEIGHTS AND THE NUMBER
!     OF ZERO WEIGHTS.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  DECEMBER 29, 1982
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      INTEGER
     +   MSGTYP,N,NNZW,NNZWMN,NZW
      LOGICAL
     +   ERROR,HEAD
!
!  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   WT(*)
      CHARACTER
     +   NMSUB(6)*1,NMVAR1(8)*1,NMVAR2(8)*1
!
!  LOCAL SCALARS
      INTEGER
     +   I,IPRT
!
!  EXTERNAL SUBROUTINES
      EXTERNAL EHDR,IPRINT
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     LOGICAL ERROR
!        VALUE(S) INDICATING WHETHER AN ERROR WAS DETECTED (TRUE) OR NOT
!        (FALSE).
!     LOGICAL HEAD
!        A FLAG INDICATING WHETHER THE HEADING SHOULD BE PRINTED
!        (TRUE) OR NOT (FALSE).  IF A HEADING IS PRINTED, THE VALUE
!        OF HEAD WILL BE CHANGED TO FALSE.
!     INTEGER I
!        AN INDEX ARGUMENT.
!     INTEGER IPRT
!        THE UNIT NUMBER FOR PRINTED OUTPUT.
!     INTEGER MSGTYP
!        AN ARGUMENT USED TO INDICATE THE TYPE OF MESSAGE TO BE
!        PRINTED, WHERE IF ERROR IS TRUE AND
!        MSGTYP = 1, THE INPUT VALUE WAS TOO SMALL BASED ON LIMITS
!                    IMPOSED BY STARPAC.
!        MSGTYP = 2, THE INPUT VALUE WAS TOO SMALL BASED ON OTHER
!                    INPUT ARGUMENTS.
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS.
!     CHARACTER*1 NMSUB(6)
!        THE CHARACTERS OF THE CALLING SUBROUTINES NAME.
!     CHARACTER*1 NMVAR1(8)
!        THE CHARACTERS OF THE NAME OF THE ARGUMENT BEING CHECKED.
!     CHARACTER*1 NMVAR2(8)
!        THE CHARACTERS OF THE NAME OF THE ARGUMENT BEING CHECKED
!        AGAINST.
!     INTEGER NNZW
!        THE NUMBER OF NONZERO WEIGHTS.
!     INTEGER NNZWMN
!        THE MINIMUM NUMBER OF NONZERO WEIGHTS WHICH IS ACCEPTABLE.
!     INTEGER NZW
!        THE NUMBER OF ZERO WEIGHTS.
!     DOUBLE PRECISION WT(N)
!        THE WEIGHT VECTOR.
!
      ERROR = .FALSE.
!
      IF (N.LE.0) RETURN
!
!     CHECK FOR NEGATIVE WEIGHTS AND COUNT NUMBER OF ZERO WEIGHTS.
!
      NZW = 0
      DO 10 I = 1, N
         IF (WT(I) .LT. 0.0D0) GO TO 20
         IF (WT(I) .EQ. 0.0D0) NZW = NZW + 1
   10 CONTINUE
!
      NNZW = N - NZW
      IF (NNZW .GE. NNZWMN) RETURN
!
!     INSUFFICIENT NUMBER OF POSITIVE WEIGHTS FOUND
!
      ERROR = .TRUE.
!
      CALL IPRINT(IPRT)
!
      CALL EHDR(NMSUB, HEAD)
!
      WRITE (IPRT, 1010) NNZW
      IF (MSGTYP.EQ.1) WRITE(IPRT, 1030) (NMVAR1(I), I=1,6), NNZWMN
      IF (MSGTYP.EQ.2) WRITE(IPRT,1040) (NMVAR1(I),I=1,6),
     +   (NMVAR2(I),I=1,8)
!
      RETURN
!
!     NEGATIVE WEIGHTS FOUND
!
   20 ERROR = .TRUE.
!
      CALL IPRINT(IPRT)
      CALL EHDR(NMSUB, HEAD)
      WRITE(IPRT, 1020) (NMVAR1(I), I = 1, 6)
!
      RETURN
!
!     FORMAT STATEMENTS
!
 1010 FORMAT(/
     +   40H THE NUMBER OF NONZERO WEIGHTS FOUND IS , I6, '.')
 1020 FORMAT(/
     +   42H NEGATIVE VALUES WERE FOUND IN THE VECTOR , 6A1, '.'/
     +   51H ALL WEIGHTS MUST BE GREATER THAN OR EQUAL TO ZERO.)
 1030 FORMAT(
     +   34H THE NUMBER OF NONZERO WEIGHTS IN , 6A1,
     +   34H MUST BE GREATER THAN OR EQUAL TO , I6, '.')
 1040 FORMAT(
     +   34H THE NUMBER OF NONZERO WEIGHTS IN , 6A1,
     +   34H MUST BE GREATER THAN OR EQUAL TO , 8A1, '.')
!
      END
