!ERSGT
      SUBROUTINE ERSGT(NMSUB, NMVAR, VAL, VALMN, MSGTYP, HEAD, ERROR,
     +   NMMIN)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THE ROUTINE CHECKS WHETHER THE VALUE   VAL   IS GREATER THAN
!        VALMN   , AND PRINTS A DIAGNOSTIC IF IT IS NOT.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  JUNE 10, 1982
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      DOUBLE PRECISION
     +   VAL,VALMN
      INTEGER
     +   MSGTYP
      LOGICAL
     +   ERROR,HEAD
!
!  ARRAY ARGUMENTS
      CHARACTER
     +   NMMIN(8)*1,NMSUB(6)*1,NMVAR(8)*1
!
!  LOCAL SCALARS
      INTEGER
     +   I,IPRT
!
!  EXTERNAL SUBROUTINES
      EXTERNAL EHDR,IPRINT
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     LOGICAL ERROR
!        VALUE(S) INDICATING WHETHER AN ERROR WAS DETECTED (TRUE) OR NOT
!        (FALSE).
!     LOGICAL HEAD
!        A FLAG INDICATING WHETHER THE HEADING SHOULD BE PRINTED
!        (TRUE) OR NOT (FALSE).  IF A HEADING IS PRINTED, THE VALUE
!        OF HEAD WILL BE CHANGED TO FALSE.
!     INTEGER I
!        AN INDEX ARGUMENT.
!     INTEGER IPRT
!        THE UNIT NUMBER FOR PRINTED OUTPUT.
!     INTEGER MSGTYP
!        AN ARGUMENT USED TO INDICATE THE TYPE OF MESSAGE TO BE
!        PRINTED, WHERE IF ERROR IS .TRUE. AND
!        MSGTYP = 1 THE INPUT VALUE WAS TOO SMALL BASED
!                   ON LIMITS IMPOSED BY STARPAC
!        MSGTYP = 2 THE INPUT VALUE WAS TOO SMALL BASED ON
!                   LIMITS BASED ON OTHER INPUT ARGUMENTS.
!     CHARACTER*1 NMMIN(8)
!        THE NAME OF THE ARGUMENT SPECIFYING THE MINIMUM.
!     CHARACTER*1 NMSUB(6)
!        THE CHARACTERS OF THE CALLING SUBROUTINES NAME.
!     CHARACTER*1 NMVAR(8)
!        THE CHARACTERS OF THE ARGUMENTS NAME.
!     DOUBLE PRECISION VAL
!        THE INPUT VALUE OF THE ARGUMENT BEING CHECKED.
!     DOUBLE PRECISION VALMN
!        THE MINIMUM VALUE THE ARGUMENT CAN VALIDLY HAVE.
!
      ERROR = .FALSE.
!
      IF (VAL .GT. VALMN) RETURN
!
      ERROR = .TRUE.
      CALL IPRINT (IPRT)
      CALL EHDR(NMSUB, HEAD)
!
      WRITE (IPRT, 1000) (NMVAR(I),I=1,6), VAL
!
      GO TO (10, 20), MSGTYP
!
   10 WRITE (IPRT, 1010) (NMVAR(I),I=1,6), VALMN
      RETURN
!
   20 WRITE (IPRT, 1020) (NMVAR(I),I=1,6), (NMMIN(I),I=1,8)
      RETURN
!
!     FORMAT STATEMENTS
!
 1000 FORMAT (/20H THE INPUT VALUE OF , 6A1, 4H IS , G15.8, '.')
 1010 FORMAT(
     +   27H THE VALUE OF THE ARGUMENT , 6A1,
     +   22H MUST BE GREATER THAN , G21.14, '.')
 1020 FORMAT(
     +   27H THE VALUE OF THE ARGUMENT , 6A1,
     +   22H MUST BE GREATER THAN , 8A1, '.')
!
      END
