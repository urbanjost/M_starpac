!AMLST
      SUBROUTINE AMLST (IAMHD, PAR, NPAR, NFAC, MSPECT, N, VCVL,
     &   LVCVL, SCALE, LSCALE, STPT, LSTPT, IFIXD, RSS, RSD, NPARDF,
     &   NPARE, IDF)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE PRINTS THE PARAMETER SUMMARY OUTPUT FROM THE
!     ARIMA FORECASTING SUBROUTINES.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  JANUARY 4, 1982
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) ::
     &   RSD,RSS
      INTEGER
     &   IAMHD,IDF,LSCALE,LSTPT,LVCVL,N,NFAC,NPAR,NPARDF,NPARE
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   PAR(*),SCALE(*),STPT(*),VCVL(*)
      INTEGER
     &   IFIXD(*),MSPECT(NFAC,4)
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     &   FPLM,T975
      INTEGER
     &   IPARMN,IPARMX,IPRT,LBLTYP
!
!  EXTERNAL FUNCTIONS
!      REAL(KIND=WP) ::
!     +   PPFT,D1MACH
!      EXTERNAL PPFT,D1MACH
!
!  EXTERNAL SUBROUTINES
!      EXTERNAL AMLST1,IPRINT
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL(KIND=WP) :: FPLM
!        THE FLOATING POINT LARGEST MAGNITUDE.
!     INTEGER IAMHD
!        THE INDICATOR VALUE USED TO DESIGNATE THE TYPE OF LIST
!        TO BE GENERATED
!        IF IAMHD=1, THE LIST IS FOR THE INITIAL SUMMARY OF THE
!                    ESTIMATION ROUTINES.
!        IF IAMHD=2, THE LIST IS FOR THE INITIAL REPORT OF THE
!                    FORECASTING ROUTINES.
!        IF IAMHD=3, THE LIST IS FOR THE FINAL REPORT OF THE
!                    ESTIMATION ROUTINES.
!     INTEGER IDF
!        THE DEGREES OF FREEDOM IN THE FIT.
!     INTEGER IFIXD(NPAR)
!        THE INDICATOR VALUES USED TO DESIGNATE WHETHER THE
!        PARAMETERS ARE TO BE OPTIMIZED OR ARE TO BE HELD FIXED.
!        IF IFIXED(I).NE.0, THEN PAR(I) WILL BE HELD FIXED.
!        IF IFIXED(I).EQ.0, THEN PAR(I) WILL BE OPTIMIZED.
!     INTEGER IPARMN
!        THE SMALLEST PARAMETER INDEX INCLUDED IN THIS TERM.
!     INTEGER IPARMX
!        THE LARGEST PARAMETER INDEX INCLUDED IN THIS TERM.
!     INTEGER IPRT
!        THE UNIT NUMBER FOR PRINTED OUTPUT.
!     INTEGER LSCALE
!        THE DIMENSION OF VECTOR SCALE.
!     INTEGER LSTPT
!        THE DIMENSION OF VECTOR STPT.
!     INTEGER LVCVL
!        THE DIMENSION OF VECTOR VCVL.
!     INTEGER MSPECT(NFAC,4)
!        THE ARRAY CONTAINING THE VALUES OF P, D, Q, AND S FOR EACH FACT
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS.
!     INTEGER NPARDF
!        THE ORDER OF THE EXPANDED DIFFERENCE FILTER.
!     INTEGER NFAC
!        THE NUMBER OF FACTORS IN THE MODEL
!     INTEGER NPAR
!        THE NUMBER OF PARAMETERS IN THE MODEL.
!     INTEGER NPARE
!        THE NUMBER OF PARAMETERS TO BE OPTIMIZED.
!     REAL(KIND=WP) :: PAR(NPAR)
!        THE CURRENT ESTIMATES OF THE PARAMETERS.
!     REAL(KIND=WP) :: RSD
!        THE RESIDUAL STANDARD DEVIATION.
!     REAL(KIND=WP) :: RSS
!        THE RESIDUAL SUM OF SQUARES.
!     REAL(KIND=WP) :: SCALE(LSCALE)
!        THE TYPICAL SIZE OF THE PARAMETERS.
!     REAL(KIND=WP) :: STPT(LSTPT)
!        THE STEP SIZE ARRAY.
!     REAL(KIND=WP) :: T975
!        THE VALUE OF THE 97.5 PERCENT POINT FUNCTION FOR THE
!        T DISTRIBUTION.
!     REAL(KIND=WP) :: VCVL(LVCVL)
!        THE LOWER HALF OF THE VARIANCE-COVARIANCE MATRIX, STORED
!        ROW WISE.
!
      FPLM = D1MACH(2)
!
      CALL IPRINT(IPRT)
!
!     PRINT HEADING FOR INFORMATION ABOUT PARAMETERS
!
      WRITE(IPRT, 1001)
!
      IF (IAMHD .EQ. 1) WRITE(IPRT, 1004)
      IF (IAMHD .EQ. 2) WRITE(IPRT, 1005)
      IF (IAMHD .EQ. 3) WRITE(IPRT, 1006)
      WRITE(IPRT, 1001)
!
!     PRINT MODEL SUMMARY INFORMATION
!
      IPARMN = 1
      IPARMX = 0
      T975 = PPFT(0.95_WP, N-NPAR)
!
!     PRINT AUTOREGRESSIVE TERMS
!
      LBLTYP = 1
      CALL AMLST1 (IAMHD, PAR, NPAR, MSPECT, NFAC, VCVL, LVCVL,
     &  SCALE, LSCALE, STPT, LSTPT, IPARMN, IPARMX, LBLTYP, T975, IFIXD)
!
!     PRINT MEAN OR TREND TERM
!
      LBLTYP = 2
      CALL AMLST1 (IAMHD, PAR, NPAR, MSPECT, 1, VCVL, LVCVL,
     &  SCALE, LSCALE, STPT, LSTPT, IPARMN, IPARMX, LBLTYP, T975, IFIXD)
!
!     PRINT MOVING AVERAGE TERMS
!
      LBLTYP = 3
      CALL AMLST1 (IAMHD, PAR, NPAR, MSPECT, NFAC, VCVL, LVCVL,
     &  SCALE, LSCALE, STPT, LSTPT, IPARMN, IPARMX, LBLTYP, T975, IFIXD)
!
      WRITE (IPRT, 1160) N
      IF (IAMHD.GE.2)
     &   WRITE (IPRT, 1040) RSS, RSD, N, NPARDF, NPARE, IDF
      RETURN
!
!     FORMAT STATEMENTS
!
 1001 FORMAT(1X)
 1004 FORMAT (//73X, '  --STEP SIZE FOR'/
     &  39X, '  ------PARAMETER', 17X, '  --APPROXIMATING'/
     &  ' -----------------PARAMETER DESCRIPTION  STARTING VALUES',
     &  '  ----------SCALE  -----DERIVATIVE'/
     &  ' INDEX  ---------TYPE  --ORDER  --FIXED  ----------(PAR)',
     &  '  --------(SCALE)  ----------(STP)')
 1005 FORMAT(30X, '  ------PARAMETER'/
     &  ' --------PARAMETER DESCRIPTION  ------ESTIMATES'/
     &  ' INDEX  ---------TYPE  --ORDER  ----------(PAR)')
 1006 FORMAT(
     &  39X, '  ------PARAMETER  -----STD DEV OF', 17X,
     &  '  ---------------------APPROXIMATE'/
     &  ' -----------------PARAMETER DESCRIPTION  ------ESTIMATES',
     &  '  ------PARAMETER  ----------RATIO',
     &  '  ----95 PERCENT CONFIDENCE LIMITS'/
     &  ' INDEX  ---------TYPE  --ORDER  --FIXED  ----------(PAR)',
     &  '  ------ESTIMATES',
     &  '  PAR/(SD OF PAR)  ----------LOWER  ----------UPPER')
 1040 FORMAT (//' RESIDUAL SUM OF SQUARES       ', 8X, G15.7,
     &  '  (BACKFORECASTS INCLUDED)'//6H RESID,
     &   25HUAL STANDARD DEVIATION   , 8X, G15.7/19H BASED ON DEGREES O,
     &   9HF FREEDOM, 1X, I4, 3H - , I3, 3H - , I3, 3H = , I4)
 1160 FORMAT (//23H NUMBER OF OBSERVATIONS, 48X, 3H(N), 1X, I5)
      END
