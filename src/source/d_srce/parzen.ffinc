!PARZEN
      SUBROUTINE PARZEN (LAG, W, LW)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS SUBROUTINE COMPUTES AND STORES THE PARZEN LAG WINDOW
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  DECEMBER 7, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      INTEGER
     +   LAG,LW
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     +   W(LW)
!
!  LOCAL SCALARS
      INTEGER
     +   K,L
!
!  INTRINSIC FUNCTIONS
      INTRINSIC DBLE
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     INTEGER K
!        AN INDEX VARIABLE
!     INTEGER L
!        THE VALUE LAG/2.
!     INTEGER LAG
!        THE LAG WINDOW TRUNCATION POINT USED FOR A SPECIFIC WINDOW.
!     INTEGER LW
!        THE LENGTH OF THE VECTOR W.
!     REAL(KIND=WP) :: W(LW)
!        THE VECTOR OF LAG WINDOWS.
!
      L = LAG/2
      W(1) = 1.0E0_WP
      IF (L.LE.0) GO TO 15
      DO 10 K = 1, L
         W(K+1) = DBLE(K) / DBLE(LAG)
         W(K+1) = 1.0E0_WP + 6.0E0_WP * W(K+1) * W(K+1) * (W(K+1) - 1.0E0_WP)
   10 CONTINUE
!
   15 CONTINUE
      L = L + 1
      DO 20 K = L, LAG
         W(K+1) = 1.0E0_WP - DBLE(K) / DBLE(LAG)
         W(K+1) = 2.0E0_WP * W(K+1) * W(K+1) * W(K+1)
   20 CONTINUE
!
      RETURN
      END
