!PARZEN
      subroutine parzen (lag, w, lw)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS SUBROUTINE COMPUTES AND STORES THE PARZEN LAG WINDOW
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  DECEMBER 7, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     integer&
     &   lag,lw
!
!  ARRAY ARGUMENTS
     real(kind=wp) ::&
     &   w(lw)
!
!  LOCAL SCALARS
     integer&
     &   k,l
!
!  INTRINSIC FUNCTIONS
      intrinsic dble
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     INTEGER K
!        AN INDEX VARIABLE
!     INTEGER L
!        THE VALUE LAG/2.
!     INTEGER LAG
!        THE LAG WINDOW TRUNCATION POINT USED FOR A SPECIFIC WINDOW.
!     INTEGER LW
!        THE LENGTH OF THE VECTOR W.
!     REAL(KIND=WP) :: W(LW)
!        THE VECTOR OF LAG WINDOWS.
!
      l = lag/2
      w(1) = 1.0_wp
      if (l.le.0) go to 15
      do 10 k = 1, l
         w(k+1) = dble(k) / dble(lag)
         w(k+1) = 1.0_wp + 6.0_wp * w(k+1) * w(k+1) * (w(k+1) - 1.0_wp)
   10 continue
!
   15 continue
      l = l + 1
      do 20 k = l, lag
         w(k+1) = 1.0_wp - dble(k) / dble(lag)
         w(k+1) = 2.0_wp * w(k+1) * w(k+1) * w(k+1)
   20 continue
!
      return
      end
