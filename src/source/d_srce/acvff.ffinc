!ACVFF
      SUBROUTINE ACVFF (YFFT, N, NFFT, YMEAN, ACOV, LAGMAX, LACOV,
     &   LYFFT, WORK, LWORK)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE COMPUTES THE ACVF OF A SERIES USING TWO
!     PASSES OF A FFT.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 21, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) ::
     &   YMEAN
      INTEGER
     &   LACOV,LAGMAX,LWORK,LYFFT,N,NFFT
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   ACOV(*),WORK(*),YFFT(*)
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     &   FAC
      INTEGER
     &   I,ISN,N1,NF,NFFT2
!
!  EXTERNAL SUBROUTINES
      EXTERNAL AMEAN,FFT,FFTCT,REALTR
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL(KIND=WP) :: ACOV(LACOV)
!        THE ARRAY IN WHICH THE AUTOCOVARIANCES ARE STORED
!     REAL(KIND=WP) :: FAC
!        A FACTOR USED IN THE COMPUTATIONS.
!     INTEGER I
!        AN INDEX VARIABLE.
!     INTEGER ISN
!        AN INDICATOR VARIABLE USED BY THE FFT ROUTINES.
!     INTEGER LACOV
!        THE LENGTH OF THE VECTOR ACOV.
!     INTEGER LAGMAX
!        THE MAXIMUM LAG TO BE USED.
!     INTEGER LWORK
!        THE LENGTH OF THE VECTOR WORK.
!     INTEGER LYFFT
!        THE LENGTH OF THE VECTOR YFFT.
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS IN THE SERIES
!     INTEGER NF
!        THE NUMBER OF FOURIER FREQUENCIES.
!     INTEGER NFFT
!        THE NUMBER OF OBSERVATIONS IN THE EXTENDED SERIES.
!     INTEGER NFFT2
!        THE EFFECTIVE NUMBER OF OBSERVATIONS FOR THE FFT TRANSFORM.
!     INTEGER N1
!        THE VALUE N + 1.
!     REAL(KIND=WP) :: WORK(LWORK)
!        THE WORK AREA FOR THE COMPUTATIONS.
!     REAL(KIND=WP) :: YFFT(LYFFT)
!        THE VECTOR CONTAINING THE OBSERVED SERIES
!     REAL(KIND=WP) :: YMEAN
!        THE MEAN OF THE OBSERVED TIME SERIES
!
!     COMPUTE ARITHMETIC MEAN
!
      CALL AMEAN(YFFT, N, YMEAN)
!
!     SUBTRACT THE MEAN FROM THE SERIES
!
      DO 10 I = 1, N
         YFFT(I) = YFFT(I) - YMEAN
   10 CONTINUE
!
!     APPEND ZEROS
!
      N1 = N + 1
!
      DO 20 I = N1, NFFT
         YFFT(I) = 0.0_WP
   20 CONTINUE
!
!     COMPUTE AUTOCOVARIANCES.
!
      ISN = 2
      NFFT2 = (NFFT - 2)/ 2
!
      CALL FFT(YFFT(1), YFFT(2), NFFT2, NFFT2, NFFT2, ISN)
      CALL REALTR (YFFT(1), YFFT(2), NFFT2, ISN)
!
      NF = NFFT2 + 1
!
      DO 30 I = 1, NF
         WORK(I) = YFFT(2*I-1)*YFFT(2*I-1) + YFFT(2*I)*YFFT(2*I)
   30 CONTINUE
!
      CALL FFTCT (WORK, NFFT2, LWORK)
!
      FAC = 1.0_WP / (4 * (NFFT - 2) * N)
!
      ACOV(1) = WORK(1) * FAC
!
      DO 40 I = 1, LAGMAX
         ACOV(I+1) = WORK(I+1) * FAC
   40 CONTINUE
!
      RETURN
      END
