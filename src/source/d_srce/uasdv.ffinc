!UASDV
      SUBROUTINE UASDV(ACOV, SPCA, SPCF, LSPC, IAR, PHI, NF, FMIN, FMAX,
     +   FREQ, N, LAGMAX, FTEST, AIC, WORK, LACOV, LWORK, DELTA, ISORT,
     +   ISYM, XAXIS, YAXIS, LPCV, ALPHA, LAG, LAIC, LPHI, NPRT, VAR,
     +   WINDOW, NMSUB)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS IS THE MAIN DRIVER FOR COMPUTING THE AUTOREGRESSIVE
!     (AND FOURIER) SPECTRUMS.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  DECEMBER 7, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      DOUBLE PRECISION
     +   ALPHA,DELTA,FMAX,FMIN,VAR
      INTEGER
     +   IAR,LACOV,LAG,LAGMAX,LAIC,LPCV,LPHI,LSPC,LWORK,N,NF,NPRT
!
!  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   ACOV(LACOV),AIC(LAIC),FREQ(NF),FTEST(2,LAGMAX),PHI(LPHI),
     +   SPCA(LSPC),SPCF(LSPC),WORK(LWORK),XAXIS(LPCV),YAXIS(LPCV)
      INTEGER
     +   ISORT(NF),ISYM(LPCV)
      CHARACTER
     +   NMSUB(6)*1
!
!  SUBROUTINE ARGUMENTS
      EXTERNAL WINDOW
!
!  LOCAL SCALARS
      DOUBLE PRECISION
     +   ALOW,AUP,BW,DF,SPCAMN,SPCAMX,SPCFMN,SPCFMX,XPLTMN,XPLTMX,
     +   YPLTMN,YPLTMX
      INTEGER
     +   ISPCER,NPTS,NSPCA,NSPCF,NW
      LOGICAL
     +   AICPRT
!
!  LOCAL ARRAYS
      INTEGER
     +   LAGS(1),NLPPA(1)
!
!  EXTERNAL SUBROUTINES
      EXTERNAL AOS,SETFRQ,SPCCK,UASCFT,UASEST,UASORD,UASOUT,UFSLAG,UFSMN
!
!  INTRINSIC FUNCTIONS
      INTRINSIC IABS,INT
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     DOUBLE PRECISION ACOV(LACOV)
!        THE AUTOCOVARIANCE COMPUTED FROM THE LAG PRODUCT PAIRS.
!     DOUBLE PRECISION AIC(LAIC)
!        THE ARRAY CONTAINING THE AKAIKES CRITERIA FOR EACH ORDER(?).
!     LOGICAL AICPRT
!        AN INDICATOR VARIABLE USED TO DETERMINE IF THE AKIAKE
!        INFORMATION CRITERIA AND CHI SQUARED STATISTICS SHOULD
!        BE PRINTED.
!     DOUBLE PRECISION ALOW
!        A FACTOR USED TO COMPUTE THE LOWER CONFIDENCE LIMITS.
!     DOUBLE PRECISION ALPHA
!        THE DESIRED CONFIDENCE LEVEL.
!     DOUBLE PRECISION AUP
!        A FACTOR USED TO COMPUTE THE UPPER CONFIDENCE LIMITS.
!     DOUBLE PRECISION BW
!        THE BANDWIDTH.
!     DOUBLE PRECISION DELTA
!        THE SAMPLING INTERVAL.
!     DOUBLE PRECISION DF
!        THE EFFECTIVE DEGREES OF FREEDOM.
!     DOUBLE PRECISION FMAX, FMIN
!        THE MAXIMUM AND MINIMUM FREQUENCY FOR WHICH THE
!        SPECTRUM ESTIMATES ARE TO BE COMPUTED.
!     DOUBLE PRECISION FREQ(NF)
!        THE VECTOR OF FREQUENCIES AT WHICH THE SPECTRUM IS TO BE
!        ESTIMATED.
!     DOUBLE PRECISION FTEST(2,LAGMAX)
!        THE ARRAY IN WHICH THE F RATIO AND PROBABILITY ARE STORED.
!        THE ORDER OF THE AUTOREGRESSIVE PROCESS CHOSEN.
!     INTEGER ISORT(NF)
!        AN ARRAY USED FOR SORTING.
!     INTEGER LSPC
!         THE ACTUAL FIRST DIMENSION FOR THE SPECTRUM ARRAYS.
!     INTEGER ISPCER
!        AN ERROR FLAG USED FOR THE SPECTRUM PLOTS.
!     INTEGER ISYM(LPCV)
!        THE ARRAY CONTAINING THE CODE FOR THE PLOT SYMBOLS.
!     INTEGER LACOV
!        THE LENGTH OF THE COVARIANCE ARRAYS.
!     INTEGER LAG
!        THE LAG WINDOW TRUNCATION POINT USED FOR A SPECIFIC WINDOW.
!     INTEGER LAGMAX
!        THE MAXIMUM LAG VALUE TO BE USED.
!     INTEGER LAGS(1)
!        THE LAG WINDOW TRUNCATION POINT RETURNED FROM UFSLAG.
!     INTEGER LAIC
!        THE LENGTH OF THE ARRAY AIC.
!     INTEGER LPCV
!        THE LENGTH OF THE PLOT CO-ORDINATE VECTORS.
!     INTEGER LPHI
!        THE LENGTH OF THE VECTOR PHI.
!     INTEGER LWORK
!        THE ACTUAL LENGTH OF THE WORK ARRAY.
!     INTEGER N
!        THE INTEGER NUMBER OF OBSERVATIONS IN THE SERIES
!     INTEGER NF
!        THE NUMBER OF FREQUENCIES FOR WHICH THE SPECTRUM ESTIMATES
!        ARE TO BE ESTIMATED.
!     INTEGER NLPPA(1)
!        A DUMMY ARRAY
!     CHARACTER*1 NMSUB(6)
!        THE NAME OF THE CALLING SUBROUTINE.
!     INTEGER NPRT
!        A CODE USED TO SPECIFY THE TYPE OF PLOT, WHERE IF
!        NPRT < 0 THE PLOT IS DECIBLES/LINEAR
!        NPRT = 0 THE PLOT IS SUPPRESSED
!        NPRT > 0 THE PLOT IS LOG/LINEAR
!     INTEGER NPTS
!        THE NUMBER OF X, Y CO-ORDINATES TO BE PLOTTED.
!     INTEGER NSPCA, NSPCF
!        THE NUMBER OF VALID SPECTRUM ESTIMATES FOR THE AUTOREGRESSIVE
!        AND FOURIER SPECTRUMS, RESPECTIVELY.
!     INTEGER NW
!        THE NUMBER OF LAG WINDOW TRUNCATION POINTS SELCTED.
!     DOUBLE PRECISION PHI(LPHI)
!        THE ARRAY OF AUTOREGRESSIVE COEFFICIENTS FOR THE
!        SELECTED ORDER.
!     DOUBLE PRECISION SPCA(LSPC)
!        THE ARAY CONTAINING THE AUTOREGRESSIVE SPECTRUM ESTIMATES.
!     DOUBLE PRECISION SPCAMN, SPCAMX
!        THE MINIMUM AND MAXIMUM AUTOREGRESSIVE SPECTRUM VALUE TO BE
!        PLOTTED.
!     DOUBLE PRECISION SPCF(LSPC)
!        THE ARRAY CONTAINING THE FOURIER SPECTRUM ESTIMATES.
!     DOUBLE PRECISION SPCFMN, SPCFMX
!        THE MINIMUM AND MAXIMUM FOURIER SPECTRUM VALUE TO BE PLOTTED.
!     DOUBLE PRECISION VAR
!        THE ONE STEP PREDICTION VARIANCE.
!     EXTERNAL WINDOW
!        THE TYPE OF WINDOW TO BE USED.
!     DOUBLE PRECISION WORK(LWORK)
!        THE WORK ARRAY.
!     DOUBLE PRECISION XAXIS(LPCV)
!        THE X AXIS VALUES FOR THE SPECTRUM PLOT.
!     DOUBLE PRECISION XPLTMN, XPLTMX
!        THE MINIMUM AND MAXIMUM VALUES TO BE PLOTTED FOR THE X AXIS.
!     DOUBLE PRECISION YAXIS(LPCV)
!        THE Y AXIS VALUES FOR THE SPECTRUM PLOT.
!     DOUBLE PRECISION YPLTMN, YPLTMX
!        THE MINIMUM AND MAXIMUM VALUES TO BE PLOTTED FOR THE Y AXIS.
!
      NW = 1
!
      IF (LAG.LE.0) THEN
!
!     SET THE LAG WINDOW TRUNCATION POINT TO BE USED FOR THE
!     FOURIER SPECTRUM ESTIMATES.
!
         CALL UFSLAG(ACOV, LAGMAX, LAGS, N, NW, NW, LACOV)
         LAG = LAGS(1)/2
      END IF
!
!     SET FREQUENCIES FOR THE SPECTRUM.
!
      CALL SETFRQ(FREQ, NF, 1, FMIN, FMAX, DELTA)
!
!     COMPUTE THE FOURIER SPECTRUM ESTIMATES
!
      CALL UFSMN(ACOV, NLPPA, LAG, DF, NF, FREQ, ALPHA, BW, SPCF,
     +  ALOW, AUP, LACOV, LSPC, WINDOW, WORK, LWORK, N, DELTA,
     +  .FALSE., 1)
!
      AICPRT = .FALSE.
!
      IF (IAR.LT.0) THEN
!
!     USER HAS CHOSEN ORDER.
!     COMPUTE COEFFICIENTS AND VARIANCE USING DURBINS RECURSIVE METHOD.
!
         CALL UASCFT(ACOV, LAGMAX, LACOV, IABS(IAR), PHI, N, VAR)
!
      ELSE IF (IAR.EQ.0) THEN
!
!     SELECT MODEL ORDER AND COMPUTE COEFFICIENTS AND VARIANCE.
!
         AICPRT = .TRUE.
         CALL AOS(N, LAGMAX, ACOV, WORK, IAR, VAR, PHI,
     +            WORK, AIC, FTEST, LACOV, LAIC)
      END IF
!
!     COMPUTE THE AUTOREGRESSIVE SPECTRUM ESTIMATES.
!
      CALL UASEST(IABS(IAR), VAR, PHI, NF, FREQ, DELTA, SPCA, LPHI,
     +   LSPC)
!
      IF (NPRT.EQ.0) RETURN
!
!     SET PLOTTING VECTORS.
!
      XPLTMN = FMIN
      XPLTMX = FMAX
!
      YPLTMN = 0.0D0
      YPLTMX = 0.0D0
!
      CALL SPCCK(SPCF, ISORT, NF, SPCFMN, SPCFMX, NSPCF, ISPCER)
      IF (ISPCER.NE.0) GO TO 40
      CALL SPCCK(SPCA, ISORT, NF, SPCAMN, SPCAMX, NSPCA, ISPCER)
      IF (ISPCER.NE.0) GO TO 40
!
      CALL UASORD(SPCF, SPCA, SPCFMN, SPCFMX, SPCAMN, SPCAMX, FREQ, NF,
     +   XAXIS, YAXIS, ISYM, NPTS, LSPC, LPCV, NSPCF, NSPCA, BW, ALOW,
     +   AUP, XPLTMN, XPLTMX, YPLTMN, YPLTMX, NPRT)
!
!     PRINT RESULTS
!
   40 CALL UASOUT(XAXIS, YAXIS, ISYM, NPTS, BW, INT(DF+0.5D0), LAG,
     +   IABS(IAR), PHI, ISPCER, LPCV, XPLTMN, XPLTMX, YPLTMN, YPLTMX,
     +   FTEST, AIC, LAIC, VAR, NPRT, LAGMAX, AICPRT, N, NMSUB)
!
      RETURN
      END
