!NRANDC
      SUBROUTINE NRANDC(Y, N, ISEED, YMEAN, SIGMA)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS SUBROUTINE GENERATES N NORMALLY DISTRIBUTED PSEUDO-
!     RANDOM NUMBERS WITH MEAN YMEAN AND STANDARD DEVIATION SIGMA.  THE
!     NUMBERS GENERATED ARE DETERMINED BY ISEED.  THEY ARE RETURNED IN Y
!
!     ORIGIN - CONCEIVED BY DR. PETER TRYON TO FACILITATE USE OF
!          EXISTING RANDOM NUMBER GENERATOR
!
!     WRITTEN BY -
!          JOHN E. KOONTZ AND JANET R. DONALDSON
!          STATISTICAL ENGINEERING DIVISION
!          NATIONAL BUREAU OF STANDARDS,
!          BOULDER, COLORADO 80302
!
!     CREATION DATE  -  DECEMBER 2, 1985
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      DOUBLE PRECISION
     +   SIGMA,YMEAN
      INTEGER
     +   ISEED,N
!
!  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   Y(*)
!
!  SCALARS IN COMMON
      INTEGER
     +   IERR
!
!  LOCAL SCALARS
      INTEGER
     +   I,IPRT,ISEEDU
      LOGICAL
     +   ERR01,ERR02,HEAD
!
!  LOCAL ARRAYS
      CHARACTER
     +   LN(8)*1,LONE(8)*1,LSIGMA(8)*1,LZERO(8)*1,NMSUB(6)*1
!
!  EXTERNAL FUNCTIONS
      DOUBLE PRECISION
     +   RANDN
      EXTERNAL RANDN
!
!  EXTERNAL SUBROUTINES
      EXTERNAL EISGE,EISRNG,ERSGE,IPRINT
!
!  COMMON BLOCKS
      COMMON /ERRCHK/IERR
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     LOGICAL ERR01, ERR02
!        VALUE(S) INDICATING WHETHER AN ERROR WAS DETECTED (TRUE) OR NOT
!        (FALSE).
!     LOGICAL HEAD
!        A FLAG INDICATING WHETHER THE HEADING SOULD BE PRINTED
!        (TRUE) OR NOT (FALSE).  IF A HEADING IS PRINTED, THE VALUE
!        OF HEAD WILL BE CHANGED TO FALSE.
!     INTEGER I
!          THE INDEX OF THE COMPUTING LOOP
!     INTEGER IERR
!        THE INTEGER VALUE RETURNED BY THEIS ROUTINE DESIGNATING
!        WHETHER ANY ERRORS WERE DETECTED IN THE PARAMETER LIST.
!        IF IERR .EQ. 0, NO ERRORS WERE DETECTED.
!        IF IERR .EQ. 1, ERRORS HAVE BEEN FOUND.
!     INTEGER IPRT
!        THE STANDARD OUTPUT FILE UNIT NUMBER
!     INTEGER ISEED
!        THE ISEED TO THE RANDOM NUMBER GENERATOR.
!        ISEED MUST LIE BETWEEN 0 AND 2**((MIN(32,I1MACH(8)+1))-1) -1,
!        INCLUSIVE.  IF ISEED IS NOT EQUAL TO 0, ISEED MUST BE ODD.
!     INTEGER ISEEDU
!        THE VALUE OF THE SEED ACTUALLY USED.
!     CHARACTER*1 LN(8), LONE(8), LSIGMA(8), LZERO(8)
!        THE ARRAY(S) CONTAINING THE NAME(S) OF THE VARIABLES(S) CHECKED
!        FOR ERRORS
!     INTEGER N
!        THE LENGTH OF DATA SET GENERATED
!     CHARACTER*1 NMSUB(6)
!        THE NAME OF THIS SUBROUTINE
!     DOUBLE PRECISION SIGMA
!        THE STANDARD DEVIATION OF THE GENERATED VALUES.
!     DOUBLE PRECISION Y(N)
!        THE GENERATED RANDOM VALUES.
!     DOUBLE PRECISION YMEAN
!        THE MEAN OF THE GENERATED VALUES.
!
      DATA  NMSUB(1), NMSUB(2), NMSUB(3), NMSUB(4), NMSUB(5), NMSUB(6)
     +   /       'N',      'R',      'A',      'N',      'D',      'C'/
      DATA     LN(1),    LN(2),    LN(3),    LN(4),    LN(5),    LN(6),
     +         LN(7),    LN(8)/'N', ' ', ' ', ' ', ' ', ' ', ' ', ' '/
      DATA   LONE(1),  LONE(2),  LONE(3),  LONE(4),  LONE(5),  LONE(6),
     +       LONE(7),  LONE(8)/'O', 'N', 'E', ' ', ' ', ' ', ' ', ' '/
      DATA LSIGMA(1),LSIGMA(2),LSIGMA(3),LSIGMA(4),LSIGMA(5),LSIGMA(6),
     +     LSIGMA(7),LSIGMA(8)/'S', 'I', 'G', 'M', 'A', ' ', ' ', ' '/
      DATA  LZERO(1), LZERO(2), LZERO(3), LZERO(4), LZERO(5), LZERO(6),
     +      LZERO(7), LZERO(8)/'Z', 'E', 'R', 'O', ' ', ' ', ' ', ' '/
!
      IERR = 0
!
      HEAD = .TRUE.
!
!     CHECK FOR INPUT ERRORS
!
      CALL EISGE(NMSUB, LN, N, 1, 2, HEAD, ERR01, LONE)
      CALL ERSGE(NMSUB, LSIGMA, SIGMA, 0.0D0, 2, HEAD, ERR02, LZERO)
      CALL EISRNG(NMSUB, ISEED, ISEEDU, HEAD)
!
      IF (ERR01.OR.ERR02) THEN
!
        CALL IPRINT(IPRT)
        WRITE (IPRT,1000)
        IERR = 1
!
      ELSE
!
!     GENERATE THE PSEUDO-RANDOM NUMBERS
!
        Y(1) = RANDN(ISEEDU)
        DO 20 I=1,N
           Y(I) = RANDN(0)*SIGMA + YMEAN
   20   CONTINUE
      END IF
!
      RETURN
!
!     FORMAT STATEMENTS
!
 1000 FORMAT (/42H THE CORRECT FORM OF THE CALL STATEMENT IS//
     +   '       CALL NRANDC (Y, N, ISEED, YMEAN, SIGMA)')
      END
