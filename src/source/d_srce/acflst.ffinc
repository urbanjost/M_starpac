!ACFLST
      SUBROUTINE ACFLST (RHO, SDRHO, NLPPA, LAGMAX, IFMISS, CHIA,
     +   NDFCHI, CHIAP)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE ACTUALLY LISTS THE AUTOCORRELATIONS OR
!     PARTIAL AUTOCORRELATIONS AND OTHER PERTINENT INFORMATION.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 21, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      DOUBLE PRECISION
     +   CHIA,CHIAP
      INTEGER
     +   LAGMAX,NDFCHI
      LOGICAL
     +   IFMISS
!
!  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   RHO(*),SDRHO(*)
      INTEGER
     +   NLPPA(*)
!
!  LOCAL SCALARS
      DOUBLE PRECISION
     +   FPLM
      INTEGER
     +   I,IMAX,IMIN,IPRT,LAG,NPERL
!
!  EXTERNAL FUNCTIONS
      DOUBLE PRECISION
     +   D1MACH
      EXTERNAL D1MACH
!
!  EXTERNAL SUBROUTINES
      EXTERNAL IPRINT
!
!  INTRINSIC FUNCTIONS
      INTRINSIC MIN
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     DOUBLE PRECISION CHIA, CHIAP
!        THE VARIABLES IN CHICH THE CHI SQUARE STATISTIC AND
!        PROBABILITY FOR THE AUTOCORRELATIONS ARE STORED.
!     DOUBLE PRECISION FPLM
!        THE FLOATING POINT LARGEST MAGNITUDE.
!     INTEGER I
!        AN INDEX VARIABLE.
!     LOGICAL IFMISS
!        THE INDICATOR VARIABLE USED TO DETERMINE
!        WHETHER THE INPUT SERIES HAS MISSING DATA OR NOT.
!     INTEGER IMAX, IMIN
!        THE INDEX VALUES OF THE FIRST AND LAST OBSERVATION
!        TO BE PRINTED PER LINE
!     INTEGER IPRT
!        THE UNIT NUMBER FOR PRINTED OUTPUT
!     INTEGER LAG
!        THE LAG VALUE OF THE AUTOCORRELATION OR PARTIAL
!        AUTOCORRELATION BEING PRINTED.
!     INTEGER LAGMAX
!        THE NUMBER OF AUTOCORRELATIONS OR PARTIAL AUTOCORRELATIONS
!        TO BE PRINTED.
!     INTEGER NDFCHI
!        THE DEGREES OF FREEDOM FOR THE CHI SQUARED STATISTIC.
!     INTEGER NLPPA(LAGMAX)
!        THE ARRAY IN WHICH THE NUMBER OF LAGGED PRODUCT PAIRS USED TO
!        COMPUTE EACH AUTOCORRELATION IS STORED
!     INTEGER NPERL
!        THE NUMBER OF VALUES TO BE PRINTED PER LINE.
!     DOUBLE PRECISION RHO(LAGMAX)
!        THE ARRAY IN WHICH THE AUTOCORRELATIONS ARE STORED.
!     DOUBLE PRECISION SDRHO(LAGMAX)
!        THE ARRAY IN WHICH THE STANDARD ERRORS OF THE AUTOCORRELATIONS
!        ARE STORED
!
!
      FPLM = D1MACH(2)
!
      CALL IPRINT(IPRT)
!
      NPERL = 12
      DO 30 I = 1, LAGMAX, NPERL
         IMIN = I
         IMAX = MIN(I + NPERL - 1, LAGMAX)
         WRITE(IPRT, 1000) (LAG, LAG = IMIN, IMAX)
         WRITE(IPRT, 1001) (RHO(LAG), LAG = IMIN, IMAX)
         WRITE(IPRT, 1002) (SDRHO(LAG), LAG = IMIN, IMAX)
         IF (IFMISS) WRITE(IPRT, 1003) (NLPPA(LAG), LAG = IMIN, IMAX)
   30 CONTINUE
!
      IF (SDRHO(LAGMAX) .EQ. FPLM) WRITE(IPRT, 1004) FPLM
!
      WRITE (IPRT, 1005) CHIA, NDFCHI, CHIAP
!
      RETURN
!
!     FORMAT STATEMENTS
!
 1000 FORMAT(/19H LAG               , 12(1X, I6))
 1001 FORMAT( 19H ACF               , 12(2X, F5.2))
 1002 FORMAT( 19H STANDARD ERROR    , 12(2X, F5.2))
 1003 FORMAT( 19H NO. OF OBS. USED  , 12(1X, I6))
 1004 FORMAT(///5X, F5.2, 38H INDICATES VALUE COULD NOT BE COMPUTED,
     +   ' DUE TO MISSING DATA.')
 1005 FORMAT(///33H THE CHI SQUARE TEST STATISTIC OF/
     +   40H THE NULL HYPOTHESIS OF WHITE NOISE    =, G21.4/
     +   40H DEGREES OF FREEDOM                    =, I17/
     +   40H OBSERVED SIGNIFICANCE LEVEL           =, F17.4)
      END
