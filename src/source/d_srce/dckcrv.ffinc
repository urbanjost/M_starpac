!DCKCRV
      SUBROUTINE DCKCRV(J, D, PAR, NPAR, ETA, TAU, MDL, XM, N,
     +   NROW, M, IXM, PV, PVTEMP, MSG, LMSG, FD, PARMX, PVPSTP, STP)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE CHECKS WHETHER HIGH CURVATURE COULD BE THE CAUSE
!     OF THE DISAGREEMENT BETWEEN THE NUMERICAL AND ANALYTIC DERIVATIVES
!
!     WRITTEN BY  -  ROBERT B. SCHNABEL (CODED BY JANET R. DONALDSON)
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  APRIL 2, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      DOUBLE PRECISION
     +   D,ETA,FD,PARMX,PV,PVPSTP,STP,TAU
      INTEGER
     +   IXM,J,LMSG,M,N,NPAR,NROW
!
!  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   PAR(NPAR),PVTEMP(N),XM(IXM,M)
      INTEGER
     +   MSG(LMSG)
!
!  SUBROUTINE ARGUMENTS
      EXTERNAL MDL
!
!  LOCAL SCALARS
      DOUBLE PRECISION
     +   CURVE,FPLRS,PVMCRV,PVPCRV,STPCRV,TEMP,THIRD
!
!  EXTERNAL FUNCTIONS
      DOUBLE PRECISION
     +   D1MACH
      EXTERNAL D1MACH
!
!  EXTERNAL SUBROUTINES
      EXTERNAL DCKFPA
!
!  INTRINSIC FUNCTIONS
      INTRINSIC ABS,SIGN
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     DOUBLE PRECISION CURVE
!        A MEASURE OF THE CURVATURE IN THE MODEL.
!     DOUBLE PRECISION D
!        THE SCALAR IN WHICH ROW   NROW   OF THE DERIVATIVE
!        MATRIX WITH RESPECT TO THE JTH UNKNOWN PARAMETER
!        IS STORED.
!     DOUBLE PRECISION ETA
!        THE RELATIVE NOISE IN THE MODEL.
!     DOUBLE PRECISION FD
!        THE FORWARD DIFFERENCE QUOTIENT DERIVATIVE WITH RESPECT TO THE
!        JTH PARAMETER.
!     DOUBLE PRECISION FPLRS
!        THE FLOATING POINT LARGEST RELATIVE SPACING.
!     INTEGER IXM
!        THE FIRST DIMENSION OF THE INDEPENDENT VARIABLE ARRAY.
!     INTEGER J
!        THE INDEX OF THE PARTIAL DERIVATIVE BEING EXAMINED.
!     INTEGER LMSG
!        THE LENGTH OF THE VECTOR MSG.
!     INTEGER M
!        THE NUMBER OF INDEPENDENT VARIABLES.
!     EXTERNAL MDL
!        THE NAME OF THE USER SUPPLIED SUBROUTINE WHICH COMPUTES THE
!        PREDICTED VALUES BASED ON THE CURRENT PARAMETER ESTIMATES.
!     INTEGER MSG(LMSG)
!        AN ARRAY USED TO STORE MESSAGE PARAMETERS.
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS.
!     INTEGER NPAR
!        THE NUMBER OF UNKNOWN PARAMETERS IN THE MODEL.
!     INTEGER NROW
!        THE NUMBER OF THE ROW OF THE INDEPENDENT VARIABLE ARRAY AT
!        WHICH THE DERIVATIVE IS TO BE CHECKED.
!     DOUBLE PRECISION PAR(NPAR)
!        THE ARRAY IN WHICH THE CURRENT ESTIMATES OF THE UNKNOWN
!        PARAMETERS ARE STORED.
!     DOUBLE PRECISION PARMX
!        THE MAXIMUM OF THE CURRENT PARAMETER ESTIMATE.
!     DOUBLE PRECISION PV
!        THE SCALAR IN WHICH THE PREDICTED VALUE FROM THE MODEL FOR
!        ROW   NROW   IS STORED.
!     DOUBLE PRECISION PVMCRV
!        THE PREDICTED VALUE FOR ROW    NROW   OF THE MODEL
!        BASED ON THE CURRENT PARAMETER ESTIMATES
!        FOR ALL BUT THE JTH PARAMETER VALUE, WHICH IS PAR(J)-STPCRV.
!     DOUBLE PRECISION PVPCRV
!        THE PREDICTED VALUE FOR ROW    NROW   OF THE MODEL
!        BASED ON THE CURRENT PARAMETER ESTIMATES
!        FOR ALL BUT THE JTH PARAMETER VALUE, WHICH IS PAR(J)+STPCRV.
!     DOUBLE PRECISION PVPSTP
!        THE PREDICTED VALUE FOR ROW    NROW   OF THE MODEL
!        BASED ON THE CURRENT PARAMETER ESTIMATES
!        FOR ALL BUT THE JTH PARAMETER VALUE, WHICH IS PAR(J) + STP.
!     DOUBLE PRECISION PVTEMP(N)
!        THE VECTOR OF PREDICTED VALUES FROM THE MODEL.
!     DOUBLE PRECISION STP
!        THE STEP SIZE CURRENTLY BEING EXAMINED FOR THE FINITE DIFFERENC
!        DERIVATIVE
!     DOUBLE PRECISION STPCRV
!        THE STEP SIZE SELECTED TO CHECK FOR CURVATURE IN THE MODEL.
!     DOUBLE PRECISION TAU
!        THE AGREEMENT TOLERANCE.
!     DOUBLE PRECISION TEMP
!        A TEMPORARY LOCATION IN WHICH THE CURRENT ESTIMATE OF THE JTH
!        PARAMETER IS STORED.
!     DOUBLE PRECISION THIRD
!        THE VALUE ONE THIRD.
!     DOUBLE PRECISION XM(IXM,M)
!        THE ARRAY IN WHICH ONE ROW OF THE INDEPENDENT VARIABLE ARRAY
!        IS STORED.
!
      FPLRS = D1MACH(4)
!
      THIRD = 1.0D0/3.0D0
!
      STPCRV = (ETA**THIRD*PARMX*SIGN(1.0D0,PAR(J))+PAR(J)) - PAR(J)
!
      TEMP = PAR(J)
      PAR(J) = TEMP + STPCRV
      CALL MDL(PAR, NPAR, XM, N, M, IXM, PVTEMP)
!
      PVPCRV = PVTEMP(NROW)
!
      PAR(J) = TEMP - STPCRV
      CALL MDL(PAR, NPAR, XM, N, M, IXM, PVTEMP)
      PAR(J) = TEMP
!
      PVMCRV = PVTEMP(NROW)
!
!     ESTIMATE CURVATURE BY SECOND DERIVATIVE OF MODEL WITH RESPECT TO
!     PAR(J)
!
      CURVE = ((PVPCRV+PVMCRV)-2*PV) / (STPCRV*STPCRV)
      CURVE = CURVE + (ETA ** THIRD) * (ABS(PVPCRV) +
     +   ABS(PVMCRV) + 2.0D0 * ABS(PV)) / (PARMX * PARMX)
!
!     COMPARE NUMERICAL AND ANALYTICAL DERIVATIVES USING A FUDGE
!     FACTOR OF 10.0D0.
!
      IF (ABS(CURVE*STP)*5.0D0.LT.ABS(FD-D)) THEN
!
!     CURVATURE CANNOT ACCOUNT FOR DISCREPANCY.
!     CHECK IF FINITE PRECISION ARITHMETIC COULD BE THE CULPRIT.
!
         CALL DCKFPA(J,D,PAR,NPAR,ETA,TAU,MDL,XM,N,NROW,
     +      M,IXM,PV,PVTEMP,MSG,LMSG,FD,PARMX,STP,PVPSTP,CURVE)
!
      ELSE
!
!     HIGH CURVATURE COULD BE THE PROBLEM.  TRY A SMALLER STEP SIZE.
!     COMPUTE SMALLER STEPSIZE
!
         STP = (2.0D0*TAU*ABS(D)*SIGN(1.0D0,PAR(J))/ABS(CURVE)+PAR(J))
     +          - PAR(J)
!
         IF (ABS(STP).LE.FPLRS*ABS(PAR(J))) THEN
!
!     NEW STEP SIZE IS TOO SMALL TO USE.
!
            IF (MSG(1).EQ.0) MSG(1) = 1
            MSG(J+1) = 1
         ELSE
!
!     TRY NEW STEP SIZE
!
            TEMP = PAR(J)
            PAR(J) = TEMP + STP
            CALL MDL(PAR, NPAR, XM, N, M, IXM, PVTEMP)
            PAR(J) = TEMP
            PVPSTP = PVTEMP(NROW)
!
!     COMPUTE THE NEW NUMERICAL DERIVATIVE
!
            FD = (PVPSTP-PV)/STP
!
!     CHECK WHETHER THE NEW NUMERICAL DERIVATIVE IS NOT OK
!
            IF (ABS(FD-D).GT.2.0D0*TAU*ABS(D)) THEN
!
!     NUMERICAL DERIVATIVE COMPUTED USING NEW STEP SIZE DOES
!     NOT AGREE WITH ANALYTIC DERIVATIVE.
!
!     CHECK IF THE PROBLEM COULD BE THE FORWARD DIFFERENCE QUOTIENT
!     DERIVATIVE.
!     (FUDGE FACTOR IS 2)
!
               IF (ABS(STP*(FD-D)).GE.2.0D0*ETA*ABS(PV+PVPSTP)) THEN
!
!     FINITE PRECISION COULD NOT BE THE CULPRIT
!
                  MSG(1) = 2
                  MSG(J+1) = 2
               ELSE
!
!     FINITE PRECISION MAY BE THE CULPRIT
!
                  IF (MSG(1).EQ.0) MSG(1) = 1
                  MSG(J+1) = 1
               END IF
            END IF
         END IF
      END IF
      RETURN
!
      END
