!FITPT2
      SUBROUTINE FITPT2 (SDRES, PV, WT, N, NNZW, WEIGHT, RES, RSS)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS SUBROUTINE, ADAPTED FROM OMNITAB II, PRINTS
!     THE FOUR STANDARDIZED RESIDUAL PLOTS.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  APRIL 2, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      DOUBLE PRECISION
     +   RSS
      INTEGER
     +   N,NNZW
      LOGICAL
     +   WEIGHT
!
!  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   PV(N),RES(N),SDRES(N),WT(N)
!
!  SCALARS IN COMMON
      INTEGER
     +   IERR
!
!  LOCAL SCALARS
      DOUBLE PRECISION
     +   ANNZW,DOT,FAC1,FAC2,FPLM,GAMMA,PI,PVDIV,PVMAX,PVMID,PVMIN,
     +   RATIO,ROWDIV,ROWMAX,ROWMID,ROWMIN,W,XDIV,XMAX,XMIN,YLABEL,
     +   YMAX,YMIN
      INTEGER
     +   I,I1,I2,IDOT,IFIRST,IMID,IPLOT,IPRB,IPRT,IPV,IROW,IX,K,L,
     +   NCOL,NCOLP1,NCOLPL,NCOLT2,NDOT,NROW
      CHARACTER
     +   IBLANK*1,IMINUS*1,IPLUS*1,ISTAR*1
!
!  LOCAL ARRAYS
      CHARACTER
     +   LINE(102)*1
!
!  EXTERNAL FUNCTIONS
      DOUBLE PRECISION
     +   D1MACH
      LOGICAL
     +   MVCHK
      EXTERNAL D1MACH,MVCHK
!
!  EXTERNAL SUBROUTINES
      EXTERNAL GETPI,IPRINT
!
!  INTRINSIC FUNCTIONS
      INTRINSIC INT,MAX,MIN,MOD
!
!  COMMON BLOCKS
      COMMON /ERRCHK/IERR
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     DOUBLE PRECISION ANNZW
!        THE NUMBER OF NONZERO WEIGHTS, USED IN COMPUTING
!        THE NORMAL PROBABILITY PLOT.
!     DOUBLE PRECISION DOT
!        ...
!     DOUBLE PRECISION FAC1, FAC2
!        FACTORS USED IN COMPUTING THE NORMAL PROBABILITY PLOT.
!     DOUBLE PRECISION FPLM
!        THE FLOATING POINT LARGEST MAGNITUDE.
!     DOUBLE PRECISION GAMMA
!        A VALUE USED IN COMPUTING THE NORMAL PROBABILITY PLOT.
!     INTEGER I
!        AN INDEX VARIABLE.
!     CHARACTER*1 IBLANK
!        THE VALUE OF THE CHARACTER -BLANK-.
!     INTEGER IERR
!        THE INTEGER VALUE DESIGNATING WHETHER ANY ERRORS HAVE
!        BEEN DETECTED.
!        IF IERR .EQ. 0, NO ERRORS WERE DETECTED
!        IF IERR .NE. 0, ERRORS HAVE BEEN DETECTED.
!     INTEGER IFIRST
!        THE FIRST ROW OF THE VARIABLES TO BE PLOTTED.
!     INTEGER IMID
!        THE MIDPOINT OF THE FIRST PLOT OF THE SECOND SET
!     CHARACTER*1 IMINUS
!        THE CHARACTER MINUS.
!     INTEGER IPLOT
!        AN INDICATOR VARIABLE DESIGNATING WHETHER THE FIRST OR
!        SECOND SET OF TWO PLOTS ARE BEING PRINTED.
!     CHARACTER*1 IPLUS
!        THE CHARACTER PLUS.
!     INTEGER IPRB
!        THE LOCATION IN THE PLOT STRING OF THE SYMBOL FOR THE
!        PROBABILITY PLOT.
!     INTEGER IPRT
!        THE UNIT NUMBER FOR PRINTED OUTPUT.
!     INTEGER IPV
!        THE LOCATION IN THE PLOT STRING OF THE SYMBOL FOR THE PLOT
!        VERSUS PREDICTED VALUE.
!     INTEGER IROW
!        THE ROW OF THE VARIABLES BEING PLOTTED.
!     CHARACTER*1 ISTAR
!        THE CHARACTER STAR.
!     INTEGER IX
!        THE LOCATION IN THE PLOT STRING OF THE SYMBOL FOR THE PLOTS
!        VERSUS THE INDEPENDENT VARIABLE.
!     INTEGER I1, I2
!        ...
!     INTEGER K, L
!        INDEX VARIABLES.
!     CHARACTER*1 LINE(102)
!        THE SYMBOLS (BLANKS AND CHARACTERS) FOR A GIVEN LINE
!        OF THE PLOT.
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS IN EACH COLUMN OF DATA.
!     INTEGER NCOL, NCOLPL, NCOLP1, NCOLT2
!        THE NUMBER OF COLUMNS IN THE PLOT, NCOL+L, NCOL+1,
!        AND NCOL * 2.
!     INTEGER NDOT
!        ...
!     INTEGER NNZW
!        THE NUMBER OF NON ZERO WEIGHTS.
!     INTEGER NROW
!        THE NUMBER OF COLUMNS IN THE PLOT.
!     DOUBLE PRECISION PI
!        THE VALUE OF PI.
!     DOUBLE PRECISION PV(N)
!        THE PREDICTED VALUE BASED ON THE CURRENT COEFFICIENT ESTIMATES
!     DOUBLE PRECISION PVDIV
!        THE VALUE OF A DIVISION ALONG THE -PREDICTED VALUE- AXIS.
!     DOUBLE PRECISION PVMAX
!        THE LARGEST VALUE IN THE VECTOR PV.
!     DOUBLE PRECISION PVMID
!        THE MIDPOINT OF THE RANGE OF VALUES IN THE VECTOR PV.
!     DOUBLE PRECISION PVMIN
!        THE SMALLEST VALUE IN THE VECTOR PV.
!     DOUBLE PRECISION RATIO
!        A VALUE USED TO PRODUCE THE NORMAL PROBABILITY PLOT.
!     DOUBLE PRECISION RES(N)
!        THE RESIDUALS FROM THE FIT.
!     DOUBLE PRECISION ROWDIV
!        THE VALUE OF A DIVISION ALONG THE -ROW- AXIS.
!     DOUBLE PRECISION ROWMAX
!        THE LARGEST ROW VALUE.
!     DOUBLE PRECISION ROWMID
!        THE MIDPOINT OF THE RANGE OF THE ROWS PLOTTED.
!     DOUBLE PRECISION ROWMIN
!        THE SMALLEST ROW VALUE PLOTTED.
!     DOUBLE PRECISION RSS
!        THE RESIDUAL SUM OF SQUARES.
!     DOUBLE PRECISION SDRES(N)
!        THE STANDARD DEVIATIONS OF THE RESIDUALS.
!     DOUBLE PRECISION W
!        THE VALUE OF THE WEIGHT FOR THE CURRENT VALUE BEING PLOTTED.
!     LOGICAL WEIGHT
!        THE VARIABLE USED TO INDICATE WHETHER WEIGHTED ANALYSIS IS TO
!        BE PERFORMED (TRUE) OR NOT (FALSE).
!     DOUBLE PRECISION WT(N)
!        THE USER SUPPLIED WEIGHTS.
!     DOUBLE PRECISION XDIV
!        THE VALUE OF A DIVISION ALONG THE X AXIS.
!     DOUBLE PRECISION XMAX
!        THE LARGEST VALUE ALONG THE X AXIS.
!     DOUBLE PRECISION XMIN
!        THE SMALLEST VALUE ALONG THE X AXIS.
!     DOUBLE PRECISION YLABEL
!        THE LABEL TO BE PRINTED ALONG THE Y AXIS.
!     DOUBLE PRECISION YMAX
!        THE LARGEST VALUE ALONG THE Y AXIS
!     DOUBLE PRECISION YMIN
!        THE SMALLEST VALUE ALONG THE Y AXIS.
!
      DATA IPLUS/'+'/, IMINUS/'-'/, ISTAR/'*'/, IBLANK/' '/
!
      CALL IPRINT(IPRT)
!
      FPLM = D1MACH(2)
!
!     CHECK FOR INSUFFICIENT POINTS TO PLOT
!
      IF (IERR.EQ.4) THEN
         DO 1 I = 1, N
            IF (SDRES(I).NE.FPLM) GO TO 5
    1    CONTINUE
         WRITE (IPRT, 1090)
         RETURN
      END IF
    5 CONTINUE
!
!     INITIALIZE VARIABLES FOR PROBABILITY PLOT
!
      CALL GETPI(PI)
      GAMMA = PI/8.0D0
      ANNZW = NNZW
      FAC1 = 1.0D0 / (ANNZW - 2.0D0*GAMMA + 1.0D0)
      FAC2 = 10.0D0
!
!     INITIALIZE THE PLOT SIZE (IN PLOT UNITS)
!
      NROW = 26
      NCOL = 51
      NCOLP1 = NCOL + 1
      NCOLT2 = 2*NCOL
      IMID = (NCOL-1)/2
!
!     FIND THE FIRST ROW OF OBSERVATIONS WITH NONZERO WEIGHTS
!
      IFIRST = 1
      IF (.NOT. WEIGHT) GO TO 20
      DO 10 I=1,N
         IF (WT(I).LE.0.0D0) GO TO 10
         IFIRST = I
         GO TO 20
   10 CONTINUE
!
!     BEGIN COMPUTATIONS FOR FIRST SET OF PLOTS
!
   20 IPLOT = 1
!
!     SET X AXIS LIMITS FOR STANDARDIZED RESIDUAL VS ROW PLOT,
!     AND STANDARDIZED RESIDUALS VS PREDICTED VALUES PLOT.
!
      ROWMIN = IFIRST
      PVMIN = PV(IFIRST)
      PVMAX = PV(IFIRST)
      ROWMAX = IFIRST
      DO 30 I=IFIRST,N
         W = 1.0D0
         IF (WEIGHT) W = WT(I)
         IF (W.GT.0.0D0) THEN
            ROWMAX = I
            IF (PV(I).LT.PVMIN) PVMIN = PV(I)
            IF (PV(I).GT.PVMAX) PVMAX = PV(I)
         END IF
   30 CONTINUE
!
      IF (PVMIN.LT.PVMAX) GO TO 35
         IF (PVMIN.EQ.0.0D0) GO TO 33
            PVMIN = PVMIN - PVMIN/2.0D0
            PVMAX = PVMAX + PVMAX/2.0D0
         GO TO 35
   33    CONTINUE
            PVMIN = -0.5D0
            PVMAX = 0.5D0
   35 CONTINUE
!
      ROWMID = (ROWMAX+ROWMIN)/2.0D0
      ROWDIV = (ROWMAX-ROWMIN)/(NCOL-1)
      PVMID = (PVMAX+PVMIN)/2.0D0
      PVDIV = (PVMAX-PVMIN)/(NCOL-1)
!
!     PRINT TITLES FOR FIRST PLOTS
!
      WRITE (IPRT,1000)
      GO TO 90
!
!     BEGIN COMPUTATIONS FOR SECOND SET OF PLOTS
!
   40 IPLOT = 2
!
!     SET AXIS LIMITS FOR THE STANDARDIZED RESIDUALS VS
!     STANDARDIZED RESIDUALS LAGED BY ONE AND FOR PROBABILITY PLOT
!
      XMIN = -3.75D0
      XMAX = 3.75D0
      XDIV = (XMAX-XMIN)/(NCOL-1)
!
!     PRINT TITLES FOR SECOND PLOTS
!
      WRITE (IPRT,1050)
!
!     WRITE FIRST LINE OF PLOTS
!
   90 CONTINUE
!
!     PRINT PLOTS, ONE LINE AT A TIME
!
      YLABEL = 3.75D0
      YMAX = FPLM
      YMIN = 4.05D0
      DO 160 K=1,NROW
         YMIN = YMIN - 0.3D0
         IF (-3.70D0.GE.YMIN) YMIN = -FPLM
         DO 100 L=1,NCOL
            NCOLPL = L + NCOL
            LINE(L) = IBLANK
            LINE(NCOLPL) = IBLANK
            IF ((K.NE.1) .AND. (K.NE.NROW)) GO TO 100
               LINE(L) = IMINUS
               LINE(NCOLPL) = IMINUS
               IF ((MOD(L,10).NE.1) .AND. (L.NE.1+NCOL/2)) GO TO 100
                  LINE(L) = IPLUS
                  LINE(NCOLPL) = IPLUS
  100    CONTINUE
         DO 110 I=1,N
            IF (WEIGHT) THEN
               W = WT(I)
            ELSE
               W = 1.0D0
            END IF
            IF ((W.NE.0.0D0) .AND. (.NOT.MVCHK(SDRES(I),FPLM))) THEN
               IF ((SDRES(I).GT.YMIN) .AND. (SDRES(I).LE.YMAX)) THEN
                  IF (IPLOT.EQ.1) THEN
!
!     SET PLOT LINE FOR FIRST SET OF PLOTS
!
                     IROW = INT(((I-ROWMIN)/ROWDIV)+1.5D0)
                     LINE(IROW) = ISTAR
                     IPV = INT((PV(I)-PVMIN)/PVDIV+1.5D0) + NCOL
                     LINE(IPV) = ISTAR
                  ELSE
!
!     SET PLOT LINE FOR PROBABILITY PLOT
!
                     RATIO = (ANNZW-GAMMA) * FAC1
                     IPRB = INT(4.91D0*(RATIO**0.14D0-
     +                         (1.0D0-RATIO)**0.14D0)*FAC2) + 77
                     IF (IPRB.LE.NCOL) IPRB = NCOL+1
                     IF (IPRB.GE.103) IPRB = 102
                     LINE(IPRB) = ISTAR
                     ANNZW = ANNZW - 1.0D0
                     IF ((ANNZW.LT.2.0D0) .AND. (NNZW.LE.10)) THEN
                        GAMMA = 1.0D0/3.0D0
                     END IF
                  END IF
               END IF
            END IF
  110    CONTINUE
!
!     SET PLOT LINE FOR CORRELATION PLOT
!
         IF (IPLOT.EQ.2) THEN
            IF (K.LE.N-1) THEN
              DOT = 0.0D0
              IF (WEIGHT) THEN
                NDOT = 0
                DO 120 IDOT = 1, N-K
                  IF ((WT(IDOT).GT.0.0D0) .AND.
     +                (WT(IDOT+K).GT.0.0D0)) THEN
                    NDOT = NDOT + 1
                    DOT = DOT + RES(IDOT)*RES(IDOT+K)
                  END IF
  120           CONTINUE
                IF (NDOT.GE.1) THEN
                   DOT = DOT * (N-K) / NDOT
                END IF
              ELSE
                DO 130 IDOT = 1, N-K
                  DOT = DOT + RES(IDOT)*RES(IDOT+K)
  130           CONTINUE
              END IF
              IX = INT(IMID*DOT/RSS) + IMID + 1
              I1 = MIN(IX,IMID+1)
              I2 = MAX(IX,IMID+1)
              DO 140 IX=I1,I2
                LINE(IX) = ISTAR
  140         CONTINUE
            END IF
         END IF
         IF (MOD(K,5).EQ.1) THEN
            IF (IPLOT.EQ.1) THEN
               WRITE (IPRT,2020) YLABEL, (LINE(L),L=1,NCOL), YLABEL,
     +         (LINE(L),L=NCOLP1,NCOLT2)
            ELSE
               WRITE (IPRT,1020) K, (LINE(L),L=1,NCOL), YLABEL,
     +         (LINE(L),L=NCOLP1,NCOLT2)
            END IF
            YLABEL = YLABEL - 1.5
         ELSE
            WRITE (IPRT,1030) (LINE(L),L=1,102)
         END IF
         YMAX = YMIN
  160 CONTINUE
!
!     PRINT BOTTOM LINE OF GRAPHS
!
      IF (IPLOT.EQ.1) THEN
!
!     PRINT X AXIS LABELS FOR FIRST SET OF PLOTS
!
         WRITE (IPRT,1040) ROWMIN, ROWMID, ROWMAX, PVMIN, PVMID, PVMAX
         GO TO 40
      ELSE
!
!     PRINT X AXIS LABELS FOR SECOND SET OF PLOTS
!
         WRITE (IPRT,1070)
      END IF
!
      RETURN
!
!     FORMAT STATEMENTS
!
 1000 FORMAT (/20X, 23H STD RES VS ROW NUMBER , 35X,
     +   29H STD RES VS PREDICTED VALUES )
!1010 FORMAT (7X, 2('+', 9A1), '+', 4A1, 'X', 4A1, 2('+', 9A1), '+',
!    *   10X, 2('+', 9A1), '+', 4A1, 'X', 4A1, 2('+', 9A1), '+')
 1020 FORMAT (1X, I5, '+', 51A1, '+', 3X, F5.2, '+', 51A1, '+')
 1030 FORMAT (6X, '-', 51A1, '-', 8X, '-', 51A1, '-')
 1040 FORMAT (1X, F8.1, 17X, F8.1, 17X, F8.1, 4X, G11.4, 14X, G11.4,
     +   10X, G11.4)
 1050 FORMAT (/13X, 'AUTOCORRELATION FUNCTION OF RESIDUALS',
     +   23X, 36H NORMAL PROBABILITY PLOT OF STD RES )
!1060 FORMAT ('+', F5.2, '+', 51A1, '+', 3X, F5.2, '+', 51A1, '+')
 1070 FORMAT (4X, 5H-1.00, 22X, 3H0.0, 21X, 4H1.00, 5X, 4H-2.5, 23X,
     +   3H0.0, 22X, 3H2.5)
!1080 FORMAT ('+', 6X, 2('+', 9A1), '+', 4A1, 'X', 4A1, 2('+', 9A1),
!    *   '+', 10X, 2('+', 9A1), '+', 4A1, 'X', 4A1, 2('+', 9A1), '+')
 1090 FORMAT (// 1X, 13('*')/ 1X, 13H*  WARNING  */ 1X, 13('*')//
     +   54H THE STANDARDIZED RESIDUAL PLOTS HAVE BEEN SUPPRESSED.,
     +   45H  NONE OF THE STANDARDIZED RESIDUALS COULD BE,
     +   10H COMPUTED,/
     +   50H BECAUSE FOR EACH OBSERVATION EITHER THE WEIGHT OR,
     +   48H THE STANDARD DEVIATION OF THE RESIDUAL IS ZERO.)
 2020 FORMAT (1X, F5.2, '+', 51A1, '+', 3X, F5.2, '+', 51A1, '+')
      END
