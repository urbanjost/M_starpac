!PPFT
      FUNCTION PPFT(P, IDF)
      REAL(KIND=WP) :: PPFT
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS FUNCTION IS A VERSION OF DATAPAC SUBROUTINE
!     TPPF, WITH MODIFICATIONS TO FACILITATE CONVERSION TO
!     REAL(KIND=WP) :: AUTOMATICALLY USING THE NAG, INC. CODE APT,
!     AND TO CORRESPOND TO STARPAC CONVENTIONS.
!
!     PURPOSE--THIS SUBROUTINE COMPUTES THE PERCENT POINT
!              FUNCTION VALUE FOR THE STUDENT"S T DISTRIBUTION
!              WITH INTEGER DEGREES OF FREEDOM PARAMETER = IDF.
!              THE STUDENT"S T DISTRIBUTION USED
!              HEREIN IS DEFINED FOR ALL X,
!              AND ITS PROBABILITY DENSITY FUNCTION IS GIVEN
!              IN THE REFERENCES BELOW.
!              NOTE THAT THE PERCENT POINT FUNCTION OF A DISTRIBUTION
!              IS IDENTICALLY THE SAME AS THE INVERSE CUMULATIVE
!              DISTRIBUTION FUNCTION OF THE DISTRIBUTION.
!     ERROR CHECKING--NONE
!     RESTRICTIONS--IDF SHOULD BE A POSITIVE INTEGER VARIABLE.
!                 --P SHOULD BE BETWEEN 0.0_WP (EXCLUSIVELY)
!                   AND 1.0_WP (EXCLUSIVELY).
!     COMMENT--FOR IDF = 1 AND IDF = 2, THE PERCENT POINT FUNCTION
!              FOR THE T DISTRIBUTION EXISTS IN SIMPLE CLOSED FORM
!              AND SO THE COMPUTED PERCENT POINTS ARE EXACT.
!            --FOR OTHER SMALL VALUES OF IDF (IDF BETWEEN 3 AND 6,
!              INCLUSIVELY), THE APPROXIMATION
!              OF THE T PERCENT POINT BY THE FORMULA
!              GIVEN IN THE REFERENCE BELOW IS AUGMENTED
!              BY 3 ITERATIONS OF NEWTON"S METHOD FOR
!              ROOT DETERMINATION.
!              THIS IMPROVES THE ACCURACY--ESPECIALLY FOR
!              VALUES OF P NEAR 0 OR 1.
!     REFERENCES--NATIONAL BUREAU OF STANDARDS APPLIED MATHMATICS
!                 SERIES 55, 1964, PAGE 949, FORMULA 26.7.5.
!               --JOHNSON AND KOTZ, CONTINUOUS UNIVARIATE
!                 DISTRIBUTIONS--2, 1970, PAGE 102,
!                 FORMULA 11.
!               --FEDERIGHI, "EXTENDED TABLES OF THE
!                 PERCENTAGE POINTS OF STUDENT"S T
!                 DISTRIBUTION, JOURNAL OF THE
!                 AMERICAN STATISTICAL ASSOCIATION,
!                 1969, PAGES 683-688.
!               --HASTINGS AND PEACOCK, STATISTICAL
!                 DISTRIBUTIONS--A HANDBOOK FOR
!                 STUDENTS AND PRACTITIONERS, 1975,
!                 PAGES 120-123.
!     WRITTEN BY--JAMES J. FILLIBEN
!                 STATISTICAL ENGINEERING DIVISION
!                 NATIONAL BUREAU OF STANDARDS
!                 WASHINGTON, D. C. 20234
!     ORIGINAL VERSION--OCTOBER   1975.
!     UPDATED         --NOVEMBER  1975.
!
!     MODIFIED BY     --JANET R. DONALDSON, DECEMBER 7, 1981
!                       STATISTICAL ENGINEERING DIVISION
!                       NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) ::
     &   P
      INTEGER
     &   IDF
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     &   ARG,B21,B31,B32,B33,B34,B41,B42,B43,B44,B45,
     &   B51,B52,B53,B54,B55,B56,C,CON,D1,D3,D5,D7,D9,DF,PI,PPFN,
     &   S,SQRT2,TERM1,TERM2,TERM3,TERM4,TERM5,Z
      INTEGER
     &   IPASS,MAXIT
!
!  EXTERNAL FUNCTIONS
      REAL(KIND=WP) ::
     &   PPFNML
      EXTERNAL PPFNML
!
!  EXTERNAL SUBROUTINES
      EXTERNAL GETPI
!
!  INTRINSIC FUNCTIONS
      INTRINSIC ATAN,COS,SIN,SQRT
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL(KIND=WP) :: ARG
!        *
!     REAL(KIND=WP) :: B21
!        *
!     REAL(KIND=WP) :: B31, B32, B33, B34
!        *
!     REAL(KIND=WP) :: B41, B42, B43, B44, B45
!        *
!     REAL(KIND=WP) :: B51, B52, B53, B54, B55, B56
!        *
!     REAL(KIND=WP) :: C, CON
!        *
!     REAL(KIND=WP) :: DF
!        THE DEGREES OF FREEDOM.
!     REAL(KIND=WP) :: D1, D3, D5, D7, D9
!        *
!     INTEGER IDF
!        THE (INTEGER) DEGREES OF FREEDOM.
!     INTEGER IPASS
!        *
!     INTEGER MAXIT
!        *
!     REAL(KIND=WP) :: P
!        THE PROBABILITY AT WHICH THE PERCENT POINT IS TO BE EVALUATED.
!     REAL(KIND=WP) :: PI
!        THE VALUE OF PI.
!     REAL(KIND=WP) :: PPFN
!        THE NORMAL PERCENT POINT VALUE.
!     REAL(KIND=WP) :: S
!        *
!     REAL(KIND=WP) :: SQRT2
!        THE SQUARE ROOT OF TWO.
!        *
!     REAL(KIND=WP) :: TERM1, TERM2, TERM3, TERM4, TERM5
!        *
!     REAL(KIND=WP) :: Z
!        *
!
!     DEFINE CONSTANTS USED IN THE APPROXIMATIONS
!
      DATA B21 /4.0_WP/
      DATA B31, B32, B33, B34 /96.0_WP, 5.0_WP, 16.0_WP, 3.0_WP/
      DATA B41, B42, B43, B44, B45
     &  /384.0_WP, 3.0_WP, 19.0_WP, 17.0_WP, -15.0_WP/
      DATA B51, B52, B53, B54, B55, B56
     &   /9216.0_WP, 79.0_WP, 776.0_WP, 1482.0_WP,
     &   -1920.0_WP, -945.0_WP/
!
      CALL GETPI(PI)
!
      SQRT2 = SQRT(2.0_WP)
!
      DF = IDF
      MAXIT = 5
!
      IF (IDF.GE.3) GO TO 50
      IF (IDF.EQ.1) GO TO 30
      IF (IDF.EQ.2) GO TO 40
      PPFT = 0.0_WP
      RETURN
!
!     TREAT THE IDF = 1 (CAUCHY) CASE
!
   30 ARG = PI*P
      PPFT = -COS(ARG)/SIN(ARG)
      RETURN
!
!     TREAT THE IDF = 2 CASE
!
   40 TERM1 = SQRT2/2.0_WP
      TERM2 = 2.0_WP*P - 1.0_WP
      TERM3 = SQRT(P*(1.0_WP-P))
      PPFT = TERM1*TERM2/TERM3
      RETURN
!
!     TREAT THE IDF GREATER THAN OR EQUAL TO 3 CASE
!
   50 PPFN = PPFNML(P)
      D1 = PPFN
      D3 = PPFN**3
      D5 = PPFN**5
      D7 = PPFN**7
      D9 = PPFN**9
      TERM1 = D1
      TERM2 = (1.0_WP/B21)*(D3+D1)/DF
      TERM3 = (1.0_WP/B31)*(B32*D5+B33*D3+B34*D1)/(DF**2)
      TERM4 = (1.0_WP/B41)*(B42*D7+B43*D5+B44*D3+B45*D1)/(DF**3)
      TERM5 = (1.0_WP/B51)*(B52*D9+B53*D7+B54*D5+B55*D3+B56*D1)/(DF**4)
      PPFT = TERM1 + TERM2 + TERM3 + TERM4 + TERM5
      IF (IDF.GE.7) RETURN
      IF (IDF.EQ.3) GO TO 60
      IF (IDF.EQ.4) GO TO 80
      IF (IDF.EQ.5) GO TO 100
      IF (IDF.EQ.6) GO TO 120
      RETURN
!
!     AUGMENT THE RESULTS FOR THE IDF = 3 CASE
!
   60 CON = PI*(P-0.5_WP)
      ARG = PPFT/SQRT(DF)
      Z = ATAN(ARG)
      DO 70 IPASS=1,MAXIT
         S = SIN(Z)
         C = COS(Z)
         Z = Z - (Z+S*C-CON)/(2.0_WP*C*C)
   70 CONTINUE
      PPFT = SQRT(DF)*S/C
      RETURN
!
!     AUGMENT THE RESULTS FOR THE IDF = 4 CASE
!
   80 CON = 2.0_WP*(P-0.5_WP)
      ARG = PPFT/SQRT(DF)
      Z = ATAN(ARG)
      DO 90 IPASS=1,MAXIT
         S = SIN(Z)
         C = COS(Z)
         Z = Z - ((1.0_WP+0.5_WP*C*C)*S-CON)/(1.5_WP*C*C*C)
   90 CONTINUE
      PPFT = SQRT(DF)*S/C
      RETURN
!
!     AUGMENT THE RESULTS FOR THE IDF = 5 CASE
!
  100 CON = PI*(P-0.5_WP)
      ARG = PPFT/SQRT(DF)
      Z = ATAN(ARG)
      DO 110 IPASS=1,MAXIT
         S = SIN(Z)
         C = COS(Z)
         Z = Z - (Z+(C+(2.0_WP/3.0_WP)*C*C*C)*S-CON)/
     &   ((8.0_WP/3.0_WP)*C**4)
  110 CONTINUE
      PPFT = SQRT(DF)*S/C
      RETURN
!
!     AUGMENT THE RESULTS FOR THE IDF = 6 CASE
!
  120 CON = 2.0_WP*(P-0.5_WP)
      ARG = PPFT/SQRT(DF)
      Z = ATAN(ARG)
      DO 130 IPASS=1,MAXIT
         S = SIN(Z)
         C = COS(Z)
         Z = Z - ((1.0_WP+0.5_WP*C*C+0.375_WP*C**4)*S-CON)/
     &           ((15.0_WP/8.0_WP)*C**5)
  130 CONTINUE
      PPFT = SQRT(DF)*S/C
      RETURN
!
      END
