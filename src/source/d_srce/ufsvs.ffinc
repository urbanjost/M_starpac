!UFSVS
      SUBROUTINE UFSVS(ACOV, LAGMAX, N, NW, LAGS, NF,
     &   FMIN, FMAX, NPRT, SPCF, ISPCF, FREQ, LDSTAK)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS IS THE USER CALLABLE ROUTINE FOR TIME SERIES FOURIER
!     SPECTRUM ANALYSIS AND USER SUPPLIED ACVF VALUES (LONG CALL).
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  DECEMBER 7, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) ::
     &   FMAX,FMIN
      INTEGER
     &   ISPCF,LAGMAX,LDSTAK,N,NF,NPRT,NW
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   ACOV(*),FREQ(*),SPCF(*)
      INTEGER
     &   LAGS(*)
!
!  SCALARS IN COMMON
      INTEGER
     &   IERR
!
!  ARRAYS IN COMMON
      REAL(KIND=WP) :: DSTAK(12)
!
!  LOCAL SCALARS
      REAL(KIND=WP) :: ALPHA,DELTA,YMISS
      INTEGER
     &   IFP,IO,IPRT,ISORT,ISYM,LACOV,LDSMIN,LNLPPA,LPCV,LWORK,LY,
     &   NALL0,WORK,XAXIS,YAXIS
!
!  LOCAL ARRAYS
      REAL(KIND=WP) ::
     &   RSTAK(12),Y(1)
      INTEGER
     &   ISTAK(12),NLPPA(1)
      LOGICAL
     &   OPTION(4)
      CHARACTER
     &   NMSUB(6)*1
!
!  EXTERNAL FUNCTIONS
!      INTEGER
!     +   STKST
!      EXTERNAL STKST
!
!  EXTERNAL SUBROUTINES
!      EXTERNAL IPRINT,LDSCMP,PARZEN,STKCLR,STKSET,UFSDRV
!
!  COMMON BLOCKS
      COMMON /CSTAK/DSTAK
      COMMON /ERRCHK/IERR
!
!  EQUIVALENCES
      EQUIVALENCE (DSTAK(1),RSTAK(1))
      EQUIVALENCE (DSTAK(1),ISTAK(1))
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL(KIND=WP) :: ACOV(LAGMAX+1)
!        THE AUTOCOVARIANCES OF THE SERIES.
!     REAL(KIND=WP) :: ALPHA
!        THE DESIRED CONFIDENCE LEVEL.
!     REAL(KIND=WP) :: DELTA
!        THE SAMPLING INTERVAL.
!     REAL(KIND=WP) :: DSTAK(12)
!        THE DOUBLE PRECISION VERSION OF THE /CSTAK/ WORK AREA.
!     REAL(KIND=WP) :: FMAX, FMIN
!        THE MAXIMUM AND MINIMUM FREQUENCY AT WHICH THE SPECTRUM
!        IS TO BE COMPUTED.
!     REAL(KIND=WP) :: FREQ(NF)
!        THE VECTOR OF FREQUENCIES AT WHICH THE SPECTRUM IS TO BE
!        COMPUTED.
!     INTEGER IERR
!        THE INTEGER VALUE RETURNED BY THIS ROUTINE DESIGNATING
!        WHETHER ANY ERRORS WERE DETECTED IN THE PARAMETER LIST
!        IF ERR01, NO ERRORS WERE DETECTED
!        IF IERR .EQ. 1, ERRORS HAVE BEEN DETECTED
!     INTEGER IFP
!        AN INDICATOR FOR STACK ALLOCATION TYPE, WHERE IFP=3 INDICATES
!        REAL AND IFP=4 INDICATES DOUBLE PRECISION.
!     INTEGER IO
!        A VARIABLE USED TO DETERMINE THE AMOUNT OF STORAGE REQUIRED
!        BASED ON PRINTED OUTPUT REQUESTED.
!     INTEGER IPRT
!        THE LOGICAL UNIT USED FOR PRINTED OUTPUT.
!     INTEGER ISORT
!        THE STARTING LOCATION FOR THE ARRAY USED FOR SORTING.
!     INTEGER ISPCF
!         THE ACTUAL FIRST DIMENSION OF THE SPECTRUM ARRAYS.
!     INTEGER ISTAK(12)
!        THE INTEGER VERSION OF THE /CSTAK/ WORK AREA.
!     INTEGER ISYM
!        THE STARTING LOCATION IN THE WORK AREA FOR ARRAY ISYM.
!     INTEGER LACOV
!        THE LENGTH OF THE ACVF VECTORS.
!     INTEGER LAGMAX
!        THE MAXIMUM LAG VALUE TO BE USED.
!     INTEGER LAGS(NW)
!        THE ARRAY USED TO SPECIFY THE LAG WINDOW TRUNCATION
!        POINTS USED FOR EACH SET OF SPECTRUM VALUES.
!     INTEGER LDSMIN
!        THE MINIMUM LENGTH ALLOWED FOR DSTAK.
!     INTEGER LDSTAK
!        THE LENGTH OF THE VECTOR DSTAK IN COMMON CSTAK.
!     INTEGER LNLPPA
!        THE LENGTH OF THE VECTOR NLPPA.
!     INTEGER LPCV
!        THE LENGTH OF THE VECTORS USED FOR PLOTTING.
!     INTEGER LWORK
!        THE LENGTH OF THE VECTOR W.
!     INTEGER LY
!        THE LENGTH OF THE VECTOR Y.
!     INTEGER N
!        THE INTEGER NUMBER OF OBSERVATIONS IN THE SERIES.
!     INTEGER NALL0
!        THE NUMBER OF ALLOCATIONS OUTSTANDING AT THE TIME THAT
!        THIS ROUTINE WAS CALLED.
!     INTEGER NF
!        THE NUMBER OF FREQUENCIES AT WHICH THE SPECTRUM IS
!        TO BE COMPUTED.
!     INTEGER NLPPA(1)
!        A DUMMY ARRAY FOR SERIES WITHOUT MISSING VALUES.
!     CHARACTER*1 NMSUB(6)
!        THE ARRAY CONTAINING THE NAME OF THIS SUBROUTINE.
!     INTEGER NPRT
!        A CODE USED TO SPECIFY THE TYPE OF PLOT, WHERE IF
!        NPRT < 0 THE PLOT IS DECIBLES/LINEAR
!        NPRT = 0 THE PLOT IS SUPPRESSED
!        NPRT > 0 THE PLOT IS LOG/LINEAR
!     INTEGER NW
!        THE VARIABLE USED TO DETERMINE THE NUMBER OF DIFFERENT
!        BANDWIDTHS TO BE USED.
!     LOGICAL OPTION(4)
!        AN INDICATOR ARRAY USED TO DESIGNATE WHETHER ANY OF THE
!        FOUR POSSIBLE OPTIONS (F, M, V, OR S) HAVE BEEN USED (TRUE)
!        OR NOT (FALSE).
!     EXTERNAL PARZEN
!        THE SUBROUTINE USED TO COMPUTE THE WINDOW.
!     REAL(KIND=WP) :: RSTAK(12)
!        THE DOUBLE PRECISION VERSION OF THE /CSTAK/ WORK AREA.
!     REAL(KIND=WP) :: SPCF(ISPCF,NW)
!        THE ARRAYS IN WHICH THE SPECTRUM IS STORED
!        FOR EACH LAG WINDOW.
!     INTEGER WORK
!        THE STARTING LOCATION IN RSTAK FOR
!        THE WORK VECTOR.
!     INTEGER XAXIS
!        THE STARTING LOCATION IN THE WORK AREA FOR ARRAY XAXIS.
!     REAL(KIND=WP) :: Y(1)
!        A DUMMY ARRAY.
!     INTEGER YAXIS
!        THE STARTING LOCATION IN THE WORK AREA FOR ARRAY YAXIS.
!     REAL(KIND=WP) :: YMISS
!        A DUMMY VARIABLE.
!
!     SET UP NAME ARRAYS
!
      DATA NMSUB(1), NMSUB(2), NMSUB(3), NMSUB(4), NMSUB(5), NMSUB(6) /
     &   'U','F','S','V','S',' '/
!
!     SET UP
!
      OPTION(4) = .TRUE.
      OPTION(3) = .TRUE.
      OPTION(2) = .FALSE.
      OPTION(1) = .FALSE.
!
      LACOV = LAGMAX+1
!
!     COMPUTE MINIMUM ALLOWABLE STACK LENGTH
!
      IO = 1
      IF (NPRT.EQ.0) IO = 0
!
      CALL LDSCMP(4, 0, IO*(NF+5), 0, 0, 0, 'D',
     &   LAGMAX+1+IO*(2*NF+10), LDSMIN)
!
      YMISS = 1.0_WP
!
      LY = 1
      LNLPPA = 1
      LPCV = NF + 5
      LWORK = LAGMAX+1
!
!     SET SIZE OF WORK AREA.
!     SET THE NUMBER OF OUTSTANDING ALLOCATIONS.
!     SET THE STACK ALLOCATION TYPE.
!
      CALL STKSET(LDSTAK, 4)
      NALL0 = STKST(1)
!
      IFP = 4
!
!     SET STARTING LOCATIONS IN THE WORK AREA FOR VARIOUS ARRAYS.
!
      IF ((LDSMIN.GT.LDSTAK) .OR. (LDSMIN.LE.6)) THEN
         WORK = 1
         XAXIS = 1
         YAXIS = 1
         ISYM = 1
         ISORT = 1
      ELSE
         WORK = STKGET(LWORK,IFP)
         IF (NPRT.NE.0) THEN
            XAXIS = STKGET(LPCV,IFP)
            YAXIS = STKGET(LPCV,IFP)
            ISYM = STKGET(LPCV,2)
            ISORT = ISYM
         ELSE
            XAXIS = WORK
            YAXIS = WORK
            ISYM = WORK
            ISORT = ISYM
         END IF
      END IF
!
!     CALL THE CONTROLLING ROUTINE FOR FOURIER SPECTRUM ROUTINES
!
      CALL UFSDRV(Y, LY, YMISS, ACOV, NLPPA, SPCF, ISPCF, NF, FMIN,
     &   FMAX, FREQ, N, NW, LAGMAX, LAGS, RSTAK(WORK), LACOV, LWORK,
     &   DELTA, ISTAK(ISORT), ISTAK(ISYM), RSTAK(XAXIS), RSTAK(YAXIS),
     &   LPCV, ALPHA, NPRT, PARZEN, NMSUB, LDSMIN, LDSTAK, OPTION,
     &   LNLPPA, LY)
!
      CALL STKCLR(NALL0)
!
!     CHECK FOR ERRORS
!
      IF (IERR.EQ.0) RETURN
!
      CALL IPRINT(IPRT)
      WRITE (IPRT,1000)
      RETURN
!
!     FORMAT STATEMENTS
!
 1000 FORMAT (/42H THE CORRECT FORM OF THE CALL STATEMENT IS//
     &   '       CALL UFSVS (ACOV, LAGMAX, N,'/
     &   '      +            NW, LAGS, NF, FMIN, FMAX, NPRT,'/
     &   '      +            SPCF, ISPCF, FREQ, LDSTAK)')
      END
