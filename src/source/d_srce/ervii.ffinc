!ERVII
      SUBROUTINE ERVII (NMSUB, NMVAR, Y, N, YLB, YUB, NVMX,
     &   HEAD, MSGTYP, NV, ERROR)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE CHECKS FOR VALUES IN THE INPUT VECTOR   Y
!     WHICH ARE OUTSIDE THE (INCLUSIVE) LIMITS YLB TO YUB, PRINTS
!     AN ERROR MESSAGE IF THE NUMBER OF VIOLATIONS EXCEEDS THE LARGEST
!     NUMBER OF VIOLATIONS ALLOWED, AND RETURNS THE NUMBER OF
!     VIOLATIONS AND AN ERROR FLAG INDICATING THE RESULTS.  THREE
!     MESSAGES ARE AVAILABLE, SPECIFIED BY    MSGTYP   .
!        IF (MSGTYP = 0) NO VIOLATIONS ARE ALLOWED.
!        IF (MSGTYP = 1) THE NUMBER OF VIOLATIONS MUST
!                        BE LESS THAN   NVMX   .
!        IF (MSGTYP = 2) VIOLATIONS ARE COUNTED ONLY IF THE
!                        THE FIRST ELEMENT IS NOT IN VIOLATION.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  MAY 17, 1982
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) ::
     &   YLB,YUB
      INTEGER
     &   MSGTYP,N,NV,NVMX
      LOGICAL ERROR,HEAD
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   Y(*)
      CHARACTER
     &   NMSUB(6)*1,NMVAR(8)*1
!
!  LOCAL SCALARS
      INTEGER
     &   I,IPRT,NNV,NNVMN
!
!  EXTERNAL SUBROUTINES
!      EXTERNAL EHDR,IPRINT
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     LOGICAL ERROR
!        THE VALUE RETURNED FROM THE ERROR CHECKING ROUTINES TO INDICATE
!        WHETHER AN ERROR WAS DETECTED (TRUE) OR NOT (FALSE).
!     LOGICAL HEAD
!        A FLAG INDICATING WHETHER THE HEADING SHOULD BE PRINTED (TRUE)
!        OR NOT (FALSE).
!     INTEGER I
!        AN INDEX VARIABLE.
!     INTEGER IPRT
!        THE UNIT NUMBER FOR PRINTED OUTPUT.
!     INTEGER MSGTYP
!        THE INDICATOR VARIABLE FOR THE TYPE OF MESSAGE.
!        IF (MSGTYP = 0) NO VIOLATIONS ARE ALLOWED.
!        IF (MSGTYP = 1) THE NUMBER OF VIOLATIONS MUST
!                        BE LESS THAN   NVMX   .
!        IF (MSGTYP = 2) VIOLATIONS ARE COUNTED ONLY IF THE
!                        THE FIRST ELEMENT IS NOT IN VIOLATION.
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS.
!     CHARACTER*1 NMSUB(6)
!        THE CHARACTERS OF THE CALLING SUBROUTINES NAME.
!     CHARACTER*1 NMVAR(8)
!        THE CHARACTERS OF THE PARAMETERS NAME.
!     INTEGER NNV
!        THE NUMBER OF VALUES NOT IN VIOLATION.
!     INTEGER NNVMN
!        THE SMALLEST NUMBER OF VALUES NOT IN VIOLATION ALLOWED.
!     INTEGER NV
!        THE NUMBER OF VIOLATIONS FOUND.
!     INTEGER NVMX
!        THE LARGEST NUMBER OF VIOLATIONS ALLOWED.
!     REAL(KIND=WP) :: Y(N)
!        THE VECTOR BEING TESTED.
!     REAL(KIND=WP) :: YLB, YUB
!        THE (INCLUSIVE) RANGE THAT THE VECTOR IS BEING TESTED
!        AGAINST.
!
      ERROR = .FALSE.
!
      IF (N .LE. 0) RETURN
!
!     TEST WHETHER TESTING IS NECESSRY
!
      IF ((MSGTYP .EQ. 2) .AND.
     &    (Y(1) .LT. YLB) .OR. (Y(1) .GT. YUB)) RETURN
!
!     CHECK FOR VIOLATIONS
!
      NV = 0
      DO 10 I = 1, N
         IF ((Y(I).LT.YLB) .OR. (Y(I).GT.YUB)) NV = NV + 1
   10 CONTINUE
!
      IF (NV .LE. NVMX) RETURN
!
!     VIOLATIONS FOUND
!
      ERROR = .TRUE.
      CALL IPRINT(IPRT)
!
      CALL EHDR(NMSUB, HEAD)
!
      NNV = N - NV
      NNVMN = N - NVMX
!
      IF (MSGTYP .EQ. 0)
     &   WRITE(IPRT, 1010) (NMVAR(I), I = 1, 6), YLB, YUB, NV
      IF (MSGTYP .EQ. 1)
     &   WRITE(IPRT, 1020) (NMVAR(I), I = 1, 6), YLB, YUB,
     &   NNVMN, NNV
      IF (MSGTYP .EQ. 2)
     &   WRITE(IPRT, 1030) (NMVAR(I), I = 1, 6), YLB, YUB
!
      RETURN
!
!     FORMAT STATEMENTS
!
 1010 FORMAT(/
     &   26H THE VALUES IN THE VECTOR , 6A1,
     &   26H MUST ALL BE IN THE RANGE , 1PE14.7, 3H TO/
     &   1X, 1PE14.7,
     &      52H, INCLUSIVE. THE NUMBER OF VALUES OUTSIDE THIS RANGE,
     &   4H IS , I5, '.')
 1020 FORMAT(/
     &   36H THE NUMBER OF VALUES IN THE VECTOR , 6A1,
     &   14H IN THE RANGE , 1PE14.7, 3H TO/
     &   1X, 1PE14.7, 34H, INCLUSIVE, MUST EQUAL OR EXCEED , I5, '.'/
     &   ' THE NUMBER OF VALUES IN THIS RANGE IS ', I5, '.')
 1030 FORMAT(/
     &   34H IF THE FIRST VALUE OF THE VECTOR , 6A1,
     &   16H IS IN THE RANGE, 1PE14.7, 3H TO/
     &   1X, 1PE14.7,
     &      52H INCLUSIVE, ALL OF THE VALUES MUST BE IN THIS RANGE.)
!
      END
