!UFSEST
      SUBROUTINE UFSEST(ACOV, W, LAG, SPCF, ISPCF, LACOV, LW, NF, FREQ,
     &   DELTA)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE COMPUTES THE SPECTRUM, SPCF, AND
!     THEIR LOWER AND UPPER CONFIDENCE LIMITS, SPCLCL AND SPCUCL,
!     RESPECTIVELY.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  DECEMBER 7, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) ::
     &   DELTA
      INTEGER
     &   ISPCF,LACOV,LAG,LW,NF
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   ACOV(LACOV),FREQ(NF),SPCF(ISPCF),W(LW)
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     &   C,PI,V0,V1,V2
      INTEGER
     &   I,K,KK
!
!  EXTERNAL SUBROUTINES
!       EXTERNAL GETPI
!
!  INTRINSIC FUNCTIONS
      INTRINSIC COS
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL(KIND=WP) :: ACOV(LACOV)
!        THE AUTOCOVARIANCES OF THE SERIES.
!     REAL(KIND=WP) :: C
!        A VALUE USED TO COMPUTE THE SPECTRUM VALUES.
!     REAL(KIND=WP) :: DELTA
!        THE SAMPLING INTERVAL.
!     REAL(KIND=WP) :: FREQ(NF)
!        THE VECTOR OF FREQUENCIES AT WHICH THE SPECTRUM IS TO BE
!        COMPUTED.
!     INTEGER I
!        AN INDEX VARIABLE
!     INTEGER ISPCF
!         THE ACTUAL FIRST DIMENSION OF THE SPECTRUM ARRAYS.
!     INTEGER K, KK
!        INDEXING VARIABLES.
!     INTEGER LACOV
!        THE LENGTH OF VECTOR ACOV.
!     INTEGER LAG
!        THE LAG WINDOW TRUCCATION POINT.
!     INTEGER LW
!        THE LENGTH OF THE VECTOR W.
!     INTEGER NF
!        THE NUMBER OF FREQUENCIES AT WHICH THE SPECTRUM IS
!        TO BE COMPUTED.
!     REAL(KIND=WP) :: PI
!        THE VALUE OF PI.
!     REAL(KIND=WP) :: SPCF(ISPCF)
!        THE ARRAYS IN WHICH THE SPECTRUM IS STORED.
!     REAL(KIND=WP) :: V0, V1, V2
!        CONSTANTS USED FOR COMPUTING THE SPECTRUM VALUES.
!     REAL(KIND=WP) :: W(LW)
!        THE VECTOR OF LAG WINDOWS.
!
      CALL GETPI(PI)
!
!        COMPUTE THE SPECTRUM AND ITS CONFIDENCE LIMITS.
!
      DO 20 I=1,NF
         C = COS(2.0_WP*PI*FREQ(I))
         V0 = 0.0_WP
         V1 = 0.0_WP
         DO 10 K=1,LAG
            KK = LAG + 1 - K
            V2 = 2.0_WP*C*V1 - V0 + W(KK+1)*ACOV(KK+1)
            V0 = V1
            V1 = V2
   10    CONTINUE
         SPCF(I) = DELTA*(ACOV(1)*W(1)+2.0_WP*(V1*C-V0))
         IF (SPCF(I).LT.0.0_WP) SPCF(I) = 0.0_WP
   20 CONTINUE
      RETURN
      END
