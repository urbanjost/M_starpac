!AMESTP
      SUBROUTINE AMESTP(XM, N, M, IXM, MDL, PAR, NPAR, STP,
     &   EXMPT, NETA, SCALE, LSCALE, NPRT, HDR, PAGE, WIDE, ISUBHD,
     &   HLFRPT, PRTFXD, IFIXED, LIFIXD, STPOUT, PVPAD)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE CONTROLS THE STEP SIZE SELECTION PROCESS.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  APRIL 2, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) ::
     &   EXMPT
      INTEGER
     &   ISUBHD,IXM,LIFIXD,LSCALE,M,N,NETA,NPAR,NPRT,PVPAD
      LOGICAL
     &   HLFRPT,PAGE,PRTFXD,WIDE
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   PAR(*),SCALE(*),STP(*),XM(IXM,*)
      INTEGER
     &   IFIXED(*)
!
!  SUBROUTINE ARGUMENTS
      EXTERNAL HDR,MDL,STPOUT
!
!  SCALARS IN COMMON
      REAL(KIND=WP) ::
     &   Q
      INTEGER
     &   IERR
!
!  ARRAYS IN COMMON
      REAL(KIND=WP) :: DSTAK(12)
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     &   ETA,EXM,FPLRS,SCL,TAU
      INTEGER
     &   CD,FD,FDLAST,FDSAVE,IFAILJ,IFIXD,IFP,ITEMP,J,MXFAIL,NALL0,
     &   NDD,NDGT1,NEXMPT,NFAIL,NFAILJ,PARTMP,PV,PVMCD,PVNEW,PVPCD,
     &   PVSTP,PVTEMP
      LOGICAL
     &   HEAD
!
!  LOCAL ARRAYS
      REAL(KIND=WP) ::
     &   RSTAK(12)
      INTEGER
     &   ISTAK(12)
!
!  EXTERNAL FUNCTIONS
      REAL(KIND=WP) ::
     &   D1MACH
      INTEGER
     &   STKST
      EXTERNAL D1MACH,STKST
!
!  EXTERNAL SUBROUTINES
      EXTERNAL CPYVII,ETAMDL,SETIV,STKCLR,STPMN
!
!  INTRINSIC FUNCTIONS
      INTRINSIC ABS,INT,LOG10,MAX,MIN
!
!  COMMON BLOCKS
      COMMON /CSTAK/DSTAK
      COMMON /ERRCHK/IERR
      COMMON /NOTOPT/Q
!
!  EQUIVALENCES
      EQUIVALENCE (DSTAK(1),ISTAK(1))
      EQUIVALENCE (DSTAK(1),RSTAK(1))
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     INTEGER CD
!        THE STARTING LOCATION IN THE WORK AREA OF
!        THE CENTRAL DIFFERENCE QUOTIENT APPROXIMATION TO THE
!        DERIVATIVE OF THE MODEL WITH RESPECT TO THE JTH PARAMETER.
!     REAL(KIND=WP) :: DSTAK(12)
!        THE DOUBLE PRECISION VERSION OF THE /CSTAK/ WORK AREA.
!     REAL(KIND=WP) :: ETA
!        THE RELATIVE NOISE IN THE MODEL.
!     REAL(KIND=WP) :: EXM
!        THE PROPORTION OF OBSERVATIONS ACTUALLY USED FOR WHICH THE
!        COMPUTED NUMERICAL DERIVATIVES WRT A GIVEN PARAMETER ARE
!        EXEMPTED FROM MEETING THE DERIVATIVE ACCEPTANCE CRITERIA.
!     REAL(KIND=WP) :: EXMPT
!        THE PROPORTION OF OBSERVATIONS FOR WHICH THE COMPUTED
!        NUMERICAL DERIVATIVES WRT A GIVEN PARAMETER ARE EXEMPTED
!        FROM MEETING THE DERIVATIVE ACCEPTANCE CRITERIA.
!     INTEGER FD
!        THE STARTING LOCATION IN THE WORK AREA OF
!        THE FORWARD DIFFERENCE QUOTIENT APPROXIMATION TO THE
!        DERIVATIVE OF THE MODEL WITH RESPECT TO THE JTH PARAMETER.
!     INTEGER FDLAST
!        THE STARTING LOCATION IN THE WORK AREA OF
!        THE FORWARD DIFFERENCE QUOTIENT APPROXIMATION TO THE
!        DERIVATIVE OF THE MODEL WITH RESPECT TO THE JTH PARAMETER
!        FOR THE LAST STEP SIZE TRIED.
!     INTEGER FDSAVE
!        THE STARTING LOCATION IN THE WORK AREA OF
!        THE FORWARD DIFFERENCE QUOTIENT APPROXIMATION TO THE
!        DERIVATIVE OF THE MODEL WITH RESPECT TO THE JTH PARAMETER
!        FOR THE BEST STEP SIZE TRIED SO FAR.
!     REAL(KIND=WP) :: FPLRS
!        THE FLOATING POINT LARGEST RELATIVE SPACING.
!     EXTERNAL HDR
!        THE NAME OF THE ROUTINE WHICH PRODUCES THE HEADING
!     LOGICAL HEAD
!        A FLAG INDICATING WHETHER THE HEADING SHOULD BE PRINTED
!        (TRUE) OR NOT (FALSE).  IF A HEADING IS PRINTED, THE VALUE
!        OF HEAD WILL BE CHANGED TO FALSE.
!     LOGICAL HLFRPT
!        THE VARIABLE WHICH INDICATES WHETHER THE DERIVATIVE
!        CHECKING ROUTINE HAS ALREADY PRINTED PART OF THE
!        INITIAL SUMMARY (TRUE) OR NOT (FALSE).
!     INTEGER IERR
!        THE VALUE RETURNED BY THIS ROUTINE DESIGNATING
!        WHETHER ANY ERRORS WERE DETECTED IN THE PARAMETER LIST.
!        IF IERR .EQ. 0, NO ERRORS WERE DETECTED.
!        IF IERR .GE. 1, ERRORS WERE DETECTED.
!     INTEGER IFAILJ
!        THE STARTING LOCATION IN ISTAK FOR
!        THE ARRAY OF INDICATOR VARIABLES DESIGNATING WHETHER
!        THE SETP SIZE SELECTED WAS SATISFACOTRY FOR A GIVEN
!        OBSERVATION AND THE JTH PARAMETER.
!     INTEGER IFIXD
!        THE STARTING LOCATION IN /CSTAK/ OF VECTOR IFIXD CONTAINING
!        THE INDICATOR VALUES USED TO DESIGNATE WHETHER THE
!        PARAMETERS ARE TO BE OPTIMIZED OR ARE TO BE HELD FIXED.
!        IF IFIXED(I).NE.0, THEN PAR(I) WILL BE HELD FIXED.
!        IF IFIXED(I).EQ.0, THEN PAR(I) WILL BE OPTIMIZED.
!     INTEGER IFIXED(LIFIXD)
!        THE INDICATOR VALUES USED TO DESIGNATE WHETHER THE
!        PARAMETERS ARE TO BE OPTIMIZED OR ARE TO BE HELD FIXED.
!        IF IFIXED(I).NE.0, THEN PAR(I) WILL BE HELD FIXED.
!        IF IFIXED(I).EQ.0, THEN PAR(I) WILL BE OPTIMIZED.
!     INTEGER IFP
!        AN INDICATOR FOR THE PRECISION OF THE STACK ALLOCATION TYPE,
!        WHERE IFP=3 INDICATES SINGLE AND IFP=4 INDICATES DOUBLE.
!     INTEGER ISTAK(12)
!        THE INTEGER VERSION OF THE /CSTAK/ WORK AREA.
!     INTEGER ISUBHD
!        AN INDICATOR VALUE SPECIFYING SUBHEADINGS TO BE PRINTED.
!     INTEGER ITEMP
!        THE STARTING LOCATION IN ISTAK FOR
!        A TEMPORARY STORAGE VECTOR.
!     INTEGER IXM
!        THE FIRST DIMENSION OF MATRIX XM.
!     INTEGER J
!        AN INDEX VARIABLE.
!     INTEGER LIFIXD
!        THE DIMENSION OF VECTOR IFIXED.
!     INTEGER LSCALE
!        THE DIMENSION OF VECTOR SCALE.
!     INTEGER M
!        THE NUMBER OF INDEPENDENT VARIABLES.
!     EXTERNAL MDL
!        THE NAME OF THE USER SUPPLIED SUBROUTINE WHICH COMPUTES THE
!        PREDICTED VALUES BASED ON THE CURRENT PARAMETER ESTIMATES.
!     INTEGER MXFAIL
!        THE MAXIMUM NUMBER OF FAILURES FOR ANY PARAMETER.
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS.
!     INTEGER NALL0
!        NUMBER OF STACK ALLOCATIONS OUTSTANDING.
!     INTEGER NDD
!        THE NUMBER OF DECIMAL DIGITS CARRIED FOR A SINGLE
!        PRECISION DOUBLE PRECISION NUMBER.
!     INTEGER NDGT1
!        THE NUMBER OF RELIABLE DIGITS IN THE MODEL USED, EITHER
!        SET TO THE USER SUPPLIED VALUE OF NETA, OR COMPUTED
!        BY ETAMDL.
!     INTEGER NETA
!        THE NUMBER OF ACCURATE DIGITS IN THE MODEL RESULTS.
!     INTEGER NEXMPT
!        THE NUMBER OF OBSERVATIONS FOR WHICH A GIVEN STEP SIZE
!        DOES NOT HAVE TO BE SATISFACTORY AND THE SELECTED STEP
!        SIZE STILL BE CONSIDERED OK.
!     INTEGER NFAIL
!        THE NUMBER OF OBSERVATIONS FOR WHICH THE SELECTED STEP SIZE
!        FOR THE PARAMETER DOES NOT MEET THE CRITERIA.
!     INTEGER NFAILJ
!        THE NUMBER OF OBSERVATIONS FOR WHICH THE SELECTED STEP SIZE
!        FOR THE JTH PARAMETER DOES NOT MEET THE CRITERIA.
!     INTEGER NPAR
!        THE NUMBER OF PARAMETERS IN THE MODEL.
!     INTEGER NPRT
!        THE PARAMETER USED TO INDICATE HOW MUCH PRINTED OUTPUT IS
!        TO BE PROVIDED.
!     LOGICAL PAGE
!        THE VARIABLE USED TO INDICATE WHETHER A GIVEN SECTION OF
!        THE OUTPUT IS TO BEGIN ON A NEW PAGE (TRUE) OR NOT (FALSE).
!     REAL(KIND=WP) :: PAR(NPAR)
!        THE CURRENT ESTIMATES OF THE PARAMETERS.
!     INTEGER PARTMP
!        THE STARTING LOCATION IN THE WORK AREA OF
!        THE MODIFIED MODEL PARAMETERS
!     LOGICAL PRTFXD
!        THE INDICATOR VALUE USED TO DESIGNATE WHETHER THE
!        OUTPUT IS TO INCLUDE INFORMATION ON WHETHER THE
!        PARAMETER IS FIXED (TRUE) OR NOT (FALSE).
!     INTEGER PV
!        THE STARTING LOCATION IN THE WORK AREA OF
!        THE PREDICTED VALUE BASED ON THE CURRENT PARAMETER ESTIMATES
!     INTEGER PVMCD
!        THE STARTING LOCATION IN THE WORK AREA OF
!        THE PREDICTED VALUE BASED ON THE CURRENT PARAMETER ESTIMATES
!     INTEGER PVNEW
!        THE STARTING LOCATION IN THE WORK AREA OF
!        THE PREDICTED VALUE BASED ON THE CURRENT PARAMETER ESTIMATES
!        FOR ALL BUT THE JTH PARAMETER VALUE, WHICH IS PAR(J)+STPNEW.
!     INTEGER PVPAD
!       ADDITIONAL WORKSPACE NEEDED IN PV FOR THE EVALUATION OF THE
!       MODEL.
!     INTEGER PVPCD
!        THE STARTING LOCATION IN THE WORK AREA OF
!        THE PREDICTED VALUE BASED ON THE CURRENT PARAMETER ESTIMATES
!        FOR ALL BUT THE JTH PARAMETER VALUE, WHICH IS PAR(J)+STPCD.
!     INTEGER PVSTP
!        THE STARTING LOCATION IN THE WORK AREA OF
!        THE PREDICTED VALUE BASED ON THE CURRENT PARAMETER ESTIMATES
!        FOR ALL BUT THE JTH PARAMETER VALUE, WHICH IS PAR(J)+STP(J).
!     INTEGER PVTEMP
!        THE STARTING LOCATION IN THE WORK AREA OF
!        A TEMPORY STORAGE LOCATION FOR PREDICTED VALUES BEGINS.
!     REAL(KIND=WP) :: Q
!        A DUMMY VARIABLE WHICH IS USED, ALONG WITH COMMON NOTOPT (NO
!        OPTIMIZATION), TO COMPUTE THE STEP SIZE.
!     REAL(KIND=WP) :: RSTAK(12)
!        THE DOUBLE PRECISION VERSION OF THE /CSTAK/ WORK AREA.
!     REAL(KIND=WP) :: SCALE(LSCALE)
!        THE TYPICAL SIZE OF THE PARAMETERS.
!     REAL(KIND=WP) :: SCL
!        THE ACTUAL TYPICAL SIZE USED.
!     REAL(KIND=WP) :: STP(NPAR)
!        THE SELECTED STEP SIZES.
!     EXTERNAL STPOUT
!        THE ROUTINE FOR PRINTING THE OUTPUT.
!     REAL(KIND=WP) :: TAU
!        THE AGREEMENT TOLERANCE.
!     LOGICAL WIDE
!        THE VARIABLE USED TO INDICATE WHETHER THE HEADING SHOULD
!        BE FULL WIDTH (TRUE) OR NOT (FALSE).
!     REAL(KIND=WP) :: XM(IXM,M)
!        THE INDEPENDENT VARIABLE.
!
!
      NALL0 = STKST(1)
!
      FPLRS = D1MACH(4)
      IFP = 4
!
!     SET PRINT CONTROLS
!
      HEAD = .TRUE.
!
!     SUBDIVIDE WORK AREA
!
      IFIXD = STKGET(NPAR, 2)
      ITEMP = STKGET(N, 2)
      IFAILJ = STKGET(N, 2)
      NFAIL = STKGET(NPAR, 2)
!
      CD = STKGET(N, IFP)
      FD = STKGET(N, IFP)
      FDLAST = STKGET(N, IFP)
      FDSAVE = STKGET(N, IFP)
      PV = STKGET(N+PVPAD, IFP)
      PVMCD = STKGET(N+PVPAD, IFP)
      PVNEW = STKGET(N+PVPAD, IFP)
      PVPCD = STKGET(N+PVPAD, IFP)
      PVSTP = STKGET(N+PVPAD, IFP)
      PVTEMP = STKGET(N+PVPAD, IFP)
!
      IF (IERR .EQ. 1) RETURN
!
      PARTMP = CD
!
!     SET UP IFIXD
!
      IF (IFIXED(1).LT.0) CALL SETIV(ISTAK(IFIXD), NPAR, 0)
      IF (IFIXED(1).GE.0) CALL CPYVII(NPAR, IFIXED, 1, ISTAK(IFIXD), 1)
!
!     SET PARAMETERS NECESSARY FOR THE COMPUTATIONS
!
      NDD = INT(-LOG10(FPLRS))
!
      IF ((NETA .GE. 2) .AND. (NETA .LE. NDD)) THEN
            ETA = 10.0_WP ** (-NETA)
            NDGT1 = NETA
      ELSE
            CALL ETAMDL(MDL, PAR, NPAR, XM, N, M, IXM, ETA, NDGT1,
     &         RSTAK(PARTMP), RSTAK(PVTEMP), 0)
      END IF
!
      TAU = MIN(ETA**0.25_WP, 0.01_WP)
!
      EXM = EXMPT
      IF ((EXM.LT.0.0_WP) .OR. (EXM.GT.1.0_WP)) EXM = 0.10_WP
      NEXMPT = INT(EXM * N)
      IF (EXM .NE. 0.0_WP) NEXMPT = MAX(NEXMPT, 1)
!
!     COMPUTE PREDICTED VALUES OF THE MODEL USING THE INPUT PARAMETER
!     ESTIMATES
!
      CALL MDL(PAR, NPAR, XM, N, M, IXM, RSTAK(PV))
!
      MXFAIL = 0
      NFAILJ = NFAIL
!
      DO 120 J = 1, NPAR
!
         IF (SCALE(1) .LE. 0.0_WP) THEN
            IF (PAR(J) .EQ. 0.0_WP) THEN
               SCL = 1.0_WP
            ELSE
               SCL = ABS(PAR(J))
            END IF
         ELSE
            SCL = SCALE(J)
         END IF
!
         CALL STPMN(J, XM, N, M, IXM, MDL, PAR, NPAR, NEXMPT,
     &      ETA, TAU, SCL, STP(J), ISTAK(NFAILJ), ISTAK(IFAILJ),
     &      RSTAK(CD), ISTAK(ITEMP), RSTAK(FD), RSTAK(FDLAST),
     &      RSTAK(FDSAVE), RSTAK(PV), RSTAK(PVMCD), RSTAK(PVNEW),
     &      RSTAK(PVPCD), RSTAK(PVSTP), RSTAK(PVTEMP))
!
!     COMPUTE THE MAXIMUM NUMBER OF FAILURES FOR ANY PARAMETER
!
         MXFAIL = MAX(ISTAK(NFAILJ), MXFAIL)
!
!     PRINT RESULTS IF THEY ARE DESIRED
!
         IF ((NPRT.NE.0) .OR. (MXFAIL.GT.NEXMPT))
     &      CALL STPOUT(HEAD, N, EXM, NEXMPT, NDGT1, J, PAR, NPAR,
     &         STP, ISTAK(NFAIL), ISTAK(IFAILJ), SCALE,  LSCALE, HDR,
     &         PAGE, WIDE, ISUBHD, NPRT, PRTFXD, ISTAK(IFIXD))
!
         NFAILJ = NFAILJ + 1
!
  120 CONTINUE
!
      HLFRPT = .FALSE.
      IF ((NPRT.NE.0) .OR. (MXFAIL.GT.NEXMPT)) HLFRPT = .TRUE.
!
      IF (MXFAIL.GT.NEXMPT) IERR = 2
!
      CALL STKCLR(NALL0)
!
      RETURN
!
      END
