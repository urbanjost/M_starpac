!XAOV1
      SUBROUTINE XAOV1(LDSTAK,typ)
      real(kind=wp),intent(in) :: typ
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE EXCERCISES THE ONEWAY FAMILY ROUTINES.
!
!     WRITTEN BY -
!        LINDA MITCHELL
!        STATISTICAL ENGINEERING DIVISION
!        NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  MAY 17, 1982
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      INTEGER
     &   LDSTAK
!
!  SCALARS IN COMMON
      INTEGER
     &   IERR
!
!  ARRAYS IN COMMON
      REAL(KIND=WP) :: DSTAK(12)
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     &   FI
      INTEGER
     &   I,IGSTAT,IPRT,LDSMIN,N,NG
!
!  LOCAL ARRAYS
      REAL(KIND=WP) ::
     &   GSTAT(10,4),TAG(20),Y(20),Z(10),ZTAG(10)
!
!  EXTERNAL SUBROUTINES
!      EXTERNAL AOV1,AOV1S,AOV1XP,IPRINT,LDSCMP,MSGX,SETRV
!
!  COMMON BLOCKS
      COMMON /CSTAK/DSTAK
      COMMON /ERRCHK/IERR
!
!     VARIABLE DEFINITIONS (APHABETICALLY)
!
!     REAL(KIND=WP) :: DSTAK(12)
!        THE DOUBLE PRECISION VERSION OF THE /CSTAK/ WORK AREA.
!     REAL(KIND=WP) :: FI
!        FLOAT OF INDEX I
!     REAL(KIND=WP) :: GSTAT(10,4)
!        THE GROUP STATISTICS.  COLUMNS CORRESPOND TO THE TAG
!        VALUE, SAMPLE SIZE, GROUP MEAN, AND GROUP STANDARD DEVIATION.
!     INTEGER I
!        INDEX VARIABLE
!     INTEGER IERR
!        COMMON FLAG INDICATING WHETHER OR NOT THERE WERE ANY ERRORS
!     INTEGER IGSTAT
!        THE FIRST DIMENSION OF GSTAT.
!     INTEGER IPRT
!        THE OUTPUT LOGICAL UNIT NUMBER
!     INTEGER LDSMIN
!        THE SMALLEST ACCEPTABLE SIZE OF THE COMMON CSTAK
!     INTEGER LDSTAK
!        THE SIZE OF THE COMMON CSTAK
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS
!     INTEGER NG
!        THE NUMBER OF DIFFERENT GROUPS
!     REAL(KIND=WP) :: TAG(20)
!        THE TAG VALUES FOR EACH OBSERVATION
!     REAL(KIND=WP) :: Y(20)
!        THE VECTOR OF OBSERVATIONS
!     REAL(KIND=WP) :: Z(10)
!        TEST VECTOR
!     REAL(KIND=WP) :: ZTAG(10)
!        TEST TAG VECTOR
!
      DATA   Y( 1),   Y( 2),   Y( 3),   Y( 4),   Y( 5)
     &   /    61.0_WP,    61.0_WP,    67.0_WP,    67.0_WP,    64.0_WP/
      DATA   Y( 6),   Y( 7),   Y( 8),   Y( 9),   Y(10)
     &   /    78.0_WP,    71.0_WP,    75.0_WP,    72.0_WP,    74.0_WP/
      DATA   Y(11),   Y(12),   Y(13),   Y(14),   Y(15)
     &   /    83.0_WP,    81.0_WP,    76.0_WP,    78.0_WP,    79.0_WP/
      DATA   Y(16),   Y(17)
     &   /    72.0_WP,   72.0_WP/
!
      DATA TAG( 1), TAG( 2), TAG( 3), TAG( 4), TAG( 5)
     &   /    11.5_WP,    11.5_WP,    11.5_WP,    11.5_WP,    11.5_WP/
      DATA TAG( 6), TAG( 7), TAG( 8), TAG( 9), TAG(10)
     &   /    12.0_WP,    12.0_WP,    12.0_WP,    12.0_WP,    12.0_WP/
      DATA TAG(11), TAG(12), TAG(13), TAG(14), TAG(15)
     &   /    11.0_WP,    11.0_WP,    11.0_WP,    11.0_WP,    11.0_WP/
      DATA TAG(16), TAG(17)
     &   /   -11.0_WP,   11.0_WP/
!
      CALL IPRINT(IPRT)
!
!     SET VARIOUS DIMENSIONS AND PROGRAM VARIABLES
!
      N = 17
      IGSTAT = 10
!
!**** TEST WITH CORRECT CALL STATEMENTS ****
!
      WRITE (IPRT,1060)
!
!     TEST AOV1
!
      WRITE (IPRT,1000)
      CALL AOV1(Y, TAG, N, LDSTAK)
      CALL MSGX(0, IPRT)
      WRITE (IPRT,1010)
      WRITE (IPRT,1020) (TAG(I),I=1,N)
!
!     TEST OF AOV1S
!
!     PRINTOUT NOT SUPRESSED
!
      WRITE (IPRT,1040)
      WRITE (IPRT,1030)
      CALL AOV1S(Y, TAG, N, LDSTAK, 1, GSTAT, IGSTAT, NG)
      CALL MSGX(0, IPRT)
!
!     PRINT STORAGE AND ZERO VECTORS BEFORE USING AGAIN
!
      CALL AOV1XP(GSTAT, IGSTAT, NG)
!
!     PRINTOUT SUPRESSED
!
      WRITE (IPRT,1050)
      CALL AOV1S(Y, TAG, N, LDSTAK, 0, GSTAT, IGSTAT, NG)
      CALL MSGX(0, IPRT)
!
!     PRINT STORAGE AND ZERO VECTORS BEFORE USING AGAIN
!
      CALL AOV1XP(GSTAT, IGSTAT, NG)
!
!**** NUMBER OF OBSERVATIONS LESS THAN 2 ****
!
      WRITE (IPRT,1090)
      CALL AOV1(Y, TAG, 1, LDSTAK)
      CALL MSGX(1, IPRT)
!
      CALL AOV1S(Y, TAG, -14, LDSTAK, 0, GSTAT, IGSTAT, NG)
      CALL MSGX(1, IPRT)
!
!**** ALL OBSERVATIONS THE SAME VALUE ****
!
      WRITE (IPRT,1100)
      WRITE (IPRT,1000)
      CALL SETRV(Z, 10, 0.0_WP)
      CALL AOV1(Z, TAG, 10, LDSTAK)
      CALL MSGX(0, IPRT)
!
      CALL SETRV(Z, 10, 2.0_WP)
      WRITE (IPRT,1100)
      WRITE (IPRT,1030)
      CALL AOV1S(Z, TAG, 10, LDSTAK, 1, GSTAT, IGSTAT, NG)
      CALL MSGX(0, IPRT)
!
!**** TEST WORK AREA SIZE HANDLING ****
!
      CALL LDSCMP(11, 0, 33, 0, 0, 0, 'D', 40, LDSMIN)
      WRITE (IPRT,1070)
      WRITE (IPRT,1000)
      CALL AOV1(Y, TAG, N, 1)
      CALL MSGX(1, IPRT)
      WRITE (IPRT,1070)
      WRITE (IPRT,1000)
      CALL AOV1(Y, TAG, N, LDSMIN-1)
      CALL MSGX(1, IPRT)
      WRITE (IPRT,1010)
      WRITE (IPRT,1020) (TAG(I),I=1,N)
      WRITE (IPRT,1080)
      WRITE (IPRT,1000)
      CALL AOV1(Y, TAG, N, LDSMIN)
      CALL MSGX(0, IPRT)
!
      CALL LDSCMP(11, 0, 33, 0, 0, 0, 'D', 28, LDSMIN)
      WRITE (IPRT,1070)
      WRITE (IPRT,1030)
      CALL AOV1S(Y, TAG, N, 1, 0, GSTAT, IGSTAT, NG)
      CALL MSGX(1, IPRT)
      WRITE (IPRT,1070)
      WRITE (IPRT,1030)
      CALL AOV1S(Y, TAG, N, LDSMIN-1, 0, GSTAT, IGSTAT, NG)
      CALL MSGX(1, IPRT)
      WRITE (IPRT,1010)
      WRITE (IPRT,1020) (TAG(I),I=1,N)
      WRITE (IPRT,1080)
      WRITE (IPRT,1030)
      CALL AOV1S(Y, TAG, N, LDSMIN, 1, GSTAT, IGSTAT, NG)
      CALL MSGX(0, IPRT)
!
!**** SAME NUMBER OF GROUPS AS NON-ZERO TAGS ****
!
      WRITE (IPRT,1120)
      DO 20 I=1,10
         FI = I
         ZTAG(I) = FI
         Z(I) = 13.0_WP - FI
   20 CONTINUE
      CALL AOV1(Z, ZTAG, 10, LDSTAK)
      CALL MSGX(1, IPRT)
      CALL AOV1S(Z, ZTAG, 10, LDSTAK, 1, GSTAT, IGSTAT, NG)
      CALL MSGX(1, IPRT)
!
!**** LESS THAN 2 DIFFERENT TAG GROUPS ****
!
      WRITE (IPRT,1130)
      CALL SETRV(ZTAG, 10, 1.0_WP)
      CALL AOV1(Z, ZTAG, 10, LDSTAK)
      CALL MSGX(1, IPRT)
      CALL AOV1S(Z, ZTAG, 10, LDSTAK, 1, GSTAT, IGSTAT, NG)
      CALL MSGX(1, IPRT)
!
!**** LESS THAN 2 TAGS ****
!
      CALL SETRV(ZTAG, 9, 0.0_WP)
      WRITE (IPRT,1140)
      CALL AOV1(Z, ZTAG, 10, LDSTAK)
      CALL MSGX(1, IPRT)
      CALL AOV1S(Z, ZTAG, 10, LDSTAK, 1, GSTAT, IGSTAT, NG)
      CALL MSGX(1, IPRT)
!
!**** INCORRECT DIMENSION OF GSTAT ****
!
      WRITE (IPRT,1150)
      CALL AOV1S(Y, TAG, N, LDSTAK, 1, GSTAT, 2, NG)
      CALL MSGX(1, IPRT)
!
!**** ALL OBSERVATIONS WITHIN A GROUP SAME VALUE ****
!
      Z(1) = 53.0_WP
      ZTAG(1) = 1.0_WP
      Z(2) = 62.0_WP
      ZTAG(2) = 3.0_WP
      Z(3) = 53.0_WP
      ZTAG(3) = 1.0_WP
      Z(4) = 71.0_WP
      ZTAG(4) = 4.0_WP
      Z(5) = 89.0_WP
      ZTAG(5) = 2.0_WP
      Z(6) = 71.0_WP
      ZTAG(6) = 4.0_WP
      Z(7) = 89.0_WP
      ZTAG(7) = 2.0_WP
      Z(8) = 62.0_WP
      ZTAG(8) = 3.0_WP
      Z(9) = 71.0_WP
      ZTAG(9) = 4.0_WP
      Z(10) = 62.0_WP
      ZTAG(10) = 3.0_WP
      WRITE (IPRT,1160)
      CALL AOV1(Z, ZTAG, 10, LDSTAK)
      CALL MSGX(0, IPRT)
!
!**** 2 TAGS ****
!
      WRITE (IPRT,1170)
      CALL AOV1(Z, ZTAG, 3, LDSTAK)
      CALL MSGX(0, IPRT)
!
!**** ALL GROUPS(EXCEPT FOR 1) WITH 1 OBSERVATION ****
!
      WRITE (IPRT,1180)
      CALL AOV1(Z, ZTAG, 5, LDSTAK)
      CALL MSGX(0, IPRT)
!
      RETURN
!
!     FORMAT STATEMENTS
!
 1000 FORMAT(' TEST OF AOV1 ')
 1010 FORMAT(' CHECK TO SEE IF TAGS HAVE BEEN CHANGED')
 1020 FORMAT(4F12.6)
 1030 FORMAT(' TEST OF AOV1S ')
 1040 FORMAT('1PRINTOUT NOT SUPRESSED.')
 1050 FORMAT(' PRINTOUT SUPRESSED.')
 1060 FORMAT('1****TEST ROUTINES WITH CORRECT CALL****')
 1070 FORMAT('1****TEST WITH INSUFFICIENT WORK AREA****')
 1080 FORMAT('1****TEST WITH EXACTLY THE RIGHT AMOUNT OF WORK AREA****')
 1090 FORMAT('1****NUMBER OF OBSERVATIONS LESS THAN 2****')
 1100 FORMAT('1****ALL OBSERVATIONS WITH SAME VALUE****')
 1120 FORMAT('1****SAME NUMBER OF GROUPS AS NON-ZERO TAGS****')
 1130 FORMAT(' ****LESS THAN 2 DIFFERENT TAG GROUPS****')
 1140 FORMAT(' ****LESS THAN 2 TAGS****')
 1150 FORMAT('1****INCORRECT DIMENSION OF GSTAT****')
 1160 FORMAT('1****ALL OBSERVATIONS WITHIN A GROUP SAME VALUE****')
 1170 FORMAT('1****TEST WITH 2 TAGS****')
 1180 FORMAT('1****ALL GROUPS EXCEPT FOR 1 WITH 1 OBSERVATION ****')
      END
