!FLTMA
      SUBROUTINE FLTMA (Y, N, K, HMA, YF, NYF)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE FILTERS THE INPUT SERIES Y USING THE K TERMS
!     OF H, COPYING THE FILTERED SERIES INTO YF.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DEVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 26, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) ::
     &   HMA
      INTEGER
     &   K,N,NYF
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   Y(N),YF(N)
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     &   SUM
      INTEGER
     &   I,I1,II,J
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL(KIND=WP) :: HMA
!        THE VALUE OF EACH OF THE SIMPLE MOVING AVERAGE LINEAR FILTER
!        COEFFICIENTS.
!     INTEGER I, II, I1, J
!        INDEXING VARIABLES.
!     INTEGER K
!        THE NUMBER OF FILTER TERMS.
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS IN THE SERIES Y.
!     INTEGER NYF
!        THE NUMBER OF OBSERVATIONS IN THE FILTERED SERIES YF.
!     REAL(KIND=WP) :: SUM
!        A TEMPORARY LOCATION USED IN COMPUTING THE FILTERED SERIES.
!     REAL(KIND=WP) :: Y(N)
!        THE VECTOR CONTAINING THE OBSERVED TIME SERIES.
!     REAL(KIND=WP) :: YF(N)
!        THE VECTOR IN WHICH THE FILTERED SERIES IS RETURNED.
!
      DO 10 I = 1, N
         YF(I) = Y(I)
   10 CONTINUE
!
      NYF = N - (K - 1)
!
      DO 30 I = 1, NYF
         II = I - 1
         SUM = 0.0_WP
         DO 20 J = 1, K
            II = II + 1
            SUM = SUM + HMA*YF(II)
   20    CONTINUE
         YF(I) = SUM
   30 CONTINUE
!
      I1 = NYF + 1
!
      DO 40 I = I1, N
         YF(I) = 0.0_WP
   40 CONTINUE
!
      RETURN
      END
