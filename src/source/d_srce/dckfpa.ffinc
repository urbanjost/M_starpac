!DCKFPA
      SUBROUTINE DCKFPA(J, D, PAR, NPAR, ETA, TAU, MDL, XM, N,
     &   NROW, M, IXM, PV, PVTEMP, MSG, LMSG, FD, PARMX, STP, PVPSTP,
     &   CURVE)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     CHECK WHETHER FINITE PRECISION ARITHMETIC COULD POSSIBLY BE THE
!     PROBLEM
!
!     WRITTEN BY  -  ROBERT B. SCHNABEL (CODED BY JANET R. DONALDSON)
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  APRIL 2, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) ::
     &   CURVE,D,ETA,FD,PARMX,PV,PVPSTP,STP,TAU
      INTEGER
     &   IXM,J,LMSG,M,N,NPAR,NROW
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   PAR(NPAR),PVTEMP(N),XM(IXM,M)
      INTEGER
     &   MSG(LMSG)
!
!  SUBROUTINE ARGUMENTS
      EXTERNAL MDL
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     &   TEMP
      LOGICAL
     &   LARGE
!
!  INTRINSIC FUNCTIONS
      INTRINSIC ABS,SIGN
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL(KIND=WP) :: CURVE
!        A MEASURE OF THE CURVATURE IN THE MODEL.
!     REAL(KIND=WP) :: D
!        THE SCALAR IN WHICH ROW   NROW   OF THE DERIVATIVE
!        MATRIX WITH RESPECT TO THE JTH UNKNOWN PARAMETER
!        IS STORED.
!     REAL(KIND=WP) :: ETA
!        THE RELATIVE NOISE IN THE MODEL
!     REAL(KIND=WP) :: FD
!        THE FORWARD DIFFERENCE QUOTIENT DERIVATIVE WITH RESPECT TO THE
!        JTH PARAMETER
!     INTEGER IXM
!        THE FIRST DIMENSION OF THE INDEPENDENT VARIABLE ARRAY.
!     INTEGER J
!        THE INDEX OF THE PARTIAL DERIVATIVE BEING EXAMINED.
!     LOGICAL LARGE
!        AN INDICATOR VALUE INDICATING WHETHER THE RECOMMENDED
!        INCREASE IN THE STEP SIZE WOULD BE GREATER THAN PARMX.
!     INTEGER LMSG
!        THE LENGTH OF THE VECTOR MSG.
!     INTEGER M
!        THE NUMBER OF INDEPENDENT VARIABLES.
!     EXTERNAL MDL
!        THE NAME OF THE USER SUPPLIED SUBROUTINE WHICH COMPUTES THE
!        PREDICTED VALUES BASED ON THE CURRENT PARAMETER ESTIMATES.
!     INTEGER MSG(LMSG)
!        AN ARRAY USED TO STORE MESSAGE PARAMETERS.
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS.
!     INTEGER NPAR
!        THE NUMBER OF UNKNOWN PARAMETERS IN THE MODEL.
!     INTEGER NROW
!        THE NUMBER OF THE ROW OF THE INDEPENDENT VARIABLE ARRAY AT
!        WHICH THE DERIVATIVE IS TO BE CHECKED.
!     REAL(KIND=WP) :: PAR(NPAR)
!        THE ARRAY IN WHICH THE CURRENT ESTIMATES OF THE UNKNOWN
!        PARAMETERS ARE STORED.
!     REAL(KIND=WP) :: PARMX
!        THE MAXIMUM OF THE CURRENT PARAMETER ESTIMATE AND THE
!        TYPICAL VALUE OF THAT PARAMETER
!     REAL(KIND=WP) :: PV
!        THE SCALAR IN WHICH THE PREDICTED VALUE FROM THE MODEL FOR
!        ROW   NROW   IS STORED.
!     REAL(KIND=WP) :: PVPSTP
!        THE PREDICTED VALUE FOR ROW    NROW   OF THE MODEL
!        BASED ON THE CURRENT PARAMETER ESTIMATES
!        FOR ALL BUT THE JTH PARAMETER VALUE, WHICH IS PAR(J) + STP.
!     REAL(KIND=WP) :: PVTEMP(N)
!        THE VECTOR OF PREDICTED VALUES FROM THE MODEL.
!     REAL(KIND=WP) :: STP
!        THE STEP SIZE CURRENTLY BEING EXAMINED FOR THE FINITE DIFFERENC
!        DERIVATIVE
!     REAL(KIND=WP) :: TAU
!        THE AGREEMENT TOLERANCE.
!     REAL(KIND=WP) :: TEMP
!        A TEMPORARY LOCATION IN WHICH THE CURRENT ESTIMATE OF THE JTH
!        PARAMETER IS STORED.
!     REAL(KIND=WP) :: XM(IXM,M)
!        THE ARRAY IN WHICH ONE ROW OF THE INDEPENDENT VARIABLE ARRAY
!        IS STORED.
!
!     CHECK WHETHER FINITE PRECISION COULD BE THE PROBLEM
!
      IF (ABS(STP*(FD-D)) .GE.
     &   10.0_WP*ETA*(ABS(PV)+ABS(PVPSTP))) THEN
!
!     DISCREPANCY BETWEEN NUMERICAL AND ANALYTICAL DERIVATIVES CANNOT
!     BE ACCOUNTED FOR BY FINITE PRECISION ARITHMETIC
!
         MSG(1) = 2
         MSG(J+1) = 2
         RETURN
!
      END IF
!
!     FINITE PRECISION ARITHMETIC COULD BE THE PROBLEM.
!
!     TRY A LARGER STEP SIZE
!
      STP = (ETA*(ABS(PV)+ABS(PVPSTP))*SIGN(1.0_WP,PAR(J))/
     &   (TAU*ABS(D))+PAR(J)) - PAR(J)
!
      LARGE = .FALSE.
!
      IF (ABS(STP).GT.PARMX) THEN
         STP = PARMX*SIGN(1.0_WP,PAR(J))
         LARGE = .TRUE.
      END IF
!
!     CALCULATE NUMERICAL DERIVATIVE USNG NEW, LARGER, STEPSIZE
!
      TEMP = PAR(J)
      PAR(J) = PAR(J) + STP
      CALL MDL(PAR, NPAR, XM, N, M, IXM, PVTEMP)
      PAR(J) = TEMP
!
      PVPSTP = PVTEMP(NROW)
!
      FD = (PVPSTP-PV)/STP
!
!     CHECK FOR AGREEMENT
!
      IF ((ABS(FD-D)).LE.2.0_WP*TAU*ABS(D)) THEN
!
!     FORWARD DIFFERENCE QUOTIENT AND ANALYTIC DERIVATIVES AGREE FOR
!     THIS STEP SIZE
!
         RETURN
      END IF
!
!     FORWARD DIFFERENCE QUOTIENT AND ANALYTIC DERIVATIVES STILL
!     DISAGREE
!
!     CHECK IF CURVATURE IS THE PROBLEM
!
      IF (ABS(CURVE*STP) .LT. ABS(FD-D) .AND. (.NOT. LARGE)) THEN
!
!        CURVATURE COULDNT BE THE CULPRIT
!
             MSG(1) = 2
             MSG(J+1) = 2
             RETURN
      ELSE
!
!        CURVATURE MAY BE THE CULPRIT
!
             IF (MSG(1).EQ.0) MSG(1) = 1
             IF (LARGE) MSG(J+1) = 6
             IF (.NOT. LARGE) MSG(J+1) = 1
             RETURN
      END IF
!
      END
