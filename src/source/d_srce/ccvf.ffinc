!CCVF
      SUBROUTINE CCVF(Y1, Y2, N, LAGMAX, Y1MEAN, Y2MEAN, CCOV12, CCOV21,
     +   ICCOV)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE COMPUTES THE CROSS COVARIANCE FUNCTION
!     BETWEEN TWO SERIES.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 21, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      DOUBLE PRECISION
     +   Y1MEAN,Y2MEAN
      INTEGER
     +   ICCOV,LAGMAX,N
!
!  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   CCOV12(ICCOV),CCOV21(ICCOV),Y1(N),Y2(N)
!
!  LOCAL SCALARS
      DOUBLE PRECISION
     +   DOTXY
      INTEGER
     +   LAG,NDOTXY
!
!  EXTERNAL SUBROUTINES
      EXTERNAL DOTC
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     DOUBLE PRECISION CCOV12(ICCOV), CCOV21(ICCOV)
!        THE ARRAYS IN WHICH THE CCVF FOR SERIES 1 LAGGED
!        BEHIND SERIES 2 AND VISA VERSA, RESPECTIVELY, ARE
!        STORED.
!     DOUBLE PRECISION DOTXY
!        VARIOUS CROSS PRUDUCTS BETWEEN SERIES Y1 AND Y2.
!     INTEGER ICCOV
!        THE ROW DIMENSION OF THE ARRAYS CCOV12 AND CCOV21.
!     INTEGER LAG
!        THE INDEXING VARIABLE INDICATING THE LAG VALUE OF THE
!        AUTOCORRELATION BEING COMPUTED.
!     INTEGER LAGMAX
!        THE MAXIMUM NUMBER OF LAGS TO BE USED.
!     INTEGER N
!        THE INTEGER NUMBER OF OBSERVATIONS IN THE SERIES
!     INTEGER NDOTXY
!        THE NUMBER OF OBSERVATIONS USED TO COMPUTE DOTXY.
!     DOUBLE PRECISION Y1(N), Y1MEAN
!        THE FIRST SERIES, AND ITS MEAN.
!     DOUBLE PRECISION Y2(N), Y2MEAN
!        THE SECOND SERIES, AND ITS MEAN.
!
!     COMPUTE THE CROSS COVARIANCES
!
      CALL DOTC ( Y1, Y1MEAN, N, Y2, Y2MEAN, N, DOTXY, NDOTXY)
      CCOV12(1) = DOTXY / N
      CCOV21(1) = CCOV12(1)
!
      DO 10 LAG = 1, LAGMAX
!
         CALL DOTC (Y1, Y1MEAN, N, Y2(LAG + 1), Y2MEAN, N - LAG,
     +       DOTXY, NDOTXY)
         CCOV12(LAG+1) = DOTXY / N
!
         CALL DOTC (Y2, Y2MEAN, N, Y1(LAG + 1), Y1MEAN, N - LAG,
     +       DOTXY, NDOTXY)
         CCOV21(LAG+1) = DOTXY / N
!
   10 CONTINUE
!
      RETURN
      END
