!SUMDS
     subroutine sumds(x, n, lo, mid, hi, xmeanw, sumda, sumd2, sumd3,&
     &   sumd4)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     A ROUTINE TO CALCULATE UNWEIGHTED SUMS OF POWERS OF
!     DIFFERENCES FROM THE WEIGHTED MEAN FOR A SORTED
!     VECTOR IN WHICH THE MIDTH ELEMENT IS THE ELEMENT
!     CLOSEST TO ZERO.  USED BY THE STAT FAMILY.
!
!     WRITTEN BY - JOHN E. KOONTZ
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS
!
!     CREATION DATE  -  MAY 17, 1982
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     real(kind=wp) ::&
     &   sumd2,sumd3,sumd4,sumda,xmeanw
     integer&
     &   hi,lo,mid,n
!
!  ARRAY ARGUMENTS
     real(kind=wp) ::&
     &   x(n)
!
!  LOCAL SCALARS
     real(kind=wp) ::&
     &   diff
     integer&
     &   curhi,curlo,i,irev
!
!  INTRINSIC FUNCTIONS
      intrinsic abs
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     INTEGER CURHI
!        THE UPPER BOUND OF THE CURRENT INTERVAL.
!     INTEGER CURLO
!        THE LOWER BOUND OF THE CURRENT INTERVAL.
!     REAL(KIND=WP) :: DIFF
!        THE DIFFERENCES BETWEEN X(I) AND XMEANW.
!     INTEGER HI
!        INPUT PARAMETER.  THE UPPER BOUND OF THE INITIAL
!        INTERVAL.
!     INTEGER I
!        A LOOP PARAMETER.
!     INTEGER IREV
!        A VARIABLE WHICH RUNS IN THE SAME INTERVAL AS I, BUT
!        IN THE REVERSE ORDER.
!     INTEGER LO
!        INPUT PARAMETER.  THE LOWER BOUND OF THE INITIAL
!        INTERVAL.
!     INTEGER MID
!        INPUT PARAMETER.  THE INDEX OF THE ELEMENT IN X CLOSEST TO
!        ZERO IN VALUE.  THE POINT OUT FROM WHICH THE SUMMING IS
!        DONE.
!     INTEGER N
!        INPUT PARAMETER.  THE LENGTH OF THE ARRAY X.
!     REAL(KIND=WP) :: SUMDA
!        OUTPUT PARAMETER.  THE SUM OF THE ABSOLUTE VALUES OF THE
!        DIFFERENCES DIFF.
!     REAL(KIND=WP) :: SUMD2
!        OUTPUT PARAMETER.  THE SUM OF THE SQUARES OF THE
!        DIFFERENCES DIFF.
!     REAL(KIND=WP) :: SUMD3
!        OUTPUT PARAMETER.  THE SUM OF THE CUBES OF THE
!        DIFFERENCES DIFF.
!     REAL(KIND=WP) :: SUMD4
!        OUTPUT PARAMETER.  THE SUM OF THE HYPERCUBES OF THE
!        DIFFERENCES DIFF.
!     REAL(KIND=WP) :: X(N)
!        INPUT PARAMETER.  THE DATA ARRAY X IN WHICH THE SUMS ARE TAKEN.
!     REAL(KIND=WP) :: XMEANW
!        INPUT PARAMETER.  THE WEIGHTED MEAN OF X.
!
!     INITIALIZE SUMMATION VARIABLES.
!
      diff = x(mid) - xmeanw
      sumda = abs(diff)
      sumd2 = diff*diff
      sumd3 = diff*diff*diff
      sumd4 = diff*diff*diff*diff
      curlo = mid - 1
      curhi = mid + 1
!
!     SUM OUTWARDS FROM THE VALUE NEAREST ZERO.  THAT IS, SUM FROM
!     THE LEAST IN MAGNITUDE TO THE GREATEST.
!
   10 if (curhi.gt.hi .or. curlo.lt.lo) go to 30
      if (abs(x(curhi)).lt.abs(x(curlo))) go to 20
      diff = x(curlo) - xmeanw
      sumda = sumda + abs(diff)
      sumd2 = sumd2 + diff*diff
      sumd3 = sumd3 + diff*diff*diff
      sumd4 = sumd4 + diff*diff*diff*diff
      curlo = curlo - 1
      go to 10
   20 diff = x(curhi) - xmeanw
      sumda = sumda + abs(diff)
      sumd2 = sumd2 + diff*diff
      sumd3 = sumd3 + diff*diff*diff
      sumd4 = sumd4 + diff*diff*diff*diff
      curhi = curhi + 1
      go to 10
!
!     SUM UP ANY VALUES WHICH MAY REMAIN BECAUSE OF AN OFF
!     CENTER ZERO POINT.
!
   30 if (curhi.gt.hi) go to 50
      do 40 i=curhi,hi
         diff = x(i) - xmeanw
         sumda = sumda + abs(diff)
         sumd2 = sumd2 + diff*diff
         sumd3 = sumd3 + diff*diff*diff
         sumd4 = sumd4 + diff*diff*diff*diff
   40 continue
   50 if (curlo.lt.lo) go to 70
      do 60 irev=lo,curlo
         i = lo - irev + curlo
         diff = x(i) - xmeanw
         sumda = sumda + abs(diff)
         sumd2 = sumd2 + diff*diff
         sumd3 = sumd3 + diff*diff*diff
         sumd4 = sumd4 + diff*diff*diff*diff
   60 continue
   70 return
      end
