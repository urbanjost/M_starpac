!ACVF
      SUBROUTINE ACVF (Y, N, YMEAN, ACOV, LAGMAX, LACOV)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE COMPUTES THE AUTOCOVARIANCE FUNCTION OF A SERIES.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 21, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) ::
     &   YMEAN
      INTEGER
     &   LACOV,LAGMAX,N
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   ACOV(*),Y(*)
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     &   DOTXY,DOTYY
      INTEGER
     &   LAG,NDOTXY,NDOTYY
!
!  EXTERNAL SUBROUTINES
      EXTERNAL AMEAN,DOTC
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL(KIND=WP) :: ACOV(LACOV)
!        THE ARRAY IN WHICH THE AUTOCOVARIANCES ARE STORED
!     REAL(KIND=WP) :: DOTXY, DOTYY
!        THE DOT PRODUCT BETWEEN VECTORS (Y(I) - YMEAN)) AND
!        (Y(LAG) - YMEAN)), AND (Y(I) - YMEAN)) AND (Y(I) - YMEAN)),
!        RESPECTIVELY.
!     INTEGER LACOV
!        THE ACTUAL DIMENSION OF ACOV.
!     INTEGER LAG, LAGMAX
!        THE INDEXING VARIABLE INDICATING THE LAG VALUE OF THE
!        AUTOCOVARIANCE BEING COMPUTED AND THE MAXIMUM LAG TO BE USED,
!        RESPECTIVELY.
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS IN THE SERIES
!     INTEGER NDOTXY, NDOTYY
!        THE NUMBER OF OBSERVATIONS USED TO COMPUTE DOTXY AND
!        DOTYY, RESPECTIVELY.
!     REAL(KIND=WP) :: Y(N)
!        THE VECTOR CONTAINING THE OBSERVED SERIES
!     REAL(KIND=WP) :: YMEAN
!        THE MEAN OF THE OBSERVED TIME SERIES
!
!     COMPUTE ARITHMETIC MEAN
!
      CALL AMEAN(Y, N, YMEAN)
!
!     COMPUTE THE VARIANCE OF THE SERIES Y
!
      CALL DOTC (Y, YMEAN, N, Y, YMEAN, N, DOTYY, NDOTYY)
      ACOV(1) = DOTYY / NDOTYY
!
!     COMPUTE AUTOCOVARIANCES.
!
      DO 10 LAG = 1, LAGMAX
         CALL DOTC (Y, YMEAN, N, Y(LAG+1), YMEAN, N - LAG,
     &       DOTXY, NDOTXY)
         ACOV(LAG + 1) = DOTXY / N
   10 CONTINUE
!
      RETURN
      END
