!VPCNT
      SUBROUTINE VPCNT (YM, YMMISS, N, M, IYM, MULTI, ILOG, YLB, YUB,
     +  XLB, XINC, NS, IRLIN, IBAR, NMSUB, ISCHCK, ISYM, ISIZE,
     +  MISS, LISYM)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS IS THE CONTROLING ROUTINE FOR USER CALLED VERTICAL PLOTS
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  JANUARY 21, 1982
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      DOUBLE PRECISION
     +   XINC,XLB,YLB,YUB
      INTEGER
     +   IBAR,ILOG,IRLIN,ISCHCK,ISIZE,IYM,LISYM,M,N,NS
      LOGICAL
     +   MISS,MULTI
!
!  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   YM(*),YMMISS(*)
      INTEGER
     +   ISYM(*)
      CHARACTER
     +   NMSUB(6)*1
!
!  SCALARS IN COMMON
      INTEGER
     +   IERR
!
!  LOCAL SCALARS
      DOUBLE PRECISION
     +   YMN,YMX
      INTEGER
     +   NSAMPL
      LOGICAL
     +   ERROR,XCHECK
!
!  EXTERNAL SUBROUTINES
      EXTERNAL PLTCHK,VERSP,VPLMT,VPMN
!
!  INTRINSIC FUNCTIONS
      INTRINSIC MAX,MOD
!
!  COMMON BLOCKS
      COMMON /ERRCHK/IERR
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     LOGICAL ERROR
!        A VALUE INDICATING WHETHER AN ERROR WAS DETECTEC (TRUE)
!        OR NOT (FALSE).
!     INTEGER IBAR
!        THE INDICATOR USED TO DESIGNATE WHETHER THE PLOT IS TO BE A
!        BAR GRAPH (IBAR.GE.1) OR NOT (IBAR.LE.0).
!     INTEGER IERR
!        THE COMMON VARIABLE USED TO INDICATE ERRORS,
!        IF =0, THEN NO ERRORS
!     INTEGER ILOG
!        THE TWO DIGIT INTEGER, PQ, USED TO SELECT AXIS SCALE, WHERE
!        P DESIGNATES THE X-AXIS AND Q DESIGNATES THE Y-AXIS.
!        IF P.EQ.0 (Q.EQ.0), THEN THE X-AXIS (Y-AXIS) IS LINEAR.
!        IF P.NE.0 (Q.NE.0), THEN THE X-AXIS (Y-AXIS) IS LOG.
!     INTEGER IRLIN
!        THE INDICATOR USED TO DESIGNATE WHETHER THE PLOT WILL HAVE
!        THE LOCATION OF ZERO PLOTTED AS A REFERENCE LINE (IRLIN.EQ.0),
!        THE LOCATION OF THE MEAN PLOTTED AS A REFERENCE LINE (IRLIN.GE.
!        OR NO REFERENCE LINE (IRLIN.LT.0).
!     INTEGER ISCHCK
!        THE INTEGER VALUE INDICATING HOW THE PLOTTING SYMBOLS
!           WILL BE DESIGNATED, WHERE
!           0 INDICATES THE PLOTTING SYMBOLS HAVE NOT BEEN DESIGNATED IN
!             THE N VECTOR ISYM AND ONLY THE SYMBOL + IS TO BE USED
!           1 INDICATES THE PLOTTING SYMBOLS HAVE BEEN DESIGNATED IN THE
!             N VECTOR ISYM
!           2 INDICATES THAT M SERIES ARE BEING PLOTTED.
!             SYMBOL I+4 WILL BE USED FOR COLUMN I OF YM.
!     INTEGER ISIZE
!        THE TWO DIGIT INTEGER, PQ, USED TO SELECT AXIS SIZE, WHERE
!        P DESIGNATES THE X-AXIS AND Q DESIGNATES THE Y-AXIS.
!        IF P.EQ.0 (Q.EQ.0), THEN THE X-AXIS (Y-AXIS) IS THE MAXIMUM.
!        IF P.NE.0 (Q.NE.0), THEN THE X-AXIS (Y-AXIS) IS HALF THE MAXIMU
!     INTEGER ISYM(LISYM)
!        VECTOR CONTAINING SYMBOLS FOR PLOTTING, NOT USED IN SOME CASES
!     INTEGER IYM
!        THE FIRST DIMENSION OF ARRAY YM.
!     INTEGER LISYM
!        THE LENGTH OF ARRAY ISYM.
!     INTEGER M
!        NUMBER OF Y VECTORS
!     LOGICAL MISS
!        INDICATOR VARIABLE USED TO DESIGNATE WHETHER MISSING VALUES
!        MAY BE PRESENT (MISS = .TRUE.) OR NOT (MISS = .FALSE.)
!     LOGICAL MULTI
!        INDICATOR VARIABLE USED TO DESIGNATE WHETHER MULTIPLE Y VALUES
!        ARE TO BE PLOTTED (MULTI = .TRUE.) OR NOT (MULTI = .FALSE.)
!     INTEGER N
!        LENGTH OF VECTORS
!     CHARACTER*1 NMSUB(6)
!        THE CHARACTERS OF THE CALLING ROUTINES NAME.
!     INTEGER NS, NSAMPL
!        THE SAMPLING FREQUENCY.
!     LOGICAL XCHECK
!        INDICATOR VARIABLE USED TO DESIGNATE WHETHER X-AXIS VALUES
!        ARE TO BE CHECKED (XCHECK = .TRUE.) OR NOT (XCHECK = .FALSE.)
!     DOUBLE PRECISION XINC
!        THE INCREMENT FOR THE X-AXIS.
!     DOUBLE PRECISION XLB
!        THE LOWER BOUND FOR THE X-AXIS.  (XLB=XUB INDICATES LIMITS ARE
!        TO BE DETERMINED FROM THE RANGE OF THE DATA.)
!     DOUBLE PRECISION YLB
!        THE LOWER BOUND FOR THE Y-AXIS.  (YLB=YUB INDICATES LIMITS ARE
!        TO BE DETERMINED FROM THE RANGE OF THE DATA.)
!     DOUBLE PRECISION YM(IYM,M)
!        MULTIVARIATE OBSERVATIONS FOR THE Y COORDINATES
!     DOUBLE PRECISION YMMISS(M)
!        THE MISSING VALUE CODE FOR THE Y-AXIS.
!     DOUBLE PRECISION YMN, YMX
!        THE Y-AYIS LOWER AND UPPER LIMITS ACTUALLY USED.
!     DOUBLE PRECISION YUB
!        THE UPPER BOUND FOR THE Y-AXIS.  (YLB=YUB INDICATES LIMITS ARE
!        TO BE DETERMINED FROM THE RANGE OF THE DATA.)
!
!
!     COMMENCE BODY OF ROUTINE
!
      XCHECK = .FALSE.
      CALL PLTCHK (YM, YMMISS, YM, YMMISS, N, M, IYM, MULTI,
     +   ILOG, YLB, YUB, XLB, XINC, NMSUB, MISS, XCHECK)
!
      IF (IERR.EQ.0) THEN
!
!       DETERMINE THE BOUNDS FOR THEY AXIS AND COMPLETE ERROR CHECKING
!
        NSAMPL = MAX(1, NS)
        CALL VPLMT (YM, YMMISS, N, M, IYM, YLB, YUB, YMN, YMX,
     +    ERROR, NMSUB, MISS, NSAMPL)
!
        IF (ERROR) THEN
          IERR = 1
        ELSE
!
!       PRINT PLOT
!
        IF (MOD(MAX(0,ISIZE),10).EQ.0) THEN
          CALL VERSP(.TRUE.)
        ELSE
          CALL VERSP(.FALSE.)
        END IF
        CALL VPMN (YM, YMMISS, N, M, IYM, NSAMPL, ISCHCK, ISYM, LISYM,
     +    ISIZE, YMN, YMX, XLB, XINC, MISS, ILOG, IRLIN, IBAR)
!
        END IF
      END IF
!
      RETURN
!
      END
