!FLTARM
      SUBROUTINE FLTARM (Y, YMISS, N, IAR, PHI, YF, YFMISS, NYF)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE FILTERS THE INPUT SERIES Y, WHICH CONTAINS MISSING
!     DATA, USING THE IAR TERMS OF THE AUTOREGRESSIVE FILTER PHI,
!     COPYING THE FILTERED SERIES INTO YF.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DEVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 26, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) ::
     +   YFMISS,YMISS
      INTEGER
     +   IAR,N,NYF
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     +   PHI(*),Y(*),YF(*)
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     +   FPLM,TEMP
      INTEGER
     +   I,I1,J,K
!
!  EXTERNAL FUNCTIONS
      REAL(KIND=WP) ::
     +   D1MACH
      LOGICAL
     +   MVCHK
      EXTERNAL D1MACH,MVCHK
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL(KIND=WP) :: FPLM
!        THE FLOATING POINT LARGEST MAGNITUDE.
!     INTEGER I
!        AN INDEXING VARIABLE.
!     INTEGER IAR
!        THE NUMBER OF FILTER TERMS.
!     INTEGER I1, J, K
!        INDEXING VARIABLES.
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS IN THE SERIES Y.
!     INTEGER NYF
!        THE NUMBER OF OBSERVATIONS IN THE FILTERED SERIES YF.
!     REAL(KIND=WP) :: PHI(IAR)
!        THE ARRAY IN WHICH THE FILTER COEFFICIENTS ARE STORED.
!     REAL(KIND=WP) :: TEMP
!        A TEMPORARY STORAGE LOCATION.
!     REAL(KIND=WP) :: Y(N)
!        THE VECTOR CONTAINING THE OBSERVED TIME SERIES.
!     REAL(KIND=WP) :: YF(N)
!        THE VECTOR IN WHICH THE FILTERED SERIES IS RETURNED.
!     REAL(KIND=WP) :: YFMISS
!        THE MISSING VALUE CODE USED IN THE FILTERED SERIES TO
!        INDICATE THE VALUE COULD NOT BE COMPUTED DUE TO MISSING DATA.
!     REAL(KIND=WP) :: YMISS
!        THE MISSING VALUE CODE USED IN THE INPUT SERIES TO INDICATE
!        AN OBSERVATION IS MISSING.
!
      FPLM = D1MACH(2)
!
      YFMISS = FPLM
!
      DO 10 I = 1, N
         YF(I) = Y(I)
   10 CONTINUE
!
      NYF = N - IAR
!
      DO 50 I = 1, NYF
         TEMP = YFMISS
         K = I + IAR
         IF (MVCHK(YF(K), YMISS)) GO TO 40
         TEMP = YF(K)
         DO 30 J = 1, IAR
            K = K - 1
            IF (.NOT. MVCHK(YF(K), YMISS)) GO TO 20
            TEMP = YFMISS
            GO TO 40
   20       CONTINUE
            TEMP = TEMP - PHI(J) * YF(K)
   30    CONTINUE
   40    YF(I) = TEMP
   50 CONTINUE
!
      I1 = NYF + 1
!
      DO 60 I = I1, N
         YF(I) = 0.0E0_WP
   60 CONTINUE
      RETURN
      END
