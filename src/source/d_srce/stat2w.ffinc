!STAT2W
      SUBROUTINE STAT2W(Y, WT, N, NNZW, STS, SUMDA, SUMDI, SUMWD2,
     &   SUMD2, SUMD3, SUMD4, SUMW)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS SUBROUTINE COMPUTES FOR A VECTOR Y THE STATISTICS THAT DO
!     NOT REQUIRE SORTING OF THE VECTOR, THAT IS, THOSE NOT COMPUTED
!     BY STAT1W.  WEIGHTS ARE USED IN THE CALCULATIONS.
!
!     THIS SUBROUTINE IS BASED ON A MODIFICATION OF THE STATIS
!     CODE USED IN OMNITAB, VERSION 5 (6/16/72), WRITTEN BY
!     SALLY PEAVY.  THE ORIGINAL ADAPTATION TO STARPAC WAS
!     DONE BY JANET DONALDSON.
!
!     WRITTEN BY - JOHN E. KOONTZ
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  MAY 17, 1982
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) ::
     &   SUMD2,SUMD3,SUMD4,SUMDA,SUMDI,SUMW,SUMWD2
      INTEGER
     &   N,NNZW
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   STS(53),WT(N),Y(N)
!
!  SCALARS IN COMMON
      INTEGER
     &   IERR
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     &   DIF,T,TA,TK1,TK2
      INTEGER
     &   I,IC,ICI,ICOUNT,IDRUNS,IMINUS,IPLUS,IRUN,J
!
!  EXTERNAL FUNCTIONS
      REAL(KIND=WP) ::
     &   CDFF,PPFCHS,PPFT
      EXTERNAL CDFF,PPFCHS,PPFT
!
!  INTRINSIC FUNCTIONS
      INTRINSIC ABS,SQRT
!
!  COMMON BLOCKS
      COMMON /ERRCHK/IERR
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL(KIND=WP) :: DIF
!        THE SUM OF THE DIFFERENCES BETWEEN SUCCEEDING ELEMENTS
!        IN Y.
!     INTEGER I
!        A LOOP INDEX.
!     INTEGER IC
!        PREVIOUS SIGN IN RUNS CALCULATION.
!     INTEGER ICI
!        CURRENT SIGN IN RUNS CALCULATION.
!     INTEGER ICOUNT
!        USED IN RUNS CALCULATIONS.
!     INTEGER IDRUNS
!        THE NUMBER OF RUNS.
!     INTEGER IERR
!        AN ERROR FLAG SET IN COMMON ERRCHK.
!     INTEGER IMINUS, IPLUS
!        COUNTS OF SIGNS OF DEVIATIONS.
!     INTEGER IRUN
!        THE NUMBER OF RUNS UP AND DOWN.
!     INTEGER J
!        A LOOP INDEX.
!     INTEGER N
!        INPUT PARAMETER.  THE LENGTH OF Y AND WT.
!     INTEGER NNZW
!        INPUT PARAMETER.  THE NUMBER OF POSITIVE ELEMENTS IN WT.
!     REAL(KIND=WP) :: STS(53)
!        OUTPUT PARAMETER.  THE VECTOR OF 53 STATISTICS COMPUTED.
!     ROW STATISTIC                    ROW STATISTIC
!         1  LENGTH OF VECTOR             TESTS FOR NONRANDOMNESS
!         2  NUMBER OF NONZERO WEIGHTS    23  NUMBER OF RUNS UP AND DOWN
!        MEASURES OF LOCATION             24  EXPECTED NUMBER OF RUNS
!         3  UNWEIGHTED MEAN              25  S.D. OF NUMBER OF RUNS
!         4  WEIGHTED MEAN                26  MEAN SQR. SUCCESSIVE DIFF.
!         5  MEDIAN                       27  MEAN SQR. SUCC. DIFF./VAR.
!         6  MID-RANGE                    DEVIATIONS FROM WTD MEAN
!         7  25 P.C. UNWTD. TRIMMED MEAN  28  NUMBER OF + SIGNS
!         8  25 P.C. WTD. TRIMMED MEAN    29  NUMBER OF - SIGNS
!        MEASURES OF DISPERSION           30  NUMBER OF RUNS
!         9  STANDARD DEVIATION (S.D.)    31  EXPECTED NUMBER OF RUNS
!        10  S.D. OF MEAN                 32  S.D. OF RUNS
!        11  RANGE                        33  DIFF./S.D. OF RUNS
!        12  MEAN VARIATION               OTHER STATISTICS
!        13  VARIANCE (VAR.)              34  MINIMUM
!        14  COEFFICIENT OF VARIATION     35  MAXIMUM
!        CONFIDENCE INTERVALS             36  BETA 1
!        15  LOWER CONFIDENCE LIMIT, MEAN 37  BETA 2
!        16  UPPER CONFIDENCE LIMIT, MEAN 38  WTD. SUM OF VALUES
!        17  LOWER CONFIDENCE LIMIT, S.D. 39  WTD. SUM OF SQUARES
!        18  UPPER CONFIDENCE LIMIT, S.D. 40  WTD. SUM OF SQRD. DEVS.
!        LINEAR TREND STATISTICS          41  STUDENTS T
!        19  SLOPE                        42  WTD. SUM OF ABS. VALUES
!        20  S.D. OF SLOPE                43  WTD. AVG. ABS. VALUES
!        21  SLOPE/S.D. OF SLOPE = T      44-53 FREQ. DISTRIBUTION
!        22  PROB ( X .GT. ABS(OBS. T))
!     REAL(KIND=WP) :: SUMDA
!        INPUT PARAMETER.  THE SUM OF THE ABSOLUTE VALUES OF THE
!        DIFFERENCES DIFF.
!     REAL(KIND=WP) :: SUMDI
!        INPUT PARAMETER.  THE SUM OF THE PRODUCTS OF I AND THE
!        ITH DIFFERENCE DIFF.
!     REAL(KIND=WP) :: SUMD2
!        INPUT PARAMETER.  THE SUM OF THE SQUARES OF THE
!        DIFFERENCES DIFF.
!     REAL(KIND=WP) :: SUMD3
!        INPUT PARAMETER.  THE SUM OF THE CUBES OF THE
!        DIFFERENCES DIFF.
!     REAL(KIND=WP) :: SUMD4
!        INPUT PARAMETER.  THE SUM OF THE HYPERCUBES OF THE
!        DIFFERENCES DIFF.
!     REAL(KIND=WP) :: SUMW
!        INPUT PARAMETER.  THE SUM OF THE WEIGHTS VECTOR WT.
!     REAL(KIND=WP) :: SUMWD2
!        INPUT PARAMETER.  THE WEIGHTED SUM OF THE SQUARED
!        DIFFERENCES DIFF.
!     REAL(KIND=WP) :: T
!        A RESIDUAL (Y(I) - MEANY)
!     REAL(KIND=WP) :: TA
!        A TEMPORARY VARIABLE IN THE RUNS CALCULATION.
!     REAL(KIND=WP) :: TK1, TK2
!        CHI-SQUARED VALUES.
!     REAL(KIND=WP) :: WT(N)
!        INPUT PARAMETER.  THE VECTOR OF WEIGHTS FOR THE
!        Y OBSERVATIONS.
!     REAL(KIND=WP) :: Y(N)
!        INPUT PARAMETER.  A SET OF N DATA POINTS, SORTED
!        INTO ASCENDING ORDER.
!
!
!     BEGIN STORAGE OF STATISTICS.
!
      STS(1) = N
      STS(2) = NNZW
      STS(24) = (2.0_WP*STS(2)-1.0_WP)/3.0_WP
      STS(25) = SQRT((16.0_WP*STS(2)-29.0_WP)/90.0_WP)
      STS(43) = STS(42)/SUMW
!
!     COMPUTE RESIDUALS AND STANDARD DEVIATIONS.
!
      ICI = 0
      IPLUS = 0
      IMINUS = 0
      IDRUNS = 0
      IC = 0
      DO 30 I=1,N
         IF (WT(I).LE.0.0_WP) GO TO 30
         T = Y(I) - STS(4)
         IF (T.LT.0.0_WP) GO TO 10
         IPLUS = IPLUS + 1
         ICI = 1
         GO TO 20
   10    IMINUS = IMINUS + 1
         ICI = -1
   20    IF (IC.EQ.ICI) GO TO 30
         IC = ICI
         IDRUNS = IDRUNS + 1
   30 CONTINUE
      STS(28) = IPLUS
      STS(29) = IMINUS
      STS(31) = 1.0_WP + (2.0_WP*STS(28)*STS(29)/STS(2))
      STS(32) =
     &   SQRT((2.0_WP*STS(28)*STS(29)*(2.0_WP*STS(28)*STS(29)-
     &                                     STS(28)-STS(29)))/
     &        ((STS(28)+STS(29))**2*(STS(2)-1.0_WP)))
      STS(30) = IDRUNS
      STS(33) = 0.0_WP
      IF (STS(32).NE.0.0_WP)
     &   STS(33) = (STS(30)-STS(31))/STS(32)
      STS(13) = SUMWD2/(STS(2)-1.0_WP)
      STS(9) = SQRT(STS(13))
      STS(10) = STS(9)/(SQRT(SUMW))
      IF (STS(4).NE.0.0_WP)
     &   STS(14) = 100.0_WP*ABS(STS(9)/STS(4))
      IF (STS(4).EQ.0.0_WP) STS(14) = 0.0_WP
      STS(36) = 0.0_WP
      IF (SUMD2.NE.0.0_WP)
     &   STS(36) = (SUMD3/STS(2))**2/((SUMD2/STS(2))**3)
      STS(37) = 0.0_WP
      IF (SUMD2.NE.0.0_WP)
     &   STS(37) = (SUMD4/STS(2))/((SUMD2/STS(2))**2)
      STS(40) = SUMWD2
      STS(19) = (12.0_WP*SUMDI)/(STS(2)*(STS(2)**2-1.0_WP))
      STS(20) = (1.0_WP/(STS(2)-2.0_WP)*
     &             (12.0_WP*(SUMD2/(STS(2)*(STS(2)**2-1.0_WP)))-
     &              STS(19)**2))
      IF (STS(20).LE.0.0_WP) STS(20) = 0.0_WP
      STS(20) = SQRT(STS(20))
      IF (STS(20).EQ.0.0_WP) STS(21) = 0.0_WP
      IF (STS(20).GT.0.0_WP) STS(21) = STS(19)/STS(20)
!
      STS(22) = 1.0_WP - CDFF(STS(21)*STS(21), 1.0_WP, STS(2)-2.0_WP)
!
!     COMPUTE NUMBER OF RUNS IN THE DATA.
!
      DIF = 0.0_WP
      IRUN = 1
      TA = 0.0_WP
      DO 50 I=1,N
         IF (I.GE.N) GO TO 60
         IF (WT(I).LE.0.0_WP) GO TO 50
         J = I
   40    J = J + 1
         IF (J.GE.N) GO TO 60
         IF (WT(J).LE.0.0_WP) GO TO 40
         TA = Y(J) - Y(I)
         IF (TA.NE.0.0_WP) GO TO 60
   50 CONTINUE
   60 ICOUNT = 0
      DO 80 I=1,N
         IF (WT(I).LE.0.0_WP) GO TO 80
         ICOUNT = ICOUNT + 1
         IF (ICOUNT.GE.NNZW) GO TO 80
         J = I
   70    J = J + 1
         IF (WT(J).LE.0.0_WP) GO TO 70
         T = Y(J) - Y(I)
         DIF = DIF + T*T
         IF (TA*T.GE.0.0_WP) GO TO 80
         TA = T
         IRUN = IRUN + 1
   80 CONTINUE
      STS(23) = IRUN
      STS(26) = DIF/(STS(2)-1.0_WP)
      STS(27) = 0.0_WP
      IF (STS(13).NE.0.0_WP)
     &   STS(27) = STS(26)/STS(13)
      STS(41) = 0.0_WP
      IF (STS(9).NE.0.0_WP)
     &   STS(41) = (STS(4)*SQRT(SUMW))/STS(9)
      STS(12) = SUMDA/STS(2)
      T = PPFT(0.975_WP, NNZW-1)
      TK1 = PPFCHS(0.975_WP, NNZW-1)
      TK2 = PPFCHS(0.025_WP, NNZW-1)
      STS(15) = STS(4) - T*STS(10)
      STS(16) = STS(4) + T*STS(10)
      STS(17) = SQRT((STS(2)-1.0_WP)/TK1)*STS(9)
      STS(18) = SQRT((STS(2)-1.0_WP)/TK2)*STS(9)
      RETURN
      END
