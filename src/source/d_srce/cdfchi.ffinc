!CDFCHI
      DOUBLE PRECISION FUNCTION CDFCHI(CHISQR, DF)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE COMPUTES THE CHI SQUARE CUMULATIVE DISTRIBUTION
!     FUNCTION FROM THE INCOMPLETE GAMMA FUNCTION RATIO AS DISCUSSED IN
!     CHAPTER 17 OF DISTRIBUTIONS IN STATISTICS - CONTINUOUS UNIVARIATE
!     DISTRIBUTIONS - 1, BY JOHNSON AND KOTZ.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE - DECEMBER 7, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      DOUBLE PRECISION
     +   CHISQR,DF
!
!  LOCAL SCALARS
      DOUBLE PRECISION
     +   FPLPM
!
!  EXTERNAL FUNCTIONS
      DOUBLE PRECISION
     +   DGAMI,DGAMMA,D1MACH
      LOGICAL
     +   MVCHK
      EXTERNAL DGAMI,DGAMMA,D1MACH,MVCHK
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     DOUBLE PRECISION CHISQR
!        THE PERCENT POINT FROM THE CHI SQUARED DISTRIBUTION.
!     DOUBLE PRECISION DF
!        THE DEGREES OF FREEDOM.
!     DOUBLE PRECISION FPLPM
!        THE DOUBLE PRECISION FLOATING POINT LARGEST POSITIVE MAGNITUDE.
!
!     COMMENCE BODY OF ROUTINE
!
      FPLPM = D1MACH(2)
!
      CDFCHI = DGAMI(0.5D0*DF, 0.5D0*CHISQR)
      IF (MVCHK(CDFCHI,FPLPM)) THEN
         CDFCHI = 1.0D0
      ELSE
         CDFCHI = CDFCHI / DGAMMA(0.5D0*DF)
         IF (CDFCHI.LT.0.0D0) CDFCHI = 0.0D0
         IF (CDFCHI.GT.1.0D0) CDFCHI = 1.0D0
      END IF
!
      RETURN
      END
