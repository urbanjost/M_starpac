!GFAEST
      SUBROUTINE GFAEST (PHI, IAR, NF, FREQ, GAIN, PHAS, DELTA)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE COMPUTES THE GAIN FUNCTION OF AN AUTOREGRESSIVE
!     LINEAR FILTER USING THE ALGORITHM GIVEN ON PAGES 419 AND 420
!     JENKINS AND WATTS.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 26, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) ::
     &   DELTA
      INTEGER
     &   IAR,NF
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   FREQ(NF),GAIN(NF),PHAS(NF),PHI(IAR)
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     &   ARG,C,PI,S,V0,V1,V2,Z0,Z1,Z2
      INTEGER
     &   I,J,JJ
!
!  EXTERNAL SUBROUTINES
      EXTERNAL GETPI
!
!  INTRINSIC FUNCTIONS
      INTRINSIC ATAN2,COS,SIN,SQRT
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL(KIND=WP) :: ARG
!        A VALUE USED TO COMPUTE THE GAIN FUNCTION ESTIMATES.
!     REAL(KIND=WP) :: C
!        A VALUE USED TO COMPUTE THE GAIN FUNCTION ESTIMATES.
!     REAL(KIND=WP) :: DELTA
!        THE SAMPLING INTERVAL OF THE ORIGINAL SERIES.
!     REAL(KIND=WP) :: FREQ(NF)
!        THE VECTOR OF FREQUENCIES AT WHICH THE GAIN FUNCTION IS TO
!        BE ESTIMATED.
!     REAL(KIND=WP) :: GAIN(NF)
!        THE ARRAY IN WHICH THE GAIN FUNCTION ESTIMATES ARE RETURNED.
!     INTEGER I
!        AN INDEX VARIABLE.
!     INTEGER IAR
!        THE NUMBER OF FILTER COEFFICIENTS.
!     INTEGER J, JJ
!        INDEX VARIABLES.
!     INTEGER NF
!        THE NUMBER OF FREQUENCIES FOR WHICH THE GAIN FUNCTION ESTIMATES
!        ARE TO BE COMPUTED.
!     REAL(KIND=WP) :: PHAS(NF)
!        THE ARRAY IN WHICH THE PHASE OF THE FILTER IS RETURNED.
!     REAL(KIND=WP) :: PHI(IAR)
!        THE VECTOR CONTAINING THE AUTOREGRESSIVE FILTER COEFFICIENTS.
!     REAL(KIND=WP) :: PI
!        THE VALUE OF PI.
!     REAL(KIND=WP) :: S
!        A VALUE USED TO COMPUTE THE GAIN FUNCTION ESTIMATES.
!     REAL(KIND=WP) :: V0, V1, V2
!        CONSTANTS USED FOR COMPUTING THE GAIN FUNCTION ESTIMATES.
!     REAL(KIND=WP) :: Z0, Z1, Z2
!        CONSTANTS USED FOR COMPUTING THE GAIN FUNCTION ESTIMATES.
!
      CALL GETPI(PI)
!
!     COMPUTE GAIN FUNCTION ESTIMATES AND THEIR CONFIDENCE LIMITS.
!
      DO 30 I = 1, NF
         IF (FREQ(I).EQ.0.0_WP) THEN
            ARG = 0.0_WP
            C = 1.0_WP
            S = 0.0_WP
         ELSE IF (FREQ(I).EQ.0.25_WP) THEN
            ARG = PI/2.0_WP
            C = 0.0_WP
            S = 1.0_WP
         ELSE IF (FREQ(I).EQ.0.5_WP) THEN
            ARG = PI
            C = -1.0_WP
            S = 0.0_WP
         ELSE
            ARG = 2.0_WP * PI * FREQ(I)
            C = COS(ARG)
            S = SIN(ARG)
         END IF
         V0 = 0.0_WP
         V1 = 0.0_WP
         Z0 = 0.0_WP
         Z1 = 0.0_WP
!
         JJ = IAR + 1
!
         DO 20 J = 1, IAR
            JJ = JJ -1
!
            V2 = C * V1 - V0 - PHI(JJ)
            V0 = V1
            V1 = V2
!
            Z2 = S * Z1 - Z0 - PHI(JJ)
            Z0 = Z1
            Z1 = Z2
   20    CONTINUE
!
         V2 = DELTA * (1.0_WP + (V1 * C - V0))
!
         Z2 = DELTA * Z1 * S
!
         GAIN(I) = SQRT(V2*V2 + Z2*Z2)
!
         PHAS(I) = -0.5_WP * PI
         IF (Z2.NE.0.0_WP .AND. V2.NE.0.0_WP) PHAS(I) = ATAN2(Z2, V2)
!
   30 CONTINUE
!
      RETURN
      END
