!NLITRP
      SUBROUTINE NLITRP(NLHDR, HEAD, PAGE, WIDE, IPTOUT, NPAR, NNZW,
     &   IWORK, IIWORK, RWORK, IRWORK, IFIXD, PARE, NPARE)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS SUBROUTINE PRINTS THE ITERATION REPORTS FOR THE
!     NONLINEAR LEAST SQUARES REGRESSION SUBROUTINES.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  APRIL 2, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      INTEGER
     &   IIWORK,IPTOUT,IRWORK,NNZW,NPAR,NPARE
      LOGICAL
     &   HEAD,PAGE,WIDE
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   PARE(NPAR),RWORK(IRWORK)
      INTEGER
     &   IFIXD(NPAR),IWORK(IIWORK)
!
!  SUBROUTINE ARGUMENTS
      EXTERNAL NLHDR
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     &   RSD,RSS,RSSC,RSSPC
      INTEGER
     &   DST0,F,F0,FDIF,ICASE,IPRT,ISUBHD,MXITER,NFCALL,NITER,
     &   NREDUC,PREDUC,RELDX,STPPAR
      CHARACTER
     &   LETTRN*1,LETTRY*1
!
!  LOCAL ARRAYS
      CHARACTER
     &   ISCHKD(2)*1
!
!  EXTERNAL SUBROUTINES
!      EXTERNAL IPRINT,LSTVCF
!
!  INTRINSIC FUNCTIONS
      INTRINSIC MOD,DBLE,SQRT
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     INTEGER DST0
!        THE LOCATION IN RWORK OF THE VALUE OF THE 2 NORM OF D TIMES
!        THE  NEWTON STEP.
!     INTEGER F
!        THE LOCATION IN RWORK OF THE VALUE OF HALF THE RESIDUAL
!        SUM OF SQUARES AT THE CURRENT PARAMETER VALUES.
!     INTEGER FDIF
!        THE LOCATION IN RWORK OF THE DIFFERENCE BETWEEN THE
!        RESIDUAL SUM OF SQUARES AT THE BEGINNING AND END OF THE
!        CURRENT ITERATION.
!     INTEGER F0
!        THE LOCATION IN RWORK OF THE VALUE OF HALF THE RESIDUAL
!        VARIANCE AT THE BEGINNING OF THE CURRENT ITERATION.
!     LOGICAL HEAD
!        THE VARIABLE USED TO INDICATE WHETHER A HEADING IS TO BE
!        PRINTED DURING A GIVEN CALL TO THE ITERATION REPORT (TRUE)
!        OR NOT (FALSE).
!     INTEGER ICASE
!        AN INDICATER VARIABLE USED TO DESIGNATE THE MESSAGE TO BE
!        PRINTED.
!     INTEGER IFIXD(NPAR)
!        THE INDICATOR VALUES USED TO DESIGNATE WHETHER THE
!        PARAMETERS ARE TO BE OPTIMIZED OR ARE TO BE HELD FIXED.
!        IF IFIXED(I).NE.0, THEN PAR(I) WILL BE HELD FIXED.
!        IF IFIXED(I).EQ.0, THEN PAR(I) WILL BE OPTIMIZED.
!     INTEGER IIWORK
!        THE DIMENSION OF THE INTEGER WORK VECTOR IWORK.
!     INTEGER IPRT
!        THE UNIT NUMBER FOR PRINTED OUTPUT.
!     INTEGER IRWORK
!        THE DIMENSION OF THE DOUBLE PRECISION WORK VECTOR RWORK.
!     CHARACTER*1 ISCHKD(2)
!        THE INDICATOR USED TO DESIGNATE WHETHER THE
!        TEST VALUE WAS CHECKED FOR CONVERGENCE (Y) OR NOT (N).
!     INTEGER ISUBHD
!        AN INTEGER VALUE SPECIFYING SUBHEADINGS TO BE PRINTED.
!     INTEGER IWORK(IIWORK)
!        THE INTEGER WORK SPACE VECTOR USED BY THE NL2 SUBROUTINES.
!     CHARACTER*1 LETTRN, LETTRY
!        THE LETTERS N AND Y, RESPECTIVELY.
!     INTEGER MXITER
!        THE LOCATION IN IWORK OF THE VARIABLE DESIGNATING THE
!        MAXIMUM NUMBER OF ITERATIONS ALLOWED.
!     INTEGER NFCALL
!        THE LOCATION IN IWORK OF THE NUMBER OF FUNCTION EVALUATIONS.
!     INTEGER NITER
!        THE LOCATION IN IWORK OF THE NUMBER OF THE CURRENT ITERATION.
!     EXTERNAL NLHDR
!        THE NAME OF THE ROUTINE WHICH PRODUCES THE HEADING.
!     INTEGER NPAR
!        THE NUMBER OF UNKNOWN PARAMETERS IN THE MODEL.
!     INTEGER NPARE
!        THE NUMBER OF UNKNOWN PARAMETERS TO BE OPTIMIZED.
!     INTEGER NNZW
!        THE NUMBER OF NON ZERO WEIGHTS.
!     INTEGER NREDUC
!        THE LOCATION IN RWORK OF THE VALUE USED TO CHECK IF THE
!        HESSIAN APPROXIMATION IS POSITIVE DEFINITE.  IF
!        IF RWORK(NREDUC) .EQ. 0, THE HESSIAN IS SINGULAR, OTHERWISE
!        IT IS NOT.
!     LOGICAL PAGE
!        THE VARIABLE USED TO INDICATE WHETHER A GIVEN SECTION OF
!        THE OUTPUT IS TO BEGIN ON A NEW PAGE (TRUE) OR NOT (FALSE).
!     REAL(KIND=WP) :: PARE(NPAR)
!        THE CURRENT ESTIMATES OF THE UNKNOWN PARAMETERS, BUT ONLY
!        THOSE TO BE OPTIMIZED (NOT THOSE WHOSE VALUES ARE FIXED).
!     INTEGER PREDUC
!        THE LOCATION IN RWORK OF THE PREDICTED FUNCTION REDUCTION
!        FOR THE CURRENT STEP.
!     INTEGER RELDX
!        THE LOCATION IN RWORK OF THE SCALED RELATIVE CHANGE IN
!        THE PARAMETER VALUES CAUSED BY THE CURRENT ITERATION.
!     REAL(KIND=WP) :: RSD
!        THE RESIDUAL STANDARD DEVIATION.
!     REAL(KIND=WP) :: RSS
!        THE RESIDUAL SUM OF SQUARES.
!     REAL(KIND=WP) :: RSSC
!        THE CHANGE IN THE RESIDUAL SUM OF SQUARES CAUSED BY THIS
!        ITERATION.
!     REAL(KIND=WP) :: RSSPC
!        THE PREDICTED CHANGE IN THE RESIDUAL SUM OF SQUARES AT THIS
!        ITERATION.
!     REAL(KIND=WP) :: RWORK(IRWORK)
!        THE DOUBLE PRECISION WORK VECTOR USED BY THE NL2 SUBROUTINES.
!     INTEGER STPPAR
!        THE LOCATION IN RWORK OF THE MARQUARDT LAMBDA PARAMETER.
!     LOGICAL WIDE
!        THE VARIABLE USED TO INDICATE WHETHER THE HEADING SHOULD
!        BE FULL WIDTH (TRUE) OR NOT (FALSE).
!
!
      DATA LETTRN /'N'/, LETTRY /'Y'/
!
!     IWORK SUBSCRIPT VALUES
!
      DATA MXITER /18/, NFCALL /6/, NITER /31/
!
!     RWORK SUBSCRIPT VALUES
!
      DATA DST0 /3/, F /10/, FDIF /11/, F0 /13/, NREDUC /6/, PREDUC
     &   /7/, RELDX /17/, STPPAR /5/
!
      CALL IPRINT(IPRT)
!
      IF (IWORK(1).EQ.10) GO TO 90
      IF ((IPTOUT.EQ.1) .AND. (IWORK(NITER).NE.1) .AND.
     &   (IWORK(NITER).NE.IWORK(MXITER)) .AND. (IWORK(1).LE.2)) RETURN
!
      ISUBHD = 0
      IF (HEAD) CALL NLHDR(PAGE, WIDE, ISUBHD)
      HEAD = .FALSE.
      IF (MOD(IWORK(NITER),4).EQ.0) HEAD = .TRUE.
!
      WRITE (IPRT,1000) IWORK(NITER)
!
!     COMPUTE STATISTICS TO BE PRINTED
!
      RSS = 2.0_WP*RWORK(F)
      RSD = SQRT(RSS)
      IF (NNZW-NPARE.GE.1) RSD = RSD/SQRT(DBLE(NNZW-NPARE))
!
      RSSC = 0.0_WP
      IF (RWORK(F0).GT.0.0_WP) RSSC = RWORK(FDIF)/RWORK(F0)
!
      RSSPC = 0.0_WP
      IF (RWORK(F0).GT.0.0_WP) RSSPC = RWORK(NREDUC)/RWORK(F0)
!
!     REFERENCE NL2 SUBROUTINE ASSESS, STATEMENT LABEL 300 TO 320
!
      ISCHKD(1) = LETTRN
      ISCHKD(2) = LETTRN
      IF (RWORK(FDIF).GT.2.0_WP*RWORK(PREDUC)) GO TO 10
      IF (RWORK(DST0).LT.0.0_WP) GO TO 10
      IF (RWORK(NREDUC).GE.0.0_WP) ISCHKD(1) = LETTRY
      IF (RWORK(STPPAR).EQ.0.0_WP) ISCHKD(2) = LETTRY
   10 CONTINUE
!
      WRITE (IPRT,1010) IWORK(NFCALL), RSD, RSS, RSSC, RSSPC,
     &   ISCHKD(1), RWORK(RELDX), ISCHKD(2)
      IF (NPARE.LT.NPAR) WRITE (IPRT,1020)
      IF (NPARE.GE.NPAR) WRITE (IPRT,1150)
      CALL LSTVCF(NPARE, PARE, NPAR, IFIXD)
!
      IF (IWORK(1).LE.2) RETURN
!
!     PRINT FINAL ITERATION MESSAGE
!
      ICASE = IWORK(1) - 2
      GO TO (20, 30, 40, 50, 60, 70, 80, 90, 100, 140, 110, 120, 130),
     &   ICASE
!
!     ***** PARAMETER CONVERGENCE *****
!
   20 WRITE (IPRT,1030)
      RETURN
!
!     ***** RESIDUAL SUM OF SQUARES CONVERGENCE *****
!
   30 WRITE (IPRT,1040)
      RETURN
!
!     ***** PARAMETER AND RESIDUAL SUM OF SQUARES CONVERGENCE ****
!
   40 WRITE (IPRT,1050)
      RETURN
!
!     ***** RESIDUAL SUM OF SQUARES IS EXACTLY ZERO *****
!
   50 WRITE (IPRT,1060)
      RETURN
!
!     ***** SINGULAR CONVERGENCE *****
!
   60 WRITE (IPRT,1070)
      RETURN
!
!     ***** FALSE CONVERGENCE *****
!
   70 WRITE (IPRT,1080)
      RETURN
!
!     ***** LIMIT ON NUM. OF CALLS TO THE MODEL SUBROUTINE REACHED *****
!
   80 WRITE (IPRT,1090)
      RETURN
!
!     ***** ITERATION LIMIT REACHED *****
!
   90 WRITE (IPRT,1100)
      RETURN
!
!     ***** STOPX *****
!
  100 WRITE (IPRT,1110)
      RETURN
!
!     ***** INITIAL RESIDUAL SUM OF SQUARES OVERFLOWS *****
!
  110 WRITE (IPRT,1120)
      RETURN
!
!     ***** BAD PARAMETERS TO ASSESS *****
!
  120 WRITE (IPRT,1130)
      RETURN
!
!     ***** J COULD NOT BE COMPUTED *****
!
  130 WRITE (IPRT,1140)
      RETURN
!
  140 RETURN
!
!      FORMAT STATEMENTS
!
 1000 FORMAT (//17H ITERATION NUMBER, I5/1X, 22('-'))
 1010 FORMAT (5X, 5HMODEL, 53X, 10HFORECASTED/5X, 5HCALLS, 9X, 3HRSD,
     &   13X, 3HRSS, 8X, 12HREL CHNG RSS, 4X, 12HREL CHNG RSS, 4X,
     &   12HREL CHNG PAR/62X, 5HVALUE, 3X, 4HCHKD, 4X, 5HVALUE, 3X,
     &   4HCHKD/3X, I7, 3(2X, G14.4), 2(G12.4, 3X, A1))
 1020 FORMAT (/5X, 25H CURRENT PARAMETER VALUES, 19H (ONLY UNFIXED PARA,
     &   18HMETERS ARE LISTED))
 1030 FORMAT (/34H ***** PARAMETER CONVERGENCE *****)
 1040 FORMAT (/48H ***** RESIDUAL SUM OF SQUARES CONVERGENCE *****)
 1050 FORMAT (/44H ***** PARAMETER AND RESIDUAL SUM OF SQUARES,
     &   18H CONVERGENCE *****)
 1060 FORMAT (/50H ***** THE RESIDUAL SUM OF SQUARES IS EXACTLY ZERO,
     &   6H *****)
 1070 FORMAT (/33H ***** SINGULAR CONVERGENCE *****)
 1080 FORMAT (/30H ***** FALSE CONVERGENCE *****)
 1090 FORMAT (/44H ***** LIMIT ON NUMBER OF CALLS TO THE MODEL,
     &   25H SUBROUTINE REACHED *****)
 1100 FORMAT (/36H ***** ITERATION LIMIT REACHED *****)
 1110 FORMAT (/18H ***** STOPX *****)
 1120 FORMAT (/53H ***** INITIAL RESIDUAL SUM OF SQUARES OVERFLOWS ****,
     &   1H*)
 1130 FORMAT (/37H ***** BAD PARAMETERS TO ASSESS *****)
 1140 FORMAT (/52H ***** DERIVATIVE MATRIX COULD NOT BE COMPUTED *****)
 1150 FORMAT (/5X, 25H CURRENT PARAMETER VALUES)
      END
