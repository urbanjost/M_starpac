!AMEANM
      SUBROUTINE AMEANM (Y, YMISS, N, NUSED, YMEAN)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE COMPUTES THE ARITHMETIC MEAN OF A SERIES WHEN MISSING
!     DATA ARE INVOLVED.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  APRIL 2, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) :: YMEAN,YMISS
      INTEGER
     &   N,NUSED
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   Y(*)
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     &   SUMY
      INTEGER
     &   I
!
!  EXTERNAL FUNCTIONS
      LOGICAL
     &   MVCHK
!       EXTERNAL MVCHK
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     INTEGER I
!        AN INDEX VARIABLE
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS.
!     INTEGER NUSED
!        THE NUMBER OF ACTIVE NUMBER OF OBSERVATIONS.
!     REAL(KIND=WP) :: SUMY
!        THE VARIABLE USED TO SUM THE NON-MISSING Y VALUES.
!     REAL(KIND=WP) :: Y(N)
!        THE VECTOR CONTAINING THE OBSERVED SERIES
!     REAL(KIND=WP) :: YMEAN
!        THE MEAN OF THE OBSERVED TIME SERIES
!     REAL(KIND=WP) :: YMISS
!        THE USER SUPPLIED CODE WHICH IS USED TO DETERMINE WHETHER OR
!        NOT AN OBSERVATION IS MISSING.  IF Y(I) = YMISS,
!        THE VALUE IS ASSUMED MISSING, OTHERWISE IT IS NOT.
!
      NUSED = 0
      SUMY = 0.0_WP
      DO 10 I = 1, N
         IF (MVCHK(Y(I), YMISS)) GO TO 10
         SUMY = SUMY + Y(I)
         NUSED = NUSED + 1
   10 CONTINUE
      IF (NUSED.GE.1) THEN
         YMEAN = SUMY / NUSED
      ELSE
         YMEAN = SUMY
      END IF
      RETURN
      END
