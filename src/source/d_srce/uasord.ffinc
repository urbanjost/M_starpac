!UASORD
      SUBROUTINE UASORD (SPCF, SPCA, SPCFMN, SPCFMX, SPCAMN, SPCAMX,
     +   FREQ, NF, XAXIS, YAXIS, ISYM, NPTS, ISPC, LPCV, NSPCF, NSPCA,
     +   BW, ALOW, AUP, XPLTMN, XPLTMX, YPLTMN, YPLTMX, NPRT)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE PRODUCES CO-ORDINATES FOR THE SPECTRUM PLOTS.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  DECEMBER 7, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      DOUBLE PRECISION
     +   ALOW,AUP,BW,SPCAMN,SPCAMX,SPCFMN,SPCFMX,XPLTMN,XPLTMX,YPLTMN,
     +   YPLTMX
      INTEGER
     +   ISPC,LPCV,NF,NPRT,NPTS,NSPCA,NSPCF
!
!  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   FREQ(NF),SPCA(ISPC),SPCF(ISPC),XAXIS(LPCV),YAXIS(LPCV)
      INTEGER
     +   ISYM(LPCV)
!
!  LOCAL SCALARS
      DOUBLE PRECISION
     +   CILOW,CIMID,CIUP,YMAX
      INTEGER
     +   I,ISPCA,ISPCF
!
!  EXTERNAL SUBROUTINES
      EXTERNAL SPPLTC,SPPLTD,SPPLTL
!
!  INTRINSIC FUNCTIONS
      INTRINSIC LOG10,MAX,MIN
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     DOUBLE PRECISION ALOW
!        THE FACTOR USED TO COMPUTE THE LOWER CONFIDENCE LIMITS.
!     DOUBLE PRECISION AUP
!        THE FACTOR USED TO COMPUTE THE UPPER CONFIDENCE LIMITS.
!     DOUBLE PRECISION BW
!        THE BANDWIDTH.
!     DOUBLE PRECISION CILOW, CIMID, CIUP
!        THE Y AXIS VALUES FOR THE LOWER, MID AND UPPER CONFIDENCE
!        INTERVAL POINTS.
!     DOUBLE PRECISION FREQ(NF)
!        THE VECTOR OF FREQUENCIES AT WHICH THE SPECTRUM IS TO BE
!        ESTIMATED.
!     INTEGER I
!        AN INDEX VARIABLE
!     INTEGER ISPC
!         THE LENGTH OF THE SPECTRUM ARRAYS.
!     INTEGER ISPCA, ISPCF
!        THE INDEX FOR THE FOURIER AND AUTOREGRESSIVE  ESTIMATES,
!        RESPECTIVELY.
!     INTEGER ISYM(LPCV)
!        THE ARRAY CONTAINING THE CODE FOR THE PLOT SYMBOLS.
!     INTEGER LPCV
!        THE LENGTH OF THE PLOT CO-ORDINATE VECTORS.
!     INTEGER NF
!        THE NUMBER OF FREQUENCIES FOR WHICH THE SPECTRUM ESTIMATES
!        ARE TO BE ESTIMATED.
!     INTEGER NPRT
!        A CODE USED TO SPECIFY THE TYPE OF PLOT, WHERE IF
!        NPRT < 0 THE PLOT IS DECIBLES/LINEAR
!        NPRT = 0 THE PLOT IS SUPPRESSED
!        NPRT > 0 THE PLOT IS LOG/LINEAR
!     INTEGER NPTS
!        THE NUMBER OF X, Y CO-ORDINATES TO BE PLOTTED.
!     INTEGER NSPCA, NSPCF
!        THE NUMBER OF VALID SPECTRUM ESTIMATES FOR THE AUTOREGRESSIVE
!        AND FOURIER SPECTRUMS, RESPECTIVELY.
!     DOUBLE PRECISION SPCA(ISPC)
!        THE ARAY CONTAINING THE AUTOREGRESSIVE SPECTRUM ESTIMATES.
!     DOUBLE PRECISION SPCAMN, SPCAMX
!        THE MINIMUM AND MAXIMUM AUTOREGRESSIVE SPECTRUM VALUE TO BE
!        PLOTTED.
!     DOUBLE PRECISION SPCF(ISPC)
!        THE ARRAY CONTAINING THE FOURIER SPECTRUM ESTIMATES.
!     DOUBLE PRECISION SPCFMN, SPCFMX
!        THE MINIMUM AND MAXIMUM FOURIER SPECTRUM VALUE TO BE PLOTTED.
!     DOUBLE PRECISION XAXIS(LPCV)
!        THE X AXIS VALUES FOR THE SPECTRUM PLOT.
!     DOUBLE PRECISION XPLTMN, XPLTMX
!        THE MINIMUM AND MAXIMUM VALUES TO BE PLOTTED FOR THE X AXIS.
!     DOUBLE PRECISION YAXIS(LPCV)
!        THE Y AXIS VALUES FOR THE SPECTRUM PLOT.
!     DOUBLE PRECISION YMAX
!        THE MAXIMUM  ACTUAL SPECTRUM VALUE (IN DECIBLES) TO BE PLOTTED.
!     DOUBLE PRECISION YPLTMN, YPLTMX
!        THE MINIMUM AND MAYIMUM VALUES TO BE PLOTTED FOR THE X AXIS.
!
      ISPCF = 0
      ISPCA = NSPCF
!
      IF (NPRT.GE.1) GO TO 30
!
!     SET VARIOUS Y AXIS VALUES FOR DECIBLE PLOTS
!
      CALL SPPLTD (MIN(SPCFMN, SPCAMN), MAX(SPCFMX, SPCAMX),
     +   ALOW, AUP, YPLTMN, YPLTMX, CILOW, CIMID, CIUP, YMAX)
!
!     SET CO-ORDINATES FOR DECIBLE PLOTS
!
      DO 20 I = 1, NF
         IF (SPCF(I) .LT. SPCFMN) GO TO 10
         ISPCF = ISPCF + 1
         XAXIS(ISPCF) = FREQ(I)
         YAXIS(ISPCF) = 10.0D0 * LOG10(SPCF(I)) - YMAX
         ISYM(ISPCF) = 1
!
   10    IF (SPCA(I) .LT. SPCAMN) GO TO 20
         ISPCA = ISPCA + 1
         XAXIS(ISPCA) = FREQ(I)
         YAXIS(ISPCA) = 10.0D0 * LOG10(SPCA(I)) - YMAX
         ISYM(ISPCA) = 2
   20 CONTINUE
!
      GO TO 70
   30 CONTINUE
!
!     SET VARIOUS Y AXIS VALUES FOR LOG PLOTS
!
      CALL SPPLTL (MIN(SPCFMN, SPCAMN), MAX(SPCFMX, SPCAMX),
     +   ALOW, AUP, YPLTMN, YPLTMX, CILOW, CIMID, CIUP)
!
!     SET CO-ORDINATES FOR LOG PLOTS
!
      DO 60 I = 1, NF
         IF (SPCF(I) .LT. SPCFMN) GO TO 50
         ISPCF = ISPCF + 1
         XAXIS(ISPCF) = FREQ(I)
         YAXIS(ISPCF) = SPCF(I)
         ISYM(ISPCF) = 1
!
   50    IF (SPCA(I) .LT. SPCAMN) GO TO 60
         ISPCA = ISPCA + 1
         XAXIS(ISPCA) = FREQ(I)
         YAXIS(ISPCA) = SPCA(I)
         ISYM(ISPCA) = 2
   60 CONTINUE
!
   70 CONTINUE
!
      NPTS = NSPCA + NSPCF
!
!     SET CO-ORDINATES FOR BANDWIDTH AND CONFIDENCE INTERVAL.
!
      CALL SPPLTC (XAXIS, YAXIS, ISYM, NPTS, XPLTMN, XPLTMX, BW, CILOW,
     +   CIMID, CIUP, LPCV)
!
      RETURN
      END
