!ARCOEF
      SUBROUTINE ARCOEF (ACOV, PHI, RSS, LAG, LAGMAX, ACOV0)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE USES DURBINS RECURSIVE METHOD TO COMPUTE
!     THE AUTOREGRESSIVE COEFFICIENTS OF AN ORDER LAG PROCESS,
!     GIVEN ON INPUT THE COEFFICIENTS OF AN ORDER (LAG-1) PROCESS.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 21, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      DOUBLE PRECISION
     +   ACOV0,RSS
      INTEGER
     +   LAG,LAGMAX
!
!  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   ACOV(*),PHI(*)
!
!  LOCAL SCALARS
      DOUBLE PRECISION
     +   SUM1,SUM2,T
      INTEGER
     +   J,L1,L2,LJ
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     DOUBLE PRECISION ACOV(LAGMAX)
!        THE ARRAY OF ACVF ESTIMATES FOR LAGS ONE TO LAGMAX.
!     DOUBLE PRECISION ACOV0
!        THE ACVF FOR LAG ZERO.
!     INTEGER J, LAG
!        INDEX VARIABLES.
!     INTEGER LAGMAX
!        THE MAXIMUM LAG VALUE USED.
!     INTEGER LJ, L2
!        INDEX VARIABLES.
!     DOUBLE PRECISION PHI(LAGMAX)
!        THE ARRAY OF AUTOREGRESSIVE COEFFICIENTS FOR AN AR
!        PROCESS OF ORDER LAG.
!     DOUBLE PRECISION RSS
!        THE ONE STEP PREDICTION RESIDUAL SUM OF SQUARES.
!     DOUBLE PRECISION SUM1, SUM2
!        VARIABLES USED IN THE COMPUTATIONS OF THE AUTOREGRESSIVE
!        COEFFICIENTS.
!     DOUBLE PRECISION T
!        A TEMPORARY STORAGE LOCATION.
!
      L1 = LAG - 1
      SUM1 = 0.0D0
      SUM2 = 0.0D0
      DO 10 J = 1, L1
         LJ = LAG - J
         SUM1 = SUM1 + PHI(J) * ACOV(LJ)
         SUM2 = SUM2 + PHI(J) * ACOV(J)
   10 CONTINUE
      PHI(LAG) = (ACOV(LAG) - SUM1) / (ACOV0 - SUM2)
      L2 = LAG / 2
      DO 20 J = 1, L2
         LJ = LAG - J
         T = PHI(J) - PHI(LAG) * PHI(LJ)
         PHI(LJ) = PHI(LJ) - PHI(LAG) * PHI(J)
         PHI(J) = T
   20 CONTINUE
!
      RSS = RSS * (1.0D0 - PHI(LAG)*PHI(LAG))
!
      RETURN
      END
