!STAT1
      SUBROUTINE STAT1(Y, N, YMED, YMIN, YMAX, YMIDRG, YRANGE, NCELLS,
     +   YLB, YUB, YDISTR)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS COMPUTES STATISTICS FOR A VECTOR Y THAT REQUIRE OR CAN
!     MAKE USE OF SORTEDNESS IN THE Y VECTOR.
!
!     THIS SUBROUTINE IS BASED ON A MODIFICATION OF THE STATIS
!     CODE USED IN OMNITAB, VERSION 5 (6/16/72), WRITTED BY
!     SALLY PEAVY.  THE ORIGINAL ADAPTATION TO STARPAC WAS MADE
!     BY JANET DONALDSON.
!
!     WRITTEN BY - JOHN E. KOONTZ
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS
!
!     CREATION DATE  -  MAY 17, 1982
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) ::
     +   YLB,YMAX,YMED,YMIDRG,YMIN,YRANGE,YUB
      INTEGER
     +   N,NCELLS
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     +   Y(N),YDISTR(NCELLS)
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     +   DELY,YT
      INTEGER
     +   I,IC,IC1,L,M,M1
!
!  INTRINSIC FUNCTIONS
      INTRINSIC MOD
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL(KIND=WP) :: DELY
!        THE WIDTH OF AN INTERVAL ONE TENTH THE RANGE OF THE
!        DATA IN Y.
!     INTEGER I
!        A LOOP INDEX.
!     INTEGER IC
!        PREVIOUS SIGN IN RUNS CALCULATION.
!     INTEGER IC1
!        COUNT IN FREQUENCY DISTRIBUTION CALCULATIONS.
!     INTEGER L
!        A LOOP INDEX.
!     INTEGER M
!        A LOOP INDEX.
!     INTEGER M1
!        WHEN N IS EVEN, M1 IS M + 1, OTHERWISE IT IS M.
!     INTEGER N
!        INPUT PARAMETER.  THE LENGTH OF Y AND WT.
!     INTEGER NCELLS
!        THE NUMBER OF CELLS IN THE FREQUENCY DISTRIBUTION.
!     REAL(KIND=WP) :: Y(N)
!        INPUT PARAMETER.  A SET OF N DATA POINTS, SORTED
!        INTO ASCENDING ORDER.
!     REAL(KIND=WP) :: YDISTR(NCELLS)
!        OUTPUT PARAMETER.  THE NUMBERS OF Y VALUES WITH
!        POSITIVE WEIGHTS IN EACH OF NCELLS EQUAL LENGTH INTERVALS
!        THAT DIVIDE THE RANGE OF Y VALUES.
!     REAL(KIND=WP) :: YLB
!        THE LOWER BOUND FOR THE FREQUENCY DISTRIBUTION.
!        IF YLB = YUB, THE MINIMUM OBSERVATION WILL BE USED.
!     REAL(KIND=WP) :: YMAX
!        OUTPUT PARAMETER.  THE MAXIMUM OF THE OBSERVATIONS Y HAVING
!        POSITIVE WEIGHT.
!     REAL(KIND=WP) :: YMED
!        OUTPUT PARAMETER.  THE MEDIAN OF THE OBSERVATIONS Y.
!     REAL(KIND=WP) :: YMIDRG
!        OUTPUT PARAMETER.  THE MIDRANGE OF THE OBSERVATIONS Y.
!     REAL(KIND=WP) :: YMIN
!        OUTPUT PARAMETER.  THE MINIMUM OF THE OBSERVATIONS Y HAVING
!        POSITIVE WEIGHT.
!     REAL(KIND=WP) :: YRANGE
!        THE RANGE OF THE OBSERVATIONS Y.
!     REAL(KIND=WP) :: YT
!        THE MAXIMUM VALUE IN EACH INTERVAL IN THE FREQUENCY
!        DISTRIBUTIONS CALCULATIONS.
!     REAL(KIND=WP) :: YUB
!        THE UPPER BOUND FOR THE FREQUENCY DISTRIBUTION.
!        IF YLB = YUB, THE MAXIMUM OBSERVATION WILL BE USED.
!
!     CALCULATE THE MEDIAN, MIDRANGE, RANGE, AND EXTREMA.
!
      M = (N+1)/2
      M1 = M
      IF (MOD(N,2).EQ.0) M1 = M1 + 1
      YMED = (Y(M)+Y(M1))/2.0E0_WP
      YMIDRG = (Y(1)+Y(N))/2.0E0_WP
      YRANGE = Y(N) - Y(1)
      YMIN = Y(1)
      YMAX = Y(N)
!
!     COMPUTE FREQUENCY DISTRIBUTION.
!
      IF (NCELLS.LE.0) RETURN
      IC1 = 0
      IF (NCELLS.EQ.1) GO TO 40
!
      DELY = YRANGE
      YT = YMIN
      IF (YLB.GE.YUB) GO TO 5
         DELY = YUB - YLB
         YT = YLB
    5 CONTINUE
      DELY = DELY / NCELLS
      YT = YT + DELY
      L = 0
      DO 30 I=2,NCELLS
         IC = 0
   10    L = L + 1
         IF (L.GT.N) GO TO 20
         IF (Y(L).GT.YT) GO TO 20
         IC = IC + 1
         IC1 = IC1 + 1
         GO TO 10
   20    YDISTR(I-1) = IC
         L = L - 1
         YT = YT + DELY
   30 CONTINUE
   40 YDISTR(NCELLS) = N - IC1
      RETURN
      END
