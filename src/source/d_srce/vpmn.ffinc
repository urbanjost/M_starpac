!VPMN
      SUBROUTINE VPMN (YM, YMMISS, N, M, IYM, NSAMPL, ISCHCK, ISYM,
     +  LISYM, ISIZE, YMN, YMX, XLB, XINC, MISS, ILOG, IRLIN, IBAR)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE PRODUCES VERTICAL PLOTS
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 21, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      DOUBLE PRECISION
     +   XINC,XLB,YMN,YMX
      INTEGER
     +   IBAR,ILOG,IRLIN,ISCHCK,ISIZE,IYM,LISYM,M,N,NSAMPL
      LOGICAL
     +   MISS
!
!  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   YM(IYM,M),YMMISS(M)
      INTEGER
     +   ISYM(LISYM)
!
!  SCALARS IN COMMON
      INTEGER
     +   IERR
!
!  LOCAL SCALARS
      DOUBLE PRECISION
     +   REFPT,XLABEL,YDMN,YDMX,YWIDTH,YY
      INTEGER
     +   I,IEND,ILOGY,IMAX,IMIN,IPOINT,IPRT,IPTSYM,IREFPT,J,NUMCOL,
     +   NUSED
      LOGICAL
     +   IFMISS
      CHARACTER
     +   I0*1,IBLANK*1,IM*1,IPLTCH*1,IREFCH*1,FMT*72
!
!  LOCAL ARRAYS
      INTEGER
     +   ICOUNT(103)
      CHARACTER
     +   LINE(103)*1
!
!  EXTERNAL FUNCTIONS
      LOGICAL
     +   MVCHK
      EXTERNAL MVCHK
!
!  EXTERNAL SUBROUTINES
      EXTERNAL AMEAN,AMEANM,IPRINT,PLINE,PLTPLX,PLTSYM,SETIV,VPHEAD
!
!  INTRINSIC FUNCTIONS
      INTRINSIC LOG10,MAX,MIN
!
!  COMMON BLOCKS
      COMMON /ERRCHK/IERR
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     CHARACTER FMT*72
!        THE FORMAT FOR THE X-AXIS LABELS
!     INTEGER I
!        AN INDEXING VARIABLE.
!     INTEGER IBAR
!        THE VARIABLE USED TO DETERMINE IF SINGLE POINTS (IBAR .EQ. 0)
!        OR BARS (IBAR .NE. 0) ARE TO BE PLOTTED.
!     CHARACTER*1 IBLANK
!        THE PLOT SYMBOL BLANK.
!     INTEGER ICOUNT(103)
!        THE NUMBER OF PLOT SYMBOLS AT EACH LOCATION.
!     INTEGER IEND
!        THE NUMBER OF LOCATIONS IN THE PLOT STRING.
!     INTEGER IERR
!        THE INTEGER VALUE RETURNED BY THIS ROUTINE DESIGNATING
!        WHETHER ANY ERRORS WERE DETECTED IN THE PARAMETER LIST
!        IF IERR .EQ. 0, NO ERRORS WERE DETECTED
!        IF IERR .EQ. 1, ERRORS HAVE BEEN DETECTED
!     LOGICAL IFMISS
!        THE INDICATOR VARIABLE USED TO DETERMINE WHETHER THE
!        INPUT SERIES HAS MISSING DATA (TRUE) OR NOT (FALSE).
!     INTEGER ILOG
!        THE TWO DIGIT INTEGER, PQ, USED TO SELECT AXIS SCALE, WHERE
!        P DESIGNATES THE X-AXIS AND Q DESIGNATES THE Y-AXIS.
!        IF P.EQ.0 (Q.EQ.0), THEN THE X-AXIS (Y-AXIS) IS LINEAR.
!        IF P.NE.0 (Q.NE.0), THEN THE X-AXIS (Y-AXIS) IS LOG.
!     INTEGER ILOGY
!        THE VALUE OF Q (SEE ILOG).
!     CHARACTER*1 IM
!        THE PLOT SYMBOL M.
!     INTEGER IMAX
!        THE LARGEST LOCATION IN THE PLOT STRING BEING DEFINED.
!     INTEGER IMIN
!        THE SMALLEST LOCATION IN THE PLOT STRING BEING DEFINED.
!     CHARACTER*1 IPLTCH
!        THE PLOT CHARACTER USED FOR A GIVEN LINE OF THE PLOT.
!     INTEGER IPOINT
!        THE LOCATION IN THE PLOT STRING OF THE VALUE BEING PLOTTED.
!     INTEGER IPRT
!        *
!     INTEGER IPTSYM
!        AN INDICATOR VARIABLE USED TO DESIGNATE THE TYPE
!        OF PLOT.  IF ISCHCK = 1, THE PLOT IS A SYMPLE PAGE
!        OR VERTICAL PLOT.  IF ISCHCK = 2, THE PLOT IS A SYMBOL
!        PLOT.  IF ISCHCK = 3, THE PLOT IS A MULTIVARIATE PLOT.
!     CHARACTER*1 IREFCH
!        THE PLOT SYMBOL USED TO IDENTIFY THE PLOT REFERENCE LINE.
!     INTEGER IREFPT
!        THE LOCATION IN THE PLOT STRING FOR THE VALUE ZERO, OR
!        SERIES MEAN, WHICH EVER WAS REQUESTED.
!     INTEGER IRLIN
!        THE INDICATOR VARIABLE USED TO DESIGNATE WHETHER ZERO OR THE
!        SERIES MEAN IS TO BE PLOTTED AS A REFERENCE LINE, OR WHETHER
!        NO REFERENCE LINE IS TO BE PLOTTED.
!        IF IRLIN .LE. -1, NO REFERENCE LINE IS PLOTTED.
!        IF IRLIN .EQ.  0, ZERO IS PLOTTED AS THE REFERENCE LINE.
!        IF IRLIN .GE.  1, THE SERIES MEAN IS PLOTTED.
!     INTEGER ISCHCK
!        THE INTEGER VALUE INDICATING HOW THE PLOTTING SYMBOLS
!           WILL BE DESIGNATED, WHERE
!           0 INDICATES THE PLOTTING SYMBOLS HAVE NOT BEEN DESIGNATED IN
!             THE N VECTOR ISYM AND ONLY THE SYMBOL + IS TO BE USED
!           1 INDICATES THE PLOTTING SYMBOLS HAVE BEEN DESIGNATED IN THE
!             N VECTOR ISYM
!           2 INDICATES THAT M SERIES ARE BEING PLOTTED.
!             SYMBOL I+4 WILL BE USED FOR COLUMN I OF YM.
!     INTEGER ISIZE
!        THE TWO DIGIT INTEGER, PQ, USED TO SELECT AXIS SIZE, WHERE
!        P DESIGNATES THE X-AXIS AND Q DESIGNATES THE Y-AXIS.
!        IF P.EQ.0 (Q.EQ.0), THEN THE X-AXIS (Y-AXIS) IS THE MAXIMUM.
!        IF P.NE.0 (Q.NE.0), THEN THE X-AXIS (Y-AXIS) IS HALF THE MAXIMU
!     INTEGER ISYM(LISYM)
!        VECTOR CONTAINING SYMBOL DESIGNATIONS FOR PLOTTING
!     INTEGER IYM
!        THE EXACT VALUE OF THE FIRST DIMENSION OF THE MATRIX YM.
!     CHARACTER*1 I0
!        THE PLOT SYMBOL -0-.
!     INTEGER J
!        AN INDEX VARIABLE.
!     CHARACTER*1 LINE(103)
!        THE VECTOR USED FOR THE PLOT STRING.
!     INTEGER LISYM
!        *
!     INTEGER M
!        THE NUMBER OF COLUMNS OF DATA IN YM.
!     LOGICAL MISS
!        *
!     INTEGER N
!        THE INTEGER NUMBER OF OBSERVATIONS .
!     INTEGER NSAMPL
!        THE SAMPLING FREQUENCY ACTUALLY USED.
!     INTEGER NUMCOL
!        *
!     INTEGER NUSED
!        THE NUMBER OF ACTIVE OBSERVATIONS.
!     DOUBLE PRECISION REFPT
!        THE VALUE ZERO, OR THE MEAN OF THE SERIES, WHICH EVER IS
!        REQUESTED, USED AS A PLOT REFERENCE LINE.
!     DOUBLE PRECISION XINC
!        THE VARIABLE USED TO SPECIFY THE INCREMENT FOR XLABEL.
!     DOUBLE PRECISION XLABEL
!        THE VALUE USED FOR THE LABELS ON THE RIGHT SIDE OF THE PLOT.
!     DOUBLE PRECISION XLB
!        THE STARTING VALUE FOR LABELS ON THE RIGHT SIDE OF THE GRAPH.
!     DOUBLE PRECISION YDMN, YDMX
!        THE Y-AXIS DATA LIMITS ACTUALLY USED.
!     DOUBLE PRECISION YM(IYM,M)
!        THE VECTOR CONTAINING THE OBSERVED TIME SERIES
!     DOUBLE PRECISION YMMISS(M)
!        THE USER SUPPLIED CODE WHICH IS USED TO DETERMINE WHETHER OR
!        NOT AN OBSERVATION IN THE SERIES IS MISSING.  IF YM(I) = YMMISS
!        THE VALUE IS ASSUMED MISSING, OTHERWISE IT IS NOT.
!     DOUBLE PRECISION YMN, YMX
!        THE GRAPH AXIS LOWER AND UPPER LIMITS ACTUALLY USED.
!     DOUBLE PRECISION YWIDTH
!        THE SCALE INTERVAL OF THE PLOT.
!     DOUBLE PRECISION YY
!        THE VALUE OF YM ACTUALLY BEING PLOTTED
!
!
      DATA IBLANK/' '/, IM/'M'/, I0/'0'/
!
!     PRINT PLOT HEADINGS
!
      CALL IPRINT(IPRT)
      CALL VPHEAD(NSAMPL, IRLIN, IBAR,
     +            REFPT, YWIDTH, YMN, YMX, ISIZE, ILOG,
     +            LINE, NUMCOL, ILOGY, YDMN, YDMX)
      IEND = NUMCOL + 2
!
!     COMPUTE REFERENCE POINT OF GRAPH, IF REQUIRED.
!
      IF (IRLIN.GE.0) THEN
        IF (IRLIN.EQ.0) THEN
!
!         REFERENCE POINT IS ZERO
!
          REFPT = 0.0D0
          IREFCH = I0
        ELSE
!
!         REFERENCE POINT IS MEAN
!
          IF (MISS) THEN
            CALL AMEANM(YM, YMMISS, N, NUSED, REFPT)
          ELSE
            CALL AMEAN(YM, N, REFPT)
          END IF
          IF (ILOGY.NE.0) REFPT = LOG10(REFPT)
          IREFCH = IM
        END IF
!
!       COMPUTE LOCATION OF REFPT IN PLOT STRING
!
        CALL PLTPLX(REFPT, YMN, YWIDTH, IREFPT, IEND)
      ELSE
        IREFPT = 1
      END IF
!
!     BEGIN PLOTTING
!
      IPTSYM = ISCHCK + 1
      XLABEL = XLB
      DO 50 I=1,N,NSAMPL
         CALL PLINE(1, IEND, IBLANK, LINE)
         CALL SETIV(ICOUNT, IEND, 0)
         IFMISS = .FALSE.
         IPOINT = 1
         DO 30 J=1,M
           IF (MISS) THEN
             IFMISS = (IFMISS .OR. (MVCHK(YM(I,J),YMMISS(J))))
             IF (.NOT.(MVCHK(YM(I,J),YMMISS(J)))) THEN
               IF (ILOGY.EQ.0) THEN
                 YY = YM(I,J)
               ELSE
                 YY = LOG10(YM(I,J))
               END IF
               IF ((YY.GE.YDMN) .AND. (YY.LE.YDMX)) THEN
                CALL PLTPLX(YY, YMN, YWIDTH, IPOINT, IEND)
                CALL PLTSYM(IPTSYM, I, J, ISYM, N, IPOINT, LINE, ICOUNT)
                IPLTCH = LINE(IPOINT)
               ELSE
                IPOINT = IREFPT
                IPLTCH = IBLANK
               END IF
             END IF
           ELSE
             IF (ILOGY.EQ.0) THEN
               YY = YM(I,J)
             ELSE
               YY = LOG10(YM(I,J))
             END IF
             IF ((YY.GE.YDMN) .AND. (YY.LE. YDMX)) THEN
                CALL PLTPLX(YY, YMN, YWIDTH, IPOINT, IEND)
                CALL PLTSYM(IPTSYM, I, J, ISYM, N, IPOINT, LINE, ICOUNT)
                IPLTCH = LINE(IPOINT)
             ELSE
                IPOINT = IREFPT
                IPLTCH = IBLANK
             END IF
           END IF
   30    CONTINUE
         IF ((IBAR.GE.1) .AND. (.NOT.IFMISS)) THEN
            IMIN = MIN(IPOINT,IREFPT)
            IMAX = MAX(IPOINT,IREFPT)
            CALL PLINE(IMIN, IMAX, IPLTCH, LINE)
         ELSE IF (IRLIN.GE.0) THEN
            LINE(IREFPT) = IREFCH
         END IF
!
         IF (IFMISS) THEN
           WRITE(FMT,1000) NUMCOL, '8H MISSING'
           WRITE(IPRT,FMT) XLABEL, (LINE(J), J=1,IEND)
         ELSE
           WRITE(FMT,1000) NUMCOL, '1X, G11.5 '
           IF (M.EQ.1) THEN
             WRITE(IPRT,FMT) XLABEL, (LINE(J),J=1,IEND), YM(I,1)
           ELSE
             WRITE(IPRT,FMT) XLABEL, (LINE(J),J=1,IEND)
           END IF
         END IF
         XLABEL = XLABEL + XINC*NSAMPL
   50 CONTINUE
!
      RETURN
!
!     FORMAT STATEMENTS
!
 1000 FORMAT('(1X,G11.5,1X,A1,''I'',', I3, 'A1,''I'',A1,',A10,')')
      END
