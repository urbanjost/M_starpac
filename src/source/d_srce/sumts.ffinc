!SUMTS
      subroutine sumts(x, n, alpha, sumt1, xtrm)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     A ROUTINE TO CALCULATE THE UNWEIGHTED TRIMMED MEAN FOR
!     A SORTED VECTOR IN WHICH THE MIDTH ELEMENT IS THE ELEMENT
!     CLOSEST TO ZERO.  USED BY THE STAT FAMILY.
!
!     WRITTEN BY - JOHN E. KOONTZ
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS
!
!     CREATION DATE  -  MAY 17, 1982
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     real(kind=wp) ::&
     &   alpha,sumt1,xtrm
     integer&
     &   n
!
!  ARRAY ARGUMENTS
     real(kind=wp) ::&
     &   x(n)
!
!  LOCAL SCALARS
     integer&
     &   curhi,curlo,i,irev,ithi,itlo,mid,nalpha
!
!  EXTERNAL SUBROUTINES
!       EXTERNAL SUMBS
!
!  INTRINSIC FUNCTIONS
      intrinsic abs,max,min
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL(KIND=WP) :: ALPHA
!        INPUT PARAMETER.  THE PERCENTAGE OF POINTS TO TRIM
!        FROM EACH END OF THE ARRAY X FOR THE TRIMMED MEAN.
!     INTEGER CURHI
!        THE UPPER BOUND OF THE CURRENT INTERVAL.
!     INTEGER CURLO
!        THE LOWER BOUND OF THE CURRENT INTERVAL.
!     INTEGER I
!        LOOP PARAMETER.
!     INTEGER IREV
!        A VARIABLE WHICH RUNS IN THE SAME INTERVAL AS I, BUT
!        IN THE REVERSE ORDER.
!     INTEGER ITHI
!        THE COMPUTED UPPER BOUND SUCH THAT ALPHA PER
!        CENT OF THE POSITIVELY WEIGHTED POINTS ARE
!        OMITTED.
!     INTEGER ITLO
!        THE COMPUTED LOWER BOUND SUCH THAT ALPHA PER
!        CENT OF THE POSITIVELY WEIGHTED POINTS ARE
!        OMITTED.
!     INTEGER MID
!        THE INDEX OF THE ELEMENT IN X CLOSEST TO ZERO IN VALUE.
!        THE POINT OUT FROM WHICH THE SUMMING IS DONE.
!     INTEGER N
!        INPUT PARAMETER.  THE LENGTH OF THE ARRAY X.
!     INTEGER NALPHA
!        THE INTEGER WHICH IS ALPHA PER CENT OF N.
!     REAL(KIND=WP) :: SUMT1
!        OUTPUT PARAMETER.  THE UNWEIGHTED SUM OF THE TRIMMED
!        ARRAY.  ZERO-WEIGHTED ELEMENTS ARE OMITTED.
!     REAL(KIND=WP) :: X(N)
!        INPUT PARAMETER.  THE DATA ARRAY X IN WHICH THE SUMS ARE TAKEN.
!     REAL(KIND=WP) :: XTRM
!        OUTPUT PARAMETER.  THE UNWEIGHTED, TRIMMED MEAN OF X.
!
!     SET UP LIMITS TRIMMING THE NUMBER OF VALUES AT EACH END
!     BY ALPHA PER CENT.
!
      nalpha = alpha*n
      itlo = nalpha + 1
      ithi = n - nalpha
      call sumbs(x, n, itlo, mid, ithi)
!
!     INITIALIZE SUMMATION VARIABLES.
!
      sumt1 = x(mid)
      curlo = mid - 1
      curhi = mid + 1
!
!     SUM OUTWARDS FROM THE VALUE NEAREST ZERO.  THAT IS, SUM FROM
!     THE LEAST IN MAGNITUDE TO THE GREATEST.
!
   10 if (curhi.gt.ithi .or. curlo.lt.itlo) go to 30
      if (abs(x(curhi)).lt.abs(x(curlo))) go to 20
      sumt1 = sumt1 + x(curlo)
      curlo = curlo - 1
      go to 10
   20 sumt1 = sumt1 + x(curhi)
      curhi = curhi + 1
      go to 10
!
!     SUM UP ANY VALUES WHICH MAY REMAIN BECAUSE OF AN OFF
!     CENTER ZERO POINT.
!
   30 if (curhi.gt.ithi) go to 50
      curhi = max(curhi,itlo)
      do 40 i=curhi,ithi
         sumt1 = sumt1 + x(i)
   40 continue
   50 if (curlo.lt.itlo) go to 70
      curlo = min(curlo,ithi)
      do 60 irev=itlo,curlo
         i = itlo - irev + curlo
         sumt1 = sumt1 + x(i)
   60 continue
   70 xtrm = sumt1/(n-2*nalpha)
      return
      end
