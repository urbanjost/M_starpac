!RELCOM
      SUBROUTINE RELCOM(N, V, W, RELTOL, ABSTOL, NFAIL, IFAIL)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS SUBROUTINE COMPUTES THE NUMBER OF TIMES THE
!     RELATIVE DIFFERENCE BETWEEN V(I) AND W(I), I = 1, 2, ..., N,
!     IS GREATER THAN   RELTOL  .
!
!     WRITTEN BY  -  ROBERT B. SCHNABEL (CODED BY JANET R. DONALDSON)
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  APRIL 2, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) ::
     &   ABSTOL,RELTOL
      INTEGER
     &   N,NFAIL
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   V(N),W(N)
      INTEGER
     &   IFAIL(N)
!
!  LOCAL SCALARS
      INTEGER
     &   I
!
!  INTRINSIC FUNCTIONS
      INTRINSIC ABS,MAX
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL(KIND=WP) :: ABSTOL
!        THE ABSOLUTE TOLERANCE USED IN THE COMPARISON.
!     INTEGER I
!        AN INDEXING VARIABLE.
!     INTEGER IFAIL(N)
!        AN INDICATOR VARIABLE DESIGNATING WHETHER OR NOT THE COMPARISON
!        FAILED OR NOT, WHERE 0 INDICATES NOT FAILURE AND 1 INDICATES
!        FALURE.
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS.
!     INTEGER NFAIL
!        THE TOTAL NUMBER OF FAILURES.
!     REAL(KIND=WP) :: RELTOL
!        THE RELATIVE TOLERANCE USED IN THE COMPARISON.
!     REAL(KIND=WP) :: V(N), W(N)
!        THE VALUES BEING COMPARED.
!
      NFAIL = 0
!
      DO 30 I = 1, N
         IF ((ABS(V(I)-W(I)).LE.RELTOL*MAX(ABS(V(I)),ABS(W(I)))) .OR.
     &       (((V(I).EQ.0.0_WP).OR.(W(I).EQ.0.0_WP)).AND.
     &        (ABS(V(I)-W(I)).LE.ABSTOL))) THEN
            IFAIL(I) = 0
         ELSE
            IFAIL(I) = 1
            NFAIL = NFAIL + 1
         END IF
   30 CONTINUE
!
      RETURN
!
      END
