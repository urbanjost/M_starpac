!CCFMN
      SUBROUTINE CCFMN (Y1, Y2, N, LAGMAX, NCC, CCOV11, CCOV22, CCOV12,
     +   CCOV21, LCCOV, Y1MEAN, Y2MEAN, RHOC, SDRHOC, NPRT)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS IS THE MAIN SUBROUTINE FOR COMPUTING CROSS CORRELATIONS AND
!     THEIR STANDARD ERRORS.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 21, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      DOUBLE PRECISION
     +   Y1MEAN,Y2MEAN
      INTEGER
     +   LAGMAX,LCCOV,N,NCC,NPRT
!
!  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   CCOV11(LCCOV),CCOV12(LCCOV),CCOV21(LCCOV),CCOV22(LCCOV),
     +   RHOC(NCC),SDRHOC(NCC),Y1(N),Y2(N)
!
!  LOCAL SCALARS
      DOUBLE PRECISION
     +   FAC
      INTEGER
     +   I,I0,IM,IP
!
!  EXTERNAL SUBROUTINES
      EXTERNAL CCFSD,CCVF
!
!  INTRINSIC FUNCTIONS
      INTRINSIC SQRT
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     DOUBLE PRECISION CCOV11(LCCOV)
!        THE ARRAY IN WHICH THE AUTOCOVARIANCE FUNCTION ESTIMATES
!        FOR THE FIRST SERIES IS STORED.
!     DOUBLE PRECISION CCOV12(LCCOV), CCOV21(LCCOV)
!        THE ARRAYS IN WHICH THE CROSS COVARIANCE FUNCTION
!        ESTIMATES FOR THE FIRST SERIES LAGGED BEHIND THE SECOND
!        AND VISA VERSA, ARE STORED.
!     DOUBLE PRECISION CCOV22(LCCOV)
!        THE ARRAY IN WHICH THE AUTOCOVARIANCE FUNCTION ESTIMATES
!        FOR THE SECOND SERIES IS STORED.
!     DOUBLE PRECISION FAC
!        THE INVERSE OF THE SQUARE ROOT OF THE PRODUCT OF THE
!        AUTOCOVARIANCES AT LAG ZERO.
!     INTEGER I
!        THE INDEXING VARIABLE FOR THE LAG VALUE.
!     INTEGER  IM, IP, I0
!        THE LOCATIONS IN THE CCF RELATED ARRAYS
!        OF THE LAG -I, I, AND 0, RESPECTIVELY.
!     INTEGER LAGMAX
!        THE MAXIMUM LAG VALUE AT WHICH THE CCVF IS TO BE COMPUTED.
!     INTEGER LCCOV
!        THE DIMENSION OF THE COVARIANCE RELATED ARRAYS.
!     INTEGER N
!        THE INTEGER NUMBER OF OBSERVATIONS IN THE SERIES
!     INTEGER NCC
!        THE NUMBER OF CCF COMPUTED.
!     INTEGER NPRT
!        THE INDICATOR VARIABLE USED TO CONTROL COMPUTATIONS NEEDED
!        ONLY FOR PRINTED OUTPUT.
!     DOUBLE PRECISION RHOC(NCC)
!        THE ARRAY IN WHICH THE CROSS CORRELATIONS ARE STORED
!     DOUBLE PRECISION SDRHOC(NCC)
!        THE ARRAY CONTAINING THE STD. ERRORS OF THE CROSS CORRELATIONS.
!        ARE STORED
!     DOUBLE PRECISION Y1(N), Y1MEAN
!        THE FIRST SERIES, AND ITS MEAN.
!     DOUBLE PRECISION Y2(N), Y2MEAN
!        THE SECOND SERIES, AND ITS MEAN.
!
!     COMPUTE THE CROSS CORRELATIONS.
!
      CALL CCVF(Y1, Y2, N, LAGMAX, Y1MEAN, Y2MEAN, CCOV12, CCOV21,
     +   LCCOV)
!
      IF (NPRT .EQ. 0 .OR. CCOV11(1)*CCOV22(1) .EQ. 0.0D0) RETURN
!
      FAC = 1.0D0 / SQRT(CCOV11(1) * CCOV22(1))
!
      I0 = LAGMAX + 1
      RHOC(I0) = CCOV12(1) * FAC
      DO 10 I = 1, LAGMAX
         IP = I0 + I
         RHOC(IP) = CCOV12(I+1) * FAC
!
         IM = I0 - I
         RHOC(IM) = CCOV21(I+1) * FAC
   10 CONTINUE
!
!     COMPUTE STANDARD ERROR OF THE CROSSCORRELATIONS.
!
      CALL CCFSD (CCOV11, CCOV22, SDRHOC, LAGMAX, NCC, N, LCCOV)
!
      RETURN
      END
