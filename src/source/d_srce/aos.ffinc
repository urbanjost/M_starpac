!AOS
      SUBROUTINE AOS (N, LAGMAX, ACOV, PRHO, IAR, OSPVAR, PHI, WORK,
     +   AIC, FTEST, LACOV, LAIC)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE COMPUTES AUTOREGRESSIVE MODEL ORDER SELECTION
!     STATISTICS. IT PERFORMS STEPWISE FITTING OF AUTOREGRESSIVE
!     COEFFICIENTS BY DURBINS METHOD USING AKAIKES AIC CRITERION
!     FOR SELECTING ORDER.   THE ROUTINE IS MODELED AFTER
!     SUBROUTINE UFPE WRITTEN BY DICK JONES.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 21, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) ::
     +   OSPVAR
      INTEGER
     +   IAR,LACOV,LAGMAX,LAIC,N
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     +   ACOV(LACOV),AIC(LAIC),FTEST(2,LAGMAX),PHI(LAGMAX),
     +   PRHO(LAGMAX),WORK(LAGMAX)
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     +   ACOV0,AICMIN,FPLM,RSS,RSSMIN,SQPACF
      INTEGER
     +   I,J
!
!  EXTERNAL FUNCTIONS
      REAL(KIND=WP) ::
     +   CDFF,D1MACH
      EXTERNAL CDFF,D1MACH
!
!  EXTERNAL SUBROUTINES
      EXTERNAL ARCOEF
!
!  INTRINSIC FUNCTIONS
      INTRINSIC LOG,DBLE
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL(KIND=WP) :: ACOV(LACOV), ACOV0
!        THE AUTOCOVARIANCES FOR LAGS ZERO TO LAGMAX, AND THE
!        AUTOCOVARIANCE AT LAG ZERO.
!     REAL(KIND=WP) :: AIC(LAIC), AICMIN
!        THE ARRAY CONTAINING AKIAKES CRITERIA FOR EACH ORDER, WHERE
!        AIC(I+1) IS THE CRITERIA FOR ORDER I-1, AND THE MINIMUM
!        CRITERIA COMPUTED.
!     REAL(KIND=WP) :: FPLM
!        THE FLOATING POINT LARGEST MAGNITUDE.
!     REAL(KIND=WP) :: FTEST(2, LAGMAX)
!        THE ARRAY IN WHICH THE F PERCENTAGE POINT AND PROBABILITY ARE
!        STORED.
!     INTEGER I
!        AN INDEX VARIABLE.
!     INTEGER IAR
!        THE ORDER OF THE AUTOREGRESSIVE PROCESS CHOSEN.
!     INTEGER J
!        AN INDEX VARIABLE.
!     INTEGER LACOV
!        THE LENGTH OF THE VECTOR ACOV.
!     INTEGER LAGMAX
!        THE MAXIMUM LAG VALUE TO BE USED.
!     INTEGER LAIC
!        THE LENGTH OF THE VECTOR AIC.
!     INTEGER N
!        THE INTEGER NUMBER OF OBSERVATIONS IN EACH SERIES
!     REAL(KIND=WP) :: OSPVAR
!        THE ONE STEP PREDICTION VARIANCE FOR THE ORDER SELECTED (IAR).
!     REAL(KIND=WP) :: PHI(LAGMAX)
!        THE ARRAY OF AUTOREGRESSIVE COEFFICIENTS FOR THE
!        SELECTED ORDER.
!     REAL(KIND=WP) :: PRHO(LAGMAX)
!        THE ARRAY CONTAINING THE PARTIAL AUTOCORRELATION
!        COEFFICIENTS.
!     REAL(KIND=WP) :: RSS, RSSMIN
!        THE ONE STEP PREDICTION RESIDUAL SUM OF SQUARES AND THE
!        MINIMUM ONE STEP PREDICTION RESIDUAL SUM OF SQUARES.
!     REAL(KIND=WP) :: SQPACF
!        THE SQUARED VALUE OF THE PARTIAL AUTOCORRELATION FUNCTION AT
!        LAG I.
!     REAL(KIND=WP) :: WORK(LAGMAX)
!        A DOUBLE PRECISION WORK AREA.
!
      FPLM = D1MACH(2)
!
      RSS = ACOV(1) * N
      RSSMIN = RSS
      AIC(1) = N * LOG(RSS * (N+1) / (N-1))
      AICMIN = AIC(1)
      IAR = 0
!
!  START STEPWISE PROCEDURE
!
      WORK(1) = ACOV(2) / ACOV(1)
      PRHO(1) = WORK(1)
      RSS = RSS * (1.0E0_WP - WORK(1)*WORK(1))
      AIC(2) = N * LOG(RSS * (N+2) / (N-2))
!
      SQPACF = WORK(1) * WORK(1)
      FTEST(1, 1) = FPLM
      FTEST(2, 1) = 0.0E0_WP
      IF (SQPACF .GE. 1.0E0_WP) GO TO 5
!
      FTEST(1,1) = (N-2) * SQPACF / (1.0E0_WP - SQPACF)
!
      FTEST(2,1) = 1.0E0_WP - CDFF(FTEST(1,1), 1.0E0_WP, DBLE(N-2))
!
    5 CONTINUE
!
      IF (AIC(2).GE.AICMIN) GO TO 10
      AICMIN = AIC(2)
      RSSMIN = RSS
      IAR = 1
      PHI(1) = WORK(1)
!
   10 IF (LAGMAX.LE.1) GO TO 40
!
      ACOV0 = ACOV(1)
!
      DO 30 I=2,LAGMAX
         CALL ARCOEF (ACOV(2), WORK, RSS, I, LAGMAX, ACOV0)
         PRHO(I) = WORK(I)
         AIC(I+1) = FPLM
         FTEST(1,I) = FPLM
         FTEST(2,I) = FPLM
         IF (I.EQ.N-1) GO TO 15
!
         AIC(I+1) = N * LOG(RSS * (N+I+1) / (N-I-1))
!
         SQPACF = WORK(I) * WORK(I)
         IF (SQPACF .GE. 1.0E0_WP) GO TO 15
!
         FTEST(1,I) = (N-I-1) * SQPACF / (1.0E0_WP - SQPACF)
!
         FTEST(2,I) = 1.0E0_WP - CDFF(FTEST(1,I), 1.0E0_WP, DBLE(N-I-1))
!
   15    CONTINUE
!
!     IF THIS AIC IS A MINIMUM AND ITS LAG DOES NOT EXCEED N/2,
!     SAVE THE COEFFICIENTS.
!
         IF ((AIC(I+1).GE.AICMIN) .OR. (I.GT.N/2)) GO TO 30
         AICMIN = AIC(I+1)
         RSSMIN = RSS
         IAR = I
         DO 20 J=1,I
            PHI(J) = WORK(J)
   20    CONTINUE
   30 CONTINUE
!
!   NORMALIZE AIC
!
   40 CONTINUE
      AIC(1) = AIC(1) - AICMIN
      DO 50 I=1,LAGMAX
         AIC(I+1) = AIC(I+1) - AICMIN
   50 CONTINUE
!
      OSPVAR = RSSMIN / (N-IAR-1)
!
      RETURN
      END
