!DCOEF
      SUBROUTINE DCOEF (NDF, ND, IOD, NPARDF, PARDF, MBO, WORK)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS SUBROUTINE EXPANDS THE DIFFERENCE FILTER SPECIFIED BY NDF,
!     IOD AND ND INTO PARDF.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DEVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 26, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      INTEGER
     &   MBO,NDF,NPARDF
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   PARDF(*),WORK(*)
      INTEGER
     &   IOD(*),ND(*)
!
!  LOCAL SCALARS
      INTEGER
     &   K,KK,L,NTIMES,NWORK1,NWORK2
!
!  EXTERNAL FUNCTIONS
      INTEGER
     &   NCHOSE
      EXTERNAL NCHOSE
!
!  EXTERNAL SUBROUTINES
      EXTERNAL MULTBP
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     INTEGER IOD(NDF)
!        THE ORDER OF EACH OF THE DIFFERENCE FACTORS.
!     INTEGER K
!        AN INDEX VARIABLE.
!     INTEGER KK
!        AN INDEX VARIABLE.
!     INTEGER L
!        AN INDEX VARIABLE.
!     INTEGER MBO
!        THE MAXIMUM BACK ORDER OPERATOR.
!     INTEGER ND(NDF)
!        THE NUMBER OF TIMES EACH DIFFERENCE FACTOR IS TO BE APPLIED.
!     INTEGER NDF
!        THE NUMBER OF DIFFERENCE FACTORS
!     INTEGER NPARDF
!        THE ORDER OF THE EXPANDED DIFFERENCE FILTER.
!     INTEGER NTIMES
!        THE NUMBER OF TIMES A GIVEN DIFFERENCE FACTOR IS TO BE APPLIED.
!     INTEGER NWORK1
!        THE NUMBER OF TERMS IN THE FIRST COLUMN OF WORK.
!     INTEGER NWORK2
!        THE NUMBER OF TERMS IN THE SECOND COLUMN OF WORK
!     REAL(KIND=WP) :: PARDF(MBO)
!        THE VECTOR CONTAINING THE DIFFERENCE FILTER PARAMETERS.
!     REAL(KIND=WP) :: WORK(MBO,2)
!        A WORK ARRAY NECESSARY TO EXPAND THE DIFFERENCE FILTER.
!
      NPARDF = 0
!
      DO 30 L = 1, NDF
         IF (ND(L).EQ.0) GO TO 30
         NTIMES = ND(L)
         NWORK1 = IOD(L) * ND(L)
         DO 10 K = 1, NWORK1
            WORK(K) = 0.0_WP
   10    CONTINUE
         DO 20 K = 1, NTIMES
            KK = K * IOD(L)
            WORK(KK) = ((-1)**(K+1)) * NCHOSE(NTIMES, K)
   20    CONTINUE
         NWORK2 = NWORK1 + NPARDF
         CALL MULTBP (WORK(1), NWORK1, PARDF, NPARDF, WORK(MBO+1),
     &      NWORK2, MBO)
   30 CONTINUE
      RETURN
      END
