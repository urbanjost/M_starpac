!ACFMN
      SUBROUTINE ACFMN(Y, N, LAGMAX, RHO, SDRHO, YMEAN, PRHO, AIC,
     +   FTEST, PHI, IAR, OSPVAR, ACOV, LACOV, LAIC, CHIA, CHIAP, WORK,
     +   NPRT)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS IS THE MAIN SUBROUTINE FOR COMPUTING AUTOCORRELATIONS AND
!     PARTIAL AUTOCORRELATIONS OF A TIME SERIES .
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 21, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) ::
     +   CHIA,CHIAP,OSPVAR,YMEAN
      INTEGER
     +   IAR,LACOV,LAGMAX,LAIC,N,NPRT
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     +   ACOV(*),AIC(*),FTEST(2,*),PHI(*),PRHO(*),RHO(*),SDRHO(*),
     +   WORK(*),Y(*)
!
!  LOCAL SCALARS
      INTEGER
     +   I
!
!  EXTERNAL SUBROUTINES
      EXTERNAL ACFSD,ACVF,AOS,CHIRHO
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL(KIND=WP) :: ACOV(LACOV)
!        THE AUTOCOVARIANCE FUNCTION ESTIMATE VECTOR.
!     REAL(KIND=WP) :: AIC(LAIC)
!        THE ARRAY CONTAINING AKAIAES CRITERION FOR EACH ORDER.
!     REAL(KIND=WP) :: CHIA, CHIAP
!        THE VARIABLES IN WHICH THE CHI SQUARE STATISTIC AND
!        CHI SQUARED STATISTIC PROBABILITY FOR THE AUTOCORRELATIONS
!        ARE STORED.
!     REAL(KIND=WP) :: FTEST(2, LAGMAX)
!        THE ARRAY IN WHICH THE F RATIO AND PROBABILITY ARE STORED.
!     INTEGER I
!        AN INDEXING VARIABLE.
!     INTEGER IAR
!        THE ORDER OF THE AUTOREGRESSIVE PROCESS CHOSEN.
!     INTEGER LACOV
!        THE LENGTH OF THE VECTOR ACOV.
!     INTEGER LAGMAX
!        THE MAXIMUM LAG VALUE TO BE USED.
!     INTEGER LAIC
!        THE LENGTH OF THE VECTOR AIC.
!     INTEGER N
!        THE INTEGER NUMBER OF OBSERVATIONS IN THE SERIES
!     INTEGER NPRT
!        THE INDICATOR VARIABLE USED TO SPECIFY WHETHER OR NOT
!        PRINTED OUTPUT IS TO BE GIVEN, WHERE IF THE VALUE OF
!        NPRT IS ZERO, NO OUTPUT IS MADE.
!     REAL(KIND=WP) :: OSPVAR
!        THE ONE STEP PREDICTION VARIANCE FOR THE SELECTED ORDER (IAR).
!     REAL(KIND=WP) :: PHI(LAGMAX)
!        THE ARRAY OF AUTOREGRESSIVE COEFFICIENTS FOR THE SELECTED
!        ORDER (IAR).
!     REAL(KIND=WP) :: PRHO(LAGMAX)
!        THE ARRAY IN WHICH THE PARTIAL AUTOCORRELATIONS ARE STORED
!     REAL(KIND=WP) :: RHO(LAGMAX)
!        THE ARRAY IN WHICH THE AUTOCORRELATIONS ARE STORED
!     REAL(KIND=WP) :: SDRHO(LAGMAX)
!        THE ARRAY IN WHICH THE STANDARD ERRORS OF THE AUTOCORRELATIONS
!     REAL(KIND=WP) :: WORK(LAGMAX)
!        AN ARRAY USED IN THE COMPUTATIONS OF THE PARTIAL
!        AUTOCORRELATIONS COEFFICIENTS.
!        ARE STORED
!     REAL(KIND=WP) :: Y(N)
!        THE VECTOR CONTAINING THE OBSERVED SERIES
!     REAL(KIND=WP) :: YMEAN
!        THE MEAN OF THE OBSERVED TIME SERIES
!
!     COMPUTE AUTOCOVARIANCESS AND STANDARD DEVIATION OF THE SERIES.
!
      CALL ACVF(Y, N, YMEAN, ACOV, LAGMAX, LACOV)
!
      IF (ACOV(1) .EQ. 0.0E0_WP) RETURN
!
!     COMPUTE PARTIAL AUTOCORRELATIONS AND THE AUTOREGRESSIVE MODEL
!     ORDER SELECTION STATISTICS.
!
      CALL AOS (N, LAGMAX, ACOV, PRHO, IAR, OSPVAR, PHI, WORK,
     +   AIC, FTEST, LACOV, LAIC)
!
      IF (NPRT .EQ. 0) RETURN
!
!     COMPUTE AUTOCORRELATIONS
!
      DO 10 I = 1, LAGMAX
         RHO(I) = ACOV(I+1) / ACOV(1)
   10 CONTINUE
!
!     COMPUTE STANDARD ERROR OF AUTOCORRELATIONS.
!
      CALL ACFSD (RHO, SDRHO, LAGMAX, N)
!
!     COMPUTE CHI STATISTIC BASED ON AUTOCORRELATION VALUES
!
      CALL CHIRHO (RHO, N, LAGMAX, CHIA, CHIAP)
!
      RETURN
      END
