!STPADJ
      SUBROUTINE STPADJ(XM, N, M, IXM, MDL, PAR, NPAR,
     &   NEXMPT, STP, NFAIL, IFAIL, J, RELTOL, ABSTOL, STPLOW, STPMID,
     &   STPUP, ITEMP, FD, FDLAST, PV, PVNEW)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS SUBROUTINE ADJUSTS THE SELECTED STEP SIZES TO OPTIMAL
!     VALUES.
!
!     WRITTEN BY  -  ROBERT B. SCHNABEL (CODED BY JANET R. DONALDSON)
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  APRIL 2, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) ::
     &   ABSTOL,RELTOL,STP,STPLOW,STPMID,STPUP
      INTEGER
     &   IXM,J,M,N,NEXMPT,NFAIL,NPAR
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   FD(N),FDLAST(N),PAR(NPAR),PV(N),PVNEW(N),XM(IXM,M)
      INTEGER
     &   IFAIL(N),ITEMP(N)
!
!  SUBROUTINE ARGUMENTS
      EXTERNAL MDL
!
!  SCALARS IN COMMON
      REAL(KIND=WP) ::
     &   Q
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     &   FACTOR,STPNEW,TEMP
      INTEGER
     &   NCOUNT
      LOGICAL
     &   DONE,FIRST
!
!  EXTERNAL SUBROUTINES
      EXTERNAL CMPFD,ICOPY,RELCOM,DCOPY
!
!  INTRINSIC FUNCTIONS
      INTRINSIC ABS,SIGN
!
!  COMMON BLOCKS
      COMMON /NOTOPT/Q
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL(KIND=WP) :: ABSTOL
!        THE ABSOLUTE AGREEMENT TOLERANCE.
!     LOGICAL DONE
!        THE VARIABLE USED TO INDICATE WHETHER THE ADJUSTMENT
!        PROCESS IS COMPLETE OR NOT.
!     REAL(KIND=WP) :: FACTOR
!        A FACTOR USED IN COMPUTING THE STEP SIZE.
!     REAL(KIND=WP) :: FD(N)
!        THE FORWARD DIFFERENCE QUOTIENT APPROXIMATION TO THE
!        DERIVATIVE OF THE MODEL WITH RESPECT TO THE JTH PARAMETER
!     REAL(KIND=WP) :: FDLAST(N)
!        THE FORWARD DIFFERENCE QUOTIENT APPROXIMATION TO THE
!        DERIVATIVE OF THE MODEL WITH RESPECT TO THE JTH PARAMETER
!        COMPUTED WITH THE MOST RECENT STEP SIZE SELECTED.
!     LOGICAL FIRST
!        THE VARIABLE USED TO INDICATE WHETHER THIS STEP SIZE
!        IS BEING USED FOR THE FIRST TIME OR WHETHER IT HAS BEEN
!        PREVIOUSLY ADJUSTED.
!     INTEGER IFAIL(N)
!        AN INDICATOR VECTOR USED TO DESIGNATE THOSE OBSERVATIONS
!        FOR WHICH THE STEP SIZE DOES NOT MEET THE CRITERIA.
!     INTEGER ITEMP(N)
!        A TEMPORARY VECTOR USED FOR STORING PAST VALUES OF ITEMP.
!     INTEGER IXM
!        THE FIRST DIMENSION OF THE INDEPENDENT VARIABLE ARRAY.
!     INTEGER J
!        THE INDEX OF THE PARAMETER BEING EXAMINED.
!     INTEGER M
!        THE NUMBER OF INDEPENDENT VARIABLES.
!     EXTERNAL MDL
!        THE NAME OF THE USER SUPPLIED SUBROUTINE WHICH COMPUTES THE
!        PREDICTED VALUES BASED ON THE CURRENT PARAMETER ESTIMATES.
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS.
!     INTEGER NPAR
!        THE NUMBER OF UNKNOWN PARAMETERS IN THE MODEL.
!     INTEGER NCOUNT
!        THE NUMBER OF OBSERVATIONS AT WHICH THE NEW STEP SIZE DOES
!        SATISFY THE CRITERIA.
!     INTEGER NEXMPT
!        THE NUMBER OF OBSERVATIONS FOR WHICH A GIVEN STEP SIZE
!        DOES NOT HAVE TO BE SATISFACTORY AND THE SELECTED STEP
!        SIZE STILL BE CONSIDERED OK.
!     INTEGER NFAIL
!        A VECTOR CONTAINING FOR EACH OBSERVATION THE NUMBER OF
!        OBSERVATIONS FOR WHICH THE STEP SIZE DID NOT MEET THE CRITERIA.
!     REAL(KIND=WP) :: PAR(NPAR)
!        THE ARRAY IN WHICH THE CURRENT ESTIMATES OF THE UNKNOWN
!        PARAMETERS ARE STORED.
!     REAL(KIND=WP) :: PV(N)
!        THE PREDICTED VALUE BASED ON THE CURRENT PARAMETER ESTIMATES
!     REAL(KIND=WP) :: PVNEW(N)
!        THE PREDICTED VALUE BASED ON THE CURRENT PARAMETER ESTIMATES
!        FOR ALL BUT THE JTH PARAMETER VALUE, WHICH IS PAR(J)+STPCD.
!     REAL(KIND=WP) :: Q
!        A DUMMY VARIABLE WHICH IS USED, ALONG WITH COMMON NOTOPT (NO
!        OPTIMIZATION), TO COMPUTE THE STEP SIZE.
!     REAL(KIND=WP) :: RELTOL
!        THE RELATIVE AGREEMENT TOLERANCE.
!     REAL(KIND=WP) :: STP
!        THE STEP SIZE CURRENTLY BEING EXAMINED FOR THE FORWARD
!        DIFFERENCE APPROXIMATION TO THE DERIVATIVE.
!     REAL(KIND=WP) :: STPLOW
!        THE LOWER LIMIT ON THE STEP SIZE.
!     REAL(KIND=WP) :: STPMID
!        THE MIDPOINT OF THE ACCEPTABLE RANGE OF THE STEP SIZE.
!     REAL(KIND=WP) :: STPNEW
!        THE VALUE OF THE NEW STEP SIZE BEING TESTED.
!     REAL(KIND=WP) :: STPUP
!        THE UPPER LIMIT ON THE STEP SIZE.
!     REAL(KIND=WP) :: TEMP
!        A TEMPORARY LOCATION IN WHICH THE CURRENT ESTIMATE OF THE JTH
!        PARAMETER IS STORED.
!     REAL(KIND=WP) :: XM(IXM,M)
!        THE ARRAY IN WHICH ONE ROW OF THE INDEPENDENT VARIABLE ARRAY
!        IS STORED.
!
      CALL ICOPY(N, IFAIL, 1, ITEMP, 1)
      NCOUNT = NFAIL
!
      IF ((STPLOW.LE.ABS(STP)) .AND. (ABS(STP).LE.STPUP)) RETURN
!
      IF (ABS(STP).GT.STPMID) THEN
!
            STPNEW = STPUP * SIGN(1.0_WP, PAR(J))
            FACTOR = 10.0_WP
      ELSE
!
            STPNEW = STPLOW * SIGN(1.0_WP, PAR(J))
            FACTOR = 0.1_WP
!
      END IF
!
      Q = STPNEW + PAR(J)
      STPNEW = Q - PAR(J)
!
      DONE = .FALSE.
      FIRST = .TRUE.
!
!     REPEAT STATEMENTS 60 TO 130 UNTIL (DONE)
!
   60 CONTINUE
!
         CALL DCOPY(N, FD, 1, FDLAST, 1)
!
         TEMP = PAR(J)
         PAR(J) = TEMP + STPNEW
         CALL MDL(PAR, NPAR, XM, N, M, IXM, PVNEW)
         PAR(J) = TEMP
!
         CALL CMPFD(N, STPNEW, PVNEW, PV, FD)
!
         CALL RELCOM(N, FD, FDLAST, RELTOL, ABSTOL, NCOUNT, ITEMP)
!
         IF (NCOUNT.LE.NEXMPT) THEN
               DONE = .TRUE.
               CALL ICOPY(N, ITEMP, 1, IFAIL, 1)
               NFAIL = NCOUNT
!
               IF (FIRST) THEN
                     STP = STPNEW
               ELSE
                     STP = STPNEW / FACTOR
               END IF
!
         ELSE
!
               FIRST = .FALSE.
               STPNEW = STPNEW * FACTOR
               Q = STPNEW + PAR(J)
               STPNEW = Q - PAR(J)
!
               IF ((FACTOR.GT.1.0_WP .AND. ABS(STPNEW).GT.ABS(STP))
     &            .OR.
     &            (FACTOR.LT.1.0_WP .AND. ABS(STPNEW).LT.ABS(STP)))
     &            DONE = .TRUE.
          END IF
!
      IF (DONE) THEN
         RETURN
      ELSE
         GO TO 60
      END IF
!
      END
