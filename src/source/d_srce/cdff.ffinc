!CDFF
      DOUBLE PRECISION FUNCTION CDFF(F, DF1, DF2)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE COMPUTES THE CUMULATIVE DISTRIBUTION FUNCTION FOR
!     THE F DISTRIBUTION.  THE APPROXIMATION USED DEPENDS ON THE
!     DEGREES OF FREEDOM IN THE NUMERATOR AND DENOMINATOR.
!     1.  IF BOTH DEGREES OF FREEDOM ARE SMALL (LESS THAN 4000),
!         THE CDF IS COMPUTED FROM THE INCOMPLETE BETA FUNCTION
!         USING EQUATION 5.45 OF STATISTICAL COMPUTING, BY KENNEDY AND
!         GENTLE.
!     2.  IF ONE OF THE DEGREES OF FREEDOM IS LARGE (GREATER THAN OR
!         EQUAL TO 4000) AND THE OTHER IS MODERATELY LARGE (GREATER THAN
!         OR EQUAL TO 100), THE CDF IS APPROXIMATED BY A NORMAL DISTRIB-
!         BUTION AS SHOWN IN EQUATION 20 ON PAGE 83 OF DISTRIBUTIONS IN
!         STATISTICS - CONTINUOUS UNIVARIATE DISTRIBUTIONS - 2, BY
!         JOHNSON AND KOTZ.
!     3.  IF ONE OF THE DEGREES OF FREEDOM IS SMALL (LESS THAN 100) AND
!         THE OTHER IS LARGE (EXCEEDING 4000), THE CDF IS APPROXIMATED
!         BY A CHI SQUARED DISTRIBUTION AS SHOWN IN THE THIRD EQUATION
!         ON PAGE 84 OF DISTRIBUTIONS IN STATISTICS - CONTINUOUS
!         UNIVARIATE DISTRIBUTIONS - 2, BY JOHNSON AND KOTZ.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE - DECEMBER 7, 1981
!                     THIS ROUTINE IS MODELED AFTER DATAPAC ROUTINE FCDF.
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      DOUBLE PRECISION
     +   DF1,DF2,F
!
!  LOCAL SCALARS
      DOUBLE PRECISION
     +   FTRANS
!
!  EXTERNAL FUNCTIONS
      DOUBLE PRECISION
     +   DBETAI,CDFCHI,CDFNML
      EXTERNAL DBETAI,CDFCHI,CDFNML
!
!  INTRINSIC FUNCTIONS
      INTRINSIC SQRT
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     DOUBLE PRECISION DF1
!        THE DEGREES OF FREEDOM IN THE NUMERATOR.
!     DOUBLE PRECISION DF2
!        THE DEGREES OF FREEDOM IN THE DENOMINATOR.
!     DOUBLE PRECISION F
!        THE PERCENTAGE POINT FROM THE F DISTRIBUTION.
!     DOUBLE PRECISION FTRANS
!        TRANSFORMS OF THE F PERCENTAGE POINT, USED FOR THE VARIOUS
!        APPROXIMATING DISTRIBUTIONS.
!
!     COMMENCE BODY OF ROUTINE
!
      IF (F.GT.0.0D0) GO TO 5
      CDFF = 0.0D0
      RETURN
!
    5 CONTINUE
!
      IF ((DF1.GT.4000.0D0) .OR. (DF2.GT.4000.0D0)) GO TO 10
!
!     BOTH DEGREES OF FREEDOM ARE LESS THAN OR EQUAL TO 4000.  USE
!     THE INCOMPLETE BETA FUNCTION TO COMPUTE THE F CDF.
!
      FTRANS = DF2 / (DF2 + DF1 * F)
!
      CDFF = 1.0D0 - DBETAI(FTRANS, 0.5D0*DF2, 0.5D0*DF1)
      RETURN
!
   10 IF ((DF1.LE.100.0D0) .OR. (DF2.LE.100.0D0)) GO TO 20
!
!     BOTH DEGREES OF FREEDOM EXCEED 4000.  USE THE NORMAL APPROXIMATION
!     TO COMPUTE THE F CDF.
!
      FTRANS =
     +   (((1.0D0-(2.0D0/(9.0D0*DF2)))*
     +     (F**(1.0D0/3.0D0)))-(1.0D0-(2.0D0/(9.0D0*DF1)))) /
     +   SQRT(((F**(2.0D0/3.0D0))/(4.5D0*DF2))+(2.0D0/(9.0D0*DF1)))
!
      CDFF = CDFNML(FTRANS)
      RETURN
!
   20 IF (DF1.GT.100.0D0) GO TO 30
!
!     THE DEGREES OF FREEDOM IN THE DENOMINATOR EXCEEDS 4000 AND THE
!     DEGREES OF FREEDOM IN NUMERATOR IS LESS THAN OR EQUAL TO 100.
!     USE THE CHI SQUARE APPROXIMATION TO COMPUTE THE F CDF.
!
      FTRANS =
     +   (DF1 + (DF1/DF2)*(0.5D0*DF1-1.0D0))/
     +   ((1.0D0/F)+(DF1/DF2)*0.5D0)
!
      CDFF = CDFCHI(FTRANS, DF1)
!
      RETURN
!
   30 CONTINUE
!
!     THE DEGREES OF FREEDOM IN THE NUMERATOR EXCEEDS 4000 AND THE
!     DEGREES OF FREEDOM IN DENOMINATOR IS LESS THAN OR EQUAL TO 100.
!     USE THE CHI SQUARE APPROXIMATION TO COMPUTE THE F CDF.
!
      FTRANS =
     +   (DF2+(DF2/DF1)*(0.5D0*DF2-1.0D0))/(F+(DF2/DF1)*0.5D0)
!
      CDFF = 1.0D0 - CDFCHI(FTRANS, DF2)
!
      RETURN
!
      END
