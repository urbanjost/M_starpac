!SUMWTS
      SUBROUTINE SUMWTS(X, W, N, NNZW, ALPHA, SUMT1, SUMTW1, XTRM,
     +   XTRMW)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     A ROUTINE TO CALCULATE THE WEIGHTED AND UNWEIGHTED MEANS
!     FOR A SORTED VECTOR IN WHICH THE MIDTH ELEMENT IS THE
!     ELEMENT CLOSEST TO ZERO.  USED BY THE STAT FAMILY.
!
!     WRITTEN BY - JOHN E. KOONTZ
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  MAY 17, 1982
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      DOUBLE PRECISION
     +   ALPHA,SUMT1,SUMTW1,XTRM,XTRMW
      INTEGER
     +   N,NNZW
!
!  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   W(N),X(N)
!
!  LOCAL SCALARS
      DOUBLE PRECISION
     +   SUMW
      INTEGER
     +   CURHI,CURLO,I,IREV,ITHI,ITLO,MID,NALPHA,NUM
!
!  EXTERNAL SUBROUTINES
      EXTERNAL SUMBS
!
!  INTRINSIC FUNCTIONS
      INTRINSIC ABS,MAX,MIN
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     DOUBLE PRECISION ALPHA
!        INPUT PARAMETER.  THE PERCENTAGE OF POINTS TO TRIM
!        FROM EACH END OF THE ARRAY X FOR THE TRIMMED MEAN.
!     INTEGER CURHI
!        THE UPPER BOUND OF THE CURRENT INTERVAL.
!     INTEGER CURLO
!        THE LOWER BOUND OF THE CURRENT INTERVAL.
!     INTEGER I
!        LOOP PARAMETER.
!     INTEGER IREV
!        A VARIABLE WHICH RUNS IN THE SAME INTERVAL AS I, BUT
!        IN THE REVERSE ORDER.
!     INTEGER ITHI
!        THE COMPUTED UPPER BOUND SUCH THAT ALPHA PER
!        CENT OF THE POSITIVELY WEIGHTED POINTS ARE
!        OMITTED.
!     INTEGER ITLO
!        THE COMPUTED LOWER BOUND SUCH THAT ALPHA PER
!        CENT OF THE POSITIVELY WEIGHTED POINTS ARE
!        OMITTED.
!     INTEGER MID
!        THE INDEX OF THE ELEMENT IN X CLOSEST TO ZERO IN VALUE.
!        THE POINT OUT FROM WHICH THE SUMMING IS DONE.
!     INTEGER N
!        INPUT PARAMETER.  THE LENGTH OF THE ARRAY X.
!     INTEGER NALPHA
!          THE INTEGER WHICH IS ALPHA PER CENT OF N.
!     INTEGER NNZW
!        INPUT PARAMETER.  THE NUMBER OF POSITIVELY WEIGHTED
!        VALUES IN X.
!     INTEGER NUM
!        THE NUMBER OF POINTS OMITTED SO FAR.
!     DOUBLE PRECISION SUMTW1
!        OUTPUT PARAMETER.  THE WEIGHTED SUM OF THE TRIMMED ARRAY
!        X.
!     DOUBLE PRECISION SUMT1
!        OUTPUT PARAMETER.  THE UNWEIGHTED SUM OF THE TRIMMED
!        ARRAY.  ZERO-WEIGHTED ELEMENTS ARE OMITTED.
!     DOUBLE PRECISION SUMW
!        THE SUM OF THE WEIGHTS W(I).
!     DOUBLE PRECISION W(N)
!        INPUT PARAMETER.  THE ARRAY OF WEIGHTS.
!     DOUBLE PRECISION X(N)
!        INPUT PARAMETER.  THE DATA ARRAY X IN WHICH THE SUMS ARE TAKEN.
!     DOUBLE PRECISION XTRM
!        OUTPUT PARAMETER.  THE UNWEIGHTED, TRIMMED MEAN OF X.
!     DOUBLE PRECISION XTRMW
!        OUTPUT PARAMETER.  THE WEIGHTED, TRIMMED MEAN OF X.
!
!     SET UP LIMITS TRIMMING THE NUMBER OF NON-ZERO WEIGHTED
!     VALUES BY ALPHA PER CENT AT EACH END OF THE RANGE OF
!     VALUES.
!
      NALPHA = ALPHA*NNZW
      NUM = 0
      DO 10 I=1,N
         IF (W(I).EQ.0.0D0) GO TO 10
         NUM = NUM + 1
         IF (NUM.EQ.NALPHA+1) GO TO 20
   10 CONTINUE
   20 ITLO = I
      NUM = 0
      DO 30 I=1,N
         IREV = N - I + 1
         IF (W(IREV).EQ.0.0D0) GO TO 30
         NUM = NUM + 1
         IF (NUM.EQ.NALPHA+1) GO TO 40
   30 CONTINUE
   40 ITHI = IREV
      CALL SUMBS(X, N, ITLO, MID, ITHI)
!
!     INITIALIZE SUMMATION VARIABLES.
!
      SUMT1 = 0.0D0
      SUMTW1 = 0.0D0
      SUMW = 0.0D0
      IF (W(MID).EQ.0.0D0) GO TO 50
      SUMT1 = X(MID)
      SUMTW1 = W(MID)*X(MID)
      SUMW = W(MID)
   50 CURLO = MID - 1
      CURHI = MID + 1
!
!     SUM OUTWARDS, BOULDER, COLORADO FROM THE VALUE NEAREST ZERO.  THAT
!     THE LEAST IN MAGNITUDE TO THE GREATEST.
!
   60 IF (CURHI.GT.ITHI .OR. CURLO.LT.ITLO) GO TO 100
      IF (ABS(X(CURHI)).LT.ABS(X(CURLO))) GO TO 80
      IF (W(CURLO).EQ.0.0D0) GO TO 70
      SUMT1 = SUMT1 + X(CURLO)
      SUMTW1 = SUMTW1 + W(CURLO)*X(CURLO)
      SUMW = SUMW + W(CURLO)
   70 CURLO = CURLO - 1
      GO TO 60
   80 IF (W(CURHI).EQ.0.0D0) GO TO 90
      SUMT1 = SUMT1 + X(CURHI)
      SUMTW1 = SUMTW1 + W(CURHI)*X(CURHI)
      SUMW = SUMW + W(CURHI)
   90 CURHI = CURHI + 1
      GO TO 60
!
!     SUM UP ANY VALUES WHICH MAY REMAIN BECAUSE OF AN OFF
!     CENTER ZERO POINT.
!
  100 IF (CURHI.GT.ITHI) GO TO 120
      CURHI = MAX(CURHI,ITLO)
      DO 110 I=CURHI,ITHI
         IF (W(I).EQ.0.0D0) GO TO 110
         SUMT1 = SUMT1 + X(I)
         SUMTW1 = SUMTW1 + W(I)*X(I)
         SUMW = SUMW + W(I)
  110 CONTINUE
  120 IF (CURLO.LT.ITLO) GO TO 140
      CURLO = MIN(CURLO,ITHI)
      DO 130 IREV=ITLO,CURLO
         I = ITLO - IREV + CURLO
         IF (W(I).EQ.0.0D0) GO TO 130
         SUMT1 = SUMT1 + X(I)
         SUMTW1 = SUMTW1 + W(I)*X(I)
         SUMW = SUMW + W(I)
  130 CONTINUE
  140 XTRM = SUMT1/(NNZW-2*NALPHA)
      XTRMW = SUMTW1/SUMW
      RETURN
      END
