!UASEST
      SUBROUTINE UASEST (IAR, VAR, PHI, NF, FREQ, DELTA, SPCA, LPHI,
     +   ISPC)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE CALCULATES THE AUTOREGRESSIVE SPECTRUM.  IT IS
!        MODELED AFTER SUBROUTINE UASEC BY DICK JONES.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  DECEMBER 7, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) ::
     +   DELTA,VAR
      INTEGER
     +   IAR,ISPC,LPHI,NF
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     +   FREQ(NF),PHI(LPHI),SPCA(ISPC)
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     +   ARG,EI,ER,EXI,EXR,EXRTMP,PI,TI,TR
      INTEGER
     +   I,J
!
!  EXTERNAL SUBROUTINES
      EXTERNAL GETPI
!
!  INTRINSIC FUNCTIONS
      INTRINSIC COS,SIN
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL(KIND=WP) :: ARG
!        THE ARGUMENT FOR THE SINE AND COSINE FUNCTIONS USED IN
!        CALCULATIONS OF THE SPECTRUM.
!     REAL(KIND=WP) :: DELTA
!        THE SAMPLING INTERVAL.
!     REAL(KIND=WP) :: EI, ER, EXI, EXR, EXRTMP
!        'COMPLEX' VARIABLES USED IN THE COMPUTATIONS.
!     REAL(KIND=WP) :: FREQ(NF)
!        THE ARRAY CONTAINING THE FREQUENCIES AT WHICH THE SPECTRUM
!        IS TO BE COMPUTED.
!     INTEGER I
!        AN INDEX VARIABLE.
!     INTEGER IAR
!        THE ORDER OF THE AUTOREGRESSIVE PROCESS CHOSEN.
!     INTEGER ISPC
!        THE LENGTH OF THE ARRAY SPCA.
!     INTEGER J
!        AN INDEX VARIABLE.
!     INTEGER LPHI
!        THE LENGTH OF THE ARRAY PHI.
!     INTEGER NF
!        THE NUMBER OF FREQUENCIES AT WHICH THE SPECTRUM IS TO BE
!        ESTIMATED.
!     REAL(KIND=WP) :: PHI(LPHI)
!        THE ARRAY OF AUTOREGRESSIVE COEFFICIENTS FOR THE
!        SELECTED ORDER.
!     REAL(KIND=WP) :: PI
!        THE VALUE OF PI.
!     REAL(KIND=WP) :: SPCA(ISPC)
!        THE ARRAY IN WHICH THE AUTOREGRESSIVE SPECTRUM IS STORED.
!     REAL(KIND=WP) :: TI, TR
!        A VARIABLE USED IN THE COMPUTATIONS.
!     REAL(KIND=WP) :: VAR
!        THE ONE STEP PREDICTION VARIANCE FOR THE SELECTED ORDER (IAR).
!
      CALL GETPI(PI)
!
      DO 20 J=1,NF
         SPCA(J) = DELTA * VAR
         IF (IAR.GE.1) THEN
            IF (DELTA.EQ.1.0E0_WP) THEN
               IF (FREQ(J).EQ.0.0E0_WP) THEN
                  ER = 1.0E0_WP
                  EI = 0.0E0_WP
               ELSE IF (FREQ(J).EQ.0.25D0) THEN
                  ER = 0.0E0_WP
                  EI = 1.0E0_WP
               ELSE IF (FREQ(J).EQ.0.5D0) THEN
                  ER = -1.0E0_WP
                  EI = 0.0E0_WP
               ELSE
                  ARG = 2.0E0_WP * PI * DELTA * FREQ(J)
                  ER = COS(ARG)
                  EI = SIN(ARG)
               END IF
            ELSE
               ARG = 2.0E0_WP * PI * DELTA * FREQ(J)
               ER = COS(ARG)
               EI = SIN(ARG)
            END IF
            EXR = 1.0E0_WP
            EXI = 0.0E0_WP
            TR = 1.0E0_WP
            TI = 0.0E0_WP
            DO 10 I=1,IAR
               EXRTMP = EXR*ER - EXI*EI
               EXI = EXR*EI + EXI*ER
               EXR = EXRTMP
               TR = TR - PHI(I) * EXR
               TI = TI - PHI(I) * EXI
   10       CONTINUE
            SPCA(J) = SPCA(J)/(TR*TR + TI*TI)
         END IF
   20 CONTINUE
      RETURN
      END
