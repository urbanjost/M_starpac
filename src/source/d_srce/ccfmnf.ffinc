!CCFMNF
     subroutine ccfmnf (y1, y2, n, nfft, lagmax, ncc, ccov11, ccov22,&
     &   ccov12, ccov21, lccov, rhoc, sdrhoc, nprt, lyfft, work, lwork)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS IS THE MAIN SUBROUTINE FOR COMPUTING CROSS CORRELATIONS AND
!     THEIR STANDARD ERRORS OF A TIME SERIES USING A FFT.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 21, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     integer&
     &   lagmax,lccov,lwork,lyfft,n,ncc,nfft,nprt
!
!  ARRAY ARGUMENTS
     real(kind=wp) ::&
    &   ccov11(lccov),ccov12(lccov),ccov21(lccov),ccov22(lccov),&
     &   rhoc(ncc),sdrhoc(ncc),work(lwork),y1(lyfft),y2(lyfft)
!
!  LOCAL SCALARS
     real(kind=wp) ::&
     &   fac
     integer&
     &   i,i0,im,ip
!
!  EXTERNAL SUBROUTINES
!       EXTERNAL CCFSD,CCVFF
!
!  INTRINSIC FUNCTIONS
      intrinsic sqrt
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL(KIND=WP) :: CCOV11(LCCOV)
!        THE ARRAY IN WHICH THE AUTOCOVARIANCE FUNCTION ESTIMATES
!        FOR THE FIRST SERIES IS STORED.
!     REAL(KIND=WP) :: CCOV12(LCCOV), CCOV21(LCCOV)
!        THE ARRAYS IN WHICH THE CROSS COVARIANCE FUNCTION
!        ESTIMATES FOR THE FIRST SERIES LAGGED BEHIND THE SECOND
!        AND VISA VERSA, ARE STORED.
!     REAL(KIND=WP) :: CCOV22(LCCOV)
!        THE ARRAY IN WHICH THE AUTOCOVARIANCE FUNCTION ESTIMATES
!        FOR THE SECOND SERIES IS STORED.
!     REAL(KIND=WP) :: FAC
!        THE INVERSE OF THE SQUARE ROOT OF THE PRODUCT OF THE
!        AUTOCOVARIANCES AT LAG ZERO.
!     INTEGER I
!        THE INDEXING VARIABLE FOR THE LAG VALUE.
!     INTEGER  IM, IP, I0
!        THE LOCATIONS IN THE CCF RELATED ARRAYS
!        OF THE LAG -I, I, AND 0, RESPECTIVELY.
!     INTEGER LAGMAX
!        THE MAXIMUM LAG VALUE TO BE USED.
!     INTEGER LCCOV
!        THE DIMENSION OF THE COVARANCE ARRAYS.
!     INTEGER LWORK
!        THE DIMENSION OF THE WORK ARRAY.
!     INTEGER LYFFT
!        THE DIMENSION OF THE DATA ARRAYS.
!     INTEGER N
!        THE INTEGER NUMBER OF OBSERVATIONS IN THE SERIES
!     INTEGER NCC
!        THE NUMBER OF CROSS CORRELATIONS COMPUTED.
!     INTEGER NFFT
!        THE NUMBER OF OBSERVATIONS IN THE EXTENDED SERIES.
!     INTEGER NPRT
!        THE INDICATOR VARIABLE USED TO CONTROL COMPUTATIONS NEEDED
!        ONLY FOR PRINTED OUTPUT.
!     REAL(KIND=WP) :: RHOC(NCC)
!        THE ARRAY IN WHICH THE CROSS CORRELATIONS ARE STORED
!     REAL(KIND=WP) :: SDRHOC(NCC)
!        THE ARRAY CONTAINING THE STD. ERRORS OF THE CROSS CORRELATIONS.
!        ARE STORED.
!     REAL(KIND=WP) :: WORK(LWORK)
!        THE WORK ARRAY.
!     REAL(KIND=WP) :: Y1(LYFFT), Y2(LYFFT)
!        THE VECTORS CONTAINING THE OBSERVED SERIES
!
!     COMPUTE THE CROSS CORRELATIONS.
!
     call ccvff (y1, y2, n, nfft, lagmax, ccov12, ccov21, lccov, lyfft,&
     &   work, lwork)
!
      if (nprt .eq. 0 .or. ccov11(1)*ccov22(1) .eq. 0.0_wp) return
!
      fac = 1.0_wp / sqrt(ccov11(1) * ccov22(1))
!
      i0 = lagmax + 1
      rhoc(i0) = ccov12(1) * fac
      do 10 i = 1, lagmax
         ip = i0 + i
         rhoc(ip) = ccov12(i+1) * fac
!
         im = i0 - i
         rhoc(im) = ccov21(i+1) * fac
   10 continue
!
!     COMPUTE STANDARD ERROR OF THE CROSSCORRELATIONS.
!
      call ccfsd (ccov11, ccov22, sdrhoc, lagmax, ncc, n, lccov)
!
      return
      end
