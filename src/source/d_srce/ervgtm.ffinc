!ERVGTM
      SUBROUTINE ERVGTM (NMSUB, NMVAR, VEC, VCMISS, N, VECLB, NVMX,
     &   HEAD, MSGTYP, NV, ERROR, NMMIN)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE CHECKS TO ENSURE THAT NO VALUES, OR ONLY A MAXIMUM
!     OF NVMX, ARE NOT GREATER THAN A SPECIFIED LOWER BOUND VECLB,
!     WITH NAME NMMIN. THE ROUTINE ALTERNATIVELY CHECKS TO MAKE SURE
!     THAT NO VALUES ARE IN VIOLATION OF THIS LOWER BOUND IF THE FIRST
!     VALUE IN THE VECTOR IS NOT.  THE CHECKING OPTION IS SPECIFIED
!     WITH MSGTYP.  IF AN ERROR IS FOUND, THE ERROR IS PRINTED AND
!     AN ERROR FLAG AND THE NUMBER OF VIOLATINS ARE RETURNED.
!     VALUES OF VEC EQUAL TO VCMISS ARE EXEMPTED FROM THE CHECKING.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  JUNE 10, 1982
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) ::
     &   VCMISS,VECLB
      INTEGER
     &   MSGTYP,N,NV,NVMX
      LOGICAL
     &   ERROR,HEAD
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   VEC(*)
      CHARACTER
     &   NMMIN(8)*1,NMSUB(6)*1,NMVAR(8)*1
!
!  LOCAL SCALARS
      INTEGER
     &   I,NVMN
!
!  EXTERNAL FUNCTIONS
      LOGICAL
     &   MVCHK
      EXTERNAL MVCHK
!
!  EXTERNAL SUBROUTINES
      EXTERNAL ERVGTP
!
!  INTRINSIC FUNCTIONS
      INTRINSIC MOD
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     LOGICAL ERROR
!        THE VALUE RETURNED FROM THE ERROR CHECKING ROUTINES TO INDICATE
!        WHETHER AN ERROR WAS DETECTED (TRUE) OR NOT (FALSE).
!     LOGICAL HEAD
!        A FLAG INDICATING WHETHER THE HEADING SHOULD BE PRINTED (TRUE)
!        OR NOT (FALSE).
!     INTEGER I
!        AN INDEX ARGUMENT.
!     INTEGER MSGTYP
!        THE INDICATOR ARGUMENT FOR THE TYPE OF MESSAGE.
!        IF (MSGTYP.GE.4) THE MESSAGE PRINTED WILL USE NMMIN
!        OTHERWISE IT WILL USE VECLB.
!        IF (MSGTYP = 1 OR 4) NO VIOLATIONS ARE ALLOWED.
!        IF (MSGTYP = 2 OR 5) THE NUMBER OF VIOLATIONS MUST
!                             BE LESS THAN   NVMX   .
!        IF (MSGTYP = 3 OR 6) VIOLATIONS ARE COUNTED ONLY IF THE
!                             THE FIRST ELEMENT IS NOT IN VIOLATION.
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS.
!     CHARACTER*1 NMMIN(8)
!        THE NAME OF THE ARGUMENT SPECIFYING THE MINIMUM.
!     CHARACTER*1 NMSUB(6)
!        THE CHARACTERS OF THE CALLING SUBROUTINES NAME.
!     CHARACTER*1 NMVAR(8)
!        THE CHARACTERS OF THE ARGUMENTS NAME.
!     INTEGER NV
!        THE NUMBER OF VIOLATIONS FOUND.
!     INTEGER NVMN
!        THE SMALLEST NUMBER OF NON-VIOLATIONS ALLOWED.
!     INTEGER NVMX
!        THE LARGEST NUMBER OF VIOLATIONS ALLOWED.
!     REAL(KIND=WP) :: VCMISS
!        MISSING VALUE CODE IN VEC.
!     REAL(KIND=WP) :: VEC(N)
!        THE VECTOR BEING TESTED.
!     REAL(KIND=WP) :: VECLB
!        THE VALUE THAT THE VECTOR IS BEING TESTED AGAINST.
!
      ERROR = .FALSE.
!
      IF (N .LE. 0) RETURN
!
!     TEST WHETHER TESTING IS NECESSRY
!
      IF ((MOD(MSGTYP,3) .EQ. 0) .AND. (VEC(1) .LE. VECLB)) RETURN
!
!     CHECK FOR VIOLATIONS
!
      NV = 0
      DO 5 I = 1, N
         IF (MVCHK(VEC(I), VCMISS)) GO TO 5
         IF ((VEC(I).LE.VECLB)) NV = NV + 1
    5 CONTINUE
!
      IF (NV .LE. NVMX) RETURN
!
!     VIOLATIONS FOUND
!
      ERROR = .TRUE.
      NVMN = N - NVMX
      CALL ERVGTP (NMSUB, NMVAR, VECLB, NVMN, NVMX, HEAD, MSGTYP, NV,
     &   NMMIN)
!
      RETURN
!
      END
