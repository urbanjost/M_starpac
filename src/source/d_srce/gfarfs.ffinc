!GFARFS
      SUBROUTINE GFARFS (PHI, IAR, NF, FMIN, FMAX, GAIN, PHAS, FREQ,
     +   NPRT, LDSTAK)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS IS THE USER CALLABLE ROUTINE TO COMPUTE THE GAIN
!     FUNCTION OF AN AUTOREGRESSIVE FILTER (SHORT CALL).
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 26, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) ::
     +   FMAX,FMIN
      INTEGER
     +   IAR,LDSTAK,NF,NPRT
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     +   FREQ(*),GAIN(*),PHAS(*),PHI(*)
!
!  SCALARS IN COMMON
      INTEGER
     +   IERR
!
!  ARRAYS IN COMMON
      REAL(KIND=WP) :: DSTAK(12)
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     +   DELTA,FMN,FMX,YPLTMN,YPLTMX
      INTEGER
     +   IGFERR,IO,IPRT,ISORT,LDSMIN,NALL0,NORD,XORD,YORD
      LOGICAL
     +   ERR01,ERR02,ERR03,HEAD,SYM
!
!  LOCAL ARRAYS
      REAL(KIND=WP) ::
     +   RSTAK(12)
      INTEGER
     +   ISTAK(12)
      CHARACTER
     +   LIAR(8)*1,LLDS(8)*1,LNF(8)*1,NMSUB(6)*1
!
!  EXTERNAL FUNCTIONS
      INTEGER
     +   STKGET,STKST
      EXTERNAL STKGET,STKST
!
!  EXTERNAL SUBROUTINES
      EXTERNAL EISGE,GFAEST,GFORD,GFOUT,IPRINT,LDSCMP,SETFRQ,STKCLR,
     +   STKSET
!
!  INTRINSIC FUNCTIONS
      INTRINSIC MAX,MIN
!
!  COMMON BLOCKS
      COMMON /CSTAK/DSTAK
      COMMON /ERRCHK/IERR
!
!  EQUIVALENCES
      EQUIVALENCE (DSTAK(1),RSTAK(1))
      EQUIVALENCE (DSTAK(1),ISTAK(1))
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL(KIND=WP) :: DELTA
!        THE SAMPLING INTERVAL.
!     REAL(KIND=WP) :: DSTAK(12)
!        THE DOUBLE PRECISION VERSION OF THE /CSTAK/ WORK AREA.
!     LOGICAL ERR01, ERR02, ERR03
!        VALUES INDICATING WHETHER AN ERROR WAS DETECTED (TRUE) OR NOT
!        (FALSE).
!     REAL(KIND=WP) :: FMAX, FMIN
!        THE MINIMUM AND MAXIMUM FREQUENCY FOR WHICH THE GAIN
!        FUNCTION IS TO BE ESTIMATED.
!     REAL(KIND=WP) :: FMN, FMX
!        THE FREQUENCY RANGE ACTUALLY USED.
!     REAL(KIND=WP) :: FREQ(NF)
!        THE VECTOR OF FREQUENCIES AT WHICH THE GAIN FUNCTION
!        HAS BEEN ESTIMATED.
!     REAL(KIND=WP) :: GAIN(NF)
!        THE VECTOR IN WHICH THE GAIN FUNCTION ESTIMATES ARE
!        STORED.
!     LOGICAL HEAD
!        A FLAG INDICATING WHETHER THE HEADING SHOULD BE PRINTED
!        (TRUE) OR NOT (FALSE).  IF A HEADING IS PRINTED, THE VALUE
!        OF HEAD WILL BE CHANGED TO FALSE.
!     INTEGER IAR
!        THE NUMBER OF FILTER COEFFICIENTS.
!     INTEGER IERR
!        THE INTEGER VALUE RETURNED BY THIS ROUTINE DESIGNATING
!        WHETHER ANY ERRORS WERE DETECTED IN THE PARAMETER LIST.
!        IF IERR .EQ. 0, NO ERRORS WERE DETECTED.
!        IF IERR .EQ. 1, ERRORS HAVE BEEN DETECTED.
!     INTEGER IGFERR
!        AN ERROR FLAG INDICATING WHETHER COMPUTATIONS SEEM
!        TO HAVE PRODUCED REASONABLE RESULTS.
!     INTEGER IO
!        A VARIABLE USED TO DETERMINE THE AMOUNT OF STORAGE REQUIRED,
!        BASED ON PRINTED OUTPUT.
!     INTEGER IPRT
!        THE UNIT NUMBER USED FOR OUTPUT.
!     INTEGER ISORT
!        THE STARTING LOCATION IN ISTAK FOR
!        THE ARRAY USED FOR SORTING.
!     INTEGER ISTAK(12)
!        THE INTEGER VERSION OF THE /CSTAK/ WORK AREA.
!     INTEGER LDSTAK
!        THE LENGTH OF THE ARRAY DSTAK.
!     INTEGER LDSMIN
!        THE MINIMUM LENGTH ALLOWED FOR THE ARRAY DSTAK.
!     CHARACTER*1 LIAR(8), LLDS(8), LNF(8)
!        THE ARRAYS CONTAINING THE NAMES OF CHECKED VARIABLES
!     INTEGER NALL0
!        THE NUMBER OF OUTSTANDING STACK ALLOCATIONS.
!     INTEGER NF
!        THE NUMBER OF FREQUENCIES AT WHICH THE GAIN FUNCTION
!        IS TO BE ESTIMATED.
!     CHARACTER*1 NMSUB(6)
!        THE ARRAY CONTAINING THE NAME OF THIS SUBROUTINE.
!     INTEGER NORD
!        THE NUMBER OF POINTS TO BE PLOTTED.
!     INTEGER NPRT
!        A CODE USED TO SPECIFY THE TYPE OF PLOT, WHERE IF
!        NPRT < 0 THE PLOT IS DECIBELS/LINEAR
!        NPRT = 0 THE PLOT IS SUPPRESSED
!        NPRT > 0 THE PLOT IS LOG/LINEAR
!     REAL(KIND=WP) :: PHAS(NF)
!        THE ARRAY IN WHICH THE PHASE OF THE FILTER IS RETURNED.
!     REAL(KIND=WP) :: PHI(IAR)
!        THE VECTOR CONTAINING THE FILTER COEFFICIENTS.
!     REAL(KIND=WP) :: RSTAK(12)
!        THE DOUBLE PRECISION VERSION OF THE /CSTAK/ WORK AREA.
!     LOGICAL SYM
!        THE VARIABLE USED TO INDICATE WHETHER THE GAIN FUNCTION
!        COMPUTED WAS FOR A SYMMETRIC OR AUTOREGRESSIVE FILTER.
!     INTEGER XORD
!        THE STARTING LOCATION IN RSTAK/DSTAK OF
!        THE X COORDINATES FOR THE GAIN FUNCTION PLOT
!     INTEGER YORD
!        THE STARTING LOCATION IN RSTAK/DSTAK OF
!        THE Y COORDINATES FOR THE GAIN FUNCTION PLOT
!     REAL(KIND=WP) :: YPLTMN, YPLTMX
!        THE MINIMUM AND MAXIMUM VALUES TO BE PLOTTED FOR THE Y AXIS.
!
!     SET UP NAME ARRAYS
!
      DATA
     +  NMSUB(1),  NMSUB(2),  NMSUB(3),  NMSUB(4),  NMSUB(5),  NMSUB(6)
     + /     'G',       'F',       'A',       'R',       'F',       'S'/
      DATA
     +  LIAR(1), LIAR(2), LIAR(3), LIAR(4), LIAR(5), LIAR(6),
     +  LIAR(7), LIAR(8) /'I', 'A', 'R', ' ', ' ', ' ', ' ', ' '/
      DATA
     +  LLDS(1), LLDS(2), LLDS(3), LLDS(4), LLDS(5), LLDS(6), LLDS(7),
     +  LLDS(8) /  'L',   'D',   'S',   'T',   'A',   'K',   ' ',   ' '/
      DATA
     +  LNF(1), LNF(2), LNF(3), LNF(4), LNF(5), LNF(6), LNF(7), LNF(8)
     + /  'N',   'F',   ' ',   ' ',   ' ',   ' ',   ' ',   ' '/
!
!     SET UP FOR ERROR CHECKING
!
      IERR = 0
      HEAD = .TRUE.
!
      IO = 1
      IF (NPRT .EQ. 0) IO = 0
!
!     CALL ERROR CHECKING ROUTINES
!
      CALL EISGE(NMSUB, LIAR, IAR, 1, 1, HEAD, ERR01, LIAR)
!
      CALL EISGE(NMSUB, LNF, NF, 1, 1, HEAD, ERR02, LNF)
!
      IF (ERR02) GO TO 10
!
      CALL LDSCMP(3*IO, 0, NF, 0, 0, 0, 'D', 2*IO*NF, LDSMIN)
!
      CALL EISGE(NMSUB, LLDS, LDSTAK, LDSMIN, 9, HEAD, ERR03, LLDS)
!
      IF (ERR01.OR.ERR02.OR.ERR03) GO TO 10
      GO TO 20
!
   10   IERR = 1
        CALL IPRINT (IPRT)
        WRITE (IPRT, 1000)
        RETURN
!
   20 CONTINUE
!
      IF (NPRT .NE. 0) THEN
        CALL STKSET (LDSTAK, 4)
        NALL0 = STKST(1)
      ELSE
        NALL0 = 0
      END IF
!
!     SET VARIOUS PROGRAM PARAMETERS
!
      SYM = .FALSE.
!
      DELTA = 1.0E0_WP
!
!     SET FREQUENCIES AT WHICH THE GAIN FUNCTION IS TO BE ESTIMATED
!
      FMN = MAX(FMIN, 0.0E0_WP)
      FMX = MIN(FMAX, 0.5D0_WP)
      IF (FMN.GE.FMX) THEN
        FMN = 0.0E0_WP
        FMX = 0.5D0_WP
      END IF
      CALL SETFRQ (FREQ, NF, 2, FMN, FMX, DELTA)
!
!     COMPUTE THE GAIN FUNCTION
!
      CALL GFAEST (PHI, IAR, NF, FREQ, GAIN, PHAS, DELTA)
!
      IF (NPRT .EQ. 0) RETURN
!
!     SET VARIOUS POINTERS FOR THE PLOTTING ARRAYS
!
      XORD = STKGET(NF, 4)
      YORD = STKGET(NF, 4)
!
      ISORT = STKGET(NF, 2)
!
      IF (IERR .EQ. 1) GO TO 10
!
!     PLOT THE RESULTS
!
      CALL GFORD (FREQ, GAIN, ISTAK(ISORT), NF, RSTAK(XORD),
     +   RSTAK(YORD), NORD, YPLTMN, YPLTMX, NPRT, IGFERR)
      CALL GFOUT (RSTAK(XORD), RSTAK(YORD), NORD, FREQ, PHAS, NF,
     +   IAR, SYM, FMN, FMX, YPLTMN, YPLTMX, NPRT, IGFERR, NMSUB)
!
      CALL STKCLR(NALL0)
!
      IF (IERR .EQ. 1) GO TO 10
!
      RETURN
!
!     FORMAT STATEMENTS
!
 1000 FORMAT (/42H THE CORRECT FORM OF THE CALL STATEMENT IS//
     + '       CALL GFARFS (PHI, IAR,'/
     + '      +             NF, FMIN, FMAX, GAIN, PHAS, FREQ, NPRT,',
     + ' LDSTAK)')
      END
