!DCKZRO
      SUBROUTINE DCKZRO(J, PAR, NPAR, MDL, XM, N, NROW, M, IXM, PV,
     +   PVTEMP, MSG, LMSG, FD, PARMX, PVPSTP, STP)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE RECHECKS THE DERIVATIES IN THE CASE WHERE THE FINITE
!     DIFFERENCE DERIVATIVE DISAGREES WITH THE ANALYTIC DERIVATIVE AND T
!     ANALYTIC DERIVATIVE IS ZERO.
!
!     WRITTEN BY  -  ROBERT B. SCHNABEL (CODED BY JANET R. DONALDSON)
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  APRIL 2, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      DOUBLE PRECISION
     +   FD,PARMX,PV,PVPSTP,STP
      INTEGER
     +   IXM,J,LMSG,M,N,NPAR,NROW
!
!  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   PAR(NPAR),PVTEMP(N),XM(IXM,M)
      INTEGER
     +   MSG(LMSG)
!
!  SUBROUTINE ARGUMENTS
      EXTERNAL MDL
!
!  LOCAL SCALARS
      DOUBLE PRECISION
     +   CD,FPLRS,PVMSTP,TEMP,THIRD
!
!  EXTERNAL FUNCTIONS
      DOUBLE PRECISION
     +   D1MACH
      EXTERNAL D1MACH
!
!  INTRINSIC FUNCTIONS
      INTRINSIC ABS,MIN
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     DOUBLE PRECISION CD
!        THE CENTRAL DIFFERENCE QUOTIENT DERIVATIVE WITH
!        RESPECT TO THE JTH PARAMETER.
!     DOUBLE PRECISION FD
!        THE FORWARD DIFFERENCE QUOTIENT DERIVATIVE WITH RESPECT TO THE
!        JTH PARAMETER.
!     DOUBLE PRECISION FPLRS
!        THE FLOATING POINT LARGEST RELATIVE SPACING.
!     INTEGER IXM
!        THE FIRST DIMENSION OF THE INDEPENDENT VARIABLE ARRAY.
!     INTEGER J
!        THE INDEX OF THE PARTIAL DERIVATIVE BEING EXAMINED.
!     INTEGER LMSG
!        THE LENGTH OF THE VECTOR MSG.
!     INTEGER M
!        THE NUMBER OF INDEPENDENT VARIABLES.
!     EXTERNAL MDL
!        THE NAME OF THE USER SUPPLIED SUBROUTINE WHICH COMPUTES THE
!        PREDICTED VALUES BASED ON THE CURRENT PARAMETER ESTIMATES.
!     INTEGER MSG(LMSG)
!        AN ARRAY USED TO STORE MESSAGE PARAMETERS.
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS.
!     INTEGER NPAR
!        THE NUMBER OF UNKNOWN PARAMETERS IN THE MODEL.
!     INTEGER NROW
!        THE NUMBER OF THE ROW OF THE INDEPENDENT VARIABLE ARRAY AT
!        WHICH THE DERIVATIVE IS TO BE CHECKED.
!     DOUBLE PRECISION PAR(NPAR)
!        THE ARRAY IN WHICH THE CURRENT ESTIMATES OF THE UNKNOWN
!        PARAMETERS ARE STORED.
!     DOUBLE PRECISION PARMX
!        THE MAXIMUM OF THE CURRENT PARAMETER ESTIMATE AND THE TYPICAL
!        VALUE OF THAT PARAMETER.
!     DOUBLE PRECISION PV
!        THE SCALAR IN WHICH THE PREDICTED VALUE FROM THE MODEL FOR
!        ROW   NROW   IS STORED.
!     DOUBLE PRECISION PVMSTP
!        THE PREDICTED VALUE FOR ROW    NROW   OF THE MODEL
!        BASED ON THE CURRENT PARAMETER ESTIMATES
!        FOR ALL BUT THE JTH PARAMETER VALUE, WHICH IS PAR(J) - STP.
!     DOUBLE PRECISION PVPSTP
!        THE PREDICTED VALUE FOR ROW    NROW   OF THE MODEL
!        BASED ON THE CURRENT PARAMETER ESTIMATES
!        FOR ALL BUT THE JTH PARAMETER VALUE, WHICH IS PAR(J) + STP.
!     DOUBLE PRECISION PVTEMP(N)
!        THE VECTOR OF PREDICTED VALUES FROM THE MODEL.
!     DOUBLE PRECISION STP
!        THE STEP SIZE CURRENTLY BEING EXAMINED FOR THE FINITE DIFFERENC
!        DERIVATIVE
!     DOUBLE PRECISION TEMP
!        A TEMPORARY LOCATION IN WHICH THE CURRENT ESTIMATE OF THE JTH
!        PARAMETER IS STORED.
!     DOUBLE PRECISION THIRD
!        THE VALUE 1/3.
!     DOUBLE PRECISION XM(IXM,M)
!        THE ARRAY IN WHICH ONE ROW OF THE INDEPENDENT VARIABLE ARRAY
!        IS STORED.
!
      FPLRS = D1MACH(4)
!
!     RECALCULATE NUMERICAL DERIVATIVE USING CENTRAL DIFFERENCE AND STEP
!     SIZE OF 2*STP
!
      TEMP = PAR(J)
      PAR(J) = PAR(J) - STP
      CALL MDL(PAR, NPAR, XM, N, M, IXM, PVTEMP)
      PAR(J) = TEMP
!
      PVMSTP = PVTEMP(NROW)
!
      CD = (PVPSTP-PVMSTP)/(2.0D0*STP)
!
!     CHECK FOR DISAGREEMENT
!
      IF (CD.NE.0.0D0) GO TO 10
!
!     NUMERICAL AND ANALYTIC DERIVATIVES NOW AGREE, BUT BOTH EQUAL ZERO,
!     INDICATING THAT DERIVATIVES SHOULD BE RECHECKED AT ANOTHER POINT.
!
      IF (MSG(1).EQ.0) MSG(1) = 1
      MSG(J+1) = 3
      RETURN
!
   10 CONTINUE
!
!     NUMERICAL AND ANALYTIC DERIVATIVE STILL DO NOT AGREE.
!
!     CHECK IF NUMERICAL DERIVATIVE IS CLOSE TO ZERO.
!
      THIRD = 1.0D0/3.0D0
      IF (MIN(ABS(CD), ABS(FD))*PARMX .GT. ABS(PV*FPLRS**THIRD))
     +   GO TO 20
!
!     NUMERICAL DERIVATIVE IS CLOSE TO ZERO
!
      IF (MSG(1).EQ.0) MSG(1) = 1
      MSG(J+1) = 4
      RETURN
!
   20 CONTINUE
!
!     NUMERICAL DERIVATIVE NOT CLOSE TO ZERO
!
      MSG(1) = 2
      MSG(J+1) = 5
      RETURN
!
      END
