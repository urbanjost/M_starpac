!VCVOUT
      SUBROUTINE VCVOUT(NP, VCV, IVCV, EST)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS SUBROUTINE PRINTS THE VARIANCE COVARIANCE MATRIX.
!     IF EST IS TRUE, THE COVARIANCES ARE LISTED ABOVE THE
!     DIAGONAL, THE VARIANCES ON THE DIAGONAL, AND THE CORRELATION
!     COEFFICIENTS BELOW THE DIAGONAL.
!     IF EST IS FALSE, THE STANDARD DEVIATIONS ARE LISTED ON THE
!     DIAGONAL, AND THE CORRELATION COEFFICIENTS ARE BELOW THE
!     DIAGONAL.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  APRIL 2, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      INTEGER
     +   IVCV,NP
      LOGICAL
     +   EST
!
!  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   VCV(IVCV,NP)
!
!  LOCAL SCALARS
      DOUBLE PRECISION
     +   DEN,SVCVII,SVCVJJ
      INTEGER
     +   I,IPRT,J,K,MODE
!
!  EXTERNAL SUBROUTINES
      EXTERNAL IPRINT,MATPRT
!
!  INTRINSIC FUNCTIONS
      INTRINSIC SQRT
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     DOUBLE PRECISION DEN
!        DENOMINATOR OF (I, J) CORRELATION COEFFICIENT
!     LOGICAL EST
!        AN INDICATOR USED TO DESIGNATE WHETHER THE VCV TO BE PRINTED
!        IS OF THE ESTIMATED PARAMETERS (TRUE) OR NOT (FALSE).
!     INTEGER I
!        AN INDEX VARIABLE.
!     INTEGER IPRT
!        THE UNIT NUMBER FOR PRINTED OUTPUT.
!     INTEGER IVCV
!        THE EXACT FIRST DIMENSION OF THE MATRIX VCV.
!     INTEGER J
!        THE INDEX OF THE PARAMETER BEING EXAMINED.
!     INTEGER K
!        AN INDEX VARIABLE.
!     INTEGER MODE
!        IF MODE IS 1, PRINT FULL MATRIX.
!        IF MODE IS 2, PRINT LOWER TRIANGLE WITH SQUARE ROOTS OF
!                      OF THE DIAGONAL.
!     INTEGER NP
!        THE NUMBER OF UNKNOWN PARAMETERS IN THE MODEL.
!     DOUBLE PRECISION SVCVII, SVCVJJ
!        SQUARE ROOTS OF VCV(I, I) AND VCV(J, J)
!     DOUBLE PRECISION VCV(IVCV,NP)
!        THE VARIANCE COVARIANCE MATRIX.
!
!     COMMENCE BODY OF ROUTINE
!
      CALL IPRINT(IPRT)
!
!     DETERMINE WHETHER TO ISSUE NEGATIVE VARIANCE WARNING
!
      MODE = 2
      DO 10 I=1,NP
         IF (VCV(I,I).GT.0.0D0) GO TO 10
         WRITE (IPRT,1000)
         IF (EST) WRITE (IPRT,1050)
         WRITE (IPRT,1010)
         MODE = 0
         GO TO 70
   10 CONTINUE
!
      IF (EST) GO TO 20
!
!     PRINT HEADING FOR CORRELATION ROUTINES
!
      WRITE (IPRT,1040)
      WRITE (IPRT,1030)
      MODE = 2
      GO TO 30
!
   20 CONTINUE
!
!     PRINT HEADING FOR ESTIMATION ROUTINES
!
      WRITE (IPRT,1050)
      WRITE (IPRT,1020)
      MODE = 1
!
   30 CONTINUE
!
!     COMPUTE THE CORRELATION COEFFICIENTS AND STORE IN THE BOTTOM HALF
!     OF THE VARIANCE COVARIANCE MATRIX
!
      IF (NP.EQ.1) GO TO 60
      DO 50 J=2,NP
         K = J - 1
         SVCVJJ = 0.0D0
         IF (VCV(J,J).GT.0.0D0) SVCVJJ = SQRT(VCV(J,J))
         DO 40 I=1,K
            SVCVII = 0.0D0
            IF (VCV(I,I).GT.0.0D0) SVCVII = SQRT(VCV(I,I))
            DEN = SVCVII*SVCVJJ
            IF (DEN.LE.0.0D0) VCV(J,I) = 0.0D0
            IF (DEN.GT.0.0D0) VCV(J,I) = VCV(J,I)/DEN
   40    CONTINUE
   50 CONTINUE
!
   60 CONTINUE
!
   70 CALL MATPRT(VCV, VCV, NP, IPRT, MODE, 1, IVCV)
!
!     RESTORE THE VCV MATRIX
!
      IF (NP.EQ.1) RETURN
      DO 90 J=2,NP
         K = J - 1
         DO 80 I=1,K
            VCV(J,I) = VCV(I,J)
   80    CONTINUE
   90 CONTINUE
!
      RETURN
!
!     FORMAT STATEMENTS
!
 1000 FORMAT (/18H COVARIANCE MATRIX)
 1010 FORMAT (/39H     NONPOSITIVE VARIANCES ENCOUNTERED./10H     CORRE,
     +   39HLATION COEFFICIENTS CANNOT BE COMPUTED.)
 1020 FORMAT (4X, 36H- COVARIANCES ARE ABOVE THE DIAGONAL/4X, 7H- VARIA,
     +   24HNCES ARE ON THE DIAGONAL/4X, 27H- CORRELATION COEFFICIENTS ,
     +   22HARE BELOW THE DIAGONAL)
 1030 FORMAT (4X, 41H- STANDARD DEVIATIONS ARE ON THE DIAGONAL/4X,
     +   49H- CORRELATION COEFFICIENTS ARE BELOW THE DIAGONAL)
 1040 FORMAT (/19H CORRELATION MATRIX)
 1050 FORMAT (/45H VARIANCE-COVARIANCE AND CORRELATION MATRICES,
     +   28H OF THE ESTIMATED PARAMETERS/ 1X, 72('-')/)
      END
