!SUMWSS
     subroutine sumwss(x, w, n, lo, mid, hi, nnzw, sum1, sumw1, sumw2,&
     &   sumwa, sumw, xmean, xmeanw)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     A ROUTINE TO CALCULATE WEIGHTED AND UNWEIGHTED SUMS OF
!     POWERS AND THE MEAN FOR A SORTED VECTOR IN WHICH THE MID TH
!     ELEMENT IS THE ELEMENT CLOSEST TO ZERO.  USED BY THE STAT
!     FAMILY.
!
!     WRITTEN BY - JOHN E. KOONTZ
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  MAY 17, 1982
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     real(kind=wp) ::&
     &   sum1,sumw,sumw1,sumw2,sumwa,xmean,xmeanw
     integer&
     &   hi,lo,mid,n,nnzw
!
!  ARRAY ARGUMENTS
     real(kind=wp) ::&
     &   w(n),x(n)
!
!  LOCAL SCALARS
     integer&
     &   curhi,curlo,i,irev
!
!  INTRINSIC FUNCTIONS
      intrinsic abs
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     INTEGER CURHI
!        THE UPPER BOUND OF THE CURRENT INTERVAL.
!     INTEGER CURLO
!        THE LOWER BOUND OF THE CURRENT INTERVAL.
!     INTEGER HI
!        INPUT PARAMETER.  THE UPPER BOUND OF THE INITIAL
!        INTERVAL.
!     INTEGER I
!        LOOP PARAMETER.
!     INTEGER IREV
!        A VARIABLE WHICH RUNS IN THE SAME INTERVAL AS I, BUT
!        IN THE REVERSE ORDER.
!     INTEGER LO
!        INPUT PARAMETER.  THE LOWER BOUND OF THE INITIAL
!        INTERVAL.
!     INTEGER MID
!        INPUT PARAMETER.  THE INDEX OF THE ELEMENT IN X CLOSEST TO
!        ZERO IN VALUE.  THE POINT OUT FROM WHICH THE SUMMING IS
!        DONE.
!     INTEGER N
!        INPUT PARAMETER.  THE LENGTH OF THE ARRAY X.
!     INTEGER NNZW
!        INPUT PARAMETER.  THE NUMBER OF POSITIVELY WEIGHTED VALUES
!        IN X.
!     REAL(KIND=WP) :: SUMW
!        OUTPUT PARAMETER.  THE SUM OF THE WEIGHTS WT(I).
!     REAL(KIND=WP) :: SUMWA
!        OUTPUT PARAMETER.  THE WEIGHTED SUM OF THE ABSOLUTE
!        VALUES OF THE ELEMENTS OF X.
!     REAL(KIND=WP) :: SUMW1
!        OUTPUT PARAMETER.  THE WEIGHTED SUM OF THE ELEMENTS OF
!        X.
!     REAL(KIND=WP) :: SUMW2
!        OUTPUT PARAMETER.  THE WEIGHTED SUM OF THE SQUARES OF
!        THE ELEMENTS OF X.
!     REAL(KIND=WP) :: SUM1
!        OUTPUT PARAMETER.  THE UNWEIGHTED SUM OF THE ELEMENTS OF X.
!     REAL(KIND=WP) :: W(N)
!        INPUT PARAMETER.  THE ARRAY OF WEIGHTS.
!     REAL(KIND=WP) :: X(N)
!        INPUT PARAMETER.  THE DATA ARRAY X IN WHICH THE SUMS ARE TAKEN.
!     REAL(KIND=WP) :: XMEAN
!        OUTPUT PARAMETER.  THE UNWEIGHTED MEAN OF X.
!     REAL(KIND=WP) :: XMEANW
!        OUTPUT PARAMETER.  THE WEIGHTED MEAN OF X.
!
!     INITIALIZE SUMMATION VARIABLES.
!
      sum1 = 0.0_wp
      sumw1 = 0.0_wp
      sumw2 = 0.0_wp
      sumwa = 0.0_wp
      sumw = 0.0_wp
      if (w(mid).eq.0.0_wp) go to 10
      sum1 = x(mid)
      sumw1 = w(mid)*x(mid)
      sumw2 = w(mid)*x(mid)*x(mid)
      sumwa = w(mid)*abs(x(mid))
      sumw = w(mid)
   10 curlo = mid - 1
      curhi = mid + 1
!
!     SUM OUTWARDS, FROM THE VALUE NEAREST ZERO.  THAT IS, FROM
!     THE LEAST IN MAGNITUDE TO THE GREATEST.
!
   20 if (curhi.gt.hi .or. curlo.lt.lo) go to 60
      if (abs(x(curhi)).lt.abs(x(curlo))) go to 40
      if (w(curlo).eq.0.0_wp) go to 30
      sum1 = sum1 + x(curlo)
      sumw1 = sumw1 + w(curlo)*x(curlo)
      sumw2 = sumw2 + w(curlo)*x(curlo)*x(curlo)
      sumwa = sumwa + w(curlo)*abs(x(curlo))
      sumw = sumw + w(curlo)
   30 curlo = curlo - 1
      go to 20
   40 if (w(curhi).eq.0.0_wp) go to 50
      sum1 = sum1 + x(curhi)
      sumw1 = sumw1 + w(curhi)*x(curhi)
      sumw2 = sumw2 + w(curhi)*x(curhi)*x(curhi)
      sumwa = sumwa + w(curhi)*abs(x(curhi))
      sumw = sumw + w(curhi)
   50 curhi = curhi + 1
      go to 20
!
!     SUM UP ANY VALUES WHICH MAY REMAIN BECAUSE OF AN OFF
!     CENTER ZERO POINT.
!
   60 if (curhi.gt.hi) go to 80
      do 70 i=curhi,hi
         if (w(i).eq.0.0_wp) go to 70
         sum1 = sum1 + x(i)
         sumw1 = sumw1 + w(i)*x(i)
         sumw2 = sumw2 + w(i)*x(i)*x(i)
         sumwa = sumwa + w(i)*abs(x(i))
         sumw = sumw + w(i)
   70 continue
   80 if (curlo.lt.lo) go to 100
      do 90 irev=lo,curlo
         i = lo - irev + curlo
         if (w(i).eq.0.0_wp) go to 90
         sum1 = sum1 + x(i)
         sumw1 = sumw1 + w(i)*x(i)
         sumw2 = sumw2 + w(i)*x(i)*x(i)
         sumwa = sumwa + w(i)*abs(x(i))
         sumw = sumw + w(i)
   90 continue
  100 xmean = sum1/nnzw
      xmeanw = sumw1/sumw
      return
      end
