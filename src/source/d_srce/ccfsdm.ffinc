!CCFSDM
      SUBROUTINE CCFSDM (CCOV11, CCOV22, SDRHOC, LAGMAX, NCC, LAGLST, N,
     +   NLPP12, NLPP21, ICCOV, INLPPC)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS IS THE MAIN SUBROUTINE FOR COMPUTING THE STANDARD ERROR
!     OF THE CROSS CORRELATIONS WHEN THERE ARE MISSING VALUES.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 21, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      INTEGER
     +   ICCOV,INLPPC,LAGLST,LAGMAX,N,NCC
!
!  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   CCOV11(ICCOV),CCOV22(ICCOV),SDRHOC(NCC)
      INTEGER
     +   NLPP12(INLPPC),NLPP21(INLPPC)
!
!  LOCAL SCALARS
      DOUBLE PRECISION
     +   DIV,FPLM,SUM
      INTEGER
     +   I,ILAST,K,K0,KM,KP
!
!  EXTERNAL FUNCTIONS
      DOUBLE PRECISION
     +   D1MACH
      EXTERNAL D1MACH
!
!  INTRINSIC FUNCTIONS
      INTRINSIC MIN,SQRT
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     DOUBLE PRECISION CCOV11(ICCOV), CCOV22(ICCOV)
!        THE ARRAYS IN WHICH THE AUTOCOVARIANCES ARE STORED.
!     DOUBLE PRECISION DIV
!        THE SQUARE ROOT OF THE PRODUCT OF THE AUTOCOVARIANCE
!        FUNCTION VALUES AT LAG ZERO.
!     DOUBLE PRECISION FPLM
!        THE FLOATING POINT LARGEST MAGNITUDE.
!     INTEGER I
!        INDEXING VARIABLES.
!     INTEGER ICCOV
!        THE DIMENSION OF THE COVARIANCE VECTORS.
!     INTEGER ILAST
!        THE LAST LAG AT WHICH THE STANDARD ERRORS ARE TO BE COMPUTED.
!     INTEGER INLPPC
!        THE DIMENSION OF THE LAGGED PRODUCT PAIR COUNT VECTORS.
!     INTEGER K
!        INDEXING VARIABLES.
!     INTEGER KM, KP, K0
!        THE LOCATIONS IN THE ARRAYS -RHOC- AND -SDRHOC-
!        OF THE LAG -K, K AND 0, RESPECTIVELY.
!     INTEGER LAGLST
!        THE LAST AUTOCORRELATION COMPUTED BEFORE A MISSING
!        AUTOCORRELATION WAS INCOUNTERED IN EITHER SERIES.
!     INTEGER LAGMAX
!        THE MAXIMUM LAG VALUE USED.
!     INTEGER N
!        THE INTEGER NUMBER OF OBSERVATIONS IN THE SERIES
!     INTEGER NCC
!       THE NUMBER OF CROSS CORRELATIONS COMPUTED.
!     INTEGER NLPP12(INLPPC), NLPP21(INLPPC)
!        THE ARRAYS IN WHICH THE NUMBER OF OBSERVATIONS USED TO
!        COMPUTE EACH CROSS CORRELATION ARE STORED.
!     DOUBLE PRECISION SDRHOC(NCC)
!        THE ARRAY IN WHICH THE STANDARD ERRORS OF THE CROSS CORRELATION
!        ARE STORED
!     DOUBLE PRECISION SUM
!         A SUMMING VARIABLE.
!
      FPLM = D1MACH(2)
!
      DIV = CCOV11(1) * CCOV22(1)
!
      K0 = LAGMAX + 1
!
      SUM = 0.0D0
      DO 10 I = 1, LAGLST
         SUM = SUM + CCOV11(I+1) * CCOV22(I+1)
   10 CONTINUE
      SUM = SUM / DIV
      SDRHOC(K0) = FPLM
      IF (NLPP12(1) .GE. 1) SDRHOC(K0) =
     +   SQRT(N + 2.0D0 * SUM) / NLPP12(1)
!
      DO 30 K = 1, LAGMAX
         SUM = 0.0D0
         ILAST = MIN(LAGLST, N-LAGLST)
         DO 20 I = 1, ILAST
            SUM = SUM + (N-K-I) * CCOV11(I+1) * CCOV22(I+1)
   20    CONTINUE
         SUM = SUM / DIV
         KM = K0 - K
         SDRHOC(KM) = FPLM
         IF (NLPP21(K+1) .GE. 1) SDRHOC(KM) =
     +      SQRT((N - K) + 2.0D0 * SUM) * (N - K)/ (N * NLPP21(K+1))
!
         KP = K0 + K
         SDRHOC(KP) = FPLM
         IF (NLPP12(K+1) .GE. 1) SDRHOC(KP) =
     +      SQRT((N - K) + 2.0D0 * SUM) * (N - K) / (N * NLPP12(K+1))
   30 CONTINUE
!
      RETURN
      END
