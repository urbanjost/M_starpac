!MULTBP
      SUBROUTINE MULTBP(T, LT, C, LC, TEMP, LTEMP, MBO)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE MULTIPLIES TOGETHER TWO DIFFERENCE FACTORS FROM A
!     (BOX-JENKINS) TIME SERIES MODEL.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  AUGUST 1, 1985
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      INTEGER
     +   LC,LT,LTEMP,MBO
!
!  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   C(MBO),T(2*MBO),TEMP(MBO)
!
!  LOCAL SCALARS
      INTEGER
     +   I,J,JI,K
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     DOUBLE PRECISION C(MBO)
!        THE SECOND FACTOR ON INPUT AND THE EXPANDED FACTOR ON OUTPUT.
!     INTEGER I
!        AN INDEX VARIABLE.
!     INTEGER J
!        AN INDEX VARIABLE.
!     INTEGER JI
!        AN INDEX VARIABLE
!     INTEGER K
!        AN INDEX VARIABLE.
!     INTEGER LC
!        THE LARGEST ORDER OF THE SECOND FACTOR ON INPUT, AND
!        THE LARGEST ORDER OF THE EXPANDED FACTOR ON OUTPUT.
!     INTEGER LT
!        THE LARGEST ORDER OF THE FIRST FACTOR.
!     INTEGER LTEMP
!        THE LENGTH OF THE VECTOR TEMP.
!     INTEGER MBO
!        THE MAXIMUM BACK ORDER OPERATOR.
!     DOUBLE PRECISION T(2*MBO)
!        A TEMPORARY WORK VECTOR.
!     DOUBLE PRECISION TEMP(MBO)
!        A TEMPORARY WORK VECTOR
!
      IF (LC .EQ. 0) GO TO 15
      DO 10 J = 1, LC
         TEMP(J) = C(J)
   10 CONTINUE
   15 K = LC + 1
      DO 20 J=K,LTEMP
         TEMP(J) = 0.0D0
   20 CONTINUE
      IF (LT .EQ. 0) GO TO 50
      DO 40 J=1,LT
         TEMP(J) = TEMP(J) + T(J)
         IF (LC .EQ. 0) GO TO 40
         DO 30 I=1,LC
            JI = J + I
            TEMP(JI) = TEMP(JI) - C(I)*T(J)
   30    CONTINUE
   40 CONTINUE
!
   50 DO 60 J=1,LTEMP
         C(J) = TEMP(J)
   60 CONTINUE
      LC = LTEMP
      RETURN
      END
