!CORR
      SUBROUTINE CORR(YM, N, M, IYM, LDSTAK)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS IS THE USER CALLABLE ROUTINE FOR THE CORRELATION FAMILY.
!     IT IS THE SHORT CALL FORM.
!     THIS PROCEDURE CALLS CORER TO CHECK FOR ERRORS IN THE PARAMETER
!     LIST.  IF NO PROBLEMS IT THEN ALLOCATES SPACE IN CSTAK FOR THE
!     VECTORS NEEDED AND CALLS THE MAIN ROUTINE , CORRMN, TO
!     DO THE CORRELATION ANALYSIS.
!
!     WRITTEN BY -
!        LINDA L. MITCHELL
!        STATISTICAL ENGINEERING DIVISION
!        NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  MAY 17, 1982
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      INTEGER
     +   IYM,LDSTAK,M,N
!
!  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   YM(*)
!
!  SCALARS IN COMMON
      INTEGER
     +   IERR
!
!  ARRAYS IN COMMON
      DOUBLE PRECISION DSTAK(12)
!
!  LOCAL SCALARS
      INTEGER
     +   AVG,CILO,CIUP,ICOR,IFP,IPRT,IWRK,LIWRK,LWRK,NALL0,NPRT,PC,
     +   QF,QUAD,RANK,SC,SCINV,SD,SLPC,SLSC,SRCC,T,VCV,WRK
!
!  LOCAL ARRAYS
      DOUBLE PRECISION
     +   RSTAK(12)
      INTEGER
     +   ISTAK(12)
      CHARACTER
     +   NMSUB(6)*1
!
!  EXTERNAL FUNCTIONS
      INTEGER
     +   STKGET,STKST
      EXTERNAL STKGET,STKST
!
!  EXTERNAL SUBROUTINES
      EXTERNAL CORRER,CORRMN,IPRINT,STKCLR,STKSET
!
!  INTRINSIC FUNCTIONS
      INTRINSIC MAX
!
!  COMMON BLOCKS
      COMMON /CSTAK/DSTAK
      COMMON /ERRCHK/IERR
!
!  EQUIVALENCES
      EQUIVALENCE (DSTAK(1),RSTAK(1))
      EQUIVALENCE (DSTAK(1),ISTAK(1))
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     INTEGER AVG
!                  THE STARTING LOCATION IN THE WORK AREA FOR THE
!                  AVERAGES OF YM
!     INTEGER CILO
!                  OCCUPIES THE SAME SPACE AS SLPC AND IS THE LOWER
!                  CONFIDENCE INTERVAL
!     INTEGER CIUP
!                  OCCUPIES THE SAME SPACE AS SLSC AND IS THE UPPER
!                  CONFIDENCE INTERVAL
!     DOUBLE PRECISION DSTAK(12)
!                  THE DOUBLE PRECISION VERSION OF
!                  THE /CSTAK/ WORK AREA.
!     INTEGER ICOR
!                  DETERMINES WHICH SUBROUTINE CALLED CORRMN
!                  IF =1, THEN CALLED BY CORR
!                     =2, THEN CALLED BY CORRS
!     INTEGER IERR
!                  A FLAG RETURNED TO THE USER INDICATING
!                  WHETHER  ERRORS WERE DETECTED IN THE PARAMETER LIST
!                  IF IERR .EQ. 0, NO ERRORS WERE DETECTED
!                  IF IERR .EQ. 1, ERRORS HAVE BEEN DETECTED
!     INTEGER IFP
!                  AN INDICATOR FOR STACK ALLOCATION TYPE, WHERE IFP=3
!                  INDICATES SINGLE PRECISION AND IFP=4 INDICATES
!                  DOUBLE PRECISION.
!     INTEGER IPRT
!                  LOGICAL OUTPUT UNIT
!     INTEGER ISTAK(12)
!                  THE INTEGER VERSION OF THE /CSTAK/ WORK AREA.
!     INTEGER IWRK
!                  THE STARTING LOCATION IN THE WORK AREA FOR THE
!                  INTEGER WORK VECTOR.
!     INTEGER IYM
!                  THE ROW DIMENSION OF YM SPECIFIED IN THE USERS PROGRA
!     INTEGER LDSTAK
!                  SIZE OF WORK AREA ALLOCATED IN THE USERS PROGRAM
!     INTEGER LIWRK
!                  THE LENGTH OF THE INTEGER WORK VECTOR.
!     INTEGER LWRK
!                  THE LENGTH OF THE DOUBLE PRECISION WORK VECTOR.
!     INTEGER M
!                  THE NUMBER OF VARIABLES
!     INTEGER N
!                  THE NUMBER OF OBSERVATIONS
!     INTEGER NALL0
!                  THE NUMBER OF ALLOCATIONS OUTSTANDING AT THE
!                  TIME THAT THIS ROUTINE WAS CALLED.
!     CHARACTER*1 NMSUB(6)
!                  THE SUBROUTINE NAME
!     INTEGER NPRT
!                  THE VARIABLE CONTROLLING THE AUTOMATIC PRINTOUT
!                       NPRT = 0, PRINTOUT IS SUPPRESSED
!                       OTHERWISE THE PRINTOUT IS PROVIDED.
!     INTEGER PC
!                  THE STARTING LOCATION IN THE WORK AREA
!                  OF THE PARTIAL CORRELATION COEFFICIENTS MATRIX
!     INTEGER QF
!                  THE STARTING LOCATION IN THE WORK AREA FOR THE
!                  QUADRATIC FIT
!     INTEGER QUAD
!                  THE STARTING LOCATION IN THE WORK AREA FOR THE
!                  DOUBLE PRECISION VERSION OF THE QUADRATIC FIT
!     INTEGER RANK
!                  THE STARTING LOCATION IN THE WORK AREA FOR THE
!                  MATRIX CONTAINING THE RANKS OF YM
!     DOUBLE PRECISION RSTAK(12)
!                  THE DOUBLE PRECISION VERSION OF THE
!                  /CSTAK/ WORK AREA.
!     INTEGER SC
!                  THE STARTING LOCATION IN THE WORK AREA
!                  OF THE SIMPLE CORRELATION COEFFICIENTS MATRIX
!     INTEGER SCINV
!                  THE STARTING LOCATION IN THE WORK AREA FOR THE
!                  INVERSE MATRIX OF SC
!     INTEGER SD
!                  THE STARTING LOCATION IN THE WORK AREA OF THE
!                  STANDARD DEVIATIONS OF YM
!     INTEGER SLPC
!                  THE STARTING LOCATION IN THE WORK AREA OF THE
!                  SIGNIFICANCE LEVELS OF PC
!     INTEGER SLSC
!                  THE STARTING LOCATION IN THE WORK AREA OF THE
!                  SIGNIFICANCE LEVELS OF SC
!     INTEGER SRCC
!                  THE STARTING LOCATION IN THE WORK AREA OF THE
!                  SPEARMAN RANK COEFFICIENTS
!     INTEGER T
!                  STARTING LOCATION IN THE WORK AREA
!                  FOR THE ARRAY T.
!     INTEGER VCV
!                  THE STARTING LOCATION IN THE WORK AREA OF THE
!                  VARIANCE COVARIANCE MATRIX.
!     INTEGER WRK
!                  THE STARTING LOCATION IN THE WORK AREA FOR
!                  WORK SPACE
!     DOUBLE PRECISION YM(IYM,M)
!                  THE MATRIX IN WHICH THE OBSERVATIONS ARE PASSED
!
!
!     SET UP NAME ARRAY
!
      DATA  NMSUB(1), NMSUB(2), NMSUB(3), NMSUB(4), NMSUB(5), NMSUB(6)
     +   /       'C',      'O',      'R',      'R',      ' ',      ' '/
!
!     SET UP FRAMEWORK VARIABLES FOR NUMBER TYPE
!
      IFP = 4
!
!     SET CALLER FLAG, THIS IS CORR
!
      ICOR = 1
!
!     PRINTOUT IS AUTOMATICALY PROVIDED
!
      NPRT = 1
!
!     CHECK FOR ERRORS IN PARAMETERS
!
      CALL CORRER(NMSUB, M, N, IYM, M, LDSTAK, ICOR, NPRT)
!
!     IF THERE IS AN ERROR RETURN TO THE CALLERS ROUTINE
!
      IF (IERR.EQ.0) GO TO 10
!
!     PRINT CORRECT FORM OF CALL STATEMENT AND RETURN TO CALLER
!
      CALL IPRINT(IPRT)
      WRITE (IPRT,1000)
      RETURN
!
!     INITIALIZE THE STACK
!
   10 CALL STKSET (LDSTAK, 4)
      NALL0 = STKST(1)
!
!     ALLOCATE SPACE IN CSTAK FOR MATRICES.
!
      LIWRK = MAX(N,M)
      LWRK = LIWRK
!
      VCV = STKGET(M*M,IFP)
      IWRK = STKGET(LIWRK,2)
      WRK = STKGET(LWRK,IFP)
!
      T = STKGET(M,IFP)
      RANK = STKGET(N*M,IFP)
      SCINV = STKGET(M*M,IFP)
      SLSC = STKGET(M*M,IFP)
      SLPC = STKGET(M*M,IFP)
      SC = STKGET(M*M,IFP)
      PC = STKGET(M*M,IFP)
      QUAD = STKGET(3*N,IFP)
      QF = STKGET(M*M,IFP)
!
!     THE FOLLOWING VARIABLES HAVE BEEN INDIRECTLY EQUIVALENCED-
!
      CIUP = SLSC
      CILO = SLPC
      AVG = WRK
      SD = WRK
      SRCC = SCINV
!
!     CALL THE ROUTINE TO DO THE COMPUTATIONS
!
      CALL CORRMN(YM, N, M, IYM, RSTAK(AVG), RSTAK(SD), RSTAK(T),
     +   RSTAK(RANK), RSTAK(SC), RSTAK(PC), RSTAK(SCINV), ISTAK(IWRK),
     +   RSTAK(WRK), RSTAK(SLSC), RSTAK(SLPC), RSTAK(SRCC),
     +   RSTAK(QUAD), RSTAK(CIUP), RSTAK(CILO), RSTAK(QF),
     +   NPRT, RSTAK(VCV), M, LIWRK, LWRK)
!
!     RETURN AREA TO THE WORK ARRAY
!
      CALL STKCLR(NALL0)
!
      RETURN
!
!     FORMAT STATEMENT
!
 1000 FORMAT (/42H THE CORRECT FORM OF THE CALL STATEMENT IS//
     +   '       CALL CORR (YM, N, M, IYM, LDSTAK)')
      END
