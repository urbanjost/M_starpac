!ACCDIG
      SUBROUTINE ACCDIG(AX, X, AD, N)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     RETURNS NUMBER OF ACCURATE DIGITS, AD, IN AX AN APPROXIMATION TO X
!
!     WRITTEN BY DAVID HOGBEN, SEL, NBS.   10/29/69.
!
!     THIS ROUTINE WAS ADAPTED FROM THE OMNITAB ROUTINE ACCDIG
!     BY - -
!
!     JANET R. DONALDSON
!     STATISTICAL ENGINEERING DIVISION
!     NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  DECEMBER 29, 1982
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      INTEGER
     +   N
!
!  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   AD(*),AX(*),X(*)
!
!  LOCAL SCALARS
      DOUBLE PRECISION
     +   ADMAX,DIFF,FPLRS
      INTEGER
     +   I
!
!  EXTERNAL FUNCTIONS
      DOUBLE PRECISION
     +   D1MACH
      EXTERNAL D1MACH
!
!  INTRINSIC FUNCTIONS
      INTRINSIC ABS,LOG10,MAX,MIN
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     DOUBLE PRECISION AD(N)
!        THE NUMBER OF ACCURATE DIGITS.
!     DOUBLE PRECISION ADMAX
!        THE NUMBER OF DIGITS CARRIED IN A FLOATING POINT COMPUTATION.
!     DOUBLE PRECISION AX(N)
!        THE APPROXIMATION TO THE SOLUTION.
!     DOUBLE PRECISION DIFF
!        *
!     DOUBLE PRECISION FPLRS
!        THE FLOATING POINT LARGEST RELATIVE SPACING.
!     INTEGER I
!        AN INDEX.
!     INTEGER N
!        THE NUMBER OF ELEMENTS BEING COMPARED.
!     DOUBLE PRECISION X(N)
!        THE SOLUTION.
!
!
      FPLRS = D1MACH(4)
!
      ADMAX = -LOG10(FPLRS)
!
      DO 20 I=1,N
         DIFF = AX(I) - X(I)
         IF (DIFF.NE.0.0D0) GO TO 10
         AD(I) = ADMAX
         GO TO 20
   10    AD(I) = 0.0D0
         IF (ABS(X(I)).GT.0.0D0) AD(I) = LOG10(ABS(X(I)))
         IF (ABS(DIFF).GT.0.0D0) AD(I) = -LOG10(ABS(DIFF)) + AD(I)
         AD(I) = MIN(ADMAX,AD(I))
         AD(I) = MAX(-ADMAX,AD(I))
   20 CONTINUE
      RETURN
      END
