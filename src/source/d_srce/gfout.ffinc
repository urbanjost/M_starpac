!GFOUT
      SUBROUTINE GFOUT (XORD, YORD, NORD, FREQ, PHAS, NF,
     +   NTERM, SYM, XPLTMN, XPLTMX, YPLTMN, YPLTMX, NPRT, IGFERR,
     +   NMSUB)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE PRODUCES THE GAIN FUNCTION PLOTS.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 26, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) ::
     +   XPLTMN,XPLTMX,YPLTMN,YPLTMX
      INTEGER
     +   IGFERR,NF,NORD,NPRT,NTERM
      LOGICAL
     +   SYM
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     +   FREQ(NF),PHAS(NF),XORD(NORD),YORD(NORD)
      CHARACTER
     +   NMSUB(6)*1
!
!  SCALARS IN COMMON
      INTEGER
     +   IERR
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     +   PI,XMN,XMX,YMN,YMX
      INTEGER
     +   ILOG,IPRT
      LOGICAL
     +   ERROR
!
!  LOCAL ARRAYS
      INTEGER
     +   ISYM(1)
!
!  EXTERNAL SUBROUTINES
      EXTERNAL GETPI,IPRINT,PPLMT,PPMN,VERSP
!
!  COMMON BLOCKS
      COMMON /ERRCHK/IERR
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     LOGICAL ERROR
!        AN ERROR FLAG.
!     REAL(KIND=WP) :: FREQ(NF)
!        THE VECTOR OF FREQUENCIES AT WHICH THE GAIN FUNCTION IS TO
!        BE ESTIMATED.
!     INTEGER IERR
!        THE ERROR FLAG.
!     INTEGER IGFERR
!        AN ERROR FLAG INDICATING WHETHER COMPUTATIONS SEEM
!        TO HAVE PRODUCED REASONABLE RESULTS.
!     INTEGER ILOG
!        THE VARIABLE CONTROLLING THE PLOT AXIS TYPE (LOG OR LINEAR)
!     INTEGER IPRT
!        THE UNIT NUMBER FOR THE OUTPUT.
!     INTEGER ISYM(1)
!        A DUMMY ARRAY FOR THE CALL TO PPMN.
!     INTEGER NF
!        THE NUMBER OF FREQUENCIES FOR WHICH THE GAIN FUNCTION ESTIMATES
!        ARE TO BE COMPUTED.
!     CHARACTER*1 NMSUB(6)
!        THE NAME OF THE CALLING SUBROUTINE.
!     INTEGER NORD
!        THE NUMBER OF ORDINATES TO BE PLOTTED.
!     INTEGER NPRT
!        A CODE USED TO SPECIFY THE TYPE OF PLOT, WHERE IF
!        NPRT < 0 THE PLOT IS DECIBELS/LINEAR
!        NPRT = 0 THE PLOT IS SUPPRESSED
!        NPRT > 0 THE PLOT IS LOG/LINEAR
!     INTEGER NTERM
!        THE NUMBER OF TERMS IN THE FILTER FOR WHICH THE GAIN
!        FUNCTION WAS COMPUTED.
!     REAL(KIND=WP) :: PHAS(NF)
!        THE ARRAY IN WHICH THE PHASE OF THE FILTER IS RETURNED.
!     REAL(KIND=WP) :: PI
!        THE VALUE OF PI.
!     LOGICAL SYM
!        AN INDICATOR VARIABLE USED TO DETERMINE IF THE FILTER WAS
!        SYMMETRIC OR NOT.
!     REAL(KIND=WP) :: XMN, XMX
!        ...
!     REAL(KIND=WP) :: XORD(NORD)
!        THE X COORDINATES FOR THE GAIN FUNCTION PLOT
!     REAL(KIND=WP) :: XPLTMN, XPLTMX
!        THE MINIMUM AND MAXIMUM VALUES TO BE PLOTTED FOR THE X AXIS.
!     REAL(KIND=WP) :: YMN, YMX
!        ...
!     REAL(KIND=WP) :: YORD(NORD)
!        THE Y COORDINATES FOR THE GAIN FUNCTION PLOT
!     REAL(KIND=WP) :: YPLTMN, YPLTMX
!        THE MINIMUM AND MAXIMUM VALUES TO BE PLOTTED FOR THE Y AXIS.
!
!     SET UNIT NUMBER FOR OUTPUT AND SET OUTPUT WIDTH.
!
      CALL IPRINT (IPRT)
!
      CALL VERSP(.TRUE.)
!
      IF (SYM) WRITE (IPRT, 1000) NTERM
      IF (.NOT. SYM) WRITE (IPRT, 1003) NTERM
!
      IF (IGFERR .EQ. 0) GO TO 5
      WRITE (IPRT, 1004)
      RETURN
!
    5 CONTINUE
!
      IF (NPRT.GE.1) THEN
        ILOG = 1
      ELSE
        ILOG = 0
      END IF
!
!     DETERMINE THE BOUNDS FOR THE X AND Y AXIS AND COMPLETE THE
!     ERROR CHECKING
!
      CALL PPLMT (YORD, YORD, XORD, XORD, NORD, 1, NORD,
     +   YPLTMN, YPLTMX, YMN, YMX, XPLTMN, XPLTMX, XMN, XMX,
     +   ERROR, NMSUB, .FALSE.)
      IF (ERROR) THEN
        IERR = 1
        RETURN
      ELSE
        CALL PPMN (YORD, YORD, XORD, XORD, NORD, 1, NORD, 0, ISYM, 1, 0,
     +   0, YMN, YMX, XMN, XMX, .FALSE., ILOG)
      END IF
!
      IF (XPLTMN .NE. 0.0E0_WP .OR. XPLTMX .NE. 0.5D0_WP) GO TO 10
!
      WRITE (IPRT, 1002)
!
   10 IF (SYM) RETURN
!
      WRITE (IPRT, 1006)
!
      CALL VERSP(.TRUE.)
!
!     PRINT PHASE PLOT FOR AUTOREGRESSIVE FILTER
!
      CALL GETPI(PI)
!
      WRITE (IPRT, 1005) NTERM
!
      CALL PPLMT (PHAS, PHAS, FREQ, FREQ, NORD, 1, NORD,
     +   -PI, PI, YMN, YMX, XPLTMN, XPLTMX, XMN, XMX,
     +   ERROR, NMSUB, .FALSE.)
      IF (ERROR) THEN
        IERR = 1
        RETURN
      ELSE
        CALL PPMN (PHAS, PHAS, FREQ, FREQ, NORD, 1, NORD, 0, ISYM, 1, 0,
     +   0, YMN, YMX, XMN, XMX, .FALSE., 0)
      END IF
!
      IF (XPLTMN .NE. 0.0E0_WP .OR. XPLTMX .NE. 0.5D0_WP) RETURN
!
      WRITE (IPRT, 1002)
!
      RETURN
!
!     FORMAT STATEMENTS
!
 1000 FORMAT (18H GAIN FUNCTION OF , I3, 15H TERM SYMMETRIC,
     +   14H LINEAR FILTER)
 1002 FORMAT(5H+FREQ/
     +   7H PERIOD, 9X, 3HINF, 7X, 3H20., 7X, 3H10., 8X, 6H6.6667, 4X,
     +   2H5., 8X, 2H4., 8X, 6H3.3333, 4X, 6H2.8571, 4X, 3H2.5, 7X,
     +   6H2.2222, 4X, 2H2.)
 1003 FORMAT (18H GAIN FUNCTION OF , I3, 21H TERM AUTOREGRESSIVE,,
     +   22H OR DIFFERENCE, FILTER)
 1004 FORMAT (//51H THE PLOT HAS BEEN SUPRESSED BECAUSE FEWER THAN TWO/
     +   45H NON ZERO GAIN FUNCTION VALUES WERE COMPUTED.)
 1005 FORMAT (19H PHASE FUNCTION OF , I3, 21H TERM AUTOREGRESSIVE,,
     +   22H OR DIFFERENCE, FILTER)
 1006 FORMAT ('1')
      END
