!SPPLTD
     subroutine sppltd (spcmn, spcmx, alow, aup, ypltmn, ypltmx,&
     &   cilow, cimid, ciup, ymax)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE SETS VARIOUS Y AXIS LIMITS FOR DECIBLE
!     SPECTRUM PLOTS.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  DECEMBER 7, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     real(kind=wp) ::&
     &   alow,aup,cilow,cimid,ciup,spcmn,spcmx,ymax,ypltmn,ypltmx
!
!  LOCAL SCALARS
     real(kind=wp) ::&
     &   rngmn,ymin
!
!  INTRINSIC FUNCTIONS
      intrinsic abs,log10
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL(KIND=WP) :: ALOW, AUP
!        FACTORS USED TO COMPUTE THE CONFIDENCE INTERVALS.
!     REAL(KIND=WP) :: CILOW, CIMID, CIUP
!        THE Y CORDNATES FOR THE LOWER  MID AND UPPER CONFIDENCE
!        INTERVAL POINTS.
!     REAL(KIND=WP) :: RNGMN
!        THE MINIMUM Y AXIS RANGE FOR THE PLOT.
!     REAL(KIND=WP) :: SPCMN, SPCMX
!        THE MINIMUM AND MAXIMUM SPECTRAL VALUE TO BE PLOTTED.
!     REAL(KIND=WP) :: YMAX, YMIN
!        THE MAXIMUM AND MINIMUM ACTUAL SPECTRAL VALUE
!        (IN DECIBELS) TO BE PLOTTED.
!     REAL(KIND=WP) :: YPLTMN, YPLTMX
!        THE MINIMUM AND MAXIMUM VAUES TO BE PLOTTED FOR THE Y AXIS.
!
!     SET CO-ORDINATES FOR DECIBLE PLOTS
!
      ymax = 10.0_wp * log10(spcmx)
      ymin = 10.0_wp * log10(spcmn) - ymax
!
      ypltmx = 0.0_wp
      rngmn = 20.0_wp * (log10(aup) - log10(alow))
      if (abs(ymin) .lt. rngmn) ypltmx = (rngmn + ymin) * 0.5_wp
      ypltmn = ymin - ypltmx
      ciup = ypltmx
      cimid = ciup - 10.0_wp * log10(aup)
      cilow = cimid + 10.0_wp * log10(alow)
!
      return
      end
