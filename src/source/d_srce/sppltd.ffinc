!SPPLTD
      SUBROUTINE SPPLTD (SPCMN, SPCMX, ALOW, AUP, YPLTMN, YPLTMX,
     +   CILOW, CIMID, CIUP, YMAX)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE SETS VARIOUS Y AXIS LIMITS FOR DECIBLE
!     SPECTRUM PLOTS.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  DECEMBER 7, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      DOUBLE PRECISION
     +   ALOW,AUP,CILOW,CIMID,CIUP,SPCMN,SPCMX,YMAX,YPLTMN,YPLTMX
!
!  LOCAL SCALARS
      DOUBLE PRECISION
     +   RNGMN,YMIN
!
!  INTRINSIC FUNCTIONS
      INTRINSIC ABS,LOG10
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     DOUBLE PRECISION ALOW, AUP
!        FACTORS USED TO COMPUTE THE CONFIDENCE INTERVALS.
!     DOUBLE PRECISION CILOW, CIMID, CIUP
!        THE Y CORDNATES FOR THE LOWER  MID AND UPPER CONFIDENCE
!        INTERVAL POINTS.
!     DOUBLE PRECISION RNGMN
!        THE MINIMUM Y AXIS RANGE FOR THE PLOT.
!     DOUBLE PRECISION SPCMN, SPCMX
!        THE MINIMUM AND MAXIMUM SPECTRAL VALUE TO BE PLOTTED.
!     DOUBLE PRECISION YMAX, YMIN
!        THE MAXIMUM AND MINIMUM ACTUAL SPECTRAL VALUE
!        (IN DECIBELS) TO BE PLOTTED.
!     DOUBLE PRECISION YPLTMN, YPLTMX
!        THE MINIMUM AND MAXIMUM VAUES TO BE PLOTTED FOR THE Y AXIS.
!
!     SET CO-ORDINATES FOR DECIBLE PLOTS
!
      YMAX = 10.0D0 * LOG10(SPCMX)
      YMIN = 10.0D0 * LOG10(SPCMN) - YMAX
!
      YPLTMX = 0.0D0
      RNGMN = 20.0D0 * (LOG10(AUP) - LOG10(ALOW))
      IF (ABS(YMIN) .LT. RNGMN) YPLTMX = (RNGMN + YMIN) * 0.5D0
      YPLTMN = YMIN - YPLTMX
      CIUP = YPLTMX
      CIMID = CIUP - 10.0D0 * LOG10(AUP)
      CILOW = CIMID + 10.0D0 * LOG10(ALOW)
!
      RETURN
      END
