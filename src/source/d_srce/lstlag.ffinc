!LSTLAG
      INTEGER FUNCTION LSTLAG (NLPPA, LAGMAX, LACOV)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE FINDS THE LAG VALUE OF THE LAST AUTOCOVARIANCE
!     COMPUTED BEFORE ONE COULD NOT BE COMPUTED DUE TO MISSING DATA.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  DECEMBER 7, 1981
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      INTEGER
     +   LACOV,LAGMAX
!
!  ARRAY ARGUMENTS
      INTEGER
     +   NLPPA(LACOV)
!
!  LOCAL SCALARS
      INTEGER
     +   LAG
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     INTEGER LACOV
!        THE LENGTH OF THE VECTOR ACOV.
!     INTEGER LAG
!        THE INDEXING VARIABLE INDICATING THE LAG VALUE OF THE
!        AUTOCORRELATION.
!     INTEGER NLPPA(LACOV)
!        THE ARRAY CONTAINING THE NUMBERS OF LAGGED PRODUCT PAIRS
!        USED TO COMPUTE THE ACVF AT EACH LAG.
!
!     FIND THE LAST AUTOCORRELATION TO BE COMPUTED BEFORE
!     ONE COULD NOT BE COMPUTED DUE TO MISSING DATA
!
      LSTLAG = -1
      IF (NLPPA(1) .LE. 0) RETURN
      DO 20 LAG = 1, LAGMAX
         IF (NLPPA(LAG + 1) .GE. 1) GO TO 20
         LSTLAG = LAG - 1
         RETURN
   20 CONTINUE
      LSTLAG = LAGMAX
      RETURN
      END
