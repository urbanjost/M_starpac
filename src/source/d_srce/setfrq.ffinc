!SETFRQ
      SUBROUTINE SETFRQ (FREQ, NF, NPRT, FMIN, FMAX, H)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE COMPUTES THE FREQUENCIES AT WHICH THE
!     SPECTRUM IS TO BE ESTIMATED.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 26, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      DOUBLE PRECISION
     +   FMAX,FMIN,H
      INTEGER
     +   NF,NPRT
!
!  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   FREQ(NF)
!
!  LOCAL SCALARS
      DOUBLE PRECISION
     +   DELTAF
      INTEGER
     +   I
!
!  INTRINSIC FUNCTIONS
      INTRINSIC LOG10
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     DOUBLE PRECISION DELTAF
!        THE FREQUENCY INCREMENT.
!     DOUBLE PRECISION FMAX, FMIN
!        THE MAXIMUM AND MINIMUM FREQUENCIES AT WHICH THE
!        SPECTRUM IS TO BE ESTIMATED.
!     DOUBLE PRECISION FREQ(NF)
!        THE VECTOR OF FREQUENCIES AT WHICH THE SPECTRUM IS TO BE
!        ESTIMATED.
!     DOUBLE PRECISION H
!        THE SAMPLING INTERVAL.
!     INTEGER I
!        AN INDEXING VARIABLE.
!     INTEGER NF
!        THE NUMBER OF FREQUENCIES FOR WHICH THE SPECTRAL ESTIMATES
!        ARE TO BE ESTIMATED.
!     INTEGER NPRT
!        A CODE USED TO SPECIFY THE TYPE OF PLOT, WHERE IF
!        NPRT EQUALS 2 THE FREQUENCY SCALE IS LINEAR, AND IF
!        NPRT EQUALS 3 THE FREQUENCY SCALE IS LOG.
!
      IF (NPRT .EQ. 3) GO TO 20
!
!     COMPUTE FREQUENCY VALUES FOR LINEAR SCALE
!
      FREQ(1) = FMIN
!
      IF (NF .EQ. 1) RETURN
!
      DELTAF = (FMAX - FMIN) / (H * (NF - 1))
      DO 10 I = 2, NF
         FREQ(I) = FREQ(I-1) + DELTAF
   10 CONTINUE
!
      FREQ(NF) = FMAX
      RETURN
!
   20 CONTINUE
!
!     COMPUTE FREQUENCY VALUES FOR LOG SCALE
!
      DELTAF = (LOG10(FMAX) - LOG10(FMIN)) / (H * (NF - 1))
!
      FREQ(1) = FMIN
!
      IF (NF .EQ. 1) RETURN
!
      DO 30 I = 2, NF
         FREQ(I) = 10.0D0**(LOG10(FREQ(I-1)) + DELTAF)
   30 CONTINUE
!
      FREQ(NF) = FMAX
!
!
      RETURN
      END
