!STAT1W
      SUBROUTINE STAT1W(Y, WT, N, YMED, YMIN, YMAX, YMIDRG, YRANGE,
     &   NCELLS, YLB, YUB, YDISTR, NNZW)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS COMPUTES STATISTICS FOR A VECTOR Y THAT REQUIRE OR CAN
!     MAKE USE OF SORTEDNESS IN THE VECTOR.  WEIGHTS ARE USED.
!
!     THIS SUBROUTINE IS BASED ON A MODIFICATION OF THE STATIS
!     CODE USED IN OMNITAB, VERSION 5 (6/16/72), WRITTED BY
!     SALLY PEAVY.  THE ORIGINAL ADAPTATION TO STARPAC WAS MADE
!     BY JANET DONALDSON.
!
!     WRITTEN BY - JOHN E. KOONTZ
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  MAY 17, 1982
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) ::
     &   YLB,YMAX,YMED,YMIDRG,YMIN,YRANGE,YUB
      INTEGER
     &   N,NCELLS,NNZW
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   WT(N),Y(N),YDISTR(NCELLS)
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     &   DELY,YT
      INTEGER
     &   I,IC,IC1,KK,L,M,M1,MAX,MAXY,MINY,N2
!
!  INTRINSIC FUNCTIONS
      INTRINSIC MOD
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL(KIND=WP) :: DELY
!        THE WIDTH OF AN INTERVAL ONE TENTH THE RANGE OF THE
!        DATA IN Y.
!     INTEGER I
!        A LOOP INDEX.
!     INTEGER IC
!        PREVIOUS SIGN IN RUNS CALCULATION.
!     INTEGER IC1
!        COUNT IN FREQUENCY DISTRIBUTION CALCULATIONS.
!     INTEGER KK
!        A BACKWARDS, BOULDER, COLORADO LOOP INDEX.
!     INTEGER L
!        A LOOP INDEX.
!     INTEGER M
!        A LOOP INDEX.
!     INTEGER MAX
!        INDEX OF A MAXIMUM ELEMENT.
!     INTEGER MAXY
!        LAST ELEMENT OF Y, THE LARGEST ONE.
!     INTEGER MINY
!        FIRST ELEMENT OF Y, THE LEAST ONE.
!     INTEGER M1
!        ...
!     INTEGER N
!        INPUT PARAMETER.  THE LENGTH OF Y AND WT.
!     INTEGER NCELLS
!        THE NUMBER OF CELLS IN THE FREQUENCY DISTRIBUTION.
!     INTEGER NNZW
!        INPUT PARAMETER.  THE NUMBER OF POSITIVE ELEMENTS IN WT.
!     INTEGER N2
!        (NNZW + 1)/2
!     REAL(KIND=WP) :: WT(N)
!        INPUT PARAMETER.  THE VECTOR OF WEIGHTS FOR THE
!        Y OBSERVATIONS.
!     REAL(KIND=WP) :: Y(N)
!        INPUT PARAMETER.  A SET OF N DATA POINTS, SORTED
!        INTO ASCENDING ORDER.
!     REAL(KIND=WP) :: YDISTR(NCELLS)
!        OUTPUT PARAMETER.  THE NUMBERS OF Y VALUES WITH
!        POSITIVE WEIGHTS IN EACH OF TEN EQUAL LENGTH INTERVALS.
!        THAT DIVIDE THE RANGE OF Y VALUES.
!     REAL(KIND=WP) :: YLB
!        THE LOWER BOUND FOR THE FREQUENCY DISTRIBUTION.
!        IF YLB = YUB, THE MINIMUM OBSERVATION WILL BE USED.
!     REAL(KIND=WP) :: YMAX
!        OUTPUT PARAMETER.  THE MAXIMUM OF THE OBSERVATIONS Y HAVING
!        POSITIVE WEIGHT.
!     REAL(KIND=WP) :: YMED
!        OUTPUT PARAMETER.  THE MEDIAN OF THE OBSERVATIONS Y.
!     REAL(KIND=WP) :: YMIDRG
!        OUTPUT PARAMETER.  THE MIDRANGE OF THE OBSERVATIONS Y.
!     REAL(KIND=WP) :: YMIN
!        OUTPUT PARAMETER.  THE MINIMUM OF THE OBSERVATIONS Y HAVING
!        POSITIVE WEIGHT.
!     REAL(KIND=WP) :: YRANGE
!        OUTPUT PARAMETER.  THE RANGE OF THE OBSERVATIONS Y.
!     REAL(KIND=WP) :: YT
!        THE MAXIMUM VALUE IN EACH INTERVAL IN THE FREQUENCY
!        DISTRIBUTIONS CALCULATIONS.
!     REAL(KIND=WP) :: YUB
!        THE UPPER BOUND FOR THE FREQUENCY DISTRIBUTION.
!        IF YLB = YUB, THE MAXIMUM OBSERVATION WILL BE USED.
!
!     CALCULATE THE MEDIAN.
!
      N2 = (NNZW+1)/2
      M = 1
      DO 10 I=1,N
         IF (I.GT.N2) GO TO 20
         M = I
         IF (WT(M).LE.0.0_WP) N2 = N2 + 1
   10 CONTINUE
   20 M1 = M
      IF (MOD(NNZW,2).NE.0) GO TO 40
      DO 30 M1=M,N
         IF (WT(M1).GT.0.0_WP) GO TO 40
   30 CONTINUE
   40 YMED = (Y(I)+Y(M1))/2.0_WP
!
!     CALCULATE THE MIDRANGE, RANGE, MINIMUM, AND MAXIMUM.
!
      MAX = N - NNZW + 1
      DO 50 I=1,MAX
         MINY = I
         IF (WT(MINY).GT.0.0_WP) GO TO 60
   50 CONTINUE
   60 DO 70 I=1,MAX
         KK = N + 1 - I
         MAXY = KK
         IF (WT(MAXY).GT.0.0_WP) GO TO 80
   70 CONTINUE
   80 YMIDRG = (Y(MINY)+Y(MAXY))/2.0_WP
      YRANGE = Y(MAXY) - Y(MINY)
      YMIN = Y(MINY)
      YMAX = Y(MAXY)
!
!     COMPUTE FREQUENCY DISTRIBUTION
!
      DELY = YRANGE
      YT = YMIN
      IF (YLB.GE.YUB) GO TO 5
         DELY = YUB - YLB
         YT = YLB
    5 CONTINUE
      DELY = DELY / NCELLS
      YT = YT + DELY
      L = 0
      IC1 = 0
      DO 110 I=2,NCELLS
         IC = 0
   90    L = L + 1
         IF (L.GT.N) GO TO 100
         IF (WT(L).LE.0.0_WP) GO TO 90
         IF (Y(L).GT.YT) GO TO 100
         IC = IC + 1
         IC1 = IC1 + 1
         GO TO 90
  100    YDISTR(I-1) = IC
         L = L - 1
         YT = YT + DELY
  110 CONTINUE
      YDISTR(NCELLS) = NNZW - IC1
      RETURN
      END
