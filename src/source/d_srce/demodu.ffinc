!DEMODU
      SUBROUTINE DEMODU (Y, N, FD, AMPL, PHAS)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS SUBROUTINE DEMODULATES THE SERIES Y AT FREQUENCY
!     FD.  THE REAL AND IMAGINARY PARTS OF THE DEMODULATED
!     SERIES ARE RETURNED IN AMPL AND PHAS, RESPECTIVELY.
!
!     WRITTEN BY  -  PETER BLOOMFIELD
!                    FOURIER ANALYSIS OF TIME SERIES- AN
!                       INTRODUCTION
!                    JOHN WILEY AND SONS, NEW YORK, 1976
!                    PAGE 148
!     ADAPTED FOR STARPAC BY  -  JANET R. DONALDSON
!                                STATISTICAL ENGINEERING DEVISION
!                                NATIONAL BUREAU OF STANDARDS
!                                BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 26, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      DOUBLE PRECISION
     +   FD
      INTEGER
     +   N
!
!  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   AMPL(N),PHAS(N),Y(N)
!
!  LOCAL SCALARS
      DOUBLE PRECISION
     +   ARG,PI
      INTEGER
     +   I
!
!  EXTERNAL SUBROUTINES
      EXTERNAL GETPI
!
!  INTRINSIC FUNCTIONS
      INTRINSIC COS,SIN
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     DOUBLE PRECISION AMPL(N)
!        THE ARRAY IN WHICH THE AMPLITUDES ARE STORED.
!     DOUBLE PRECISION ARG
!        A VALUE USED IN COMPUTING THE DEMODULATED SERIES.
!     DOUBLE PRECISION FD
!        THE DEMODULATION FREQUENCY.
!     INTEGER I
!        AN INDEXING VARIABLE.
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS IN THE SERIES BEING DEMODULATED.
!     DOUBLE PRECISION PHAS(N)
!        THE ARRAYS CONTAINING THE PRIMARY PHASE ESTIMATES.
!     DOUBLE PRECISION PI
!        THE VALUE OF PI.
!     DOUBLE PRECISION Y(N)
!        THE INPUT ARRAY ARRAY CONTAINING THE OBSERVED SERIES.
!
      CALL GETPI(PI)
!
      DO 10 I = 1, N
         IF (FD.EQ.0.25D0) THEN
            AMPL(I) = 0.0D0
            PHAS(I) = -2.0D0*Y(I)
         ELSE IF (FD.EQ.0.5D0) THEN
            AMPL(I) = -2.0D0*Y(I)
            PHAS(I) = 0.0D0
         ELSE
            ARG = (I-1) * FD * 2.0D0 * PI
            AMPL(I) = Y(I)*COS(ARG)*2.0D0
            PHAS(I) = -Y(I)*SIN(ARG)*2.0D0
         END IF
   10 CONTINUE
      RETURN
      END
