!DEMODU
      SUBROUTINE DEMODU (Y, N, FD, AMPL, PHAS)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS SUBROUTINE DEMODULATES THE SERIES Y AT FREQUENCY
!     FD.  THE REAL AND IMAGINARY PARTS OF THE DEMODULATED
!     SERIES ARE RETURNED IN AMPL AND PHAS, RESPECTIVELY.
!
!     WRITTEN BY  -  PETER BLOOMFIELD
!                    FOURIER ANALYSIS OF TIME SERIES- AN
!                       INTRODUCTION
!                    JOHN WILEY AND SONS, NEW YORK, 1976
!                    PAGE 148
!     ADAPTED FOR STARPAC BY  -  JANET R. DONALDSON
!                                STATISTICAL ENGINEERING DEVISION
!                                NATIONAL BUREAU OF STANDARDS
!                                BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 26, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) ::
     &   FD
      INTEGER
     &   N
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   AMPL(N),PHAS(N),Y(N)
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     &   ARG,PI
      INTEGER
     &   I
!
!  EXTERNAL SUBROUTINES
      EXTERNAL GETPI
!
!  INTRINSIC FUNCTIONS
      INTRINSIC COS,SIN
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL(KIND=WP) :: AMPL(N)
!        THE ARRAY IN WHICH THE AMPLITUDES ARE STORED.
!     REAL(KIND=WP) :: ARG
!        A VALUE USED IN COMPUTING THE DEMODULATED SERIES.
!     REAL(KIND=WP) :: FD
!        THE DEMODULATION FREQUENCY.
!     INTEGER I
!        AN INDEXING VARIABLE.
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS IN THE SERIES BEING DEMODULATED.
!     REAL(KIND=WP) :: PHAS(N)
!        THE ARRAYS CONTAINING THE PRIMARY PHASE ESTIMATES.
!     REAL(KIND=WP) :: PI
!        THE VALUE OF PI.
!     REAL(KIND=WP) :: Y(N)
!        THE INPUT ARRAY ARRAY CONTAINING THE OBSERVED SERIES.
!
      CALL GETPI(PI)
!
      DO 10 I = 1, N
         IF (FD.EQ.0.25_WP) THEN
            AMPL(I) = 0.0_WP
            PHAS(I) = -2.0_WP*Y(I)
         ELSE IF (FD.EQ.0.5_WP) THEN
            AMPL(I) = -2.0_WP*Y(I)
            PHAS(I) = 0.0_WP
         ELSE
            ARG = (I-1) * FD * 2.0_WP * PI
            AMPL(I) = Y(I)*COS(ARG)*2.0_WP
            PHAS(I) = -Y(I)*SIN(ARG)*2.0_WP
         END IF
   10 CONTINUE
      RETURN
      END
