!BFSMN
      SUBROUTINE BFSMN(SPCF1, SPCF2, CEVEN, CODD, W, LW, LAG, DF, NPRT,
     +   NF, CSPC2, PHAS, FREQ, NPTS, XAXIS, YAXIS, ISYM, LPCV, ALPHA,
     +   LAGMX1, DELTA)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE COMPUTES THE SQUARED COHERENCY AND PHASE COMPONENTS
!     OF A BIVARIATE SPECTRUM.
!
!     REFERENCE - JENKINS AND WATTS
!                 SPECTRAL ANALYSIS AND ITS APPLICATIONS
!
!     WRITTEN BY - STEPHEN M. KEEFER AND JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  DECEMBER 2, 1985
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      DOUBLE PRECISION
     +   ALPHA,DELTA,DF
      INTEGER
     +   LAG,LAGMX1,LPCV,LW,NF,NPRT,NPTS
!
!  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   CEVEN(*),CODD(*),CSPC2(*),FREQ(*),PHAS(*),SPCF1(*),SPCF2(*),
     +   W(*),XAXIS(*),YAXIS(*)
      INTEGER
     +   ISYM(*)
!
!  LOCAL SCALARS
      DOUBLE PRECISION
     +   ARG,BARL,BARQ,BARY,C,CI,FAC,FPLM,FPLRS,FPSPM,G,PI,PIT2,SN,V0,
     +   V1,V2,Z0,Z1,Z2
      INTEGER
     +   I,K
!
!  EXTERNAL FUNCTIONS
      DOUBLE PRECISION
     +   PPFNML,D1MACH
      EXTERNAL PPFNML,D1MACH
!
!  EXTERNAL SUBROUTINES
      EXTERNAL GETPI
!
!  INTRINSIC FUNCTIONS
      INTRINSIC ATAN2,COS,LOG,SIGN,SIN,SQRT,TANH
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     DOUBLE PRECISION ALPHA
!        THE DESIRED CONFIDENCE LEVEL.
!     DOUBLE PRECISION ARG
!        AN ARGUMENT USED IN THE SPECTRUM COMPUTATIONS.
!     DOUBLE PRECISION BARL
!        THE SMOOTHED COSPECTRAL ESTIMATES.
!     DOUBLE PRECISION BARQ
!        THE SMOOTHED QUADRATURE SPECTRAL ESTIMATES.
!     DOUBLE PRECISION BARY
!        A TRANSFORMATION OF THE SQUARED COHERENCY COMPONENT.
!     DOUBLE PRECISION C
!        AN ARGUMENT USED IN THE SPECTRUM COMPUTATIONS.
!     DOUBLE PRECISION CEVEN(LAGMX1)
!        THE SUMS OF THE COVARIANCES FOR EACH LAG.
!     DOUBLE PRECISION CI
!        THE CONFIDENCE INTERVAL FOR THE SQUARED COHERENCY COMPONENT.
!     DOUBLE PRECISION CODD(LAGMX1)
!        THE DIFFERENCES OF THE AUTOCOVARIANCES FOR EACH LAG.
!     DOUBLE PRECISION CSPC2(NF)
!        THE SQUARED COHERENCY COMPONENT OF THE BIVARIATE SPECTRA.
!     DOUBLE PRECISION DELTA
!        THE SAMPLING INTERVAL.
!     DOUBLE PRECISION DF
!        THE EFFECTIVE DEGREES OF FREEDOM.
!     DOUBLE PRECISION FAC
!        THE CONVERSION FACTOR FROM RADIANS TO DEGREES.
!     DOUBLE PRECISION FPLM
!        THE FLOATING POINT LARGEST MAGNITUDE.
!     DOUBLE PRECISION FPLRS
!        THE FLOATING POINT LARGEST RELATIVE SPACING.
!     DOUBLE PRECISION FPSPM
!        THE FLOATING POINT SMALLEST POSITIVE MAGNITUDE.
!     DOUBLE PRECISION FREQ(NF)
!        THE FREQUENCIES AT WHICH THE SPECTRUM IS COMPUTED.
!     DOUBLE PRECISION G
!        AN ARGUMENT USED IN THE COMPUTATION OF THE ALPHA PERCENT
!        SIGNIFICANCE LEVEL.
!     INTEGER I
!        AN INDEX VALUE.
!     INTEGER ISYM(LPCV)
!        THE VECTOR CONTAINING THE CODES FOR THE PLOT SYMBOLS.
!     INTEGER K
!         AN INDEX VALUE.
!     INTEGER LAG
!        THE LAG WINDOW TRUNCATION POINT USED FOR A SPECIFIC WINDOW.
!     INTEGER LAGMX1
!        THE VALUE LAGMAX+1.
!     INTEGER LPCV
!        THE LENGTH OF THE PLOT CO-ORDINATE VECTORS.
!     INTEGER LW
!         THE LENGTH OF VECTOR W.
!     INTEGER NF
!        THE NUMBER OF FREQUENCIES AT WHICH THE SPECTRUM IS
!        TO BE COMPUTED.
!     INTEGER NPRT
!        A CODE USED TO SPECIFY THE TYPE OF PLOT.
!        IF NPRT = 0 THE PLOT IS SUPPRESSED.
!        IF NPRT = 2 THE PLOT IS PROVIDED.
!     INTEGER NPTS
!        THE NUMBER OF X, Y CO-ORDINATES TO BE PLOTTED.
!     DOUBLE PRECISION PHAS(NF)
!        THE PHASE COMPONENT OF THE BIVARIATE SPECTRA.
!     DOUBLE PRECISION PI, PIT2
!        THE VALUE OF PI AND PI*2.
!     DOUBLE PRECISION SN
!        AN ARGUMENT USED IN THE COMPUTATION OF THE SPECTRUM.
!     DOUBLE PRECISION SPCF1(NF), SPCF2(NF)
!        THE UNIVARIATE SPECTRUM FOR EACH SERIES.
!     DOUBLE PRECISION V0, V1, V2
!        ARGUMENTS USED IN THE COMPUTATION OF THE SPECTRUM.
!     DOUBLE PRECISION W(LW)
!        THE WINDOW.
!     DOUBLE PRECISION XAXIS(LPCV)
!        THE X AXIS VALUES FOR THE SPECTRUM PLOTS.
!     DOUBLE PRECISION YAXIS(LPCV)
!        THE Y AXIS VALUES FOR THE SPECTRUM PLOTS.
!     DOUBLE PRECISION Z0, Z1, Z2
!        ARGUMENTS USED IN THE COMPUTATION OF THE SPECTRUM.
!
!
      CALL GETPI(PI)
      PIT2 = PI*2.0D0
!
      FPSPM = D1MACH(1)
      FPLM = D1MACH(2)
      FPLRS = D1MACH(4)
!
      FAC = 180.0D0/PI
!
!
!     COMPUTE SMOOTHED CO-SPECTRAL ESTIMATE
!
      DO 40 I=1,NF
!
!           COMPUTE SMOOTHED CO- AND QUADRATURE SPECTRA USING
!           THE ALGORITHM SHOWN ON PAGE 420 OF JENKINS AND WATTS
!
         IF (FREQ(I).EQ.0.0D0) THEN
            C = 1.0D0
            SN = 0.0D0
         ELSE IF (FREQ(I).EQ.0.25D0) THEN
            C = 0.0D0
            SN = 1.0D0
         ELSE IF (FREQ(I).EQ.0.5D0) THEN
            C = -1.0D0
            SN = 0.0
         ELSE
            ARG = PIT2*FREQ(I)
            C = COS(ARG)
            SN = SIN(ARG)
         END IF
         V0 = 0.0D0
         V1 = 0.0D0
         Z0 = 0.0D0
         Z1 = 0.0D0
         DO 10 K=LAG-1,1,-1
            V2 = 2.0D0*C*V1 - V0 + W(K+1)*CEVEN(K+1)
            Z2 = 2.0D0*C*Z1 - Z0 + W(K+1)*CODD(K+1)
            V0 = V1
            V1 = V2
            Z0 = Z1
            Z1 = Z2
   10    CONTINUE
         BARL = DELTA*(CEVEN(1)+2.0D0*(V1*C-V0))
         BARQ = 2.0D0*DELTA*Z1*SN
!
!     COMPUTE THE SMOOTHED SQUARED COHERENCY SPECTRA
!
         IF (SPCF1(I)*SPCF2(I).GT.0.0D0) THEN
            CSPC2(I) = (BARL*BARL+BARQ*BARQ)
            CSPC2(I) = CSPC2(I)/(SPCF1(I)*SPCF2(I))
         ELSE
            CSPC2(I) = FPLM
         END IF
!
!     COMPUTE PHASE (IN RADIANS)
!
         IF ((BARQ.NE.0.0D0) .OR. (BARL.NE.0.0D0)) THEN
            PHAS(I) = ATAN2(-BARQ,BARL)
         ELSE
            IF (I.EQ.1) THEN
               PHAS(I) = 0.0D0
            ELSE
               PHAS(I) = SIGN(PI,PHAS(I-1))
            END IF
         END IF
   40 CONTINUE
!
      IF (NPRT.EQ.0) RETURN
!
!     COMPUTE SMOOTHED SQUARED COHERENCY PLOT VECTORS
!
      CI = PPFNML(ALPHA)*SQRT(1.0D0/DF)
      G = 2.0D0/DF
      G = 1.0D0 - (1.0D0-ALPHA)**(G/(1.0D0-G))
      NPTS = 0
      DO 60 I=1,NF
         NPTS = NPTS + 1
!
!     COMPUTE 95 PER CENT SIGNIFICANCE LEVEL
!
         YAXIS(NPTS) = G
         XAXIS(NPTS) = FREQ(I)
         ISYM(NPTS) = 4
         IF (SPCF1(I)*SPCF2(I).LE.0.0D0) GO TO 60
!
!     COMPUTE COHERENCE SPECTRAL ESTIMATE
!
         IF (CSPC2(I).GT.1.0D0) GO TO 60
         NPTS = NPTS + 1
         YAXIS(NPTS) = CSPC2(I)
         XAXIS(NPTS) = FREQ(I)
         ISYM(NPTS) = 1
         IF (CSPC2(I).LT.G) GO TO 60
!
!     COMPUTE CONFIDENCE INTERVAL
!
         BARY = SQRT(CSPC2(I))
         BARY = 0.5D0*(LOG((1.0D0+BARY)/(1.0D0-BARY)))
         NPTS = NPTS + 1
         YAXIS(NPTS) = (TANH(BARY+CI))*(TANH(BARY+CI))
         XAXIS(NPTS) = FREQ(I)
         ISYM(NPTS) = 2
         NPTS = NPTS + 1
         YAXIS(NPTS) = (TANH(BARY-CI))*(TANH(BARY-CI))
         XAXIS(NPTS) = FREQ(I)
         ISYM(NPTS) = 2
   60 CONTINUE
!
      RETURN
!
      END
