!ERVGT
     subroutine ervgt (nmsub, nmvar, vec, n, veclb, nvmx,&
     &   head, msgtyp, nv, error, nmmin)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE CHECKS TO ENSURE THAT NO VALUES, OR ONLY A MAXIMUM
!     OF NVMX, ARE NOT GREATER THAN A SPECIFIED LOWER BOUND VECLB,
!     WITH NAME NMMIN. THE ROUTINE ALTERNATIVELY CHECKS TO MAKE SURE
!     THAT NO VALUES ARE IN VIOLATION OF THIS LOWER BOUND IF THE FIRST
!     VALUE IN THE VECTOR IS NOT.  THE CHECKING OPTION IS SPECIFIED
!     WITH MSGTYP.  IF AN ERROR IS FOUND, THE ERROR IS PRINTED AND
!     AN ERROR FLAG AND THE NUMBER OF VIOLATINS ARE RETURNED.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DIVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  OCTOBER 3, 1983
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
     real(kind=wp) ::&
     &   veclb
     integer&
     &   msgtyp,n,nv,nvmx
     logical&
     &   error,head
!
!  ARRAY ARGUMENTS
     real(kind=wp) ::&
     &   vec(*)
     character&
     &   nmmin(8)*1,nmsub(6)*1,nmvar(8)*1
!
!  LOCAL SCALARS
     integer&
     &   i,nvmn
!
!  EXTERNAL SUBROUTINES
!       EXTERNAL ERVGTP
!
!  INTRINSIC FUNCTIONS
      intrinsic mod
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     LOGICAL ERROR
!        THE VALUE RETURNED FROM THE ERROR CHECKING ROUTINES TO INDICATE
!        WHETHER AN ERROR WAS DETECTED (TRUE) OR NOT (FALSE).
!     LOGICAL HEAD
!        A FLAG INDICATING WHETHER THE HEADING SHOULD BE PRINTED (TRUE)
!        OR NOT (FALSE).
!     INTEGER I
!        AN INDEX ARGUMENT.
!     INTEGER MSGTYP
!        THE INDICATOR ARGUMENT FOR THE TYPE OF MESSAGE.
!        IF (MSGTYP.GE.4) THE MESSAGE PRINTED WILL USE NMMIN
!        OTHERWISE IT WILL USE VECLB.
!        IF (MSGTYP = 1 OR 4) NO VIOLATIONS ARE ALLOWED.
!        IF (MSGTYP = 2 OR 5) THE NUMBER OF VIOLATIONS MUST
!                             BE LESS THAN   NVMX   .
!        IF (MSGTYP = 3 OR 6) VIOLATIONS ARE COUNTED ONLY IF THE
!                             THE FIRST ELEMENT IS NOT IN VIOLATION.
!     INTEGER N
!        THE NUMBER OF OBSERVATIONS.
!     CHARACTER*1 NMMIN(8)
!        THE NAME OF THE ARGUMENT SPECIFYING THE MINIMUM.
!     CHARACTER*1 NMSUB(6)
!        THE CHARACTERS OF THE CALLING SUBROUTINES NAME.
!     CHARACTER*1 NMVAR(8)
!        THE CHARACTERS OF THE ARGUMENTS NAME.
!     INTEGER NV
!        THE NUMBER OF VIOLATIONS FOUND.
!     INTEGER NVMN
!        THE SMALLEST NUMBER OF NON-VIOLATIONS ALLOWED.
!     INTEGER NVMX
!        THE LARGEST NUMBER OF VIOLATIONS ALLOWED.
!     REAL(KIND=WP) :: VEC(N)
!        THE VECTOR BEING TESTED.
!     REAL(KIND=WP) :: VECLB
!        THE VALUE THAT THE VECTOR IS BEING TESTED AGAINST.
!
      error = .false.
!
      if (n .le. 0) return
!
!     TEST WHETHER TESTING IS NECESSRY
!
      if ((mod(msgtyp,3) .eq. 0) .and. (vec(1) .le. veclb)) return
!
!     CHECK FOR VIOLATIONS
!
      nv = 0
      do 5 i = 1, n
         if ((vec(i).le.veclb)) nv = nv + 1
    5 continue
!
      if (nv .le. nvmx) return
!
!     VIOLATIONS FOUND
!
      error = .true.
      nvmn = n - nvmx
     call ervgtp (nmsub, nmvar, veclb, nvmn, nvmx, head, msgtyp, nv,&
     &   nmmin)
!
      return
!
      end
