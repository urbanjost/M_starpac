!CORRMN
      SUBROUTINE CORRMN(YM, N, M, IYM, AVG, SD, T, RANK, SC, PC, SCINV,
     +   IWRK, WRK, SLSC, SLPC, SRCC, QUAD, CIUP, CILO, QF, NPRT,
     +   VCV, IVCV, LIWRK, LWRK)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS IS THE MAIN ROUTINE FOR THE CORRELATION FAMILY, IT WAS
!     ADAPTED FROM THE OMNITAB ROUTINE CORREL. IT DOES A  CORRELATION
!     ANALYSIS OF A MULTIVARIATE RANDOM SAMPLE.
!     THE FOLLOWING TABLES ARE PRODUCED
!        SIMPLE CORRELATION COEFFICIENTS,
!        PARTIAL CORRELATION COEFFICIENTS,
!        AND THEIR SIGINIFICANCE LEVELS,
!        SPEARMAN RANK COEFFICIENTS,
!        QUADRATIC RELATIONSHIP,
!        95 AND 99 PERCENT CONFIDENCE INTERVALS.
!
!     THIS ROUTINE WAS ADAPTED FROM AN OMNITAB ROUTINE.
!
!     ADAPTED BY -
!        JANET R. DONALDSON AND LINDA L. MITCHELL
!        STATISTICAL ENGINEERING DIVISION
!        NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  MAY 17, 1982
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      INTEGER
     +   IVCV,IYM,LIWRK,LWRK,M,N,NPRT
!
!  ARRAY ARGUMENTS
      DOUBLE PRECISION
     +   AVG(M),CILO(M,M),CIUP(M,M),PC(M,M),QF(M,M),QUAD(N,3),
     +   RANK(N,M),SC(M,M),SCINV(M,M),SD(M),SLPC(M,M),SLSC(M,M),
     +   SRCC(M,M),T(M),VCV(IVCV,M),WRK(LWRK),YM(IYM,M)
      INTEGER
     +   IWRK(LIWRK)
!
!  SCALARS IN COMMON
      INTEGER
     +   IERR
!
!  LOCAL SCALARS
      DOUBLE PRECISION
     +   B,F,FN3,FPLM,HL1,HL2,SQSUM,SUM,Z,ZZ
      INTEGER
     +   I,IER,IPRT,J,K,K1,K2,NSUM
!
!  LOCAL ARRAYS
      DOUBLE PRECISION
     +   C(3),D(3),RR(3,3),XX(3)
      INTEGER
     +   INERT(3)
!
!  EXTERNAL FUNCTIONS
      DOUBLE PRECISION
     +   CDFF,D1MACH
      EXTERNAL CDFF,D1MACH
!
!  EXTERNAL SUBROUTINES
      EXTERNAL AMEAN,CORRHD,DOTC,IPRINT,MATPRT,MGS,RANKO,DSIDI,DSIFA,
     +   VCVOUT
!
!  INTRINSIC FUNCTIONS
      INTRINSIC ABS,INT,LOG,MAX,MIN,DBLE,SIGN,SQRT,TANH
!
!  COMMON BLOCKS
      COMMON /ERRCHK/IERR
!
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     DOUBLE PRECISION AVG(M)
!                  CONTAINS THE COLUMN AVERAGES OF YM
!     DOUBLE PRECISION B
!                  = (N-1) * N * (N+1) / 6
!     DOUBLE PRECISION C(3)
!                  *
!     DOUBLE PRECISION CILO(M,M)
!                  LOWER CONFIDENCE INTERVAL FOR SC
!     DOUBLE PRECISION CIUP(M,M)
!                  UPPER CONFIDENCE INTERVAL FOR SC
!     DOUBLE PRECISION D(3)
!                  A DUMMY ARRAY.
!     DOUBLE PRECISION F
!                  SQUARE ROOT OF FN3
!     DOUBLE PRECISION FN3
!                  DOUBLE PRECISION REPRESENTATION OF (N-3).
!     DOUBLE PRECISION FPLM
!                  THE FLOATING POINT LARGEST MAGNITUDE
!     DOUBLE PRECISION HL1
!                  *
!     DOUBLE PRECISION HL2
!                  *
!     INTEGER I
!                  AN INDEX VARIABLE
!     INTEGER IER
!                  ERROR FLAG FOR SUBROUTINES CALLED FROM THIS ROUTINE
!     INTEGER IERR
!                  WHEN RETURNED BY THIS ROUTINE, DESGINATES WHETHER ANY
!                  ERRORS WERE DETECTED DURING COMPUTATIONS
!                     IF IERR .EQ. 0, NO ERRORS
!                             .EQ. 1, ERRORS WERE DETECTED
!     INTEGER INERT(3)
!                  THE INERTIA OF THE SIMPLE CORRELATION MATRIX.
!     INTEGER IPRT
!                  THE LOGICAL OUTPUT NUMBER
!     INTEGER IVCV
!                  THE ROW DIMENSION OF VCV SPECIFIED IN THE USERS PROGR
!     INTEGER IWRK(LIWRK)
!                  A WORK VECTOR FOR THE INVERSION ROUTINE
!     INTEGER IYM
!                  THE ROW DIMENSION OF YM SPECIFIED IN THE USERS PROGRA
!     INTEGER J
!                  AN INDEX VARIABLE
!     INTEGER K
!                  AN INDEX VARIABLE
!     INTEGER K1
!                  *
!     INTEGER K2
!                  *
!     INTEGER LIWRK
!                  THE LENGTH OF THE INTEGER WORK VECTOR.
!     INTEGER LWRK
!                  THE LENGTH OF THE DOUBLE PRECISION WORK VECTOR.
!     INTEGER M
!                  THE NUMBER OF VARIABLES (THE COLUMN DIMENSION OF YM)
!     INTEGER N
!                  THE NUMBER OF OBSERVATIONS
!     INTEGER NPRT
!                  THE VARIABLE CONTROLLING THE AUTOMATIC PRINTOUT
!                  NPRT =0, PRINTOUT IS SUPPRESSED
!                  OTHERWISE PRINTOUT IS PROVIDED
!     INTEGER NSUM
!                  THE NUMBER OF OBSERVATIONS IN THE DOT PRODUCT
!     DOUBLE PRECISION PC(M,M)
!                  PARTIAL CORRELATION COEFFICIENTS MATRIX
!     DOUBLE PRECISION QF(M,M)
!                  QUADRATIC FIX MATRIX
!     DOUBLE PRECISION QUAD(N,3)
!                  DOUBLE VERSION OF QF MATRIX
!     DOUBLE PRECISION RANK(N,M)
!                  THE MATRIX CONTAING THE RANKS OF YM
!     DOUBLE PRECISION RR(3,3)
!                  *
!     DOUBLE PRECISION SC(M,M)
!                  SIMPLE CORRELATION COEFFICIENTS MATRIX
!     DOUBLE PRECISION SCINV(M,M)
!                  THE INVERSE MATRIX OF SC
!     DOUBLE PRECISION SD(M)
!                  STANDARD DEVIATION OF THE COMLUMNS OF YM
!     DOUBLE PRECISION SLPC(M,M)
!                  SIGNIFICANCE LEVELS OF PC
!     DOUBLE PRECISION SLSC(M,M)
!                  SIGNIFICANCE LEVELS OF SC
!     DOUBLE PRECISION SQSUM
!                  THE SUM OF SQUARES OF THE ITH COLUMN OF YM
!     DOUBLE PRECISION SRCC(M,M)
!                  SPEARMAN RANK CORRELATION COEFFICIENTS
!     DOUBLE PRECISION SUM
!                  USED IN CALCULATING THE STATISTICS OF YM
!     DOUBLE PRECISION T(M)
!                  USED IN DETERMINING SRCC
!     DOUBLE PRECISION VCV(IVCV,M)
!                  THE VARIANCE COVARIANCE MATRIX.
!     DOUBLE PRECISION WRK(LWRK)
!                  WORK STORAGE
!     DOUBLE PRECISION XX(3)
!                  *
!     DOUBLE PRECISION YM(IYM,M)
!                  THE MATRIX WHOSE COLUMNS EACH CONTAIN ONE OF M SETS
!                  OF N OBSERVATIONS. EACH COLUMN REPRESENTS A DIFFERENT
!                  VARIABLE
!     DOUBLE PRECISION Z
!                  USED IN COMPUTING CONFIDENCE INTERVALS
!     DOUBLE PRECISION ZZ
!                  USED IN DETERMINING CORRELATION COEFFICIENTS
!
      IERR = 0
!
      FPLM = D1MACH(2)
      CALL IPRINT(IPRT)
!
      DO 10 J=1,M
         CALL AMEAN(YM(1,J), N, AVG(J))
   10 CONTINUE
!
!     COMPUTE VARIANCE-COVARIANCE MATRIX
!
      DO 30 J=1,M
         DO 20 I=J,M
            CALL DOTC(YM(1,J), AVG(J), N, YM(1,I), AVG(I), N, SUM, NSUM)
            VCV(J,I) = SUM/(NSUM-1)
            VCV(I,J) = VCV(J,I)
   20    CONTINUE
   30 CONTINUE
!
      IF (NPRT.EQ.0) RETURN
!
!     PRINT VARIANCE-COVARIANCE MATRIX AND SIMPLE CORRELATION
!     COEFFICIENTS
!
      CALL CORRHD(IPRT, M, N)
      CALL VCVOUT(M, VCV, IVCV, .FALSE.)
!
!     COMPUTE STANDARD DEVIATIONS
!
      DO 40 I=1,M
         IF (VCV(I,I).LE.0.0D0) GO TO 90
         SD(I) = SQRT(VCV(I,I))
   40 CONTINUE
!
!     COMPUTE SIMPLE CORRELATION COEFFICIENTS
!
      DO 60 J=1,M
         DO 50 I=J,M
            SC(I,J) = 1.0D0
            SCINV(J,I) = 1.0D0
            IF (I.EQ.J) GO TO 50
            SC(I,J) = VCV(I,J)/SD(I)/SD(J)
            SC(J,I) = SC(I,J)
            SCINV(J,I) = SC(I,J)
   50    CONTINUE
   60 CONTINUE
!
      IF ((M.LE.2) .OR. (N.LE.M)) GO TO 190
!
!     CALCULATE PARTIAL CORRELATION COEFFICIENTS.
!
      CALL DSIFA(SCINV, M, M, IWRK, IER)
      IF (IER.EQ.0) GO TO 100
   90 WRITE (IPRT,1000)
      IERR = 1
      RETURN
  100 CONTINUE
      CALL DSIDI(SCINV, M, M, IWRK, D, INERT, WRK, 1)
      DO 130 J=1,M
         DO 120 I=J,M
            PC(I,J) = 1.0D0
            IF (I.EQ.J) GO TO 120
            ZZ = SCINV(I,I)*SCINV(J,J)
            PC(I,J) = FPLM
            IF (ZZ.LE.0.0D0) GO TO 110
            PC(I,J) = -SCINV(J,I)/SQRT(ZZ)
            IF (ABS(PC(I,J)).GT.1.0D0) PC(I,J) = SIGN(1.0D0,PC(I,J))
  110       PC(J,I) = PC(I,J)
  120    CONTINUE
!
  130 CONTINUE
!
!     COMPUTE SIGNIFICANCE LEVELS OF PARTIAL CORRELATION COEFFICIENTS.
!     NOTE, LOWER TRIANGULAR MATRIX STORED IN SQUARE MATRIX.
!
      DO 180 I=1,M
         DO 170 J=1,I
            IF (PC(I,J).NE.0.0D0) GO TO 140
            SLPC(I,J) = 1.0D0
            GO TO 170
  140       IF (ABS(PC(I,J)).LT.1.0D0) GO TO 150
            SLPC(I,J) = 0.0D0
            GO TO 170
  150       F = PC(I,J)*PC(I,J)
            IF (1.0D0-F.NE.0.0D0) GO TO 160
            SLPC(I,J) = FPLM
            GO TO 170
  160       F = (N-M)*F/(1.0D0-F)
            SLPC(I,J) = 1.0D0 - CDFF(F,1.0D0,DBLE(N-M))
  170    CONTINUE
  180 CONTINUE
!
!     COMPUTE SIGNIFICANCE LEVELS OF SIMPLE CORRELATION COEFFICIENTS
!     NOTE, ONLY LOWER TRIANGULAR STORED IN SQUARE MATRIX.
!
  190 DO 250 I=1,M
         DO 240 J=1,I
            IF (I.NE.J) GO TO 200
            SLSC(I,J) = 0.0D0
            GO TO 240
  200       IF (SC(I,J).NE.0.0D0) GO TO 210
            SLSC(I,J) = 1.0D0
            GO TO 240
  210       IF (ABS(SC(I,J)).LT.1.0D0) GO TO 220
            SLSC(I,J) = 0.0D0
            GO TO 240
  220       F = SC(I,J)*SC(I,J)
            IF (F.NE.1.0D0) GO TO 230
            SLSC(I,J) = FPLM
            GO TO 240
  230       F = (N-2)*F/(1.0D0-F)
            SLSC(I,J) = 1.0D0 - CDFF(F,1.0D0,DBLE(N-2))
  240    CONTINUE
  250 CONTINUE
!
!     PRINT SIGNIFICANCE LEVELS OF SIMPLE CORRELATION COEFFICIENTS,
!     PARTIAL CORRELATION COEFFICIENTS AND SIGNIFICANCE LEVELS
!
      WRITE (IPRT,1020)
      CALL MATPRT(SLSC, SLSC, M, IPRT, 0, 1, M)
      IF ((M.GT.2) .AND. (N.GT.M)) GO TO 260
      WRITE (IPRT,1010)
      GO TO 270
  260 I = M - 2
      WRITE (IPRT,1030) I
      CALL MATPRT(PC, PC, M, IPRT, 0, 1, M)
      WRITE (IPRT,1040)
      CALL MATPRT(SLPC, SLPC, M, IPRT, 0, 1, M)
  270 CONTINUE
!
!     DETERMINE THE RANKS OF THE OBSERVATIONS.
!
      DO 280 I=1,M
         CALL RANKO(N, YM(1,I), IWRK, RANK(1,I), T(I))
         T(I) = T(I) / 12.0D0
  280 CONTINUE
!
!     COMPUTE SPEARMAN RANK CORRELATION COEFFICIENTS.
!     NOTE, LOWER TRIANGULAR MATRIX, STORED IN SQUARE MATRIX.
!
      B = (N-1)*N*(N+1)/6
      DO 330 I=1,M
         DO 320 J=1,I
            SRCC(I,J) = 1.0D0
            IF (I.EQ.J) GO TO 320
            K1 = 2.0D0*T(I) + 0.4D0
            K2 = 2.0D0*T(J) + 0.4D0
            SRCC(I,J) = FPLM
            IF ((INT(B)-K1.LE.0) .OR. (INT(B)-K2.LE.0)) GO TO 320
            SUM = 0.0D0
            DO 310 K=1,N
               ZZ = RANK(K,I) - RANK(K,J)
               SUM = SUM + ZZ*ZZ
  310       CONTINUE
            ZZ = (B - 2.0D0*T(I))*(B - 2.0D0*T(J))
            IF (ZZ.GT.0.0D0) SRCC(I,J) = (B-SUM-T(I)-T(J))/SQRT(ZZ)
  320    CONTINUE
  330 CONTINUE
!
!     PRINT SPEARMAN RANK CORRELATIONS COEFFICIENTS
!
      WRITE (IPRT,1050)
      CALL MATPRT(SRCC, SRCC, M, IPRT, 0, 1, M)
!
      IF (N.GT.3) GO TO 340
      WRITE (IPRT,1060)
      RETURN
!
!     COMPUTE THE SIGNIFICANCE LEVELS OF THE QUADRATIC FIT OVER THE
!     LINEAR FIT.
!
!     THESE CALCULATIONS MAY PRODUCE VARIABLE RESULTS IN VARYING
!     MACHINE/COMPILATION ENVIRONMENTS, IN CASES IN WHICH THE
!     YM MATRIX IS NEAR SINGULAR (ESSENTIALLY SINGULAR BUT THE
!     SINGULARITY IS UNDETECTED BY THE CODE).  THE OBSERVED SYMPTOMS
!     ARE ALTERNATION BETWEEN QF VALUES OF 0.0D0 (FIRST F = LINE
!     PRODUCES ZERO) AND 1.0D0 (FIRST F = LINE PRODUCES APPROX.
!     ZERO, AND NUMERATOR IN SECOND F = LINE IS NONZERO).
!
  340 FN3 = N-3
      DO 410 J=1,M
         DO 400 I=1,M
            IF (I.NE.J) GO TO 350
            QF(I,J) = 1.0D0
            GO TO 400
  350       SQSUM = 0.0D0
            DO 360 K=1,N
              QUAD(K,1) = 1.0D0
               QUAD(K,2) = YM(K,J)
               QUAD(K,3) = YM(K,J)*YM(K,J)
               SQSUM = SQSUM + YM(K,I)*YM(K,I)
               WRK(K) = YM(K,I)
  360       CONTINUE
            CALL MGS(QUAD, WRK, N, 3, XX, C, D, RR, 3, N, IER)
            IF (IER.EQ.0) GO TO 370
            WRITE (IPRT,1090)
            GO TO 420
  370       DO 380 K=1,3
               C(K) = C(K)*SQRT(D(K))
  380       CONTINUE
            F = (SQSUM-C(1)*C(1)-C(2)*C(2)-C(3)*C(3))
            QF(I,J) = 0.0D0
            IF (F.EQ.0.0D0) GO TO 400
            F = (C(3)*C(3)*FN3)/F
            QF(I,J) = 1.0D0
            IF (F.GT.0.0D0) QF(I,J) = 1.0D0 - CDFF(F,1.0D0,FN3)
  400    CONTINUE
  410 CONTINUE
!
!     PRINT THE QUADRATIC FIT MATRIX
!
      J = 2
      K = 1
      I = N - 3
      WRITE (IPRT,1070) I, QF(J,K), J, K
      CALL MATPRT(QF, QF, M, IPRT, 1, 1, M)
!
!     COMPUTE CONFIDENCE LIMITS FOR SIMPLE CORRELATION COEFFICIENTS
!
  420 F = SQRT(FN3)
      HL1 = 2.5758293D0/F
      HL2 = 1.9599640D0/F
      DO 520 J=1,M
         DO 510 I=1,M
            IF (I.NE.J) GO TO 430
            CIUP(I,J) = 99.0D0
            CILO(I,J) = 95.0D0
            GO TO 510
  430       IF (I.LT.J) GO TO 470
!
!           COMPUTE 95 PERCENT INTERVALS.
!
            IF (SC(I,J).GE.1.0D0) GO TO 440
            IF (SC(I,J).GT.-1.0D0) GO TO 450
            Z = -1.0D0
            GO TO 460
  440       Z = 1.0D0
            GO TO 460
  450       Z = 0.5D0*LOG((1.0D0+SC(I,J))/(1.0D0-SC(I,J)))
  460       CIUP(I,J) = MIN(TANH(Z+HL2),1.0D0)
            CILO(I,J) = MAX(TANH(Z-HL2),-1.0D0)
            GO TO 510
!
!           COMPUTE 99 PERCENT INTERVALS.
!
  470       IF (SC(J,I).GE.1.0D0) GO TO 480
            IF (SC(J,I).GT.-1.0D0) GO TO 490
            Z = -1.0D0
            GO TO 500
  480       Z = 1.0D0
            GO TO 500
  490       Z = 0.5D0*LOG((1.0D0+SC(J,I))/(1.0D0-SC(J,I)))
  500       CIUP(I,J) = MIN(TANH(Z+HL1),1.0D0)
            CILO(I,J) = MAX(TANH(Z-HL1),-1.0D0)
  510    CONTINUE
  520 CONTINUE
!
!     PRINT CONFIDENCE LIMITS
!
      WRITE (IPRT,1080)
      CALL MATPRT(CIUP, CILO, M, IPRT, 1, 2, M)
!
      RETURN
!
!     FORMAT STATEMENTS
!
 1000 FORMAT (/ 46H COMPUTATION STOPPED DUE TO SINGULAR OR ILL-CO,
     +   28HNDITIONED COVARIANCE MATRIX.)
 1010 FORMAT (//42H THE PARTIAL CORRELATION COEFFICIENTS (AND, 6H SIGNI,
     +   50HFICANCE LEVELS) ARE NOT PRINTED OR DEFINED BECAUSE/6H EITHE,
     +   52HR THE NUMBER OF VECTORS BEING COMPARED IS TWO OR THE,
     +   59H NUMBER OF MEASUREMENTS IS LESS THAN OR EQUAL TO THE NUMBER/
     +   27H OF VECTORS BEING COMPARED.)
 1020 FORMAT (// 44H SIGNIFICANCE LEVELS OF SIMPLE CORRELATION C,
     +   32HOEFFICIENTS (ASSUMING NORMALITY))
 1030 FORMAT (// 38H PARTIAL CORRELATION COEFFICIENTS WITH, I3,
     +   26H REMAINING VARIABLES FIXED)
 1040 FORMAT (// 44H SIGNIFICANCE LEVELS OF PARTIAL CORRELATION ,
     +   33HCOEFFICIENTS (ASSUMING NORMALITY))
 1050 FORMAT (// 44H SPEARMAN RANK CORRELATION COEFFICIENTS (ADJ,
     +   15HUSTED FOR TIES))
 1060 FORMAT (// 44H NONLINEARITY TEST AND APPROXIMATION OF CONF,
     +   39HIDENCE INTERVALS NOT DEFINED FOR N = 3.)
 1070 FORMAT (// 45H SIGNIFICANCE LEVEL OF QUADRATIC FIT OVER LIN,
     +   35HEAR FIT BASED ON F RATIO WITH 1 AND, I5, 15H DEGREES OF FRE,
     +   4HEDOM/14H (FOR EXAMPLE,, F7.4, 19H IS THE SIGNIFICANC,
     +   41HE LEVEL OF THE QUADRATIC TERM WHEN COLUMN, I3, 9H IS FITTE,
     +   11HD TO COLUMN, I3, ')')
 1080 FORMAT (// 44H CONFIDENCE INTERVALS FOR SIMPLE CORRELATION,
     +   43H COEFFICIENTS (USING FISHER TRANSFORMATION)/' 95 PER C',
     +   59HENT LIMITS BELOW DIAGONAL, 99 PER CENT LIMITS ABOVE DIAGONA,
     +   'L')
 1090 FORMAT (/ 46H SIGNIFICANCE LEVELS OF QUADRATIC FIT OVER LIN,
     +   30HEAR FIT HAVE NOT BEEN COMPUTED/' DUE TO SINGULARITY IN',
     +   32H DESIGN MATRIX. CHECK YOUR DATA.)
      END
