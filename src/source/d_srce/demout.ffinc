!DEMOUT
      SUBROUTINE DEMOUT(FD, FC, K, AMPL, PHASE, IPHASE, NDEM, NMSUB)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS SUBROUTINE PRODUCES THE OUTPUT FOR THE TIME SERIES
!     DEMODULATION ROUTINE.
!
!     WRITTEN BY  -  JANET R. DONALDSON
!                    STATISTICAL ENGINEERING DEVISION
!                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 26, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) ::
     &   FC,FD
      INTEGER
     &   IPHASE,K,NDEM
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   AMPL(NDEM),PHASE(IPHASE,2)
      CHARACTER
     &   NMSUB(6)*1
!
!  SCALARS IN COMMON
      INTEGER
     &   IERR
!
!  LOCAL SCALARS
      REAL(KIND=WP) ::
     &   PI,YMN,YMX
      INTEGER
     &   IPRT
      LOGICAL
     &   ERROR
!
!  LOCAL ARRAYS
      INTEGER
     &   ISYM(1)
!
!  EXTERNAL SUBROUTINES
!      EXTERNAL GETPI,IPRINT,VERSP,VPLMT,VPMN
!
!  COMMON BLOCKS
      COMMON /ERRCHK/IERR
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL(KIND=WP) :: AMPL(NDEM)
!        THE ARRAY IN WHICH THE AMPLITUDES OF THE DEMODULATED SERIES
!        ARE STORED.
!     LOGICAL ERROR
!        AN ERROR FLAG
!     REAL(KIND=WP) :: FC, FD
!        THE CUTOFF FREQUENCY AND THE DEMODULATION FREQUENCY.
!     INTEGER IERR
!        THE ERROR FLAG
!     INTEGER IPHASE
!
!     INTEGER IPRT
!        THE UNIT NUMBER FOR PRINTED OUTPUT.
!     INTEGER ISYM(1)
!        A DUMMY ARRAY.
!     INTEGER K
!        THE NUMBER OF TERMS IN THE LOW PASS FILTER USED TO SMOOTH
!        THE DEMODULATED SERIES.
!     INTEGER NDEM
!        THE NUMBER OF VALUES IN THE DEMODULATED SERIES.
!     CHARACTER*1 NMSUB(6)
!        THE NAME OF THE CALLING SUBROUTINE
!     REAL(KIND=WP) :: PHASE(IPHASE,2)
!        THE ARRAY CONTAINING THE PRIMARY AND SECONDARY PHASE VALUES.
!     REAL(KIND=WP) :: PI
!        THE VALUE OF PI.
!     REAL(KIND=WP) :: YMN, YMX
!
!
      CALL IPRINT(IPRT)
!
      CALL VERSP(.TRUE.)
!
      CALL GETPI(PI)
!
      WRITE (IPRT, 1000) FD, FC, K
!
      WRITE (IPRT, 1001)
      CALL VPLMT(AMPL, AMPL, NDEM, 1, NDEM, 0.0_WP, 0.0_WP,
     &  YMN, YMX, ERROR, NMSUB, .FALSE., 1)
!
      IF (ERROR) THEN
        IERR = 1
        RETURN
      ELSE
        CALL VPMN(AMPL, AMPL, NDEM, 1, NDEM, 1, 0, ISYM, 1, 0,
     &   YMN, YMX, 1.0_WP, 1.0_WP, .FALSE., 0, 1, 0)
      END IF
!
      WRITE (IPRT, 1002)
      CALL VERSP(.TRUE.)
      WRITE (IPRT, 1003)
      CALL VPMN(PHASE, PHASE, NDEM, 2, IPHASE, 1, 2, ISYM, 1, 0,
     &   -2.0_WP*PI, 2.0_WP*PI, 1.0_WP, 1.0_WP, .FALSE., 0, 0, 0)
!
      RETURN
!
!     FORMAT STATEMENTS
!
 1000 FORMAT (/25H TIME SERIES DEMODULATION//
     &   26H DEMODULATION FREQUENCY IS, F10.8/
     &   26H CUTOFF FREQUENCY IS      , F10.8/
     &   38H THE NUMBER OF TERMS IN THE FILTER IS , I5///)
 1001 FORMAT (49H PLOT OF AMPLITUDE OF SMOOTHED DEMODULATED SERIES)
 1002 FORMAT ('1')
 1003 FORMAT (45H PLOT OF PHASE OF SMOOTHED DEMODULATED SERIES)
      END
