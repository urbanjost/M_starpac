!DOTCM
      SUBROUTINE DOTCM (Y, YMEAN, YMISS, NY, X, XMEAN, XMISS,
     &   NX, DOTXY, NDOTXY)
!
!     LATEST REVISION  -  03/15/90  (JRD)
!
!     THIS ROUTINE COMPUTES THE DOT PRODUCT OF TWO
!     SERIES WITH MISSING DATA, CENTERED ABOUT THEIR RESPECTIVE MEANS.
!
!     WRITTEN BY - JANET R. DONALDSON
!                  STATISTICAL ENGINEERING DIVISION
!                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
!
!     CREATION DATE  -  NOVEMBER 21, 1980
!
!
!  VARIABLE DECLARATIONS
!
!  SCALAR ARGUMENTS
      REAL(KIND=WP) :: DOTXY,XMEAN,XMISS,YMEAN,YMISS
      INTEGER
     &   NDOTXY,NX,NY
!
!  ARRAY ARGUMENTS
      REAL(KIND=WP) ::
     &   X(NX),Y(NY)
!
!  LOCAL SCALARS
      INTEGER
     &   I,M
!
!  EXTERNAL FUNCTIONS
      LOGICAL
     &   MVCHK
!       EXTERNAL MVCHK
!
!  INTRINSIC FUNCTIONS
      INTRINSIC MIN
!
!     VARIABLE DEFINITIONS (ALPHABETICALLY)
!
!     REAL(KIND=WP) :: DOTXY
!        THE DOT PRODUCT OF THE SERIES (Y(I) - YMEAN) AND
!        (X(I) - XMEAN).
!     INTEGER I
!        AN INDEX VARIABLE.
!     INTEGER M
!        THE SMALLER OF THE NUMBER OF OBSERVATIONS IN X AND Y
!     INTEGER NDOTXY
!        THE NUMBER OF OBSERVATIONS USED TO COMPUTE DOTXY
!     INTEGER NX, NY
!        THE NUMBER OF OBSERVATIONS IN SERIES X AND Y, RESPECTIVELY.
!     REAL(KIND=WP) :: X(NX)
!        THE VECTOR CONTAINING THE SECOND SERIES
!     REAL(KIND=WP) :: XMEAN
!        THE MEAN OF THE SECOND SERIES.
!     REAL(KIND=WP) :: XMISS
!        THE USER SUPPLIED CODE WHICH IS USED TO DETERMINE WHETHER OR
!        NOT AN OBSERVATION IN THE SERIES IS MISSING.  IF X(I) = XMISS,
!        THE VALUE IS ASSUMED MISSING, OTHERWISE IT IS NOT.
!     REAL(KIND=WP) :: Y(NY)
!        THE VECTOR CONTAINING THE FIRST SERIES
!     REAL(KIND=WP) :: YMEAN
!        THE MEAN OF THE FIRST SERIES.
!     REAL(KIND=WP) :: YMISS
!        THE USER SUPPLIED CODE WHICH IS USED TO DETERMINE WHETHER OR
!        NOT AN OBSERVATION IN THE SERIES IS MISSING.  IF Y(I) = YMISS,
!        THE VALUE IS ASSUMED MISSING, OTHERWISE IT IS NOT.
!
      NDOTXY = 0
      DOTXY = 0.0_WP
      M = MIN(NY, NX)
      DO 10 I = 1, M
         IF (MVCHK(Y(I), YMISS) .OR. MVCHK(X(I), XMISS)) GO TO 10
         DOTXY = DOTXY + (Y(I) - YMEAN) * (X(I) - XMEAN)
         NDOTXY = NDOTXY + 1
   10 CONTINUE
      RETURN
      END
