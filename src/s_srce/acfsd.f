*ACFSD
      SUBROUTINE ACFSD (RHO, SDRHO, NC, N)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS ROUTINE COMPUTES THE STANDARD ERROR OF THE AUTOCORRELATIONS
C
C     WRITTEN BY - JANET R. DONALDSON
C                  STATISTICAL ENGINEERING DIVISION
C                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE  -  NOVEMBER 21, 1980
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      INTEGER
     +   N,NC
C
C  ARRAY ARGUMENTS
      REAL
     +   RHO(*),SDRHO(*)
C
C  LOCAL SCALARS
      INTEGER
     +   K,KLAST,LAG
C
C  INTRINSIC FUNCTIONS
      INTRINSIC MIN,REAL,SQRT
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     INTEGER K
C        AN INDEX VARIABLE.
C     INTEGER KLAST
C        THE LAST LAG VALUE TO BE USED.
C     INTEGER LAG
C        THE INDEX VARIABLE USED TO INDICATE THE AUTOCORRELATION
C        BEING EXAMINED.
C     INTEGER N
C        THE INTEGER NUMBER OF OBSERVATIONS IN EACH SERIES
C     INTEGER NC
C        THE NUMBER OF AUTOCORRELATIONS COMPUTED
C     REAL RHO(NC)
C        THE ARRAY IN WHICH THE AUTOCORRELATIONS ARE STORED
C     REAL SDRHO(NC)
C        THE ARRAY IN WHICH THE STANDARD ERRORS OF THE AUTOCORRELATIONS
C        ARE STORED
C
      SDRHO(1) = SQRT(REAL(N - 1)) / N
      IF (NC .EQ. 1) RETURN
      DO 20 LAG = 2, NC
         SDRHO(LAG) = 0.0E0
         KLAST = MIN(LAG-1, N-LAG)
         DO 10 K = 1, KLAST
            SDRHO(LAG) = SDRHO(LAG) + (N-LAG-K) * RHO(K) * RHO(K)
   10    CONTINUE
         SDRHO(LAG) = SQRT((N-LAG) + 2.0E0 * SDRHO(LAG)) / N
   20 CONTINUE
      RETURN
      END
