*XAOV1
      SUBROUTINE XAOV1(LDSTAK)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS ROUTINE EXCERCISES THE ONEWAY FAMILY ROUTINES.
C
C     WRITTEN BY -
C        LINDA MITCHELL
C        STATISTICAL ENGINEERING DIVISION
C        NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE  -  MAY 17, 1982
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      INTEGER
     +   LDSTAK
C
C  SCALARS IN COMMON
      INTEGER
     +   IERR
C
C  ARRAYS IN COMMON
      DOUBLE PRECISION DSTAK(12)
C
C  LOCAL SCALARS
      REAL
     +   FI
      INTEGER
     +   I,IGSTAT,IPRT,LDSMIN,N,NG
C
C  LOCAL ARRAYS
      REAL
     +   GSTAT(10,4),TAG(20),Y(20),Z(10),ZTAG(10)
C
C  EXTERNAL SUBROUTINES
      EXTERNAL AOV1,AOV1S,AOV1XP,IPRINT,LDSCMP,MSGX,SETRV
C
C  COMMON BLOCKS
      COMMON /CSTAK/DSTAK
      COMMON /ERRCHK/IERR
C
C     VARIABLE DEFINITIONS (APHABETICALLY)
C
C     DOUBLE PRECISION DSTAK(12)
C        THE DOUBLE PRECISION VERSION OF THE /CSTAK/ WORK AREA.
C     REAL FI
C        FLOAT OF INDEX I
C     REAL GSTAT(10,4)
C        THE GROUP STATISTICS.  COLUMNS CORRESPOND TO THE TAG
C        VALUE, SAMPLE SIZE, GROUP MEAN, AND GROUP STANDARD DEVIATION.
C     INTEGER I
C        INDEX VARIABLE
C     INTEGER IERR
C        COMMON FLAG INDICATING WHETHER OR NOT THERE WERE ANY ERRORS
C     INTEGER IGSTAT
C        THE FIRST DIMENSION OF GSTAT.
C     INTEGER IPRT
C        THE OUTPUT LOGICAL UNIT NUMBER
C     INTEGER LDSMIN
C        THE SMALLEST ACCEPTABLE SIZE OF THE COMMON CSTAK
C     INTEGER LDSTAK
C        THE SIZE OF THE COMMON CSTAK
C     INTEGER N
C        THE NUMBER OF OBSERVATIONS
C     INTEGER NG
C        THE NUMBER OF DIFFERENT GROUPS
C     REAL TAG(20)
C        THE TAG VALUES FOR EACH OBSERVATION
C     REAL Y(20)
C        THE VECTOR OF OBSERVATIONS
C     REAL Z(10)
C        TEST VECTOR
C     REAL ZTAG(10)
C        TEST TAG VECTOR
C
      DATA   Y( 1),   Y( 2),   Y( 3),   Y( 4),   Y( 5)
     +   /    61.0E0,    61.0E0,    67.0E0,    67.0E0,    64.0E0/
      DATA   Y( 6),   Y( 7),   Y( 8),   Y( 9),   Y(10)
     +   /    78.0E0,    71.0E0,    75.0E0,    72.0E0,    74.0E0/
      DATA   Y(11),   Y(12),   Y(13),   Y(14),   Y(15)
     +   /    83.0E0,    81.0E0,    76.0E0,    78.0E0,    79.0E0/
      DATA   Y(16),   Y(17)
     +   /    72.0E0,   72.0E0/
C
      DATA TAG( 1), TAG( 2), TAG( 3), TAG( 4), TAG( 5)
     +   /    11.5E0,    11.5E0,    11.5E0,    11.5E0,    11.5E0/
      DATA TAG( 6), TAG( 7), TAG( 8), TAG( 9), TAG(10)
     +   /    12.0E0,    12.0E0,    12.0E0,    12.0E0,    12.0E0/
      DATA TAG(11), TAG(12), TAG(13), TAG(14), TAG(15)
     +   /    11.0E0,    11.0E0,    11.0E0,    11.0E0,    11.0E0/
      DATA TAG(16), TAG(17)
     +   /   -11.0E0,   11.0E0/
C
      CALL IPRINT(IPRT)
C
C     SET VARIOUS DIMENSIONS AND PROGRAM VARIABLES
C
      N = 17
      IGSTAT = 10
C
C**** TEST WITH CORRECT CALL STATEMENTS ****
C
      WRITE (IPRT,1060)
C
C     TEST AOV1
C
      WRITE (IPRT,1000)
      CALL AOV1(Y, TAG, N, LDSTAK)
      CALL MSGX(0, IPRT)
      WRITE (IPRT,1010)
      WRITE (IPRT,1020) (TAG(I),I=1,N)
C
C     TEST OF AOV1S
C
C     PRINTOUT NOT SUPRESSED
C
      WRITE (IPRT,1040)
      WRITE (IPRT,1030)
      CALL AOV1S(Y, TAG, N, LDSTAK, 1, GSTAT, IGSTAT, NG)
      CALL MSGX(0, IPRT)
C
C     PRINT STORAGE AND ZERO VECTORS BEFORE USING AGAIN
C
      CALL AOV1XP(GSTAT, IGSTAT, NG)
C
C     PRINTOUT SUPRESSED
C
      WRITE (IPRT,1050)
      CALL AOV1S(Y, TAG, N, LDSTAK, 0, GSTAT, IGSTAT, NG)
      CALL MSGX(0, IPRT)
C
C     PRINT STORAGE AND ZERO VECTORS BEFORE USING AGAIN
C
      CALL AOV1XP(GSTAT, IGSTAT, NG)
C
C**** NUMBER OF OBSERVATIONS LESS THAN 2 ****
C
      WRITE (IPRT,1090)
      CALL AOV1(Y, TAG, 1, LDSTAK)
      CALL MSGX(1, IPRT)
C
      CALL AOV1S(Y, TAG, -14, LDSTAK, 0, GSTAT, IGSTAT, NG)
      CALL MSGX(1, IPRT)
C
C**** ALL OBSERVATIONS THE SAME VALUE ****
C
      WRITE (IPRT,1100)
      WRITE (IPRT,1000)
      CALL SETRV(Z, 10, 0.0E0)
      CALL AOV1(Z, TAG, 10, LDSTAK)
      CALL MSGX(0, IPRT)
C
      CALL SETRV(Z, 10, 2.0E0)
      WRITE (IPRT,1100)
      WRITE (IPRT,1030)
      CALL AOV1S(Z, TAG, 10, LDSTAK, 1, GSTAT, IGSTAT, NG)
      CALL MSGX(0, IPRT)
C
C**** TEST WORK AREA SIZE HANDLING ****
C
      CALL LDSCMP(11, 0, 33, 0, 0, 0, 'S', 40, LDSMIN)
      WRITE (IPRT,1070)
      WRITE (IPRT,1000)
      CALL AOV1(Y, TAG, N, 1)
      CALL MSGX(1, IPRT)
      WRITE (IPRT,1070)
      WRITE (IPRT,1000)
      CALL AOV1(Y, TAG, N, LDSMIN-1)
      CALL MSGX(1, IPRT)
      WRITE (IPRT,1010)
      WRITE (IPRT,1020) (TAG(I),I=1,N)
      WRITE (IPRT,1080)
      WRITE (IPRT,1000)
      CALL AOV1(Y, TAG, N, LDSMIN)
      CALL MSGX(0, IPRT)
C
      CALL LDSCMP(11, 0, 33, 0, 0, 0, 'S', 28, LDSMIN)
      WRITE (IPRT,1070)
      WRITE (IPRT,1030)
      CALL AOV1S(Y, TAG, N, 1, 0, GSTAT, IGSTAT, NG)
      CALL MSGX(1, IPRT)
      WRITE (IPRT,1070)
      WRITE (IPRT,1030)
      CALL AOV1S(Y, TAG, N, LDSMIN-1, 0, GSTAT, IGSTAT, NG)
      CALL MSGX(1, IPRT)
      WRITE (IPRT,1010)
      WRITE (IPRT,1020) (TAG(I),I=1,N)
      WRITE (IPRT,1080)
      WRITE (IPRT,1030)
      CALL AOV1S(Y, TAG, N, LDSMIN, 1, GSTAT, IGSTAT, NG)
      CALL MSGX(0, IPRT)
C
C**** SAME NUMBER OF GROUPS AS NON-ZERO TAGS ****
C
      WRITE (IPRT,1120)
      DO 20 I=1,10
         FI = I
         ZTAG(I) = FI
         Z(I) = 13.0E0 - FI
   20 CONTINUE
      CALL AOV1(Z, ZTAG, 10, LDSTAK)
      CALL MSGX(1, IPRT)
      CALL AOV1S(Z, ZTAG, 10, LDSTAK, 1, GSTAT, IGSTAT, NG)
      CALL MSGX(1, IPRT)
C
C**** LESS THAN 2 DIFFERENT TAG GROUPS ****
C
      WRITE (IPRT,1130)
      CALL SETRV(ZTAG, 10, 1.0E0)
      CALL AOV1(Z, ZTAG, 10, LDSTAK)
      CALL MSGX(1, IPRT)
      CALL AOV1S(Z, ZTAG, 10, LDSTAK, 1, GSTAT, IGSTAT, NG)
      CALL MSGX(1, IPRT)
C
C**** LESS THAN 2 TAGS ****
C
      CALL SETRV(ZTAG, 9, 0.0E0)
      WRITE (IPRT,1140)
      CALL AOV1(Z, ZTAG, 10, LDSTAK)
      CALL MSGX(1, IPRT)
      CALL AOV1S(Z, ZTAG, 10, LDSTAK, 1, GSTAT, IGSTAT, NG)
      CALL MSGX(1, IPRT)
C
C**** INCORRECT DIMENSION OF GSTAT ****
C
      WRITE (IPRT,1150)
      CALL AOV1S(Y, TAG, N, LDSTAK, 1, GSTAT, 2, NG)
      CALL MSGX(1, IPRT)
C
C**** ALL OBSERVATIONS WITHIN A GROUP SAME VALUE ****
C
      Z(1) = 53.0E0
      ZTAG(1) = 1.0E0
      Z(2) = 62.0E0
      ZTAG(2) = 3.0E0
      Z(3) = 53.0E0
      ZTAG(3) = 1.0E0
      Z(4) = 71.0E0
      ZTAG(4) = 4.0E0
      Z(5) = 89.0E0
      ZTAG(5) = 2.0E0
      Z(6) = 71.0E0
      ZTAG(6) = 4.0E0
      Z(7) = 89.0E0
      ZTAG(7) = 2.0E0
      Z(8) = 62.0E0
      ZTAG(8) = 3.0E0
      Z(9) = 71.0E0
      ZTAG(9) = 4.0E0
      Z(10) = 62.0E0
      ZTAG(10) = 3.0E0
      WRITE (IPRT,1160)
      CALL AOV1(Z, ZTAG, 10, LDSTAK)
      CALL MSGX(0, IPRT)
C
C**** 2 TAGS ****
C
      WRITE (IPRT,1170)
      CALL AOV1(Z, ZTAG, 3, LDSTAK)
      CALL MSGX(0, IPRT)
C
C**** ALL GROUPS(EXCEPT FOR 1) WITH 1 OBSERVATION ****
C
      WRITE (IPRT,1180)
      CALL AOV1(Z, ZTAG, 5, LDSTAK)
      CALL MSGX(0, IPRT)
C
      RETURN
C
C     FORMAT STATEMENTS
C
 1000 FORMAT(' TEST OF AOV1 ')
 1010 FORMAT(' CHECK TO SEE IF TAGS HAVE BEEN CHANGED')
 1020 FORMAT(4F12.6)
 1030 FORMAT(' TEST OF AOV1S ')
 1040 FORMAT('1PRINTOUT NOT SUPRESSED.')
 1050 FORMAT(' PRINTOUT SUPRESSED.')
 1060 FORMAT('1****TEST ROUTINES WITH CORRECT CALL****')
 1070 FORMAT('1****TEST WITH INSUFFICIENT WORK AREA****')
 1080 FORMAT('1****TEST WITH EXACTLY THE RIGHT AMOUNT OF WORK AREA****')
 1090 FORMAT('1****NUMBER OF OBSERVATIONS LESS THAN 2****')
 1100 FORMAT('1****ALL OBSERVATIONS WITH SAME VALUE****')
 1120 FORMAT('1****SAME NUMBER OF GROUPS AS NON-ZERO TAGS****')
 1130 FORMAT(' ****LESS THAN 2 DIFFERENT TAG GROUPS****')
 1140 FORMAT(' ****LESS THAN 2 TAGS****')
 1150 FORMAT('1****INCORRECT DIMENSION OF GSTAT****')
 1160 FORMAT('1****ALL OBSERVATIONS WITHIN A GROUP SAME VALUE****')
 1170 FORMAT('1****TEST WITH 2 TAGS****')
 1180 FORMAT('1****ALL GROUPS EXCEPT FOR 1 WITH 1 OBSERVATION ****')
      END
