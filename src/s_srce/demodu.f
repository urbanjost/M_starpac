*DEMODU
      SUBROUTINE DEMODU (Y, N, FD, AMPL, PHAS)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS SUBROUTINE DEMODULATES THE SERIES Y AT FREQUENCY
C     FD.  THE REAL AND IMAGINARY PARTS OF THE DEMODULATED
C     SERIES ARE RETURNED IN AMPL AND PHAS, RESPECTIVELY.
C
C     WRITTEN BY  -  PETER BLOOMFIELD
C                    FOURIER ANALYSIS OF TIME SERIES- AN
C                       INTRODUCTION
C                    JOHN WILEY AND SONS, NEW YORK, 1976
C                    PAGE 148
C     ADAPTED FOR STARPAC BY  -  JANET R. DONALDSON
C                                STATISTICAL ENGINEERING DEVISION
C                                NATIONAL BUREAU OF STANDARDS
C                                BOULDER, COLORADO
C
C     CREATION DATE  -  NOVEMBER 26, 1980
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      REAL
     +   FD
      INTEGER
     +   N
C
C  ARRAY ARGUMENTS
      REAL
     +   AMPL(N),PHAS(N),Y(N)
C
C  LOCAL SCALARS
      REAL
     +   ARG,PI
      INTEGER
     +   I
C
C  EXTERNAL SUBROUTINES
      EXTERNAL GETPI
C
C  INTRINSIC FUNCTIONS
      INTRINSIC COS,SIN
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     REAL AMPL(N)
C        THE ARRAY IN WHICH THE AMPLITUDES ARE STORED.
C     REAL ARG
C        A VALUE USED IN COMPUTING THE DEMODULATED SERIES.
C     REAL FD
C        THE DEMODULATION FREQUENCY.
C     INTEGER I
C        AN INDEXING VARIABLE.
C     INTEGER N
C        THE NUMBER OF OBSERVATIONS IN THE SERIES BEING DEMODULATED.
C     REAL PHAS(N)
C        THE ARRAYS CONTAINING THE PRIMARY PHASE ESTIMATES.
C     REAL PI
C        THE VALUE OF PI.
C     REAL Y(N)
C        THE INPUT ARRAY ARRAY CONTAINING THE OBSERVED SERIES.
C
      CALL GETPI(PI)
C
      DO 10 I = 1, N
         IF (FD.EQ.0.25E0) THEN
            AMPL(I) = 0.0E0
            PHAS(I) = -2.0E0*Y(I)
         ELSE IF (FD.EQ.0.5E0) THEN
            AMPL(I) = -2.0E0*Y(I)
            PHAS(I) = 0.0E0
         ELSE
            ARG = (I-1) * FD * 2.0E0 * PI
            AMPL(I) = Y(I)*COS(ARG)*2.0E0
            PHAS(I) = -Y(I)*SIN(ARG)*2.0E0
         END IF
   10 CONTINUE
      RETURN
      END
