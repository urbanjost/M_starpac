*GFAEST
      SUBROUTINE GFAEST (PHI, IAR, NF, FREQ, GAIN, PHAS, DELTA)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS ROUTINE COMPUTES THE GAIN FUNCTION OF AN AUTOREGRESSIVE
C     LINEAR FILTER USING THE ALGORITHM GIVEN ON PAGES 419 AND 420
C     JENKINS AND WATTS.
C
C     WRITTEN BY - JANET R. DONALDSON
C                  STATISTICAL ENGINEERING DIVISION
C                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE  -  NOVEMBER 26, 1980
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      REAL
     +   DELTA
      INTEGER
     +   IAR,NF
C
C  ARRAY ARGUMENTS
      REAL
     +   FREQ(NF),GAIN(NF),PHAS(NF),PHI(IAR)
C
C  LOCAL SCALARS
      REAL
     +   ARG,C,PI,S,V0,V1,V2,Z0,Z1,Z2
      INTEGER
     +   I,J,JJ
C
C  EXTERNAL SUBROUTINES
      EXTERNAL GETPI
C
C  INTRINSIC FUNCTIONS
      INTRINSIC ATAN2,COS,SIN,SQRT
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     REAL ARG
C        A VALUE USED TO COMPUTE THE GAIN FUNCTION ESTIMATES.
C     REAL C
C        A VALUE USED TO COMPUTE THE GAIN FUNCTION ESTIMATES.
C     REAL DELTA
C        THE SAMPLING INTERVAL OF THE ORIGINAL SERIES.
C     REAL FREQ(NF)
C        THE VECTOR OF FREQUENCIES AT WHICH THE GAIN FUNCTION IS TO
C        BE ESTIMATED.
C     REAL GAIN(NF)
C        THE ARRAY IN WHICH THE GAIN FUNCTION ESTIMATES ARE RETURNED.
C     INTEGER I
C        AN INDEX VARIABLE.
C     INTEGER IAR
C        THE NUMBER OF FILTER COEFFICIENTS.
C     INTEGER J, JJ
C        INDEX VARIABLES.
C     INTEGER NF
C        THE NUMBER OF FREQUENCIES FOR WHICH THE GAIN FUNCTION ESTIMATES
C        ARE TO BE COMPUTED.
C     REAL PHAS(NF)
C        THE ARRAY IN WHICH THE PHASE OF THE FILTER IS RETURNED.
C     REAL PHI(IAR)
C        THE VECTOR CONTAINING THE AUTOREGRESSIVE FILTER COEFFICIENTS.
C     REAL PI
C        THE VALUE OF PI.
C     REAL S
C        A VALUE USED TO COMPUTE THE GAIN FUNCTION ESTIMATES.
C     REAL V0, V1, V2
C        CONSTANTS USED FOR COMPUTING THE GAIN FUNCTION ESTIMATES.
C     REAL Z0, Z1, Z2
C        CONSTANTS USED FOR COMPUTING THE GAIN FUNCTION ESTIMATES.
C
      CALL GETPI(PI)
C
C     COMPUTE GAIN FUNCTION ESTIMATES AND THEIR CONFIDENCE LIMITS.
C
      DO 30 I = 1, NF
         IF (FREQ(I).EQ.0.0E0) THEN
            ARG = 0.0E0
            C = 1.0E0
            S = 0.0E0
         ELSE IF (FREQ(I).EQ.0.25E0) THEN
            ARG = PI/2.0E0
            C = 0.0E0
            S = 1.0E0
         ELSE IF (FREQ(I).EQ.0.5E0) THEN
            ARG = PI
            C = -1.0E0
            S = 0.0E0
         ELSE
            ARG = 2.0E0 * PI * FREQ(I)
            C = COS(ARG)
            S = SIN(ARG)
         END IF
         V0 = 0.0E0
         V1 = 0.0E0
         Z0 = 0.0E0
         Z1 = 0.0E0
C
         JJ = IAR + 1
C
         DO 20 J = 1, IAR
            JJ = JJ -1
C
            V2 = C * V1 - V0 - PHI(JJ)
            V0 = V1
            V1 = V2
C
            Z2 = S * Z1 - Z0 - PHI(JJ)
            Z0 = Z1
            Z1 = Z2
   20    CONTINUE
C
         V2 = DELTA * (1.0E0 + (V1 * C - V0))
C
         Z2 = DELTA * Z1 * S
C
         GAIN(I) = SQRT(V2*V2 + Z2*Z2)
C
         PHAS(I) = -0.5E0 * PI
         IF (Z2.NE.0.0E0 .AND. V2.NE.0.0E0) PHAS(I) = ATAN2(Z2, V2)
C
   30 CONTINUE
C
      RETURN
      END
