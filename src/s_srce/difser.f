*DIFSER
      SUBROUTINE DIFSER (Y, N, NDF, ND, IOD, YDIFF, NDIFF)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS SUBROUTINE PERFORMS THE DIFFERENCING OPPERATION
C     DEFINED BY ND, IOD AND NDF ON THE SERIES Y, RESULTING IN
C     THE SERIES YDIFF.
C
C     WRITTEN BY  -  JANET R. DONALDSON
C                    STATISTICAL ENGINEERING DIVIAION
C                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE  -  NOVEMBER 21, 1980
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      INTEGER
     +   N,NDF,NDIFF
C
C  ARRAY ARGUMENTS
      REAL
     +   Y(*),YDIFF(*)
      INTEGER
     +   IOD(*),ND(*)
C
C  LOCAL SCALARS
      INTEGER
     +   I,J,K,L,NTIMES
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     INTEGER I
C        AN INDEX VARIABLE.
C     INTEGER IOD(NDF)
C        THE VECTOR CONTAINING THE ORDERS OF EACH DIFFERENCE FACTOR.
C     INTEGER J, K, L
C        INDEX VARIABLES.
C     INTEGER N
C        THE NUMBER OF OBSERVATIONS.
C     INTEGER ND(NDF)
C        THE VECTOR CONTAINING THE NUMBER OF TIMES EACH DIFFERENCING
C        FACTOR IS APPLIED.
C     INTEGER NDF
C        THE NUMBER OF DIFFERENCE FACTORS.
C     INTEGER NDIFF
C        THE NUMBER OF OBSERVATIONS REMAINING IN THE DIFFERENCED SERIES.
C     INTEGER NTIMES
C        THE NUMBER OF TIMES A GIVEN DIFFERENCE FACTOR IS TO BE APPLIED.
C     REAL Y(N), YDIFF(N)
C        THE VECTOR CONTAINING THE SERIES OBSERVATIONS AND THE VECTOR
C        IN WHICH THE DIFFERENCED SERIES IS RETURNED.
C
      DO 10 I = 1, N
         YDIFF(I) = Y(I)
   10 CONTINUE
C
      NDIFF = N
C
      IF (NDF .EQ. 0) RETURN
C
      DO 40 I = 1, NDF
         NTIMES = ND(I)
         DO 30 L = 1, NTIMES
            NDIFF = NDIFF - IOD(I)
            DO 20 J = 1, NDIFF
               K = J + IOD(I)
               YDIFF(J) = YDIFF(K) - YDIFF(J)
   20       CONTINUE
   30    CONTINUE
   40 CONTINUE
      RETURN
      END
