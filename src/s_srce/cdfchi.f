*CDFCHI
      REAL FUNCTION CDFCHI(CHISQR, DF)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS ROUTINE COMPUTES THE CHI SQUARE CUMULATIVE DISTRIBUTION
C     FUNCTION FROM THE INCOMPLETE GAMMA FUNCTION RATIO AS DISCUSSED IN
C     CHAPTER 17 OF DISTRIBUTIONS IN STATISTICS - CONTINUOUS UNIVARIATE
C     DISTRIBUTIONS - 1, BY JOHNSON AND KOTZ.
C
C     WRITTEN BY - JANET R. DONALDSON
C                  STATISTICAL ENGINEERING DIVISION
C                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE - DECEMBER 7, 1981
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      REAL
     +   CHISQR,DF
C
C  LOCAL SCALARS
      REAL
     +   FPLPM
C
C  EXTERNAL FUNCTIONS
      REAL
     +    GAMI, GAMMA,R1MACH
      LOGICAL
     +   MVCHK
      EXTERNAL  GAMI, GAMMA,R1MACH,MVCHK
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     REAL CHISQR
C        THE PERCENT POINT FROM THE CHI SQUARED DISTRIBUTION.
C     REAL DF
C        THE DEGREES OF FREEDOM.
C     REAL FPLPM
C        THE REAL FLOATING POINT LARGEST POSITIVE MAGNITUDE.
C
C     COMMENCE BODY OF ROUTINE
C
      FPLPM = R1MACH(2)
C
      CDFCHI =  GAMI(0.5E0*DF, 0.5E0*CHISQR)
      IF (MVCHK(CDFCHI,FPLPM)) THEN
         CDFCHI = 1.0E0
      ELSE
         CDFCHI = CDFCHI /  GAMMA(0.5E0*DF)
         IF (CDFCHI.LT.0.0E0) CDFCHI = 0.0E0
         IF (CDFCHI.GT.1.0E0) CDFCHI = 1.0E0
      END IF
C
      RETURN
      END
