*SUMBS
      SUBROUTINE SUMBS(X, N, LO, MID, HI)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     A ROUTINE TO FIND A ZERO OR VALUE CLOSEST TO ZERO IN
C     A SORTED VECTOR.
C
C     WRITTEN BY - JOHN E. KOONTZ
C                  STATISTICAL ENGINEERING DIVISION
C                  NATIONAL BUREAU OF STANDARDS
C
C     CREATION DATE  -  MAY 17, 1982
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      INTEGER
     +   HI,LO,MID,N
C
C  ARRAY ARGUMENTS
      REAL
     +   X(N)
C
C  LOCAL SCALARS
      INTEGER
     +   CURHI,CURLO
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     INTEGER CURHI
C        THE UPPER BOUND OF THE CURRENT INTERVAL.
C     INTEGER CURLO
C        THE LOWER BOUND OF THE CURRENT INTERVAL.
C     INTEGER HI
C        INPUT PARAMETER.  THE UPPER BOUND OF THE INITIAL
C        INTERVAL.
C     INTEGER LO
C        INPUT PARAMETER.  THE LOWER BOUND OF THE INITIAL
C        INTERVAL.
C     INTEGER MID
C        OUTPUT PARAMETER.  THE MIDPOINT OF THE CURRENT
C        INTERVAL, AND ON EXIT THE POINT IN THE INITIAL
C        INTERVAL CLOSEST TO ZERO.
C     INTEGER N
C        INPUT PARAMETER.  THE LENGTH OF THE ARRAY X.
C     REAL X(N)
C        INPUT PARAMETER.  THE ARRAY X IN WHICH THE SEARCH FOR
C        A (NEAR) ZERO VALUE IS MADE.
C
C     BINARY SEARCH OF X FOR 0.0E0 OR CLOSEST TO IT.
C
      CURLO = LO
      CURHI = HI
C
C     SEE IF ZERO IS OUT OF THE RANGE X(LO) TO X(HI).
C
      IF (X(CURLO).LT.0.0E0) GO TO 10
      MID = CURLO
      GO TO 50
   10 IF (X(CURHI).GT.0.0E0) GO TO 20
      MID = CURHI
      GO TO 50
C
C     LOCATE EITHER A ZERO OR A PAIR OF ADJACENT VALUES BETWEEN
C     WHICH THERE LIES A ZERO.
C
   20 IF (CURLO+1.EQ.CURHI) GO TO 30
      MID = (CURLO+CURHI)/2
      IF (0.0E0.LT.X(MID)) CURHI = MID
      IF (0.0E0.GT.X(MID)) CURLO = MID
      IF (0.0E0.EQ.X(MID)) GO TO 50
      GO TO 20
C
C     PICK AS MIDPOINT THE CURRENT ENDPOINT CLOSEST TO ZERO.
C
   30 IF (X(CURHI)+X(CURLO).GT.0.0E0) GO TO 40
      MID = CURHI
      GO TO 50
   40 MID = CURLO
   50 RETURN
      END
