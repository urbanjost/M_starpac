*STAT1
      SUBROUTINE STAT1(Y, N, YMED, YMIN, YMAX, YMIDRG, YRANGE, NCELLS,
     +   YLB, YUB, YDISTR)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS COMPUTES STATISTICS FOR A VECTOR Y THAT REQUIRE OR CAN
C     MAKE USE OF SORTEDNESS IN THE Y VECTOR.
C
C     THIS SUBROUTINE IS BASED ON A MODIFICATION OF THE STATIS
C     CODE USED IN OMNITAB, VERSION 5 (6/16/72), WRITTED BY
C     SALLY PEAVY.  THE ORIGINAL ADAPTATION TO STARPAC WAS MADE
C     BY JANET DONALDSON.
C
C     WRITTEN BY - JOHN E. KOONTZ
C                  STATISTICAL ENGINEERING DIVISION
C                  NATIONAL BUREAU OF STANDARDS
C
C     CREATION DATE  -  MAY 17, 1982
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      REAL
     +   YLB,YMAX,YMED,YMIDRG,YMIN,YRANGE,YUB
      INTEGER
     +   N,NCELLS
C
C  ARRAY ARGUMENTS
      REAL
     +   Y(N),YDISTR(NCELLS)
C
C  LOCAL SCALARS
      REAL
     +   DELY,YT
      INTEGER
     +   I,IC,IC1,L,M,M1
C
C  INTRINSIC FUNCTIONS
      INTRINSIC MOD
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     REAL DELY
C        THE WIDTH OF AN INTERVAL ONE TENTH THE RANGE OF THE
C        DATA IN Y.
C     INTEGER I
C        A LOOP INDEX.
C     INTEGER IC
C        PREVIOUS SIGN IN RUNS CALCULATION.
C     INTEGER IC1
C        COUNT IN FREQUENCY DISTRIBUTION CALCULATIONS.
C     INTEGER L
C        A LOOP INDEX.
C     INTEGER M
C        A LOOP INDEX.
C     INTEGER M1
C        WHEN N IS EVEN, M1 IS M + 1, OTHERWISE IT IS M.
C     INTEGER N
C        INPUT PARAMETER.  THE LENGTH OF Y AND WT.
C     INTEGER NCELLS
C        THE NUMBER OF CELLS IN THE FREQUENCY DISTRIBUTION.
C     REAL Y(N)
C        INPUT PARAMETER.  A SET OF N DATA POINTS, SORTED
C        INTO ASCENDING ORDER.
C     REAL YDISTR(NCELLS)
C        OUTPUT PARAMETER.  THE NUMBERS OF Y VALUES WITH
C        POSITIVE WEIGHTS IN EACH OF NCELLS EQUAL LENGTH INTERVALS
C        THAT DIVIDE THE RANGE OF Y VALUES.
C     REAL YLB
C        THE LOWER BOUND FOR THE FREQUENCY DISTRIBUTION.
C        IF YLB = YUB, THE MINIMUM OBSERVATION WILL BE USED.
C     REAL YMAX
C        OUTPUT PARAMETER.  THE MAXIMUM OF THE OBSERVATIONS Y HAVING
C        POSITIVE WEIGHT.
C     REAL YMED
C        OUTPUT PARAMETER.  THE MEDIAN OF THE OBSERVATIONS Y.
C     REAL YMIDRG
C        OUTPUT PARAMETER.  THE MIDRANGE OF THE OBSERVATIONS Y.
C     REAL YMIN
C        OUTPUT PARAMETER.  THE MINIMUM OF THE OBSERVATIONS Y HAVING
C        POSITIVE WEIGHT.
C     REAL YRANGE
C        THE RANGE OF THE OBSERVATIONS Y.
C     REAL YT
C        THE MAXIMUM VALUE IN EACH INTERVAL IN THE FREQUENCY
C        DISTRIBUTIONS CALCULATIONS.
C     REAL YUB
C        THE UPPER BOUND FOR THE FREQUENCY DISTRIBUTION.
C        IF YLB = YUB, THE MAXIMUM OBSERVATION WILL BE USED.
C
C     CALCULATE THE MEDIAN, MIDRANGE, RANGE, AND EXTREMA.
C
      M = (N+1)/2
      M1 = M
      IF (MOD(N,2).EQ.0) M1 = M1 + 1
      YMED = (Y(M)+Y(M1))/2.0E0
      YMIDRG = (Y(1)+Y(N))/2.0E0
      YRANGE = Y(N) - Y(1)
      YMIN = Y(1)
      YMAX = Y(N)
C
C     COMPUTE FREQUENCY DISTRIBUTION.
C
      IF (NCELLS.LE.0) RETURN
      IC1 = 0
      IF (NCELLS.EQ.1) GO TO 40
C
      DELY = YRANGE
      YT = YMIN
      IF (YLB.GE.YUB) GO TO 5
         DELY = YUB - YLB
         YT = YLB
    5 CONTINUE
      DELY = DELY / NCELLS
      YT = YT + DELY
      L = 0
      DO 30 I=2,NCELLS
         IC = 0
   10    L = L + 1
         IF (L.GT.N) GO TO 20
         IF (Y(L).GT.YT) GO TO 20
         IC = IC + 1
         IC1 = IC1 + 1
         GO TO 10
   20    YDISTR(I-1) = IC
         L = L - 1
         YT = YT + DELY
   30 CONTINUE
   40 YDISTR(NCELLS) = N - IC1
      RETURN
      END
