*AOV1MN
      SUBROUTINE AOV1MN(Y, TAG, N, TVALUE, TSIZE, TMEAN, TSD, NPRT,
     +   INDEX, SRANK, GPMIN, GPMAX, B10, RANKS, ITEMP, NG, NZW)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS SUBROUTINE COMPUTES A COMPREHENSIVE SET OF RESULTS FOR
C     ANALYSIS OF A ONE-WAY CLASSIFICATION WITH OPTIONAL PRINTED OUTPUT.
C     TAG VALUES CAN BE ANY VALUE WHERE ALL MEASUREMENTS WITH TAG
C     VALUES LESS THAN OR EQUAL TO ZERO ARE EXCLUDED FROM ANALYSIS.
C
C     ORIGINALLY WRITTEN FOR OMNITAB BY -
C       DAVID HOGBEN, STATISTICAL ENGINEERING DIVISION, NBS (10/25/69)
C
C     ADAPTED  BY -
C       JANET R. DONALDSON
C       STATISTICAL ENGINEERING DIVISION
C       NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE  -  MAY 17, 1982
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      INTEGER
     +   N,NG,NPRT,NZW
C
C  ARRAY ARGUMENTS
      REAL
     +   B10(*),GPMAX(*),GPMIN(*),RANKS(*),SRANK(*),TAG(*),TMEAN(*),
     +   TSD(*),TSIZE(*),TVALUE(*),Y(*)
      INTEGER
     +   INDEX(*),ITEMP(*)
C
C  LOCAL SCALARS
      REAL
     +   AF,BF,BFRAT,BFSIG,BMS,BSS,C,CC,CFKW,DF,DFRAT,DFSIG,DMS,DSS,F,
     +   F1KW,F2KW,FMLC,FMUC,FNZW,FPLM,FSTAT,G1,GM,GR,HSTAT,IBAR,K0,
     +   MF,MKW,Q,RESSQ,RMLC,RMUC,RX,SBMS,SC,SFRAT,SFSIG,SIGKW,SLBF,
     +   SLCC,SMAX,SMIN,SMS,SNC,SOS,SQB,SQMS,SQOM,SQT,SSF,SSS,STMS,
     +   SUM,SWMS,T1,T2,T3,TIES,TMS,TSS,U1,U2,UMLC,UMUC,V1,V2,VKW,VLS,
     +   WMS,WSS,YBMAX,YBMIN,YMAX,YMIN
      INTEGER
     +   I,IPRT,ISZ,J,K,KK,KKK,L,M,M28,M3,M5,NN,NNE1,NZPNTR
      CHARACTER
     +   BLANK*1,HIGH*1,ISD*1,LOW*1,MEAN*1
C
C  LOCAL ARRAYS
      REAL
     +   STATS(15)
C
C  EXTERNAL FUNCTIONS
      REAL
     +   CDFF,PPFF,PPFT,R1MACH
      EXTERNAL CDFF,PPFF,PPFT,R1MACH
C
C  EXTERNAL SUBROUTINES
      EXTERNAL GENI,IPRINT,RANKO,SRTIR,SRTRI,SRTRRI
C
C  INTRINSIC FUNCTIONS
      INTRINSIC ABS,ANINT,INT,LOG,MAX,MIN,NINT,REAL,SQRT
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     REAL AF
C        A FOR F
C     REAL BF
C        BARTLETT F
C     REAL BFRAT
C        BETWEEN GROUPS F RATIO
C     REAL BFSIG
C        BETWEEN GROUPS SIGNIFICANCE LEVEL
C     CHARACTER*1 BLANK
C        HOLLERITH BLANK
C     REAL BMS
C        BETWEEN MS
C     REAL BSS
C        BETWEEN SS
C     REAL B10(NG)
C        *
C     REAL C
C        INTERMEDIATE RESULT STORAGE
C     REAL CC
C        COCHRANS C
C     REAL CFKW
C        CORRECTION FACTOR FOR KRUSKAL-WALLIS
C     REAL DF
C        D.F.
C     REAL DFRAT
C        DEVIATIONS ABOUT LINE F RATIO
C     REAL DFSIG
C        DEVIATIONS ABOUT LINE F SIGNIFICANCE LEVEL
C     REAL DMS
C        DEVIATIONS ABOUT LINE MS
C     REAL DSS
C        DEVIATIONS ABOUT LINE SS
C     REAL F
C        BETWEEN MEANS F-TEST
C     REAL FMLC
C        FIXED MODEL LOWER CONFIDENCE LIMIT FOR MEAN
C     REAL FMUC
C        FIXED MODEL UPPER CONFIDENCE LIMIT FOR MEAN
C     REAL FNZW
C        *
C     REAL FPLM
C        LARGEST SINGLE PRECISION MAGNITUDE OF MACHINE
C     REAL FSTAT
C        F STATISTIC ASSOCIATED WITH KRUSKAL-WALLIS H STATISTIC
C     REAL F1KW
C        F1 D.F. FOR KRUSKAL-WALLIS F
C     REAL F2KW
C        F2 D.F. FOR KRUSKAL-WALLIS F
C     REAL GM
C        GRAND MEAN, MEAN OF ALL OBSERVATIONS WITH POSITIVE
C        NON-ZERO TAGS
C     REAL GPMAX(NG)
C        THE VECTOR OF MAXIMUM OBSERVATIONS FOR EACH GROUP
C     REAL GPMIN(NG)
C        THE VECTOR OF MINIMUM OBSERVATIONS FOR EACH GROUP
C     REAL GR
C        NZW-NG
C     REAL G1
C        NG-1
C     CHARACTER*1 HIGH
C        HOLLERITH + (PLUS)
C     REAL HSTAT
C        KRUSKAL-WALLIS H STATISTIC, SEE BROWNLEE(1965), PAGE 256
C     INTEGER I
C        INDEX VARIABLE
C     REAL IBAR
C        I BAR, ((SUM OF I)/NZW), WHERE I=1,NZW
C     INTEGER INDEX(N)
C        PERMUTATION VECTOR FOR Y AND LATER FOR TMEAN
C     INTEGER IPRT
C        THE OUTPUT LOGICAL UNIT NUMBER
C     CHARACTER*1 ISD
C        CAN CONTAIN FOLLOWING CHARACTERS FOR PRINTING
C        +, -,  (BLANK)
C     INTEGER ISZ
C        INTEGER SIZE OF GROUP
C     INTEGER ITEMP(NZW)
C        TEMPORARY INDEX VECTOR USED IN COMPUTING RANKS
C     INTEGER J
C        AN INDEX VARIABLE
C     INTEGER K
C        INDEX VARIABLE
C     INTEGER KK
C        POINTER TO THE LARGEST OF THE GROUP MEANS
C     INTEGER KKK
C        POINTER TO THE SMALLEST OF THE GROUP MEANS
C     REAL K0
C        = NZW - (SNC/NZW)/ G1
C     INTEGER L
C        INDEX VARIABLE
C     CHARACTER*1 LOW
C        HOLLERITH - (MINUS)
C     INTEGER M
C        INDEX VARIABLE
C     CHARACTER*1 MEAN
C        CAN CONTAIN FOLLOWING CHARACTERS FOR PRINTING
C        +, -,  (BLANK)
C     REAL MF
C        M FOR F
C     REAL MKW
C        M FOR KRUSKAL-WALLIS MEAN
C     INTEGER M28
C        USED IN CREATING BACKWARD LOOP
C     INTEGER M3
C        NG - 2
C     INTEGER M5
C        NZW - 1
C     INTEGER N
C        THE NUMBER OF OBSERVATIONS
C     INTEGER NG
C        THE NUMBER OF GROUPS OF DIFFERENT POSITIVE TAG VALUES
C     INTEGER NN
C        AID IN PRINTING VALUES
C     INTEGER NNE1
C        NUMBER OF GROUPS WITH SIZE NOT EQUAL TO ONE
C     INTEGER NPRT
C        THE VARAIBLE CONTROLLING AUTOMATIC PRINTOUT
C        IF NPRT = 0, PRINTOUT SUPPRESSED
C           OTHERWISE PRINTOUT PROVIDED
C     INTEGER NZPNTR
C        A POINTER TO THE FIRST NON-ZERO TAG IN THE SORTED TAG VECTOR
C     INTEGER NZW
C        THE NUMBER OF OBSERVATIONS WITH POSITIVE, NON-ZERO, TAG VALUES
C     REAL Q
C        SIGNIFICANCE LEVEL FROM F-DISTRIBUTION
C     REAL RANKS(NZW)
C        THE RANKS OF THE OBSERVATIONS WITH POSITIVE TAG VALUES
C     REAL RESSQ
C        RESIDUALS SQUARED
C     REAL RMLC
C        RANDOM MODEL LOWER CONFIDENCE LIMIT FOR MEAN
C     REAL RMUC
C        RANDOM MODEL UPPER CONFIDENCE LIMIT FOR MEAN
C     REAL RX
C        USED IN CALCULATING MANDEL APPROXIMATION
C     REAL SBMS
C        SQRT BETWEEN MS
C     REAL SC
C        SUM OF N(I) CUBED, WHERE N(I) IS THE SIZE OF GROUP I
C     REAL SFRAT
C        SLOPE F RATIO
C     REAL SFSIG
C        SLOPE F SIGNIFICANCE LEVEL
C     REAL SIGKW
C        SIGNIFICANCE LEVEL FOR KRUSKAL-WALLIS F (OR H)
C     REAL SLBF
C        SIGNIFICANCE LEVEL FOR BARLETT F
C     REAL SLCC
C        SIGNIFICANCE LEVEL FOR COCHRANS C
C     REAL SMAX
C        MAXIMUM OF S(I), WHERE S(I) IS THE STD. DEV. OF GROUP I
C     REAL SMIN
C        MINIMUM OF S(I)
C     REAL SMS
C        SLOPE MS
C     REAL SNC
C        SUM OF N(I) SQUARED
C     REAL SOS
C        SUM OF S(I) SQUARED
C     REAL SQB
C        SQRT BETWEEN MS / NZW
C     REAL SQMS
C        SQRT WITHIN MS / NZW
C     REAL SQOM
C        OMEGA HAT SQUARED
C     REAL SQT
C        SQRT TOTAL MS / NZW
C     REAL SRANK(NG)
C        THE SUM OF THE RANKS FOR THE OBSERVATIONS IN EACH GROUP
C     REAL SSF
C        S * SQRT(NG-1) * F
C     REAL SSS
C        SLOPE SS
C     REAL STATS(15)
C        VECTOR OF STATISTICS
C     REAL STMS
C        SQRT TOTAL MS
C     REAL SUM
C        INTERMIDIATE VALUE
C     REAL SWMS
C        SQRT WITHIN MS
C     REAL TAG(N)
C        VECTOR OF CLASSIFICATION VALUES - VALUES LESS THAN OR EQUAL
C        TO ZERO ARE IGNORED FOR ANALYSIS.  ON ENTERING TAG IS
C        SORTED SMALLEST TO LARGEST
C     REAL TIES
C        *
C     REAL TMEAN(NG)
C        THE MEAN OF EACH OF THE GROUPS
C     REAL TMS
C        TOTAL MS
C     REAL TSD(NG)
C        THE STANDARD DEVIATIONS OF EACH OF THE GROUPS (NOT THE
C        STANDARD DEVIATIONS OF THE MEANS)
C     REAL TSIZE(NG)
C        THE SIZE OF EACH OF THE DIFFERENT GROUPS
C     REAL TSS
C        TOTAL SS
C     REAL TVALUE(NG)
C        THE DIFFERENT POSITIVE (NON-ZERO) TAG VALUES ANALYZED, ORDERED
C        FROM SMALLEST TO LARGEST
C     REAL T1
C        T(.05,NG-1)
C     REAL T2
C        T(.05,NG-1)
C     REAL T3
C        T(.05,NZW-1)
C     REAL UMLC
C        UNGROUPED MODEL LOWER CONFIDENCE LIMIT FOR MEAN
C     REAL UMUC
C        UNGROUPED MODEL UPPER CONFIDENCE LIMIT FOR MEAN
C     REAL U1, U2
C        USED IN CALCULATING MANDEL APPROXIMATION
C     REAL VKW
C        U FOR KRUSKAL-WALLIS F
C     REAL VLS
C        LARGEST VARIANCE / SMALLEST VARIANCE
C     REAL V1, V2
C        USED IN CALCULATING MANDEL APPROXIMATION
C     REAL WMS
C        WITHIN MS
C     REAL WSS
C        WITHIN SS
C     REAL Y(N)
C        OBSERVATIONS TO BE ANALYZED, SORTED BY TAGS
C        Y(NZPNTR) IS THE FIRST ELEMENT WITH A NON-ZERO TAG
C     REAL YBMAX
C        MAXIMUM YBAR(I), WHERE YBAR(I) IS THE MEAN OF GROUP I
C     REAL YBMIN
C        MINIMUN YBAR(I)
C     REAL YMAX
C        MAXIMUM OBSERVATION
C     REAL YMIN
C        MINIMUM OBSERVATION
C
C     MACHINE DEPENDENT VARIABLE - FPLM
C
      DATA BLANK /' '/
      DATA LOW /'-'/
      DATA HIGH /'+'/
C
      CALL IPRINT(IPRT)
      FPLM = R1MACH(2)
      NZPNTR = N - NZW + 1
      FNZW = NZW
C
C     ZERO WORK VECTORS
C
      DO 10 I=1,NG
         TVALUE(I) = 0.0E0
         TSIZE(I) = 0.0E0
         TMEAN(I) = 0.0E0
         TSD(I) = 0.0E0
         SRANK(I) = 0.0E0
         GPMIN(I) = 0.0E0
         GPMAX(I) = 0.0E0
         B10(I) = 0.0E0
   10 CONTINUE
C
      DO 20 I=1,15
         STATS(I) = 0.0E0
   20 CONTINUE
C
      CC = 0.0E0
      F = 0.0E0
      DMS = 0.0E0
      BSS = 0.0E0
      WSS = 0.0E0
      HSTAT = 0.0E0
      MF = 0.0E0
      SC = 0.0E0
      SMAX = 0.0E0
      NNE1 = 0
      SNC = 0.0E0
      SOS = 0.0E0
      SSS = 0.0E0
      SUM = 0.0E0
      TIES = 0.0E0
      TSS = 0.0E0
      SMIN = 0.0E0
      SFRAT = 0.0E0
C
C     BEGIN COMPUTATIONS
C
C     COMPUTE RANKS
C
      CALL RANKO(NZW, Y(NZPNTR), ITEMP, RANKS, TIES)
C
C     INITIALIZE FIRST ELEMENT BEFORE ENTERING LOOP
C
      TVALUE(1) = TAG(NZPNTR)
      SRANK(1) = RANKS(1)
      GPMIN(1) = Y(NZPNTR)
      GPMAX(1) = Y(NZPNTR)
      YMIN = GPMIN(1)
      YMAX = GPMAX(1)
      GM = Y(NZPNTR)
      IBAR = 1.0E0
      TMEAN(1) = Y(NZPNTR)
      TSIZE(1) = 1.0E0
C
C     DETERMINE MEANS AND MAXS, DO SUMMATION PRIOR TO
C     DETERIMING MEANS
C     HERE J IS THE GROUP NUMBER
C
      J = 1
      L = NZPNTR + 1
      DO 50 I=L,N
         IF (TAG(I).EQ.TAG(I-1)) GO TO 40
C
C        NEW TAG GROUP
         J = J + 1
         TVALUE(J) = TAG(I)
         GPMIN(J) = Y(I)
         GPMAX(J) = Y(I)
   40    TSIZE(J) = TSIZE(J) + 1.0E0
         TMEAN(J) = TMEAN(J) + Y(I)
         GM = GM + Y(I)
C
C     UNNECESSARY COMPUTATIONS IF PRINTED OUTPUT IS SUPRESSED
C
         IF (NPRT.EQ.0) GO TO 50
         K = I + 1 - NZPNTR
         SRANK(J) = SRANK(J) + RANKS(K)
         GPMIN(J) = MIN(GPMIN(J),Y(I))
         GPMAX(J) = MAX(GPMAX(J),Y(I))
         YMIN = MIN(YMIN,GPMIN(J))
         YMAX = MAX(YMAX,GPMAX(J))
         IBAR = IBAR + J
   50 CONTINUE
C
C     CALCULATE MEANS
C
      GM = GM/FNZW
      IBAR = IBAR/FNZW
      K = NZPNTR-1
      I = NZPNTR
      DO 70 J=1,NG
         IF (TSIZE(J).GE.2.0E0) THEN
            TMEAN(J) = TMEAN(J)/TSIZE(J)
         ELSE
            TMEAN(J) = TMEAN(J)
         END IF
         K = K + INT(TSIZE(J))
C
C     L GIVES INDEX TO Y VALUE WITHIN GROUP J
C
         DO 60 L=I,K
            RESSQ = (Y(L)-TMEAN(J))*(Y(L)-TMEAN(J))
            TSD(J) = TSD(J) + RESSQ
            IF (NPRT.EQ.0) GO TO 60
            BSS = BSS + (TMEAN(J)-GM)*(TMEAN(J)-GM)
            WSS = WSS + RESSQ
            TSS = TSS + (Y(L)-GM)*(Y(L)-GM)
   60    CONTINUE
         I = K + 1
   70 CONTINUE
C
C     NOW DONE WITH TAG VECTOR, RETURN VECTOR TO INPUT ORDER
C     INDEX IS FREED FOR USE IN OTHER PLACES
C
      CALL SRTRRI(TAG, Y, N, INDEX)
C
C     CHECK FOR PRINTED OUTPUT
C
      IF (NPRT.NE.0) GO TO 90
C
C     COMPUTE STANDARD DEVIATIONS WITHIN EACH GROUP
C
      DO 80 J=1,NG
         TSD(J) = SQRT(TSD(J))
         IF (TSIZE(J).LE.1.0E0) GO TO 80
         TSD(J) = TSD(J)/SQRT(TSIZE(J)-1.0E0)
   80 CONTINUE
C
C     COMPUTATIONS COMPLETE FOR STORED OUTPUT - RETURN IF PRINTED
C     OUTPUT IS SUPRESSED
C
      RETURN
   90 YBMIN = TMEAN(1)
      YBMAX = TMEAN(1)
      SMIN = FPLM
C     HERE I IS THE GROUP NUMBER
      DO 120 I=1,NG
         IF (TSD(I).GT.0.0E0) THEN
            B10(I) = TSIZE(I)*(TSIZE(I)-1.0E0)/TSD(I)
            STATS(1) = STATS(1) + B10(I)*TMEAN(I)
            STATS(2) = STATS(2) + B10(I)
            IF (TSIZE(I).GT.1.0E0) THEN
               TSD(I) = SQRT(TSD(I)/(TSIZE(I)-1.0E0))
            END IF
            MF = MF + (TSIZE(I)-1.0E0)*LOG(TSD(I)*TSD(I))
         END IF
         IF (NINT(TSIZE(I)).GE.2) THEN
            NNE1 = NNE1 + 1
            SMAX = MAX(SMAX,TSD(I))
            SMIN = MIN(SMIN,TSD(I))
            STATS(3) = STATS(3) + 1.0E0/(TSIZE(I)-1.0E0)
         END IF
         SSS = SSS + TSIZE(I)*(I-IBAR)*(TMEAN(I)-GM)
         STATS(4) = STATS(4) + TSIZE(I)*(I-IBAR)*(I-IBAR)
C
C        LOOK FOR SMALLEST AND LARGEST MEANS (YBAR)
C
         YBMIN = MIN(YBMIN,TMEAN(I))
         YBMAX = MAX(YBMAX,TMEAN(I))
C
         HSTAT = HSTAT + SRANK(I)*SRANK(I)/TSIZE(I)
         SUM = SUM + 1.0E0/TSIZE(I)
         STATS(5) = STATS(5) + (TMEAN(I)-GM)*(TMEAN(I)-GM)
         SOS = SOS + TSD(I)*TSD(I)
         SNC = SNC + TSIZE(I)*TSIZE(I)
         SC = SC + TSIZE(I)*TSIZE(I)*TSIZE(I)
  120 CONTINUE
      IF (STATS(2).NE.0.0E0) STATS(1) = STATS(1)/STATS(2)
      IF (STATS(4).NE.0.0E0) SSS = SSS*SSS/STATS(4)
      DSS = BSS - SSS
C
C     DEGREES OF FREEDOM FOR ANOVA
C
      G1 = NG-1
      M3 = NG - 2
      GR = NZW-NG
      M5 = NZW - 1
C
C     MEAN SQUARES
C
      BMS = BSS/G1
      SMS = SSS
      IF (NG.GE.3) DMS = DSS/M3
      WMS = WSS/GR
      TMS = TSS/M5
      IF (WMS.EQ.BMS) THEN
         BFRAT = 1.0E0
         BFSIG = 1.0E0
      ELSE IF (WMS.EQ.0.0E0) THEN
         BFRAT = FPLM
         BFSIG = 0.0E0
      ELSE
         BFRAT = BMS/WMS
         BFSIG = 1.0E0 - CDFF(BFRAT,G1,GR)
      END IF
      IF (WMS.EQ.DMS) THEN
         DFRAT = 1.0E0
         DFSIG = 1.0E0
      ELSE IF (WMS.EQ.0.0E0) THEN
         DFRAT = FPLM
         DFSIG = 0.0E0
      ELSE
         DFRAT = DMS/WMS
         DFSIG = 1.0E0 - CDFF(DFRAT,REAL(M3),REAL(NZW-2))
      END IF
      IF (SMS.EQ.DSS+WSS) THEN
         SFRAT = 1.0E0
         SFSIG = 1.0E0
      ELSE IF (DSS+WSS.EQ.0.0E0) THEN
         SFRAT = FPLM
         SFSIG = 0.0E0
      ELSE
         SFRAT = SMS/((DSS+WSS)/(FNZW-2.0E0))
         SFSIG = 1.0E0 - CDFF(SFRAT,1.0E0,GR)
      END IF
C
C     COMPUTE FOR KRUSKAL-WALLIS TEST
C     FOR FORMULAE WITH CLEARER FORM AND NAMES SEE
C     KRAFT AND VAN EEDEN  A NON PARAMETRIC INTRODUCTION TO STATISTICS,
C     PP. 238 - 240
C
      STATS(6) = NZW*(NZW+1)
      HSTAT = (12.0E0*HSTAT/STATS(6)) - (3.0E0*(NZW+1))
      IF (TIES.EQ.NZW*NZW*NZW-NZW) THEN
         CFKW = 0.0E0
         HSTAT = 0.0E0
         MKW = 0.0E0
      ELSE
         CFKW = 1.0E0 - TIES/(NZW*NZW*NZW-NZW)
         HSTAT = HSTAT/CFKW
         MKW = ((NZW*NZW*NZW-SC)/STATS(6))/CFKW
      END IF
      VKW = 2.0E0*G1 -
     +   (0.4E0*(3*NG*M3+NZW*(2*NG*(NG-3)+1)))/STATS(6)
     +   - 6.0E0*SUM/5.0E0
      IF (MKW-HSTAT.GT.0.0E0 .AND.
     +    MKW.NE.0.0E0 .AND. VKW.NE.0.0E0) THEN
         F1KW = (G1*(G1*(MKW-G1)-VKW))/(0.5E0*VKW*MKW)
         F2KW = (MKW-G1)*F1KW/G1
         FSTAT = (HSTAT*(MKW-G1))/(G1*(MKW-HSTAT))
         SIGKW = 1.0E0 - CDFF(FSTAT,ANINT(F1KW),ANINT(F2KW))
      ELSE
         SIGKW = 0.0E0
      END IF
C
C     COMPUTE TOTAL STATISTICS
C
      SWMS = SQRT(WMS)
      SBMS = SQRT(STATS(5)/G1)
      STMS = SQRT(TMS)
      SQMS = SWMS/(SQRT(FNZW))
      SQB = SBMS/(SQRT(REAL(NG)))
      SQT = STMS/(SQRT(FNZW))
      T1 = PPFT(0.975E0,INT(GR))
      T2 = PPFT(0.975E0,INT(G1))
      T3 = PPFT(0.975E0,M5)
      FMLC = GM - SQMS*T1
      RMLC = GM - SQB*T2
      UMLC = GM - SQT*T3
      FMUC = GM + SQMS*T1
      RMUC = GM + SQB*T2
      UMUC = GM + SQT*T3
C
      SSF = SWMS*SQRT(G1*PPFF(0.95E0,INT(G1),INT(GR)))
C
C     TESTS FOR HOMOGENEITY OF VARIANCES
C
      IF (SMAX*SMAX.EQ.SOS) THEN
         CC = 1.0E0
      ELSE IF (SOS.NE.0.0E0) THEN
         CC = SMAX*SMAX/SOS
      END IF
      STATS(7) = ANINT(FNZW/NG)
      IF ((NNE1.GE.2) .AND. (CC.NE.1.0E0)) THEN
         SLCC = 1.0E0 -
     +          CDFF((NNE1-1)*CC/(1.0E0-CC),STATS(7),
     +          STATS(7)*(NNE1-1))
      ELSE
         SLCC = 1.0E0
      END IF
      SLCC = (NNE1-1)*SLCC
      IF (SLCC.GT.1.0E0) SLCC = 1.0E0
C
      IF (SMIN.EQ.SMAX) THEN
         VLS = 1.0E0
      ELSE
         IF (SMIN.EQ.0.0E0) THEN
            VLS = FPLM
         ELSE
            VLS = (SMAX/SMIN)*(SMAX/SMIN)
         END IF
      END IF
      IF (WMS.GT.0.0E0) MF = GR*LOG(WMS) - MF
      AF = (STATS(3)-(1.0E0/GR))/(3.0E0*G1)
      SLBF = 1.0E0
      IF ((AF.NE.1.0E0) .AND. (NNE1.GE.2)) THEN
         DF = (NNE1+1)/(AF*AF)
         BF = (DF*MF)/((NNE1-1)*(DF/(1.0E0-AF+(2.0E0/DF))-MF))
         IF (BF.LT.0.0E0) BF = 0.0E0
         SLBF = 1.0E0 - CDFF(BF,REAL(NNE1-1),ANINT(DF))
      ELSE
         BF = FPLM
      END IF
      K0 = (FNZW-(SNC/FNZW))/G1
      SQOM = (BMS-WMS)/K0
C
C     COMPUTATIONS ARE NOW COMPLETE ************************************
C
C     PRINT ANOVA
C
      NN = N - NZW
      WRITE (IPRT,1000) NN
      NN = NG - 1
      WRITE (IPRT,1010) NN, BSS, BMS, BFRAT, BFSIG
      IF (NG.LT.3) GO TO 180
      IF (BFSIG.GE..10) GO TO 180
      NN = 1
      WRITE (IPRT,1020) NN, SSS, SMS, SFRAT, SFSIG
      WRITE (IPRT,1030) M3, DSS, DMS, DFRAT, DFSIG
  180 NN = NZW - NG
      WRITE (IPRT,1040) NN, WSS, WMS
      WRITE (IPRT,1050) M5, TSS
C
C     PRINT KRUSKAL-WALLIS TEST
C
      WRITE (IPRT,1060) HSTAT, SIGKW
C
C     PRINT ESTIMATES
C
      WRITE (IPRT,1070)
      DO 200 I=1,NG
         MEAN = BLANK
         IF (TMEAN(I).LE.YBMIN) MEAN = LOW
         IF (TMEAN(I).GE.YBMAX) MEAN = HIGH
         ISD = BLANK
         IF (TSD(I).LE.SMIN) ISD = LOW
         IF (TSD(I).GE.SMAX) ISD = HIGH
         ISZ = INT(TSIZE(I))
         IF (ISZ.LE.1) THEN
            WRITE (IPRT,1090) TVALUE(I), ISZ, TMEAN(I), MEAN, GPMIN(I),
     +         GPMAX(I), SRANK(I)
         ELSE
            STATS(9) = TSD(I)/SQRT(TSIZE(I))
            STATS(10) = PPFT(0.975E0,ISZ-1)
            STATS(8) = TMEAN(I) - STATS(9)*STATS(10)
            STATS(11) = TMEAN(I) + STATS(9)*STATS(10)
            WRITE (IPRT,1080) TVALUE(I), ISZ, TMEAN(I), MEAN, TSD(I),
     +                        ISD, STATS(9), GPMIN(I), GPMAX(I),
     +                        SRANK(I), STATS(8), STATS(11)
         END IF
  200 CONTINUE
      WRITE (IPRT,1100) NZW, GM, YMIN, YMAX, SWMS, SQMS, FMLC, FMUC,
     +   SBMS, SQB, RMLC, RMUC, STMS, SQT, UMLC, UMUC
      IF (BFSIG.LT.0.10) THEN
C
C     SORT YBAR FOR MULTIPLE COMPARISIONS OF MEANS
C
         CALL GENI(INDEX, NG, 1, 1)
         CALL SRTIR(INDEX, NG, TMEAN)
C
C     COMPUTE AND PRINT FOR MULTIPLE COMPARISIONS
C
         IF (NZW-NG.LT.4) GO TO 270
         WRITE (IPRT,1110)
C
C     NEWMAN-KEULS-HARTLEY
C
         WRITE (IPRT,1120)
         RX = -.283917E0 + 2.63532E0*(GR-1.00123E0)**(-.95862E0)
         U1 = -.314115E0 + 2.38301E0*(GR-1.03428E0)**(-.864005E0)
         U2 = 3.65961E0*U1**2 - 1.00891E0*U1 - 0.166346E0
         J = 1
         M28 = 0
  210    I = NG
  220    IF (I.LE.M28) GO TO 260
         IF (I.EQ.J) GO TO 230
         STATS(14) = ABS(TMEAN(I)-TMEAN(J))
C
C     MANDEL APPROXMATION TO PERCENT POINT OF STUDENTIZED RANGE
C
         STATS(12) = I-J+1
         C = 2.3849867E0 -
     +       2.9051857E0*(STATS(12)-0.57583164E0)**(-.069648109E0)
         V1 = 1.30153E0 -
     +        1.95073E0*(STATS(12)+.394915E0)**(-.139783E0)
         V2 = 4.72863E0*V1**2 + 0.404271E0*V1 - 0.135104E0
         STATS(13) = 6.15075E0 + 4.441409E0*RX +
     +               6.7514569E0*C + 7.4671282E0*U1*V1 -
     +               0.157537E0*U2*V2
         KK = INDEX(I)
         KKK = INDEX(J)
         STATS(13) = STATS(13)*
     +            SQRT(0.5E0*((1.0E0/TSIZE(KK))+(1.0E0/TSIZE(KKK))))*
     +            SWMS
         IF (STATS(14).LE.STATS(13)) GO TO 230
         I = I - 1
         GO TO 220
  230    IF (J.EQ.1) GO TO 250
         IF (J.GT.M28) GO TO 240
         WRITE (IPRT,1150)
         GO TO 250
  240    WRITE (IPRT,1160)
  250    WRITE (IPRT,1140) (TMEAN(M),M=J,I)
         IF (I.GE.NG) GO TO 270
         M28 = I
  260    J = J + 1
         GO TO 210
C
C     SCHEFFE METHOD
C
  270    WRITE (IPRT,1130)
         J = 1
         M28 = 0
  280    I = NG
  290    IF (I.LE.M28) GO TO 330
         IF (I.EQ.J) GO TO 300
         KK = INDEX(I)
         KKK = INDEX(J)
         STATS(14) = ABS(TMEAN(I)-TMEAN(J))
         STATS(13) = SSF*SQRT((1.0E0/TSIZE(KK))+(1./TSIZE(KKK)))
         IF (STATS(14)-STATS(13).LE.0.0E0) GO TO 300
         I = I - 1
         GO TO 290
  300    IF (J.EQ.1) GO TO 320
         IF (J.GT.M28) GO TO 310
         WRITE (IPRT,1150)
         GO TO 320
  310    WRITE (IPRT,1160)
  320    WRITE (IPRT,1140) (TMEAN(M),M=J,I)
         IF (I.GE.NG) GO TO 340
         M28 = I
  330    J = J + 1
         GO TO 280
C
C     RETURN TAG MEANS TO ORIGINAL ORDER
C
  340    CALL SRTRI(TMEAN, NG, INDEX)
      END IF
C
      IF (NNE1.LE.1) RETURN
      WRITE (IPRT,1170) CC, SLCC, BF, SLBF, VLS
      IF (SLCC.GT.0.1E0 .AND. SLBF.GT.0.1E0) GO TO 390
      DO 360 I=1,NG
         F = F + B10(I)*(TMEAN(I)-STATS(1))*(TMEAN(I)-STATS(1))
         IF (STATS(2).EQ.0.0E0 .OR. TSIZE(I).LE.1.0E0) GO TO 360
         STATS(15) = STATS(15) +
     +               (1.0E0-B10(I)*B10(I)/STATS(2)/STATS(2))/
     +               (TSIZE(I)-1.0E0)
  360 CONTINUE
      IF (STATS(15).EQ.0.0E0) GO TO 370
      STATS(15) = (NG*NG-1.0E0)/(3.0E0*STATS(15))
      IF (NG.LE.1) GO TO 370
      F = (F/G1)/(1.0E0+(2.0E0*M3/(3.0E0*STATS(15))))
      GO TO 380
  370 F = 0.0E0
  380 Q = 1.0E0 - CDFF(F,G1,ANINT(STATS(15)))
      WRITE (IPRT,1180) F, Q
  390 WRITE (IPRT,1190) SQOM
      RETURN
C
C     AUTOMATIC PRINTOUT IS FINISHED
C
C     FORMAT STATEMENTS
C
 1000 FORMAT(54H *GROUP NUMBERS HAVE BEEN ASSIGNED ACCORDING TO TAG VA,
     +   59HLUES GIVEN, WHERE THE SMALLEST TAG GREATER THAN ZERO HAS BE,
     +   14HEN ASSIGNED  */41H *GROUP NUMBER 1, THE NEXT SMALLEST, GROU,
     +   59HP NUMBER 2, ETC.  TAGS LESS THAN OR EQUAL TO ZERO HAVE NOT ,
     +   27HBEEN INCLUDED IN ANALYSIS.*/28H *NUMBER OF VALUES EXCLUDED ,
     +   17HFROM ANALYSIS IS , I4, 77X, 1H*//17X, 6HSOURCE, 14X, 4HD.F.,
     +   4X, 14HSUM OF SQUARES, 5X, 12HMEAN SQUARES, 9X, 7HF RATIO, 4X,
     +   7HF PROB./)
 1010 FORMAT(17X, 14HBETWEEN GROUPS, 5X, I4, 1P2E18.6, 4X, 0PE11.3,
     +   F10.3)
 1020 FORMAT(20X, 5HSLOPE, 14X, I4, 1P2E18.6, 3X, 0PE11.3, F10.3)
 1030 FORMAT(20X, 16HDEVS. ABOUT LINE, 3X, I4, 1P2E18.6, 3X, 0PE11.3,
     +   F10.3)
 1040 FORMAT(17X, 13HWITHIN GROUPS, 6X, I4, 1P2E18.6)
 1050 FORMAT(17X, 5HTOTAL, 14X, I4, 1PE18.6//)
 1060 FORMAT(11X, 49HKRUSKAL-WALLIS RANK TEST FOR DIFFERENCE BETWEEN G,
     +   16HROUP MEANS * H =, E11.3, 10H, F PROB =, F6.3, 10H (APPROX.)
     +   /)
 1070 FORMAT(55X, 9HESTIMATES/96X, 6HSUM OF/5X, 5H TAG , 10X, 3HNO.,
     +   6X, 4HMEAN, 7X, 11HWITHIN S.D., 2X, 12HS.D. OF MEAN, 5X,
     +   7HMINIMUM, 7X, 7HMAXIMUM, 6X, 5HRANKS, 3X, 16H95PCT CONF INT F,
     +   7HOR MEAN/)
 1080 FORMAT(1X, 1PE14.6, I8, E14.5, A1, E13.5, A1, E13.5, 2E14.5,
     +   0PF9.1, 1PE13.5, 3H TO, E12.5)
 1090 FORMAT(1X, 1PE14.6, I8, E14.5, A1, 3X, 23H ESTIMATE NOT AVAILABLE,
     +   ' ', 2E14.5, 0PF9.1, 3X, 25H********** TO ********** )
 1100 FORMAT(/11X, 5HTOTAL, I7, 1PE14.5, 28X, 2E14.5//
     +   17X, 20HFIXED EFFECTS MODEL , 2E14.5, 37X, E13.5, 3H TO, E12.5/
     +   17X, 7HRANDOM ,
     +   13HEFFECTS MODEL, 2E14.5, 37X, E13.5, 3H TO, E12.5/17X,
     +   14HUNGROUPED DATA, 6X, 2E14.5, 37X, E13.5, 3H TO, E12.5/)
 1110 FORMAT(1X, 50HPAIRWISE MULTIPLE COMPARISON OF MEANS.  THE MEANS ,
     +   59HARE PUT IN INCREASING ORDER IN GROUPS SEPARATED BY *****.  ,
     +   11HA MEAN IS  /44H ADJUDGED NON-SIGNIFICANTLY DIFFERENT FROM A,
     +   59HNY MEAN IN THE SAME GROUP AND SIGNIFICANTLY DIFFERENT AT TH,
     +   17HE .05 LEVEL FROM /38H ANY MEAN IN ANOTHER GROUP.  ***** ***,
     +   59H** INDICATES ADJACENT GROUPS HAVE NO COMMON MEAN.          ,
     +   23H                       )
 1120 FORMAT(/3X, 49HNEWMAN-KEULS TECHNIQUE, HARTLEY MODIFICATION. (AP,
     +   40HPROXIMATE IF GROUP NUMBERS ARE UNEQUAL.))
 1130 FORMAT(/3X, 18HSCHEFFE TECHNIQUE.)
 1140 FORMAT(3X, 9(1PE12.5, ','))
 1150 FORMAT(6X, 5H*****)
 1160 FORMAT(3X, 11H***** *****)
 1170 FORMAT(/36H TESTS FOR HOMOGENEITY OF VARIANCES./7X, 9HCOCHRANS ,
     +   35HC = MAX. VARIANCE/SUM(VARIANCES) = , F7.4, 6H, P = , F6.3,
     +   10H (APPROX.)/7X, 17HBARTLETT-BOX F = , F9.3, 6H, P = ,
     +   F6.3/7X, 38HMAXIMUM VARIANCE / MINIMUM VARIANCE = , F14.4)
 1180 FORMAT(7X, 50HAPPROX BETWEEN MEANS F-TEST IN PRESENCE OF HETEROG,
     +   20HENEOUS VARIANCE. F =, F8.3, 5H, P =, F6.3)
 1190 FORMAT(/35H MODEL II - COMPONENTS OF VARIANCE./7X, 10HESTIMATE O,
     +   20HF BETWEEN COMPONENT , 1PE14.6)
      END
