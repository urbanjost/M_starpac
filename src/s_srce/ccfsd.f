*CCFSD
      SUBROUTINE CCFSD (CCOV11, CCOV22, SDRHOC, LAGMAX, NCC, N, ICCOV)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS IS THE MAIN SUBROUTINE FOR COMPUTING THE STANDARD ERROR
C     OF THE CROSS CORRELATIONS.
C
C     WRITTEN BY - JANET R. DONALDSON
C                  STATISTICAL ENGINEERING DIVISION
C                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE  -  NOVEMBER 21, 1980
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      INTEGER
     +   ICCOV,LAGMAX,N,NCC
C
C  ARRAY ARGUMENTS
      REAL
     +   CCOV11(ICCOV),CCOV22(ICCOV),SDRHOC(NCC)
C
C  LOCAL SCALARS
      REAL
     +   DIV,SUM
      INTEGER
     +   I,ILAST,K,K0,KM,KP
C
C  INTRINSIC FUNCTIONS
      INTRINSIC MIN,SQRT
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     REAL CCOV11(ICCOV), CCOV22(ICCOV)
C        THE ARRAYS IN WHICH THE AUTOCOVARIANCES ARE STORED
C     REAL DIV
C        THE SQUARE ROOT OF THE PRODUCT OF THE AUTOCOVARIANCE
C        FUNCTION VALUES AT LAG ZERO.
C     INTEGER I
C        AN INDEX VARIABLE.
C     INTEGER ICCOV
C        THE DIMENSION OF THE ACVF ARRAYS.
C     INTEGER ILAST
C        THE LAST LAG AT WHICH THE STANDARD ERROR IS TO BE COMPUTED.
C     INTEGER K
C        AN INDEX VARIALBE.
C     INTEGER KM, KP, K0
C        THE LOCATIONS IN THE ARRAYS -RHOC- AND -SDRHOC-
C        OF THE LAG -K, K AND 0, RESPECTIVELY.
C     INTEGER LAGMAX
C        THE MAXIMUM LAG VALUE USED.
C     INTEGER N
C        THE INTEGER NUMBER OF OBSERVATIONS IN THE SERIES
C     INTEGER NCC
C        THE NUMBER OF CCF COMPUTED.
C     REAL SDRHOC(NCC)
C        THE ARRAY IN WHICH THE STANDARD ERRORS OF THE CROSS CORRELATION
C        ARE STORED
C     REAL SUM
C         A SUMMING VARIABLE.
C
      K0 = LAGMAX + 1
C
      DIV = CCOV11(1) * CCOV22(1)
C
      SUM = 0.0E0
      DO 10 I = 1, LAGMAX
         SUM = SUM + CCOV11(I+1) * CCOV22(I+1)
   10 CONTINUE
      SUM = SUM / DIV
      SDRHOC(K0) = SQRT(N + 2.0E0 * SUM) / N
C
      DO 30 K = 1, LAGMAX
         SUM = 0.0E0
         ILAST = MIN(LAGMAX, N-LAGMAX)
         DO 20 I = 1, ILAST
            SUM = SUM + (N-K-I)*CCOV11(I+1)*CCOV22(I+1)
   20    CONTINUE
         SUM = SUM / DIV
         KM = K0 - K
         SDRHOC(KM) = SQRT((N - K) + 2.0E0 * SUM) / N
C
         KP = K0 + K
         SDRHOC(KP) = SDRHOC(KM)
C
   30 CONTINUE
C
      RETURN
      END
