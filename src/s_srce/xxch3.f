*XXCH3
      SUBROUTINE XXCH3(LDSTAK)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS SUBPROGRAM PROVIDES A SIMPLE TEST OF
C     THE NORMAL RANDOM NUMBER GENERATOR FAMILY OF ROUTINES.
C
C     WRITTEN BY  -  JOHN E. KOONTZ, JANET R. DONALDSON
C                    STATISTICAL ENGINEERING DIVISION
C                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE  -  AUGUST 3, 1987
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      INTEGER
     +   LDSTAK
C
C  SCALARS IN COMMON
      INTEGER
     +   IERR
C
C  LOCAL SCALARS
      REAL
     +   SIGMA,YMEAN
      INTEGER
     +   I,IPRT,ISEED,IYM,M,N
C
C  LOCAL ARRAYS
      REAL
     +   YM(50,2)
C
C  EXTERNAL SUBROUTINES
      EXTERNAL IPRINT,MVP,NRAND,NRANDC
C
C  COMMON BLOCKS
      COMMON /ERRCHK/IERR
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     INTEGER I
C        AN INDEX VARIABLE.
C     INTEGER IERR
C        FLAG TO INDICATE PRESENCE OF ERROR DETECTED BY PRECEDING
C        STARPAC CALL.  (0 IS OK, 1 IS ERROR)
C     INTEGER IPRT
C        LOGICAL OUTPUT UNIT.
C     INTEGER ISEED
C        THE SEED FOR THE RANDOM NUMBER GENERATOR.
C     INTEGER IYM
C        THE EXACT VALUE OF THE FIRST DIMENSION OF ARRAY YM.
C     INTEGER LDSTAK
C        A DUMMY VARIABLE FOR THIS TEST SUBPROGRAM.
C     INTEGER M
C        THE NUMBER OF SETS OF NUMBERS TO BE GENERATED
C     INTEGER N
C        THE NUMBER OF OBSERVATIONS TO BE GENERATED.
C     REAL SIGMA
C        THE S.D. OF THE SAMPLE.
C     REAL YM(50,2)
C        DATA VECTOR FOR TESTS.
C     REAL YMEAN
C        THE MEAN OF THE SAMPLE.
C
C
C     DATA INITIALIZATION
C
      IYM = 50
      ISEED = 531
      N = 50
      M = 2
      YMEAN = 4.0E0
      SIGMA = 0.5E0
C
C     DEFINE IPRT, THE CURRENT OUTPUT UNIT.
C
      CALL IPRINT(IPRT)
C
C     WRITE HEADING
C
      WRITE (IPRT,1000)
C
C     GENERATE STANDARD NORMAL PSEUDO-RANDOM NUMBERS INTO COLUMN 1 OF YM
C
      WRITE (IPRT,1100)
      CALL NRAND(YM(1,1), N, ISEED)
      WRITE (IPRT,2000) IERR
      WRITE (IPRT, 1400) (YM(I,1),I=1,N)
C
C     GENERATE NORMAL PSEUDO-RANDOM NUMBERS
C     WITH MEAN 4.0 AND STANDARD DEVIATION 0.5 INTO COLUMN 2 OF YM
C
      WRITE (IPRT,1200)
      CALL NRANDC(YM(1,2), N, ISEED, YMEAN, SIGMA)
      WRITE (IPRT,2000) IERR
      WRITE (IPRT, 1400) (YM(I,2),I=1,N)
C
C     PLOT RESULTS, SAMPLING EVERY OBSERVATION
C
      WRITE (IPRT,1500)
      CALL MVP (YM, N, M, IYM, 1)
C
      RETURN
C
C     FORMATS
C
 1000 FORMAT ('1*CH3')
 1100 FORMAT (' SIMPLE TEST OF NRAND')
 1200 FORMAT ('1SIMPLE TEST OF NRANDC')
 2000 FORMAT (/' THE VALUE OF IERR IS ', I4)
 1400 FORMAT (/' GENERATED RESULTS = '//(5E15.8))
 1500 FORMAT ('1MVP DISPLAY OF GENERATED RESULTS')
      END
