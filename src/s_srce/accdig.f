*ACCDIG
      SUBROUTINE ACCDIG(AX, X, AD, N)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     RETURNS NUMBER OF ACCURATE DIGITS, AD, IN AX AN APPROXIMATION TO X
C
C     WRITTEN BY DAVID HOGBEN, SEL, NBS.   10/29/69.
C
C     THIS ROUTINE WAS ADAPTED FROM THE OMNITAB ROUTINE ACCDIG
C     BY - -
C
C     JANET R. DONALDSON
C     STATISTICAL ENGINEERING DIVISION
C     NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE  -  DECEMBER 29, 1982
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      INTEGER
     +   N
C
C  ARRAY ARGUMENTS
      REAL
     +   AD(*),AX(*),X(*)
C
C  LOCAL SCALARS
      REAL
     +   ADMAX,DIFF,FPLRS
      INTEGER
     +   I
C
C  EXTERNAL FUNCTIONS
      REAL
     +   R1MACH
      EXTERNAL R1MACH
C
C  INTRINSIC FUNCTIONS
      INTRINSIC ABS,LOG10,MAX,MIN
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     REAL AD(N)
C        THE NUMBER OF ACCURATE DIGITS.
C     REAL ADMAX
C        THE NUMBER OF DIGITS CARRIED IN A FLOATING POINT COMPUTATION.
C     REAL AX(N)
C        THE APPROXIMATION TO THE SOLUTION.
C     REAL DIFF
C        *
C     REAL FPLRS
C        THE FLOATING POINT LARGEST RELATIVE SPACING.
C     INTEGER I
C        AN INDEX.
C     INTEGER N
C        THE NUMBER OF ELEMENTS BEING COMPARED.
C     REAL X(N)
C        THE SOLUTION.
C
C
      FPLRS = R1MACH(4)
C
      ADMAX = -LOG10(FPLRS)
C
      DO 20 I=1,N
         DIFF = AX(I) - X(I)
         IF (DIFF.NE.0.0E0) GO TO 10
         AD(I) = ADMAX
         GO TO 20
   10    AD(I) = 0.0E0
         IF (ABS(X(I)).GT.0.0E0) AD(I) = LOG10(ABS(X(I)))
         IF (ABS(DIFF).GT.0.0E0) AD(I) = -LOG10(ABS(DIFF)) + AD(I)
         AD(I) = MIN(ADMAX,AD(I))
         AD(I) = MAX(-ADMAX,AD(I))
   20 CONTINUE
      RETURN
      END
