*CCVFF
      SUBROUTINE CCVFF (YFFT1, YFFT2, N, NFFT, LAGMAX, CCOV12, CCOV21,
     +   ICCOV, LYFFT, WORK, LWORK)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS ROUTINE COMPUTES THE CROSS COVARIANCE FUNCTION
C     BETWEEN TWO SERIES.
C
C     WRITTEN BY - JANET R. DONALDSON
C                  STATISTICAL ENGINEERING DIVISION
C                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE  -  NOVEMBER 21, 1980
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      INTEGER
     +   ICCOV,LAGMAX,LWORK,LYFFT,N,NFFT
C
C  ARRAY ARGUMENTS
      REAL
     +   CCOV12(ICCOV),CCOV21(ICCOV),WORK(LWORK),YFFT1(LYFFT),
     +   YFFT2(LYFFT)
C
C  LOCAL SCALARS
      REAL
     +   FAC
      INTEGER
     +   I,ISN,J,LAG,NF,NF2,NFFT2
C
C  EXTERNAL SUBROUTINES
      EXTERNAL FFT,REALTR
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     REAL CCOV12(ICCOV), CCOV21(ICCOV)
C        THE ARRAYS IN WHICH THE CCVF FOR SERIES 1 LAGGED
C        BEHIND SERIES 2 AND VISA VERSA, RESPECTIVELY, ARE STORED.
C     REAL FAC
C        THE APPROPRIATE FACTOR USED TO SCALE THE CCVF.
C     INTEGER I
C        AN INDEX VARIABLE.
C     INTEGER ICCOV
C        THE DIMENSION OF THE ARRAYS CCOV12 AND CCOV21.
C     INTEGER LAG, LAGMAX
C        THE INDEXING VARIABLE INDICATING THE LAG VALUE OF THE
C        CROSS CORRELATION BEING COMPUTED, AND THE MAXIMUM LAG
C        VALUE TO BE USED.
C     INTEGER LWORK
C        THE DIMENSION OF THE VECTOR WORK.
C     INTEGER LYFFT
C        THE ACTUAL LENGTH OF THE ARRAYS YFFT1 AND YFFT2.
C     INTEGER N
C        THE INTEGER NUMBER OF OBSERVATIONS IN THE SERIES
C     INTEGER NF
C        THE NUMBER OF FOURIER FREQUENCIES.
C     INTEGER NFFT
C        THE NUMBER OF OBSERVATIONS IN THE EXTENDED SERIES.
C     INTEGER NFFT2
C        THE EFFECTIVE NUMBER OF OBSERVATIONS FOR THE FFT TRANSFORM.
C     INTEGER NF2
C        TWICE THE NUMBER OF FOURIER FREQUENCIES.
C     REAL WORK(LWORK)
C        THE WORK ARRAY NEEDED FOR THE COMPUTATIONS.
C     REAL YFFT1(LYFFT), YFFT2(LYFFT)
C        THE VECTORS CONTAINING THE OBSERVED SERIES ALREADY PROCESSED
C        BY ONE PASS OF A FFT.
C
      NFFT2 = (NFFT-2) / 2
      NF = NFFT2 + 1
      NF2 = NF * 2
C
C     COMPUTE THE CROSS COVARIANCES
C
      DO 10 I = 2, NF2, 2
         WORK(I-1) = YFFT1(I-1)*YFFT2(I-1) + YFFT1(I)*YFFT2(I)
         WORK(I) = YFFT1(I)*YFFT2(I-1) - YFFT1(I-1)*YFFT2(I)
   10 CONTINUE
C
      FAC = 1.0E0 / (4 * (NFFT-2) * N)
C
      ISN = -2
C
      CALL REALTR (WORK, WORK(2), NFFT2, ISN)
C
      CALL FFT (WORK, WORK(2), NFFT2, NFFT2, NFFT2, ISN)
C
      CCOV12(1) = WORK(1) * FAC
      CCOV21(1) = CCOV12(1)
C
      DO 20 LAG = 1, LAGMAX
         CCOV21(LAG+1) = WORK(LAG+1) * FAC
C
         J = NFFT - 1 - LAG
         CCOV12(LAG+1) = WORK(J) * FAC
   20 CONTINUE
C
      RETURN
      END
