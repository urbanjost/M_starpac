*SUMWTS
      SUBROUTINE SUMWTS(X, W, N, NNZW, ALPHA, SUMT1, SUMTW1, XTRM,
     +   XTRMW)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     A ROUTINE TO CALCULATE THE WEIGHTED AND UNWEIGHTED MEANS
C     FOR A SORTED VECTOR IN WHICH THE MIDTH ELEMENT IS THE
C     ELEMENT CLOSEST TO ZERO.  USED BY THE STAT FAMILY.
C
C     WRITTEN BY - JOHN E. KOONTZ
C                  STATISTICAL ENGINEERING DIVISION
C                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE  -  MAY 17, 1982
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      REAL
     +   ALPHA,SUMT1,SUMTW1,XTRM,XTRMW
      INTEGER
     +   N,NNZW
C
C  ARRAY ARGUMENTS
      REAL
     +   W(N),X(N)
C
C  LOCAL SCALARS
      REAL
     +   SUMW
      INTEGER
     +   CURHI,CURLO,I,IREV,ITHI,ITLO,MID,NALPHA,NUM
C
C  EXTERNAL SUBROUTINES
      EXTERNAL SUMBS
C
C  INTRINSIC FUNCTIONS
      INTRINSIC ABS,MAX,MIN
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     REAL ALPHA
C        INPUT PARAMETER.  THE PERCENTAGE OF POINTS TO TRIM
C        FROM EACH END OF THE ARRAY X FOR THE TRIMMED MEAN.
C     INTEGER CURHI
C        THE UPPER BOUND OF THE CURRENT INTERVAL.
C     INTEGER CURLO
C        THE LOWER BOUND OF THE CURRENT INTERVAL.
C     INTEGER I
C        LOOP PARAMETER.
C     INTEGER IREV
C        A VARIABLE WHICH RUNS IN THE SAME INTERVAL AS I, BUT
C        IN THE REVERSE ORDER.
C     INTEGER ITHI
C        THE COMPUTED UPPER BOUND SUCH THAT ALPHA PER
C        CENT OF THE POSITIVELY WEIGHTED POINTS ARE
C        OMITTED.
C     INTEGER ITLO
C        THE COMPUTED LOWER BOUND SUCH THAT ALPHA PER
C        CENT OF THE POSITIVELY WEIGHTED POINTS ARE
C        OMITTED.
C     INTEGER MID
C        THE INDEX OF THE ELEMENT IN X CLOSEST TO ZERO IN VALUE.
C        THE POINT OUT FROM WHICH THE SUMMING IS DONE.
C     INTEGER N
C        INPUT PARAMETER.  THE LENGTH OF THE ARRAY X.
C     INTEGER NALPHA
C          THE INTEGER WHICH IS ALPHA PER CENT OF N.
C     INTEGER NNZW
C        INPUT PARAMETER.  THE NUMBER OF POSITIVELY WEIGHTED
C        VALUES IN X.
C     INTEGER NUM
C        THE NUMBER OF POINTS OMITTED SO FAR.
C     REAL SUMTW1
C        OUTPUT PARAMETER.  THE WEIGHTED SUM OF THE TRIMMED ARRAY
C        X.
C     REAL SUMT1
C        OUTPUT PARAMETER.  THE UNWEIGHTED SUM OF THE TRIMMED
C        ARRAY.  ZERO-WEIGHTED ELEMENTS ARE OMITTED.
C     REAL SUMW
C        THE SUM OF THE WEIGHTS W(I).
C     REAL W(N)
C        INPUT PARAMETER.  THE ARRAY OF WEIGHTS.
C     REAL X(N)
C        INPUT PARAMETER.  THE DATA ARRAY X IN WHICH THE SUMS ARE TAKEN.
C     REAL XTRM
C        OUTPUT PARAMETER.  THE UNWEIGHTED, TRIMMED MEAN OF X.
C     REAL XTRMW
C        OUTPUT PARAMETER.  THE WEIGHTED, TRIMMED MEAN OF X.
C
C     SET UP LIMITS TRIMMING THE NUMBER OF NON-ZERO WEIGHTED
C     VALUES BY ALPHA PER CENT AT EACH END OF THE RANGE OF
C     VALUES.
C
      NALPHA = ALPHA*NNZW
      NUM = 0
      DO 10 I=1,N
         IF (W(I).EQ.0.0E0) GO TO 10
         NUM = NUM + 1
         IF (NUM.EQ.NALPHA+1) GO TO 20
   10 CONTINUE
   20 ITLO = I
      NUM = 0
      DO 30 I=1,N
         IREV = N - I + 1
         IF (W(IREV).EQ.0.0E0) GO TO 30
         NUM = NUM + 1
         IF (NUM.EQ.NALPHA+1) GO TO 40
   30 CONTINUE
   40 ITHI = IREV
      CALL SUMBS(X, N, ITLO, MID, ITHI)
C
C     INITIALIZE SUMMATION VARIABLES.
C
      SUMT1 = 0.0E0
      SUMTW1 = 0.0E0
      SUMW = 0.0E0
      IF (W(MID).EQ.0.0E0) GO TO 50
      SUMT1 = X(MID)
      SUMTW1 = W(MID)*X(MID)
      SUMW = W(MID)
   50 CURLO = MID - 1
      CURHI = MID + 1
C
C     SUM OUTWARDS, BOULDER, COLORADO FROM THE VALUE NEAREST ZERO.  THAT
C     THE LEAST IN MAGNITUDE TO THE GREATEST.
C
   60 IF (CURHI.GT.ITHI .OR. CURLO.LT.ITLO) GO TO 100
      IF (ABS(X(CURHI)).LT.ABS(X(CURLO))) GO TO 80
      IF (W(CURLO).EQ.0.0E0) GO TO 70
      SUMT1 = SUMT1 + X(CURLO)
      SUMTW1 = SUMTW1 + W(CURLO)*X(CURLO)
      SUMW = SUMW + W(CURLO)
   70 CURLO = CURLO - 1
      GO TO 60
   80 IF (W(CURHI).EQ.0.0E0) GO TO 90
      SUMT1 = SUMT1 + X(CURHI)
      SUMTW1 = SUMTW1 + W(CURHI)*X(CURHI)
      SUMW = SUMW + W(CURHI)
   90 CURHI = CURHI + 1
      GO TO 60
C
C     SUM UP ANY VALUES WHICH MAY REMAIN BECAUSE OF AN OFF
C     CENTER ZERO POINT.
C
  100 IF (CURHI.GT.ITHI) GO TO 120
      CURHI = MAX(CURHI,ITLO)
      DO 110 I=CURHI,ITHI
         IF (W(I).EQ.0.0E0) GO TO 110
         SUMT1 = SUMT1 + X(I)
         SUMTW1 = SUMTW1 + W(I)*X(I)
         SUMW = SUMW + W(I)
  110 CONTINUE
  120 IF (CURLO.LT.ITLO) GO TO 140
      CURLO = MIN(CURLO,ITHI)
      DO 130 IREV=ITLO,CURLO
         I = ITLO - IREV + CURLO
         IF (W(I).EQ.0.0E0) GO TO 130
         SUMT1 = SUMT1 + X(I)
         SUMTW1 = SUMTW1 + W(I)*X(I)
         SUMW = SUMW + W(I)
  130 CONTINUE
  140 XTRM = SUMT1/(NNZW-2*NALPHA)
      XTRMW = SUMTW1/SUMW
      RETURN
      END
