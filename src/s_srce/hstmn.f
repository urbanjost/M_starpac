*HSTMN
      SUBROUTINE HSTMN(Y, N, NCELLS, YLB, YUB, LSORT, YDIST)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS IS THE MAIN ROUTINE FOR PRODUCING A HISTOGRAM
C
C     ORIGINAL VERSION ADAPTED FROM AN EARLY VERSION OF MINITAB.
C
C     ADAPTED BY - JANET R. DONALDSON
C                  STATISTICAL ENGINEERING DIVISION
C                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE  -  MAY 17, 1982
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      REAL
     +   YLB,YUB
      INTEGER
     +   N,NCELLS
C
C  ARRAY ARGUMENTS
      REAL
     +   Y(N),YDIST(NCELLS)
      INTEGER
     +   LSORT(N)
C
C  LOCAL SCALARS
      REAL
     +   ALPHA,B1SQRT,B2,CFRACT,CFRCTM,CNTMX,FRACT,P,SCALE,SUM1,SUM2,
     +   SUM3,SUMD2,SUMD3,SUMD4,SUMDA,SUMT1,TEMP,WIDTH,XN,XNN,YINTMP,
     +   YMAX,YMDDSD,YMEAN,YMEANT,YMED,YMIDRG,YMIN,YRANGE,YSD,YVAR
      INTEGER
     +   I,IFLAG,IPRT,J,MID,NHIGH,NLOW,NOBS,NUM,NUMS
      CHARACTER
     +   IPLUS*1
C
C  LOCAL ARRAYS
      REAL
     +   A(6)
C
C  EXTERNAL SUBROUTINES
      EXTERNAL GENI,IPRINT,SRTIR,SRTRI,STAT1,SUMBS,SUMDS,SUMSS,SUMTS,
     +   VERSP
C
C  INTRINSIC FUNCTIONS
      INTRINSIC ABS,INT,MAX,MOD,NINT,SQRT
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     REAL A(6)
C        A VECTOR USED FOR PRINTING THE HISTOGRAM SCALE.
C     REAL ALPHA
C        THE PERCENTAGE TO BE TRIMMED OFF EACH END OF Y FOR THE
C        TRIMMED MEANS CALCULATIONS.
C     REAL B1SQRT
C        BETA ONE  -  A MEASURE OF SKEWNESS
C     REAL B2
C        BETA TWO  -  A MEASURE OF KURTOSIS
C     REAL CFRACT
C        THE CUMULATIVE DISTRIBUTION
C     REAL CFRCTM
C        THE REVERSE CUMULATIVE DISTRIBUTION
C     REAL CNTMX
C        THE SIZE OF THE LARGEST CELL COUNT
C     REAL FRACT
C        THE FRACTION OF THE OBSERVATIONS IN A GIVEN CELL
C     INTEGER I
C        AN INDEX
C     INTEGER IFLAG
C        IF 1, THEN MORE THAN 50 OBS. FELL IN A SINGLE CELL,
C        AND A SCALED HISTOGRAM WILL BE PROVIDED.
C     CHARACTER*1 IPLUS
C        THE CHARACTER +
C     INTEGER IPRT
C        THE NUMBER OF THE STANDARD OUTPUT UNIT.
C     INTEGER J
C        AN INDEX.
C     INTEGER LSORT(N)
C        THE PERMUTATION VECTOR.
C     INTEGER MID
C        THE INDEX OF THE (AN) ELEMENT OF Y CLOSEST TO ZERO, WHEN
C        Y HAS BEEN SORTED.
C     INTEGER N
C        INPUT PARAMETER.  THE LENGTH OF Y.
C     INTEGER NCELLS
C        THE NUMBER OF CELLS IN THE FREQUENCY DISTRIBUTION.
C     INTEGER NHIGH
C        THE INDEX OF THE LARGEST VALUE IN THE SORTED ARRAY
C        TO BE USED IN THE HISTOGRAM
C     INTEGER NLOW
C        THE INDEX OF THE SMALLEST VALUE IN THE SORTED ARRAY
C        TO BE USED IN THE HISTOGRAM
C     INTEGER NOBS
C        THE NUMBER OF OBSERVATIONS ACTUALLY USED IN THE HISTOGRAM
C     INTEGER NUM
C        THE CELL COUNT
C     INTEGER NUMS
C        THE SCALED CELL COUNT
C     REAL P
C        A VARIABLE USED TO DETERMINE THE SCALE
C     REAL SCALE
C        THE PRINTED INCREMENT ON THE HISTOGRAM SCALE
C     REAL SUMDA
C        THE SUM OF THE ABSOLUTE DIFFERENCES FROM THE MEAN.
C     REAL SUMD2
C        THE SUM OF THE SQUARES OF THE DIFFERENCES.
C     REAL SUMD3
C        THE SUM OF THE CUBES OF THE DIFFERENCES.
C     REAL SUMD4
C        THE SUM OF THE 4TH POWERS OF THE DIFFERENCES.
C     REAL SUMT1
C        THE SUM OF THE ALPHA TRIMMED ARRAY Y.
C     REAL SUM1, SUM2, SUM3
C        VARIOUS SUMS OF THE DATA.
C     REAL TEMP
C        A TEMPORARY STORAGE VARIABLE
C     REAL WIDTH
C        THE WIDTH OF A CELL
C     REAL XN
C        THE FOATING POINT REPRESENTATION OF N
C     REAL XNN
C        THE UNROUNDED NUMBER OF PLOTTING POSISTIONS ON A SCALES
C        HISTOGRAM
C     REAL Y(N)
C        INPUT PARAMETER.  THE VECTOR OF DATA POINTS ON WHICH
C        THE STATISTICS ARE COMPUTED.  Y IS SORTED, BUT RESTORED
C        TO ITS ORIGINAL ORDER AFTERWARDS.
C     REAL YDIST(NCELLS)
C        THE FREQUENCY DISTRIBUTION USED TO CREATE THE HISTOGRAM.
C     REAL YINTMP
C        THE MIDPOINT OF THE ITH CELL
C     REAL YLB
C        THE LOWER BOUND FOR SELECTING DATA FROM Y FOR THE HISTOGRAM.
C     REAL YMAX
C        THE HISTOGRAM UPPER BOUND USED
C     REAL YMDDSD
C        THE MEAN ABSOLUTE DEVIATION / THE STANDARD DEVIATION
C     REAL YMEAN, YMEANT
C        THE MEAN OF THE OBSERVATIONS USED IN THE HISTOGRAM
C     REAL YMED
C        THE MEDIAN OF THE OBSERVATIONS USED IN THE HISTOGRAM
C     REAL YMIDRG
C        THE MID RANGE OF THE OBSERVATIONS USED IN THE HISTOGRAM
C     REAL YMIN
C        THE HISTOGRAM LOWER BOUND USED.
C     REAL YRANGE
C        THE RANGE OF THE OBSERVATIONS USED IN THE HISTOGRAM
C     REAL YSD
C        THE STANDARD DEVIATION OF THE OBSERVATIONS USED IN THE
C        HISTOGRAM.
C     REAL YUB
C        THE UPPER BOUND FOR SELECTING DATA FROM Y FOR THE HISTOGRAM.
C     REAL YVAR
C        THE VARIANCE OF THE OBSERVATIONS.
C
      DATA IPLUS /'+'/
      DATA ALPHA/0.25E0/
C
      CALL IPRINT(IPRT)
C
C     SORT DATA
C
      CALL GENI(LSORT, N, 1, 1)
      CALL SRTIR(LSORT, N, Y)
C
C     FIX UPPER AND LOWER BOUNDS.
C
      NLOW = 1
      NHIGH = N
      IF (YLB.EQ.YUB) GO TO 50
C
C     FIND INDEX OF THE FIRST VALUE OF Y .GE. YLB
C
      DO 20 I=1,N
         IF (Y(I).LT.YLB) GO TO 20
         NLOW = I
         GO TO 30
   20 CONTINUE
C
C     FIND INDEX OF THE LAST VALUE OF Y .LE. YUB
C
   30 DO 40 I=1,N
         J = N - I + 1
         IF (Y(J).GT.YUB) GO TO 40
         NHIGH = J
         GO TO 50
   40 CONTINUE
   50 CONTINUE
      XN = NHIGH-NLOW+1
      NOBS = NHIGH - NLOW + 1
C
C     COMPUTE MEDIAN, EXTREMA, MID-RANGE, RANGE AND FREQUENCY
C     DISTRIBUTION FOR NCELLS CELLS
C
      CALL STAT1(Y(NLOW), NOBS, YMED, YMIN, YMAX, YMIDRG, YRANGE,
     +   NCELLS, YLB, YUB, YDIST)
C
      IF (YLB.GE.YUB) GO TO 55
C
      YMIN = YLB
      YMAX = YUB
C
   55 CONTINUE
C
C     COMPUTE MEAN, TRIMMED MEAN, STANDARD DEVIATION,
C     MEAN DEVIATION/STANDARD DEVIATION, BETA ONE, AND BETA TWO
C
      CALL SUMBS(Y, N, NLOW, MID, NHIGH)
      CALL SUMSS(Y, N, NLOW, MID, NHIGH, SUM1, SUM2, SUM3, YMEAN)
      CALL SUMTS(Y(NLOW), NOBS, ALPHA, SUMT1, YMEANT)
      CALL SUMDS(Y, N, NLOW, MID, NHIGH, YMEAN, SUMDA, SUMD2, SUMD3,
     +   SUMD4)
C
      YVAR = 0.0E0
      YSD = 0.0E0
      B1SQRT = 0.0E0
      B2 = 0.0E0
      YMDDSD = 0.0E0
C
      IF ((SUMD2.LE.0.0E0) .OR. (NOBS.LE.1)) GO TO 60
C
      YVAR = SUMD2/(NOBS-1)
      YSD = SQRT(YVAR)
      B1SQRT = ABS((SUMD3/XN)/((SUMD2/XN)**1.5E0))
      B2 = (SUMD4/XN)/((SUMD2/XN)**2)
      YMDDSD = SUMDA/(YSD*NOBS)
C
   60 CONTINUE
C
C     OUTPUT STATISTICS
C
      CALL VERSP(.TRUE.)
      WRITE (IPRT,1070) N, Y(NLOW), Y(NHIGH), YMIN, YMAX
      WRITE (IPRT,1000)
     +   NCELLS, NOBS, YMEANT, Y(NLOW), YSD, Y(NHIGH), YMDDSD,
     +   YMEAN, B1SQRT, YMED, B2
      WRITE (IPRT,1010)
C
C     CHECK FOR MORE THAN 50 VALUES IN INTERVAL AND FIND MAX. VALUE.
C
      IFLAG = 0
      CNTMX = 0.0E0
      DO 80 I=1,NCELLS
         IF (YDIST(I).GT.CNTMX) CNTMX = YDIST(I)
   80 CONTINUE
      IF (NINT(CNTMX).GT.50) IFLAG = 1
C
C     DETERMINE SCALE.
C
      IF (IFLAG.EQ.0) THEN
         SCALE = 1.0E0
      ELSE
         P = CNTMX/XN
         SCALE = 0.05E0
         IF (P.GT.0.25E0) SCALE = 0.1E0
         IF (P.GT.0.5E0) SCALE = 0.2E0
      END IF
C
C     PRINT COLUMN HEADINGS AND HISTOGRAM SCALE.
C
      IF (IFLAG.EQ.0) WRITE (IPRT,1020)
      IF (IFLAG.NE.0) WRITE (IPRT,1080)
      WRITE (IPRT,1090)
      IF (IFLAG.NE.0) GO TO 100
      WRITE (IPRT,1030) (I,I=10,50,10)
      GO TO 120
  100 A(1) = 0.0E0
      DO 110 I=1,5
         A(I+1) = A(I) + SCALE
  110 CONTINUE
      WRITE (IPRT,1040) (A(I),I=1,6)
  120 WRITE (IPRT,1050)
      CFRACT = 0.0E0
      CFRCTM = 1.0E0
      TEMP = 0.0E0
      WIDTH = (YMAX-YMIN)/NCELLS
      YINTMP = YMIN
      YINTMP = YINTMP - WIDTH/2.0E0
      DO 150 I=1,NCELLS
         NUM = INT(YDIST(I)+0.5E0)
         IF (MOD(NCELLS,2).EQ.1 .AND. I.EQ.NCELLS/2+1
     +       .AND. YMIN.EQ.(-YMAX)) THEN
            YINTMP = 0.0E0
         ELSE
            YINTMP = YINTMP + WIDTH
         END IF
         FRACT = YDIST(I)/XN
         CFRACT = CFRACT + FRACT
         CFRCTM = 1.0E0 - TEMP
         TEMP = CFRACT
         IF (NUM.LE.0) THEN
           WRITE (IPRT,1060) YINTMP, CFRACT, CFRCTM, FRACT, NUM
         ELSE
           IF (IFLAG.EQ.0) THEN
             NUMS = NUM
           ELSE
             XNN = FRACT*10.0E0/SCALE
             NUMS = INT(XNN)
             NUMS = MAX(1, NUMS + INT(XNN-NUMS+0.5E0))
           END IF
           WRITE (IPRT,1060) YINTMP, CFRACT, CFRCTM, FRACT,
     +        NUM, (IPLUS,J=1,NUMS)
         END IF
  150 CONTINUE
C
C     RESTORE DATA TO ORIGINAL ORDER
C
      CALL SRTRI(Y, N, LSORT)
C
      RETURN
C
C     FORMAT STATEMENTS
C
 1000 FORMAT(/26H NUMBER OF CELLS        = , I15/
     +        26H OBSERVATIONS USED      = , I15, 11X,
     +              22H25 PCT TRIMMED MEAN = , 1PE15.8/
     +        26H MIN. OBSERVATION USED  = , E15.8, 11X,
     +              22HSTANDARD DEVIATION  = , E15.8/
     +        26H MAX. OBSERVATION USED  = , E15.8, 11X,
     +              22HMEAN DEV./STD. DEV. = , E15.8/
     +        26H MEAN VALUE             = , E15.8, 11X,
     +              22HSQRT(BETA ONE)      = , E15.8/
     +        26H MEDIAN VALUE           = , E15.8, 11X,
     +              22HBETA TWO            = , E15.8)
 1010 FORMAT(//44H FOR A NORMAL DISTRIBUTION, THE VALUES (MEAN,
     +   56H DEVIATION/STANDARD DEVIATION), SQRT(BETA ONE), AND BETA,
     +   22H TWO ARE APPROXIMATELY/
     +   ' 0.8, 0.0 AND 3.0, RESPECTIVELY.  TO TEST THE ',
     +   59HNULL HYPOTHESIS OF NORMALITY, SEE TABLES OF CRITICAL VALUES,
     +   13H PP. 207-208,/  22H BIOMETRIKA TABLES FOR,
     +   58H STATISTICIANS, VOL. 1.  SEE PP. 67-68 FOR A DISCUSSION OF,
     +   13H THESE TESTS.)
 1020 FORMAT(///5X,39HINTERVAL     CUM.   1-CUM.   CELL   NO.,19X,
     + 22HNUMBER OF OBSERVATIONS)
 1030 FORMAT('+',47X,1H0,8X,5(I2,8X))
 1040 FORMAT('+',46X,6(F4.2,6X))
 1050 FORMAT(4X,42('-'),2X,'+',5(10H---------+))
 1060 FORMAT(3X,1PE13.6,2X,2(0PF5.3,3X),F5.3,1X,I5,4X,50A1)
 1070 FORMAT (10H HISTOGRAM//
     +        26H NUMBER OF OBSERVATIONS = , I15/
     +        26H MINIMUM OBSERVATION    = , 1PE15.8/
     +        26H MAXIMUM OBSERVATION    = , E15.8//
     +        26H HISTOGRAM LOWER BOUND  = , E15.8/
     +        26H HISTOGRAM UPPER BOUND  = , E15.8)
 1080 FORMAT(///5X,39HINTERVAL     CUM.   1-CUM.   CELL   NO.,23X,
     + 13HCELL FRACTION)
 1090 FORMAT(5X,41HMID POINT   FRACT.  FRACT.  FRACT.  OBS. )
      END
