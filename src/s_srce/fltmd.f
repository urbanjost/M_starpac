*FLTMD
      SUBROUTINE FLTMD (X, Y, N, KMD, SYM)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS ROUTINE APPLIES ONE MODIFIED DANIEL FILTERS TO A SYMMETRIC
C     SERIES.  THIS ROUTINE IS ADAPTED FROM BLOOMFIELDS ROUTINE MODDAN.
C
C     WRITTEN BY  -  JANET R. DONALDSON
C                    STATISTICAL ENGINEERING DIVISION
C                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE  -  DECEMBER 7, 1981
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      REAL
     +   SYM
      INTEGER
     +   KMD,N
C
C  ARRAY ARGUMENTS
      REAL
     +   X(N),Y(N)
C
C  LOCAL SCALARS
      REAL
     +   CON
      INTEGER
     +   I,J,KUSED,LIM
C
C  EXTERNAL FUNCTIONS
      REAL
     +   EXTEND
      EXTERNAL EXTEND
C
C  INTRINSIC FUNCTIONS
      INTRINSIC MOD
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     REAL CON
C        A FACTOR USED IN THE COMPUTATIONS.
C     INTEGER I
C        AN INDEXING VARIABLE.
C     INTEGER J
C        AN INDEXING VARIABLE.
C     INTEGER KMD
C        THE INPUT FILTER LENGTH.
C     INTEGER KUSED
C        THE FILTER LENGTH ACTUALLY USED.
C     INTEGER LIM
C        A LOOP LIMIT.
C     INTEGER N
C        THE NUMBER OF POINTS IN THE SERIES TO BE FILTERED.
C     REAL SYM
C        AN INDICATOR VARIABLE USED TO DESIGNATE WHETHER THE SERIES
C        IS SYMMETRIC (SYM = 1.0E0) OR NOT (SYM = -1.0E0).
C     REAL X(N)
C        ON INPUT, THE SERIES TO BE FILTERED.  ON OUTPUT, THE
C        SMOOTHED SERIES.
C     REAL Y(N)
C        ON INPUT, A WORK VECTOR.  ON OUTPUT, THE INPUT SERIES X.
C
C
      DO 10 I = 1, N
         Y(I) = X(I)
   10 CONTINUE
C
      IF (KMD .LE. 0) RETURN
C
      KUSED = KMD + MOD(KMD,2)
      IF (KUSED .GT. N) KUSED = KUSED - 2
C
      LIM = KUSED-1
      CON = 1.0E0 / (2*KUSED)
      DO 40 I = 1, N
         X(I) = Y(I)
         IF (LIM .EQ. 0) GO TO 30
         DO 20 J = 1, LIM
            X(I) = X(I) + EXTEND(Y, I-J, N, SYM)
     +         + EXTEND(Y, I+J, N, SYM)
   20    CONTINUE
   30    X(I) = (X(I) + (EXTEND(Y, I-KUSED, N, SYM)
     +      + EXTEND(Y, I+KUSED, N, SYM)) * 0.5E0) * CON
   40 CONTINUE
C
      RETURN
C
      END
