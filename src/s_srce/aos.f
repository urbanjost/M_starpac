*AOS
      SUBROUTINE AOS (N, LAGMAX, ACOV, PRHO, IAR, OSPVAR, PHI, WORK,
     +   AIC, FTEST, LACOV, LAIC)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS ROUTINE COMPUTES AUTOREGRESSIVE MODEL ORDER SELECTION
C     STATISTICS. IT PERFORMS STEPWISE FITTING OF AUTOREGRESSIVE
C     COEFFICIENTS BY DURBINS METHOD USING AKAIKES AIC CRITERION
C     FOR SELECTING ORDER.   THE ROUTINE IS MODELED AFTER
C     SUBROUTINE UFPE WRITTEN BY DICK JONES.
C
C     WRITTEN BY - JANET R. DONALDSON
C                  STATISTICAL ENGINEERING DIVISION
C                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE  -  NOVEMBER 21, 1980
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      REAL
     +   OSPVAR
      INTEGER
     +   IAR,LACOV,LAGMAX,LAIC,N
C
C  ARRAY ARGUMENTS
      REAL
     +   ACOV(LACOV),AIC(LAIC),FTEST(2,LAGMAX),PHI(LAGMAX),
     +   PRHO(LAGMAX),WORK(LAGMAX)
C
C  LOCAL SCALARS
      REAL
     +   ACOV0,AICMIN,FPLM,RSS,RSSMIN,SQPACF
      INTEGER
     +   I,J
C
C  EXTERNAL FUNCTIONS
      REAL
     +   CDFF,R1MACH
      EXTERNAL CDFF,R1MACH
C
C  EXTERNAL SUBROUTINES
      EXTERNAL ARCOEF
C
C  INTRINSIC FUNCTIONS
      INTRINSIC LOG,REAL
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     REAL ACOV(LACOV), ACOV0
C        THE AUTOCOVARIANCES FOR LAGS ZERO TO LAGMAX, AND THE
C        AUTOCOVARIANCE AT LAG ZERO.
C     REAL AIC(LAIC), AICMIN
C        THE ARRAY CONTANING AKIAKES CRITERIA FOR EACH ORDER, WHERE
C        AIC(I+1) IS THE CRITERIA FOR ORDER I-1, AND THE MINIMUM
C        CRITERIA COMPUTED.
C     REAL FPLM
C        THE FLOATING POINT LARGEST MAGNITUDE.
C     REAL FTEST(2, LAGMAX)
C        THE ARRAY IN WHICH THE F PERCENTAGE POINT AND PROBABILITY ARE
C        STORED.
C     INTEGER I
C        AN INDEX VARIABLE.
C     INTEGER IAR
C        THE ORDER OF THE AUTOREGRESSIVE PROCESS CHOSEN.
C     INTEGER J
C        AN INDEX VARIABLE.
C     INTEGER LACOV
C        THE LENGTH OF THE VECTOR ACOV.
C     INTEGER LAGMAX
C        THE MAXIMUM LAG VALUE TO BE USED.
C     INTEGER LAIC
C        THE LENGTH OF THE VECTOR AIC.
C     INTEGER N
C        THE INTEGER NUMBER OF OBSERVATIONS IN EACH SERIES
C     REAL OSPVAR
C        THE ONE STEP PREDICTION VARIANCE FOR THE ORDER SELECTED (IAR).
C     REAL PHI(LAGMAX)
C        THE ARRAY OF AUTOREGRESSIVE COEFFICIENTS FOR THE
C        SELECTED ORDER.
C     REAL PRHO(LAGMAX)
C        THE ARRAY CONTAINING THE PARTIAL AUTOCORRELATION
C        COEFFICIENTS.
C     REAL RSS, RSSMIN
C        THE ONE STEP PREDICTION RESIDUAL SUM OF SQUARES AND THE
C        MINIMUM ONE STEP PREDICTION RESIDUAL SUM OF SQUARES.
C     REAL SQPACF
C        THE SQUARED VALUE OF THE PARTIAL AUTOCORRELATION FUNCTION AT
C        LAG I.
C     REAL WORK(LAGMAX)
C        A REAL WORK AREA.
C
      FPLM = R1MACH(2)
C
      RSS = ACOV(1) * N
      RSSMIN = RSS
      AIC(1) = N * LOG(RSS * (N+1) / (N-1))
      AICMIN = AIC(1)
      IAR = 0
C
C  START STEPWISE PROCEDURE
C
      WORK(1) = ACOV(2) / ACOV(1)
      PRHO(1) = WORK(1)
      RSS = RSS * (1.0E0 - WORK(1)*WORK(1))
      AIC(2) = N * LOG(RSS * (N+2) / (N-2))
C
      SQPACF = WORK(1) * WORK(1)
      FTEST(1, 1) = FPLM
      FTEST(2, 1) = 0.0E0
      IF (SQPACF .GE. 1.0E0) GO TO 5
C
      FTEST(1,1) = (N-2) * SQPACF / (1.0E0 - SQPACF)
C
      FTEST(2,1) = 1.0E0 - CDFF(FTEST(1,1), 1.0E0, REAL(N-2))
C
    5 CONTINUE
C
      IF (AIC(2).GE.AICMIN) GO TO 10
      AICMIN = AIC(2)
      RSSMIN = RSS
      IAR = 1
      PHI(1) = WORK(1)
C
   10 IF (LAGMAX.LE.1) GO TO 40
C
      ACOV0 = ACOV(1)
C
      DO 30 I=2,LAGMAX
         CALL ARCOEF (ACOV(2), WORK, RSS, I, LAGMAX, ACOV0)
         PRHO(I) = WORK(I)
         AIC(I+1) = FPLM
         FTEST(1,I) = FPLM
         FTEST(2,I) = FPLM
         IF (I.EQ.N-1) GO TO 15
C
         AIC(I+1) = N * LOG(RSS * (N+I+1) / (N-I-1))
C
         SQPACF = WORK(I) * WORK(I)
         IF (SQPACF .GE. 1.0E0) GO TO 15
C
         FTEST(1,I) = (N-I-1) * SQPACF / (1.0E0 - SQPACF)
C
         FTEST(2,I) = 1.0E0 - CDFF(FTEST(1,I), 1.0E0, REAL(N-I-1))
C
   15    CONTINUE
C
C     IF THIS AIC IS A MINIMUM AND ITS LAG DOES NOT EXCEED N/2,
C     SAVE THE COEFFICIENTS.
C
         IF ((AIC(I+1).GE.AICMIN) .OR. (I.GT.N/2)) GO TO 30
         AICMIN = AIC(I+1)
         RSSMIN = RSS
         IAR = I
         DO 20 J=1,I
            PHI(J) = WORK(J)
   20    CONTINUE
   30 CONTINUE
C
C   NORMALIZE AIC
C
   40 CONTINUE
      AIC(1) = AIC(1) - AICMIN
      DO 50 I=1,LAGMAX
         AIC(I+1) = AIC(I+1) - AICMIN
   50 CONTINUE
C
      OSPVAR = RSSMIN / (N-IAR-1)
C
      RETURN
      END
