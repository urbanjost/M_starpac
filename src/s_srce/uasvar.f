*UASVAR
      SUBROUTINE UASVAR (Y, YMEAN, N, IAR, PHI, VAR)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS ROUTINE COMPUTES THE VARIANCE FOR A GIVEN SERIES
C     AND AUTOREGRESSIVE MODEL.
C
C     WRITTEN BY  -  JANET R. DONALDSON
C                    STATISTICAL ENGINEERING DIVISION
C                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE  -  DECEMBER 7, 1981
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      REAL
     +   VAR,YMEAN
      INTEGER
     +   IAR,N
C
C  ARRAY ARGUMENTS
      REAL
     +   PHI(IAR),Y(N)
C
C  LOCAL SCALARS
      REAL
     +   RES,RSS
      INTEGER
     +   I,IAR1,J,K
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     INTEGER I
C        AN INDEX VARIABLE.
C     INTEGER IAR
C        THE ORDER OF THE AUTOREGRESSIVE PROCESS CHOSEN.
C     INTEGER IAR1
C        THE VALUE IAR + 1.
C     INTEGER J, K
C        INDEX VALUES.
C     INTEGER N
C        THE INTEGER NUMBER OF OBSERVATIONS IN EACH SERIES
C     REAL PHI(IAR)
C        THE ARRAY OF AUTOREGRESSIVE COEFFICIENTS FOR THE
C        SELECTED ORDER.
C     REAL RES
C        *
C     REAL RSS
C        THE ONE STEP PREDICTION RESIDUAL SUM OF SQUARES.
C     REAL VAR
C        THE ONE STEP PREDICTION VARIANCE.
C     REAL Y(N)
C         THE ARRAY CONTAINING THE OBSERVED TIME SERIES.
C     REAL YMEAN
C        THE MEAN OF THE OBSERVED TIME SERIES
C
      RSS = 0.0E0
      IAR1 = IAR+1
      DO 20 I = IAR1, N
         RES = Y(I) - YMEAN
         DO 10 J = 1, IAR
            K = I-J
            RES = RES - PHI(J) * (Y(K)-YMEAN)
   10    CONTINUE
         RSS = RSS + RES*RES
   20 CONTINUE
C
      VAR = RSS / (N-IAR1)
C
      RETURN
C
      END
