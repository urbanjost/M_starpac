*SPCCK
      SUBROUTINE SPCCK (SPC, ISORT, NF, SPCMN, SPCMX, NSPC, ISPCER)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS ROUTINE ANALYZES THE ORDINATES FOR THE SPECTRAL SEMI LOG
C     PLOTS PRODUCED BY THE ASPC SERIES OF ROUTINES.  Y AXIS VALUES
C     (YORD) MORE THAN 3 POWERS OF TEN LESS THAN THE NEXT LARGER
C     VALUE ARE CONSIDERED INSIGNIFICANT AND ARE CULLED FROM THE
C     ORDINATES.
C
C     WRITTEN BY - JANET R. DONALDSON
C                  STATISTICAL ENGINEERING DIVISION
C                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE  -  NOVEMBER 26, 1980
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      REAL
     +   SPCMN,SPCMX
      INTEGER
     +   ISPCER,NF,NSPC
C
C  ARRAY ARGUMENTS
      REAL
     +   SPC(NF)
      INTEGER
     +   ISORT(NF)
C
C  LOCAL SCALARS
      INTEGER
     +   I,K1,K2,LOG1,LOG2
C
C  EXTERNAL SUBROUTINES
      EXTERNAL SRTIR,SRTRI
C
C  INTRINSIC FUNCTIONS
      INTRINSIC LOG10
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     INTEGER I
C        AN INDEX VARIABLE
C     INTEGER ISORT(NF)
C        THE ARRAY IN WHICH THE ORDER PERMUTATION FOR THE SORTED
C        DATA IS STORED.
C     INTEGER ISPCER
C        AN INDICATOR VARIABLE USED TO SUPRESS THE SPECTRAL PLOTS
C        WHEN FEWER THAN 1 VALID SPECTRAL VALUES WERE COMPUTED.
C     INTEGER K1, K2
C        INDEX VARIABLES.
C     INTEGER LOG1, LOG2
C        THE ORDER OF THE SPECTRAL ESTIMATES.
C     INTEGER NF
C        THE NUMBER OF FREQUENCIES FOR WHICH THE SPECTRAL ESTIMATES
C        ARE TO BE ESTIMATED.
C     INTEGER NSPC
C        THE NUMBER OF VALID SPECTRAL ESTIMATES.
C     REAL SPC(NF)
C        THE ARRAY CONTAINING THE SPECTRAL ESTIMATES.
C     REAL SPCMN, SPCMX
C        THE MINIMUM AND MAXIMUM SPECTRAL VALUE TO BE PLOTTED.
C
C     ORDER THE SPECTRAL ESTIMATES FROM SMALLEST TO LARGEST.
C
      DO 10 I = 1, NF
         ISORT(I) = I
   10 CONTINUE
      CALL SRTIR (ISORT, NF, SPC)
C
C     DETERMINE SIGINIFICANT VALUES TO BE PLOTTED.
C
      K1 = NF
      K2 = K1
      IF ((SPC(NF) .LE. 0.0E0) .OR. (NF.EQ.1)) GO TO 30
      LOG1 = LOG10(SPC(NF))
      IF (SPC(NF) .LT. 1.0E0) LOG1 = LOG1 - 1
      DO 20 I = 2, NF
         K2 = K1 - 1
         IF (SPC(K2) .LE. 0.0E0) GO TO 30
         LOG2 = LOG10(SPC(K2))
         IF (SPC(K2) .LT. 1.0E0) LOG2 = LOG2 - 1
         IF (LOG1-LOG2 .GE. 3 .AND. NF-K2 .GE. 5) GO TO 30
         LOG1 = LOG2
         K1 = K2
   20 CONTINUE
C
   30 SPCMN = SPC(K1)
      NSPC = NF + 1 - K1
C
      SPCMX = SPC(NF)
C
      CALL SRTRI (SPC, NF, ISORT)
      ISPCER = 0
      IF (NF-K2 .LE. 0) ISPCER = 1
C
      RETURN
      END
