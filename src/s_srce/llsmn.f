*LLSMN
      SUBROUTINE LLSMN(Y, X, WT, N, M, NPAR, IX, RES, PAR, NNZW,
     +   RSD, PV, SDPV, SDRES, IPTOUT, WY, XW, VCV, FC, RED,
     +   T, PAR1, ACC, IFIT, WEIGHT, C, LLHDR, PAGE, WIDE)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS IS THE MAIN PROGRAM FOR THE LINEAR LEAST SQUARES FITTING
C     PROGRAMS
C
C     WRITTEN BY - JANET R. DONALDSON
C                  STATISTICAL ENGINEERING DIVISION
C                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE  -  DECEMBER 29, 1982
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      REAL
     +   RSD
      INTEGER
     +   IFIT,IX,M,N,NNZW,NPAR
      LOGICAL
     +   PAGE,WEIGHT,WIDE
C
C  ARRAY ARGUMENTS
      REAL
     +   ACC(NPAR),C(NPAR),FC(N,NPAR),PAR(NPAR),PAR1(NPAR),PV(N),
     +   RED(NPAR),RES(N),SDPV(N),SDRES(N),T(NPAR,NPAR),
     +   VCV(NPAR,NPAR),WT(N),WY(N),X(IX,M),XW(N,NPAR),Y(N)
      INTEGER
     +   IPTOUT(4)
C
C  SUBROUTINE ARGUMENTS
      EXTERNAL LLHDR
C
C  SCALARS IN COMMON
      INTEGER
     +   IERR
C
C  LOCAL SCALARS
      REAL
     +   COND,DF,FPLM,FPLRS,FPSPM,PROBT,PROBT1,PX,R2,RATIO,RATIO1,
     +   RSD1,RSS,RSS1,RVAR,RVAR1,SDC,SDC1,SM,TD,WTSQRT,WTSUM,WTYM,YSUM
      INTEGER
     +   I,IDF,IDF1,IER,IPRT,IREFIT,ISUBHD,J,K,NC
      LOGICAL
     +   CONST
C
C  LOCAL ARRAYS
      REAL
     +   DET(2)
C
C  EXTERNAL FUNCTIONS
      REAL
     +   CDFT,R1MACH
      EXTERNAL CDFT,R1MACH
C
C  EXTERNAL SUBROUTINES
      EXTERNAL ACCDIG,FITPT1,FITPT2,IPRINT,MGS,OANOVA,VCVOUT
      EXTERNAL STRCO,STRDI
C
C  INTRINSIC FUNCTIONS
      INTRINSIC ABS,MAX,SQRT
C
C  COMMON BLOCKS
      COMMON /ERRCHK/IERR
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     REAL ACC(NPAR)
C        THE NUMBER OF ACCURATE DIGITS.
C     REAL C(NPAR)
C        A WORK VECTOR.
C     REAL COND
C        THE CONDITION NUMBER OF THE DESIGN MATRIX.
C     LOGICAL CONST
C        AN INDICATOR VALUE DESIGNATING WHETHER THE FIRST COLUMN OF
C        THE DESIGN MATRIX IS ALL ONES FOR A CONSTANT.
C     REAL DET(2)
C        THE DETERMINENT.
C     REAL DF
C        THE DEGREES OF FREEDOM.
C     REAL FC(N,NPAR)
C        THE ORTHONORMALIZATION MATRIX.
C     REAL FPLM
C        THE FLOATING POINT LARGEST MAGNITUDE.
C     REAL FPLRS
C        THE FLOATING POINT LARGEST RELATIVE SPACING.
C     REAL FPSPM
C        THE FLOATING POINT SMALLEST POSITIVE MAGNITUDE.
C     INTEGER I
C        AN INDEX.
C     INTEGER IDF
C        THE DEGREES OF FREEDOM
C     INTEGER IDF1
C        THE DEGREES OF FREEDOM FOR THE FIT WITHOUT THE LAST INDEPENDENT
C        VARIABLE.
C     INTEGER IER
C        THE ERROR FLAG RETURNED BY THE INVERSION ROUTINES.
C     INTEGER IERR
C        THE INTEGER VALUE DESIGNATING WHETHER ANY ERRORS WERE
C        DETECTED IN THE PARAMETER LIST.
C        IF IERR .EQ. 0, NO ERRORS WERE DETECTED.
C        IF IERR .EQ. 1, ERRORS WERE DETECTED.
C     INTEGER IFIT
C        THE INDICATOR VALUE DESIGNATING WHETHER THE FIT IS OF A
C        GENERAL MODEL (IFIT=3) OR A POLYNOMIAL MODEL (IFIT=1).
C     INTEGER IPRT
C        THE UNIT NUMBER FOR PRINTED OUTPUT.
C     INTEGER IPTOUT(4)
C        THE VARIABLE USED TO CONTROL PRINTED OUTPUT FOR EACH SECTION.
C     INTEGER IREFIT
C        AN INDICATOR USED TO DESIGNATE WHETHER THE FIT IS OF THE
C        FULL DESIGN MATRIX (IREFIT=0) OR IS OMITTING THE LAST
C        INDEPENDENT VARIABLE (IREFIT=1).
C     INTEGER ISUBHD
C        AN INDICATOR VALUE SPECIFYING SUBHEADINGS TO BE PRINTED.
C     INTEGER IX
C        THE FIRST DIMENSION OF THE MATRIX X.
C     INTEGER J
C        AN INDEX.
C     INTEGER K
C        AN INDEX.
C     EXTERNAL LLHDR
C        THE NAME OF THE ROUTINE THAT PRODUCED THE HEADING.
C     INTEGER M
C        THE NUMBER OF INDEPENDENT VARIABLES.
C     INTEGER N
C        THE NUMBER OF OBSERVATIONS.
C     INTEGER NC
C        THE NUMBER OF PARAMETERS BEING FIT.
C     INTEGER NNZW
C        THE NUMBER OF NON ZERO WEIGHTS.
C     INTEGER NPAR
C        THE NUMBER OF PARAMETERS.
C     LOGICAL PAGE
C        THE VARIABLE USED TO INDICATE WHETHER A GIVEN SECTION OF
C        THE OUTPUT IS TO BEGIN ON A NEW PAGE (TRUE) OR NOT (FALSE).
C     REAL PAR(NPAR)
C        THE PARAMETERS  TO BE ESTIMATED.
C     REAL PAR1(NPAR)
C        THE PARAMETERS TO BE ESTIMATED OMMITTING THE LAST
C        INDEPENDENT VARIABLE.
C     REAL PROBT
C        ...
C     REAL PROBT1
C        ...
C     REAL PV(N)
C        THE PREDICTED VALUES.
C     REAL PX
C        A WORK VARIABLE.
C     REAL RATIO
C        THE RATIO OF THE PARAMETERS TO THEIR STANDARD DEVIATIONS.
C     REAL RATIO1
C        THE RATIO OF THE PARAMETERS COMPUTED OMITTING THE LAST
C        INDEPENDENT VARIABLE TO THEIR STANDARD DEVIATIONS.
C     REAL RED(NPAR)
C        THE REDUCTION TO THE SUM OF SQUARES DUE TO EACH PARAMETER.
C     REAL RES(N)
C        THE RESIDUALS.
C     REAL RSD
C        THE RESIDUAL STANDARD DEVIATION.
C     REAL RSD1
C        THE RESIDUAL STANDARD DEVIATION FROM THE FIT OMITTING
C        THE LAST INDEPENDENT VARIABLE.
C     REAL RSS
C        THE RESIDUAL SUM OF SQUARES.
C     REAL RSS1
C        THE RESIDUAL SUM OF SQUARES FROM THE FIT OMITTING
C        THE LAST INDEPENDENT VARIABLE.
C     REAL RVAR
C        THE RESIDUAL VARIANCE.
C     REAL RVAR1
C        THE RESIDUAL VARIANCE FROM THE FIT OMITTING
C        THE LAST INDEPENDENT VARIABLE.
C     REAL R2
C        THE MULTIPLE CORRELATION PARAMETER.
C     REAL SDC
C        THE ESTIMATED STANDARD DEVIATIONS OF THE PARAMETERS.
C     REAL SDC1
C        THE ESTIMATED STANDARD DEVIATIONS OF THE PARAMETERS OMITTING
C        THE LAST INDEPENDENT VARIABLE.
C     REAL SDPV(N)
C        THE STANDARD DEVIATIONS OF THE PREDICTED VALUES.
C     REAL SDRES(N)
C        THE STANDARDIZED RESIDUALS.
C     REAL SM
C        A WORK VARIABLE.
C     REAL T(NPAR,NPAR)
C        THE TRIANGULAR MATRIX FROM THE DECOMPOSITION.
C     REAL TD
C        A WORK VARIABLE.
C     REAL VCV(NPAR,NPAR)
C        THE VARIANCE COVARIANCE MATRIX.
C     LOGICAL WEIGHT
C        THE VARIABLE USED TO INDICATE WHETHER WEIGHTED ANALYSIS IS TO
C        BE PERFORMED (TRUE) OR NOT (FALSE).
C     LOGICAL WIDE
C        THE VARIABLE USED TO INDICATE WHETHER THE HEADING SHOULD
C        BE FULL WIDTH (TRUE) OR NOT (FALSE).
C     REAL WT(N)
C        THE WEIGHTS (A DUMMY VECTOR IN THE UNWEIGHTED CASE).
C     REAL WTSQRT
C        THE SQUARE ROOT OF THE WEIGHT.
C     REAL WTSUM
C        THE SUM OF THE WEIGHTS.
C     REAL WTYM
C        THE SUM OF THE WEIGHTED DEPENDENT VARIABLES.
C     REAL WY(N)
C        THE VECTOR CONTAINING SQRT(WT)*Y.
C     REAL X(IX,M)
C        THE INDEPENDENT VARIABLE.
C     REAL XW(N,NPAR)
C        THE MATRIX CONTAINING X * SQRT(WT).
C     REAL Y(N)
C        THE DEPENDENT VARIABLE.
C     REAL YSUM
C        THE SUM OF THE WEIGHTED DEPENDENT VARIABLES SQUARED.
C
      CALL IPRINT(IPRT)
C
C     INITIALIZE VARIABLES
C
      FPLM = R1MACH(2)
      FPLRS = R1MACH(4)
      FPSPM = R1MACH(1)
      NC = NPAR
      IDF = NNZW - NC
      DF = IDF
C
      R2 = 0.0
      RVAR1 = 0.0
      RSD1 = 0.0
      J = 0
      IDF1 = 0
      CONST = .FALSE.
C
      YSUM = 0.0E0
      WTSUM = 0.0E0
      DO 10 I=1,N
         IF (WT(I).LE.0.0E0) GO TO 10
         YSUM = YSUM + WT(I)*Y(I)*Y(I)
         WTSUM = WTSUM + WT(I)
   10 CONTINUE
C
C     BEGIN FIT
C
      IREFIT = -1
C
C     IF IREFIT IS EQUAL TO 1 FIT IS OMITTING THE LAST VARIABLE
C
   50 IREFIT = IREFIT + 1
C
C     COMPUTE WORKING VECTORS
C
      WTYM = 0.0E0
      I = 0
      DO 70 K=1,N
         IF (WT(K).LE.0.0E0) GO TO 70
         WTSQRT = SQRT(WT(K))
         I = I + 1
         PX = WTSQRT
         DO 60 J=1,NC
            IF (IFIT.EQ.1 .AND. J.GE.2) PX = PX*X(K,1)
            IF (IFIT.EQ.3) PX = X(K,J)*WTSQRT
            XW(I,J) = PX
   60    CONTINUE
C
C     COMPUTE WY ( WEIGHTS * Y VECTOR )
C
         WY(I) = Y(K)*WTSQRT
         WTYM = WTYM + WT(K)*Y(K)
   70 CONTINUE
      WTYM = WTYM/WTSUM
C
C     COMPUTE ESTIMATED VALUES OF PARAMETERS
C
      IF (IREFIT.EQ.1) GO TO 80
      IER = 0
      CALL MGS(XW, WY, NNZW, NC, PAR, C, T(1,1), T, NPAR, N, IER)
      IF (IER.EQ.0) GO TO 90
      IERR = 3
      ISUBHD = 0
      CALL LLHDR(PAGE, WIDE, ISUBHD)
      WRITE (IPRT,1160)
      WRITE (IPRT,1150)
      RETURN
   80 CONTINUE
      IER = 0
      CALL MGS(XW, WY, NNZW, NC, PAR1, C, T(1,1), T, NPAR, N, IER)
C
C     COMPUTE PREDICTED VALUES (PV) AND RESIDUALS (RES)
C             RESIDUAL SUM OF SQUARES (RSS) AND
C             RESIDUAL STANDARD DEVIATION (RSD) AND
C             SUM OF WEIGHTS (WTSUM) AND
C             RESIDUAL VARAINCE (RVAR)
C
   90 RSS = 0.0E0
      RSS1 = 0.0E0
      DO 120 I=1,N
         SM = 0.0E0
         PX = 1.0E0
         DO 100 J=1,NC
            IF (IFIT.EQ.1 .AND. J.GE.2) PX = PX*X(I,1)
            IF (IFIT.EQ.3) PX = X(I,J)
            IF (IREFIT.EQ.0) SM = SM + PAR(J)*PX
            IF (IREFIT.EQ.1) SM = SM + PAR1(J)*PX
  100    CONTINUE
         IF (IREFIT.EQ.0) THEN
            PV(I) = SM
            RES(I) = Y(I) - PV(I)
            RSS = RSS + RES(I)*WT(I)*RES(I)
         ELSE
            RSS1 = RSS1 + (Y(I)-SM)*WT(I)*(Y(I)-SM)
         END IF
  120 CONTINUE
C
      IF (IREFIT.EQ.0) GO TO 130
      RVAR1 = RSS1/(DF+1.0E0)
      RSD1 = SQRT(RVAR1)
      GO TO 380
  130 RVAR = 0.0E0
      IF (DF.GT.0.0E0) RVAR = RSS/DF
      RSD = SQRT(RVAR)
C
C     COMPUTE MULTIPLE CORRELATION PARAMETER SQUARED (R2)
C
      SM = 0.0E0
      CONST = .TRUE.
      DO 140 I=1,N
         IF ((X(I,1).NE.1.0E0) .AND. (IFIT.EQ.3)) CONST = .FALSE.
         SM = SM + (Y(I)-WTYM)*WT(I)*(Y(I)-WTYM)
  140 CONTINUE
      R2 = 1.0E0
      IF (SM.GT.0.0E0) R2 = 1.0E0-RSS/SM
C
C     CHECK FOR -EXACT- FIT
C
      TD = 0.0E0
      IF (DF.GT.0.0E0) TD = SQRT(RSS)
      IF (YSUM.GT.0.0E0) TD = TD/SQRT(YSUM)
      IF (TD.GT.10.0E0*FPLRS) GO TO 180
C
      IERR = 0
      IF ((IPTOUT(1).EQ.0) .AND. (IPTOUT(2).EQ.0) .AND. (IPTOUT(3).EQ.0)
     +   .AND. (IPTOUT(4).EQ.0)) GO TO 145
      ISUBHD = 0
      CALL LLHDR(PAGE, WIDE, ISUBHD)
      WRITE (IPRT,1180)
      WRITE (IPRT,1020) (I,PAR(I),I=1,NPAR)
C
C     ZERO OUT VCV MATRIX AND SDRES AND SDPV VECTORS
C
  145 CONTINUE
      DO 150 I=1,N
         SDPV(I) = 0.0E0
         SDRES(I) = 0.0E0
  150 CONTINUE
      DO 170 I=1,NPAR
         DO 160 J=1,NPAR
            VCV(I,J) = 0.0E0
  160    CONTINUE
  170 CONTINUE
      RETURN
C
  180 CONTINUE
C
C     COMPUTE ORTHO NORMALIZATION
C
      DO 200 I=1,NC
         T(I,I) = SQRT(T(I,1))
         DO 190 J=I,NC
            IF (I.EQ.J) GO TO 190
            T(I,J) = T(I,J)*T(I,I)
  190    CONTINUE
  200 CONTINUE
C
      CALL STRCO(T, NPAR, NC, COND, C, 1)
      IF (COND.NE.0.0E0) THEN
         COND = 1.0E0/COND
      ELSE
         COND = FPLM
      END IF
      CALL STRDI(T, NPAR, NC, DET, 011, IER)
C
      IF (IER.EQ.0) GO TO 210
      IERR = 3
      ISUBHD = 0
      CALL LLHDR(PAGE, WIDE, ISUBHD)
      WRITE (IPRT,1160)
      WRITE (IPRT,1120)
      RETURN
  210 CONTINUE
      DO 240 I=1,N
         DO 230 J=1,NC
            SM = 0.0E0
            PX = 1.0E0
            DO 220 K=1,J
               IF (IFIT.EQ.1 .AND. K.GE.2) PX = PX*X(I,1)
               IF (IFIT.EQ.3) PX = X(I,K)
               SM = SM + PX*T(K,J)
  220       CONTINUE
            FC(I,J) = SM
  230    CONTINUE
  240 CONTINUE
C
C     COMPUTE REDUCTION TO RSS DUE TO FITTING
C
      DO 260 J=1,NC
         SM = 0.0E0
         DO 250 I=1,N
            SM = SM + (FC(I,J))*WT(I)*Y(I)
  250    CONTINUE
         RED(J) = (SM*SM)
  260 CONTINUE
C
C     COMPUTE ESTIMATED VARIANCE COVARIANCE MATRIX
C
      DO 290 I=1,NC
         DO 280 J=I,NC
            SM = 0.0E0
            DO 270 K=J,NC
               SM = SM + T(I,K)*T(J,K)
  270       CONTINUE
            IF (SQRT(ABS(SM))*SQRT(RVAR).GE.SQRT(FPSPM)) THEN
               VCV(I,J) = SM*RVAR
            ELSE
               VCV(I,J) = 0.0E0
            END IF
            VCV(J,I) = VCV(I,J)
  280    CONTINUE
  290 CONTINUE
C
C     REFIT TO PREDICTED VALUES
C
      I = 0
      DO 310 K=1,N
         IF (WT(K).LE.0.0E0) GO TO 310
         WTSQRT = SQRT(WT(K))
         I = I + 1
         PX = WTSQRT
         DO 300 J=1,NC
            IF (IFIT.EQ.1 .AND. J.GE.2) PX = PX*X(K,1)
            IF (IFIT.EQ.3) PX = X(K,J)*WTSQRT
            XW(I,J) = PX
  300    CONTINUE
         WY(I) = PV(K)*WTSQRT
  310 CONTINUE
C
      IER = 0
      CALL MGS(XW, WY, NNZW, NC, PAR1, C, T(1,1), T, NPAR, N, IER)
C
      CALL ACCDIG(PAR, PAR1, ACC, NC)
C
C     COMPUTE STANDARD DEVIATION OF PREDICTED VALUES (SDPV)
C     AND STANDARDIZED RESIDUALS (SDRES)
C
      DO 360 I=1,N
         SM = 0.0E0
         DO 330 J=1,NC
            SM = 0.0E0
            PX = 1.0E0
            DO 320 K=1,NC
               IF (IFIT.EQ.1 .AND. K.GE.2) PX = PX*X(I,1)
               IF (IFIT.EQ.3) PX = X(I,K)
               SM = SM + PX*VCV(J,K)
  320       CONTINUE
            XW(I,J) = SM
  330    CONTINUE
         SM = 0.0E0
         PX = 1.0E0
         DO 340 K=1,NC
            IF (IFIT.EQ.1 .AND. K.GE.2) PX = PX*X(I,1)
            IF (IFIT.EQ.3) PX = X(I,K)
            SM = SM + XW(I,K)*PX
  340    CONTINUE
         SM = MAX(0.0E0, SM)
         SDPV(I) = SQRT(SM)
         SDRES(I) = FPLM
         IF (WT(I).LE.0.0E0) GO TO 360
         IF ((RVAR/WT(I)-SM).LE.0.0E0) IERR = 4
         IF (RVAR/WT(I)-SM.GT.0.0E0)
     +      SDRES(I) = (RES(I)/SQRT(RVAR/WT(I)-SM))
  360 CONTINUE
C
C     CHECK FOR PRINTED OUTPUT
C
      IF ((IPTOUT(1).EQ.0) .AND. (IPTOUT(2).EQ.0) .AND.
     +   (IPTOUT(3).EQ.0) .AND. (IPTOUT(4).EQ.0)) RETURN
C
C     BEGIN PRINTED OUTPUT
C
      IF (IPTOUT(1).EQ.0) GO TO 370
C
C     PRINT FIRST PAGE OF OUTPUT
C
      ISUBHD = 0
      CALL LLHDR(PAGE, WIDE, ISUBHD)
      CALL FITPT1(N, M, X, Y, PV, SDPV, RES, SDRES, WT, IX, NNZW,
     +   WEIGHT, IPTOUT(1))
C
C     PRINT PLOTS
C
  370 CONTINUE
      IF (IPTOUT(2).EQ.0) GO TO 375
      ISUBHD = 0
      CALL LLHDR(PAGE, WIDE, ISUBHD)
      CALL FITPT2 (SDRES, PV, WT, N, NNZW, WEIGHT, RES, RSS)
C
  375 CONTINUE
      IF (IPTOUT(3).EQ.0) GO TO 376
C
C     PRINT ANALYSIS OF VARIANCE
C
      ISUBHD = 0
      CALL LLHDR(PAGE, WIDE, ISUBHD)
      CALL OANOVA(YSUM, RED, NPAR, RVAR, NNZW, PAR1, IPRT)
C
  376 CONTINUE
      IF (IPTOUT(4).EQ.0) RETURN
C
      IF (NPAR.EQ.1) GO TO 450
      NC = NC - 1
      IDF1 = NNZW - NC
      GO TO 50
  380 CONTINUE
      DO 400 I=1,NC
         T(I,I) = SQRT(T(I,1))
         DO 390 J=I,NC
            IF (I.EQ.J) GO TO 390
            T(I,J) = T(I,J)*T(I,I)
  390    CONTINUE
  400 CONTINUE
C
      CALL STRDI(T, NPAR, NC, DET, 011, IER)
C
      IF (IER.EQ.0) GO TO 420
      WRITE (IPRT,1140)
      DO 410 I=1,NC
         PAR1(I) = 0.0E0
         T(I,I) = 0.0E0
  410 CONTINUE
      GO TO 450
  420 CONTINUE
      DO 440 I=1,NC
         SM = 0.0E0
         DO 430 K=I,NC
            SM = SM + T(I,K)*T(I,K)
  430    CONTINUE
         T(I,I) = SM
  440 CONTINUE
C
C     PRINT PAGE HEADINGS
C
  450 CONTINUE
      ISUBHD = 0
      CALL LLHDR(PAGE, WIDE, ISUBHD)
C
C     PRINT VARIANCE COVARIANCE MATRIX
C
      CALL VCVOUT(NPAR, VCV, NPAR, .TRUE.)
C
      WRITE (IPRT,1030)
      IF (NPAR.GT.1) WRITE (IPRT,1040)
      WRITE (IPRT,1050)
      IF (NPAR.GT.1) WRITE (IPRT,1060)
      WRITE (IPRT, 1170)
      IF (NPAR.GE.2) THEN
         DO 460 I=1,NC
            SDC = SQRT(VCV(I,I))
            RATIO = FPLM
            PROBT = 0.0E0
            IF (SDC.GT.0.0E0) RATIO = PAR(I)/SDC
            IF (SDC.GT.0.0E0)
     +         PROBT = (1.0E0-CDFT(ABS(RATIO), NNZW-NPAR)) * 2.0E0
            SDC1 = SQRT(T(I,I))*SQRT(RVAR1)
            RATIO1 = FPLM
            PROBT1 = 0.0E0
            IF (SDC1.GT.0.0E0) RATIO1 = PAR1(I)/SDC1
            IF (SDC1.GT.0.0E0)
     +         PROBT1 = (1.0E0 - CDFT(ABS(RATIO1), NNZW-NPAR+1)) * 2.0E0
            WRITE (IPRT,1070) I, PAR(I), SDC, RATIO, PROBT, ACC(I),
     +         PAR1(I), SDC1, RATIO1, PROBT1
  460    CONTINUE
      END IF
      SDC = SQRT(VCV(NPAR,NPAR))
      RATIO = FPLM
      PROBT = 0.0E0
      IF (SDC.GT.0.0E0) RATIO = PAR(NPAR)/SDC
      IF (SDC.GT.0.0E0)
     +   PROBT = (1.0E0 - CDFT(ABS(RATIO), NNZW-NPAR)) * 2.0E0
      WRITE (IPRT,1070) NPAR, PAR(NPAR), SDC, RATIO, PROBT, ACC(NPAR)
      IF (NPAR.EQ.1) WRITE (IPRT,1080) RSD
      IF (NPAR.GT.1) WRITE (IPRT,1080) RSD, RSD1
      WRITE (IPRT,1090) NNZW, NPAR, IDF
      IF (NPAR.GT.1) WRITE (IPRT,1100) NNZW, NC, IDF1
      IF (CONST) WRITE (IPRT,1010) R2
      WRITE (IPRT,1000) COND
      WRITE (IPRT,1110)
      RETURN
C
C     FORMAT STATEMENTS
C
 1000 FORMAT (/29H APPROXIMATE CONDITION NUMBER, 10X, G15.7)
 1010 FORMAT (/44H MULTIPLE CORRELATION COEFFICIENT SQUARED   , F7.4)
 1020 FORMAT (//46H THE VALUES COMPUTED FOR THE PARAMETERS ARE - /(5X,
     +   4HPAR(, I3, 4H) = , G15.7))
 1030 FORMAT (////1X, 25('-'), 1X, 18HESTIMATES FROM FIT, 1X, 24('-'))
 1040 FORMAT ('+', 72X, 4('-'), 1X,
     +   48HESTIMATES FROM FIT OMITTING LAST PREDICTOR VALUE, 1X,
     +   4('-'))
 1050 FORMAT (/2X, 19HESTIMATED PARAMETER, 7X, 9HSD OF PAR, 5X,
     +   8HT(PAR=0), 3X, 7HPROB(T), 2X, 8HACC DIG*)
 1060 FORMAT ('+', 72X, 19HESTIMATED PARAMETER, 7X, 9HSD OF PAR, 5X,
     +   8HT(PAR=0), 3X, 7HPROB(T))
 1070 FORMAT (1X, I3, 2X, G16.9, 3X, G16.9, 2X, G10.4, 1X, F5.3, 2X,
     +   F7.1, 9X, G16.9, 3X, G16.9, 2X, G10.4, 1X, F5.3)
 1080 FORMAT (//1X, 30HRESIDUAL STANDARD DEVIATION   , 9X, G15.7, 56X,
     +   G15.7)
 1090 FORMAT (1X, 27HBASED ON DEGREES OF FREEDOM, 7X, I4, 3H - , I2,
     +   3H = , I4)
 1100 FORMAT ('+', 105X, I4, 3H - , I2, 3H = , I4)
 1110 FORMAT (//52H * THE NUMBER OF CORRECTLY COMPUTED DIGITS IN EACH P,
     +   56HARAMETER USUALLY DIFFERS BY LESS THAN 1 FROM THE VALUE G,
     +   10HIVEN HERE.)
 1120 FORMAT (/47H THE PROGRAM WAS UNABLE TO COMPUTE THE VARIANCE,
     +   19H-COVARIANCE MATRIX./
     +   52H THE DESIGN MATRIX IS EITHER NEARLY SINGULAR OR VERY,
     +   17H ILL CONDITIONED./
     +   29H CHECK YOUR INPUT FOR ERRORS.)
 1140 FORMAT (54H0ESTIMATES FOR THE STANDARD DEVIATION OF THE ESTIMATED,
     +   55HPARAMETERS OMITTING THE LAST INDEPENDENT VARIABLE COULD/
     +   59H NOT BE COMPUTED.  THE ZEROS PRINTED FOR THE ESTIMATES OF T,
     +   50HHE PARAMETERS, THEIR STANDARD DEVIATIONS AND THEIR/
     +   59H RATIOS FOR THE FIT OMITTING THE LAST VARIABLE ARE MEANINGL,
     +   4HESS.)
 1150 FORMAT (/30H THE DESIGN MATRIX IS SINGULAR,
     +   29H TO WITHIN MACHINE PRECISION./
     +   50H CHECK THE DESIGN MATRIX FOR A LINEAR RELATIONSHIP,
     +   29H BETWEEN SOME OF THE COLUMNS.)
 1160 FORMAT (//1X, 11('*')/ 1X, 11H*  ERROR  */ 1X, 11('*'))
 1170 FORMAT (' ')
 1180 FORMAT (/50H THE LEAST SQUARES FIT OF THE DATA TO THE MODEL IS,
     +   35H EXACT TO WITHIN MACHINE PRECISION./
     +   38H STATISTICAL ANALYSIS IS NOT POSSIBLE.)
      END
