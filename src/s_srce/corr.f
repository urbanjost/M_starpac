*CORR
      SUBROUTINE CORR(YM, N, M, IYM, LDSTAK)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS IS THE USER CALLABLE ROUTINE FOR THE CORRELATION FAMILY.
C     IT IS THE SHORT CALL FORM.
C     THIS PROCEDURE CALLS CORER TO CHECK FOR ERRORS IN THE PARAMETER
C     LIST.  IF NO PROBLEMS IT THEN ALLOCATES SPACE IN CSTAK FOR THE
C     VECTORS NEEDED AND CALLS THE MAIN ROUTINE , CORRMN, TO
C     DO THE CORRELATION ANALYSIS.
C
C     WRITTEN BY -
C        LINDA L. MITCHELL
C        STATISTICAL ENGINEERING DIVISION
C        NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE  -  MAY 17, 1982
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      INTEGER
     +   IYM,LDSTAK,M,N
C
C  ARRAY ARGUMENTS
      REAL
     +   YM(*)
C
C  SCALARS IN COMMON
      INTEGER
     +   IERR
C
C  ARRAYS IN COMMON
      DOUBLE PRECISION DSTAK(12)
C
C  LOCAL SCALARS
      INTEGER
     +   AVG,CILO,CIUP,ICOR,IFP,IPRT,IWRK,LIWRK,LWRK,NALL0,NPRT,PC,
     +   QF,QUAD,RANK,SC,SCINV,SD,SLPC,SLSC,SRCC,T,VCV,WRK
C
C  LOCAL ARRAYS
      REAL
     +   RSTAK(12)
      INTEGER
     +   ISTAK(12)
      CHARACTER
     +   NMSUB(6)*1
C
C  EXTERNAL FUNCTIONS
      INTEGER
     +   STKGET,STKST
      EXTERNAL STKGET,STKST
C
C  EXTERNAL SUBROUTINES
      EXTERNAL CORRER,CORRMN,IPRINT,STKCLR,STKSET
C
C  INTRINSIC FUNCTIONS
      INTRINSIC MAX
C
C  COMMON BLOCKS
      COMMON /CSTAK/DSTAK
      COMMON /ERRCHK/IERR
C
C  EQUIVALENCES
      EQUIVALENCE (DSTAK(1),RSTAK(1))
      EQUIVALENCE (DSTAK(1),ISTAK(1))
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     INTEGER AVG
C                  THE STARTING LOCATION IN THE WORK AREA FOR THE
C                  AVERAGES OF YM
C     INTEGER CILO
C                  OCCUPIES THE SAME SPACE AS SLPC AND IS THE LOWER
C                  CONFIDENCE INTERVAL
C     INTEGER CIUP
C                  OCCUPIES THE SAME SPACE AS SLSC AND IS THE UPPER
C                  CONFIDENCE INTERVAL
C     DOUBLE PRECISION DSTAK(12)
C                  THE DOUBLE PRECISION VERSION OF
C                  THE /CSTAK/ WORK AREA.
C     INTEGER ICOR
C                  DETERMINES WHICH SUBROUTINE CALLED CORRMN
C                  IF =1, THEN CALLED BY CORR
C                     =2, THEN CALLED BY CORRS
C     INTEGER IERR
C                  A FLAG RETURNED TO THE USER INDICATING
C                  WHETHER  ERRORS WERE DETECTED IN THE PARAMETER LIST
C                  IF IERR .EQ. 0, NO ERRORS WERE DETECTED
C                  IF IERR .EQ. 1, ERRORS HAVE BEEN DETECTED
C     INTEGER IFP
C                  AN INDICATOR FOR STACK ALLOCATION TYPE, WHERE IFP=3
C                  INDICATES SINGLE PRECISION AND IFP=4 INDICATES
C                  DOUBLE PRECISION.
C     INTEGER IPRT
C                  LOGICAL OUTPUT UNIT
C     INTEGER ISTAK(12)
C                  THE INTEGER VERSION OF THE /CSTAK/ WORK AREA.
C     INTEGER IWRK
C                  THE STARTING LOCATION IN THE WORK AREA FOR THE
C                  INTEGER WORK VECTOR.
C     INTEGER IYM
C                  THE ROW DIMENSION OF YM SPECIFIED IN THE USERS PROGRA
C     INTEGER LDSTAK
C                  SIZE OF WORK AREA ALLOCATED IN THE USERS PROGRAM
C     INTEGER LIWRK
C                  THE LENGTH OF THE INTEGER WORK VECTOR.
C     INTEGER LWRK
C                  THE LENGTH OF THE REAL WORK VECTOR.
C     INTEGER M
C                  THE NUMBER OF VARIABLES
C     INTEGER N
C                  THE NUMBER OF OBSERVATIONS
C     INTEGER NALL0
C                  THE NUMBER OF ALLOCATIONS OUTSTANDING AT THE
C                  TIME THAT THIS ROUTINE WAS CALLED.
C     CHARACTER*1 NMSUB(6)
C                  THE SUBROUTINE NAME
C     INTEGER NPRT
C                  THE VARIABLE CONTROLLING THE AUTOMATIC PRINTOUT
C                       NPRT = 0, PRINTOUT IS SUPPRESSED
C                       OTHERWISE THE PRINTOUT IS PROVIDED.
C     INTEGER PC
C                  THE STARTING LOCATION IN THE WORK AREA
C                  OF THE PARTIAL CORRELATION COEFFICIENTS MATRIX
C     INTEGER QF
C                  THE STARTING LOCATION IN THE WORK AREA FOR THE
C                  QUADRATIC FIT
C     INTEGER QUAD
C                  THE STARTING LOCATION IN THE WORK AREA FOR THE
C                  REAL VERSION OF THE QUADRATIC FIT
C     INTEGER RANK
C                  THE STARTING LOCATION IN THE WORK AREA FOR THE
C                  MATRIX CONTAINING THE RANKS OF YM
C     REAL RSTAK(12)
C                  THE REAL VERSION OF THE
C                  /CSTAK/ WORK AREA.
C     INTEGER SC
C                  THE STARTING LOCATION IN THE WORK AREA
C                  OF THE SIMPLE CORRELATION COEFFICIENTS MATRIX
C     INTEGER SCINV
C                  THE STARTING LOCATION IN THE WORK AREA FOR THE
C                  INVERSE MATRIX OF SC
C     INTEGER SD
C                  THE STARTING LOCATION IN THE WORK AREA OF THE
C                  STANDARD DEVIATIONS OF YM
C     INTEGER SLPC
C                  THE STARTING LOCATION IN THE WORK AREA OF THE
C                  SIGNIFICANCE LEVELS OF PC
C     INTEGER SLSC
C                  THE STARTING LOCATION IN THE WORK AREA OF THE
C                  SIGNIFICANCE LEVELS OF SC
C     INTEGER SRCC
C                  THE STARTING LOCATION IN THE WORK AREA OF THE
C                  SPEARMAN RANK COEFFICIENTS
C     INTEGER T
C                  STARTING LOCATION IN THE WORK AREA
C                  FOR THE ARRAY T.
C     INTEGER VCV
C                  THE STARTING LOCATION IN THE WORK AREA OF THE
C                  VARIANCE COVARIANCE MATRIX.
C     INTEGER WRK
C                  THE STARTING LOCATION IN THE WORK AREA FOR
C                  WORK SPACE
C     REAL YM(IYM,M)
C                  THE MATRIX IN WHICH THE OBSERVATIONS ARE PASSED
C
C
C     SET UP NAME ARRAY
C
      DATA  NMSUB(1), NMSUB(2), NMSUB(3), NMSUB(4), NMSUB(5), NMSUB(6)
     +   /       'C',      'O',      'R',      'R',      ' ',      ' '/
C
C     SET UP FRAMEWORK VARIABLES FOR NUMBER TYPE
C
      IFP = 3
C
C     SET CALLER FLAG, THIS IS CORR
C
      ICOR = 1
C
C     PRINTOUT IS AUTOMATICALY PROVIDED
C
      NPRT = 1
C
C     CHECK FOR ERRORS IN PARAMETERS
C
      CALL CORRER(NMSUB, M, N, IYM, M, LDSTAK, ICOR, NPRT)
C
C     IF THERE IS AN ERROR RETURN TO THE CALLERS ROUTINE
C
      IF (IERR.EQ.0) GO TO 10
C
C     PRINT CORRECT FORM OF CALL STATEMENT AND RETURN TO CALLER
C
      CALL IPRINT(IPRT)
      WRITE (IPRT,1000)
      RETURN
C
C     INITIALIZE THE STACK
C
   10 CALL STKSET (LDSTAK, 4)
      NALL0 = STKST(1)
C
C     ALLOCATE SPACE IN CSTAK FOR MATRICES.
C
      LIWRK = MAX(N,M)
      LWRK = LIWRK
C
      VCV = STKGET(M*M,IFP)
      IWRK = STKGET(LIWRK,2)
      WRK = STKGET(LWRK,IFP)
C
      T = STKGET(M,IFP)
      RANK = STKGET(N*M,IFP)
      SCINV = STKGET(M*M,IFP)
      SLSC = STKGET(M*M,IFP)
      SLPC = STKGET(M*M,IFP)
      SC = STKGET(M*M,IFP)
      PC = STKGET(M*M,IFP)
      QUAD = STKGET(3*N,IFP)
      QF = STKGET(M*M,IFP)
C
C     THE FOLLOWING VARIABLES HAVE BEEN INDIRECTLY EQUIVALENCED-
C
      CIUP = SLSC
      CILO = SLPC
      AVG = WRK
      SD = WRK
      SRCC = SCINV
C
C     CALL THE ROUTINE TO DO THE COMPUTATIONS
C
      CALL CORRMN(YM, N, M, IYM, RSTAK(AVG), RSTAK(SD), RSTAK(T),
     +   RSTAK(RANK), RSTAK(SC), RSTAK(PC), RSTAK(SCINV), ISTAK(IWRK),
     +   RSTAK(WRK), RSTAK(SLSC), RSTAK(SLPC), RSTAK(SRCC),
     +   RSTAK(QUAD), RSTAK(CIUP), RSTAK(CILO), RSTAK(QF),
     +   NPRT, RSTAK(VCV), M, LIWRK, LWRK)
C
C     RETURN AREA TO THE WORK ARRAY
C
      CALL STKCLR(NALL0)
C
      RETURN
C
C     FORMAT STATEMENT
C
 1000 FORMAT (/42H THE CORRECT FORM OF THE CALL STATEMENT IS//
     +   '       CALL CORR (YM, N, M, IYM, LDSTAK)')
      END
