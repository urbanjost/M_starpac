*ARCOEF
      SUBROUTINE ARCOEF (ACOV, PHI, RSS, LAG, LAGMAX, ACOV0)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS ROUTINE USES DURBINS RECURSIVE METHOD TO COMPUTE
C     THE AUTOREGRESSIVE COEFFICIENTS OF AN ORDER LAG PROCESS,
C     GIVEN ON INPUT THE COEFFICIENTS OF AN ORDER (LAG-1) PROCESS.
C
C     WRITTEN BY - JANET R. DONALDSON
C                  STATISTICAL ENGINEERING DIVISION
C                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE  -  NOVEMBER 21, 1980
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      REAL
     +   ACOV0,RSS
      INTEGER
     +   LAG,LAGMAX
C
C  ARRAY ARGUMENTS
      REAL
     +   ACOV(*),PHI(*)
C
C  LOCAL SCALARS
      REAL
     +   SUM1,SUM2,T
      INTEGER
     +   J,L1,L2,LJ
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     REAL ACOV(LAGMAX)
C        THE ARRAY OF ACVF ESTIMATES FOR LAGS ONE TO LAGMAX.
C     REAL ACOV0
C        THE ACVF FOR LAG ZERO.
C     INTEGER J, LAG
C        INDEX VARIABLES.
C     INTEGER LAGMAX
C        THE MAXIMUM LAG VALUE USED.
C     INTEGER LJ, L2
C        INDEX VARIABLES.
C     REAL PHI(LAGMAX)
C        THE ARRAY OF AUTOREGRESSIVE COEFFICIENTS FOR AN AR
C        PROCESS OF ORDER LAG.
C     REAL RSS
C        THE ONE STEP PREDICTION RESIDUAL SUM OF SQUARES.
C     REAL SUM1, SUM2
C        VARIABLES USED IN THE COMPUTATIONS OF THE AUTOREGRESSIVE
C        COEFFICIENTS.
C     REAL T
C        A TEMPORARY STORAGE LOCATION.
C
      L1 = LAG - 1
      SUM1 = 0.0E0
      SUM2 = 0.0E0
      DO 10 J = 1, L1
         LJ = LAG - J
         SUM1 = SUM1 + PHI(J) * ACOV(LJ)
         SUM2 = SUM2 + PHI(J) * ACOV(J)
   10 CONTINUE
      PHI(LAG) = (ACOV(LAG) - SUM1) / (ACOV0 - SUM2)
      L2 = LAG / 2
      DO 20 J = 1, L2
         LJ = LAG - J
         T = PHI(J) - PHI(LAG) * PHI(LJ)
         PHI(LJ) = PHI(LJ) - PHI(LAG) * PHI(J)
         PHI(J) = T
   20 CONTINUE
C
      RSS = RSS * (1.0E0 - PHI(LAG)*PHI(LAG))
C
      RETURN
      END
