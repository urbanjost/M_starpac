*POLAR
      SUBROUTINE POLAR (AMPL, PHAS, N)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS SUBROUTINE CONVERTS THE PAIR OF SERIES AMPL AND PHAS
C     FROM THE REAL AND IMAGINARY PARTS OF A SERIES OF COMPLEX
C     NUMBERS TO THEIR MAGNITUDES AND PHASES.  THE CONVERSION IS
C     DONE IN PLACE.
C
C     WRITTEN BY  -  PETER BLOOMFIELD
C                    FOURIER ANALSERIESSIS OF TIME SERIES- AN
C                       INTRODUCTION
C                    JOHN WILESERIES AND SONS, NEW SERIESORK, 1976
C                    PAGE 150
C     ADAPTED FOR STARPAC BY  -  JANET R. DONALDSON
C                                 STATISTICAL ENGINEERING DEVISION
C                                 NATIONAL BUREAU OF STANDARDS
C                                 BOULDER, COLORADO
C
C     CREATION DATE  -  NOVEMBER 26, 1980
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      INTEGER
     +   N
C
C  ARRAY ARGUMENTS
      REAL
     +   AMPL(N),PHAS(N)
C
C  LOCAL SCALARS
      REAL
     +   PHASE,R
      INTEGER
     +   I
C
C  INTRINSIC FUNCTIONS
      INTRINSIC ATAN2,SQRT
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     REAL AMPL(N)
C        THE ARRAY IN WHICH THE AMPLITUDES ARE STORED.
C     INTEGER I
C        AN INDEX VARIABLE.
C     INTEGER N
C        THE NUMBER OF OBSERVATIONS.
C     REAL PHAS(N)
C        THE ARRAY IN WHICH THE PRIMARY PHASE ESTIMATES ARE RETURNED.
C     REAL PHASE
C        THE PHASE COMPONENT OF THE DEMODULATED SERIES.
C     REAL R
C        THE AMPLITUDE COMPONENT OF THE DEMODULATED SERIES.
C
      DO 10 I = 1, N
         R = SQRT(AMPL(I)*AMPL(I) + PHAS(I)*PHAS(I))
         PHASE = 0.0E0
         IF (R .NE. 0.0E0) PHASE = ATAN2(PHAS(I), AMPL(I))
         AMPL(I) = R
         PHAS(I) = PHASE
   10 CONTINUE
      RETURN
      END
