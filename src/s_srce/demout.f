*DEMOUT
      SUBROUTINE DEMOUT(FD, FC, K, AMPL, PHASE, IPHASE, NDEM, NMSUB)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS SUBROUTINE PRODUCES THE OUTPUT FOR THE TIME SERIES
C     DEMODULATION ROUTINE.
C
C     WRITTEN BY  -  JANET R. DONALDSON
C                    STATISTICAL ENGINEERING DEVISION
C                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE  -  NOVEMBER 26, 1980
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      REAL
     +   FC,FD
      INTEGER
     +   IPHASE,K,NDEM
C
C  ARRAY ARGUMENTS
      REAL
     +   AMPL(NDEM),PHASE(IPHASE,2)
      CHARACTER
     +   NMSUB(6)*1
C
C  SCALARS IN COMMON
      INTEGER
     +   IERR
C
C  LOCAL SCALARS
      REAL
     +   PI,YMN,YMX
      INTEGER
     +   IPRT
      LOGICAL
     +   ERROR
C
C  LOCAL ARRAYS
      INTEGER
     +   ISYM(1)
C
C  EXTERNAL SUBROUTINES
      EXTERNAL GETPI,IPRINT,VERSP,VPLMT,VPMN
C
C  COMMON BLOCKS
      COMMON /ERRCHK/IERR
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     REAL AMPL(NDEM)
C        THE ARRAY IN WHICH THE AMPLITUDES OF THE DEMODULATED SERIES
C        ARE STORED.
C     LOGICAL ERROR
C        AN ERROR FLAG
C     REAL FC, FD
C        THE CUTOFF FREQUENCY AND THE DEMODULATION FREQUENCY.
C     INTEGER IERR
C        THE ERROR FLAG
C     INTEGER IPHASE
C
C     INTEGER IPRT
C        THE UNIT NUMBER FOR PRINTED OUTPUT.
C     INTEGER ISYM(1)
C        A DUMMY ARRAY.
C     INTEGER K
C        THE NUMBER OF TERMS IN THE LOW PASS FILTER USED TO SMOOTH
C        THE DEMODULATED SERIES.
C     INTEGER NDEM
C        THE NUMBER OF VALUES IN THE DEMODULATED SERIES.
C     CHARACTER*1 NMSUB(6)
C        THE NAME OF THE CALLING SUBROUTINE
C     REAL PHASE(IPHASE,2)
C        THE ARRAY CONTAINING THE PRIMARY AND SECONDARY PHASE VALUES.
C     REAL PI
C        THE VALUE OF PI.
C     REAL YMN, YMX
C
C
      CALL IPRINT(IPRT)
C
      CALL VERSP(.TRUE.)
C
      CALL GETPI(PI)
C
      WRITE (IPRT, 1000) FD, FC, K
C
      WRITE (IPRT, 1001)
      CALL VPLMT(AMPL, AMPL, NDEM, 1, NDEM, 0.0E0, 0.0E0,
     +  YMN, YMX, ERROR, NMSUB, .FALSE., 1)
C
      IF (ERROR) THEN
        IERR = 1
        RETURN
      ELSE
        CALL VPMN(AMPL, AMPL, NDEM, 1, NDEM, 1, 0, ISYM, 1, 0,
     +   YMN, YMX, 1.0E0, 1.0E0, .FALSE., 0, 1, 0)
      END IF
C
      WRITE (IPRT, 1002)
      CALL VERSP(.TRUE.)
      WRITE (IPRT, 1003)
      CALL VPMN(PHASE, PHASE, NDEM, 2, IPHASE, 1, 2, ISYM, 1, 0,
     +   -2.0E0*PI, 2.0E0*PI, 1.0E0, 1.0E0, .FALSE., 0, 0, 0)
C
      RETURN
C
C     FORMAT STATEMENTS
C
 1000 FORMAT (/25H TIME SERIES DEMODULATION//
     +   26H DEMODULATION FREQUENCY IS, F10.8/
     +   26H CUTOFF FREQUENCY IS      , F10.8/
     +   38H THE NUMBER OF TERMS IN THE FILTER IS , I5///)
 1001 FORMAT (49H PLOT OF AMPLITUDE OF SMOOTHED DEMODULATED SERIES)
 1002 FORMAT ('1')
 1003 FORMAT (45H PLOT OF PHASE OF SMOOTHED DEMODULATED SERIES)
      END
