*IPGORD
      SUBROUTINE IPGORD (PERI, NF, N, FREQ, XAXIS, YAXIS, ISYM, NPTS,
     +   LPCV)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS ROUTINE PRODUCES CO-ORDINATES FOR THE SPECTRAL PLOTS.
C
C     WRITTEN BY  -  JANET R. DONALDSON
C                    STATISTICAL ENGINEERING DIVISION
C                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE  -  DECEMBER 7, 1981
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      INTEGER
     +   LPCV,N,NF,NPTS
C
C  ARRAY ARGUMENTS
      REAL
     +   FREQ(NF),PERI(NF),XAXIS(LPCV),YAXIS(LPCV)
      INTEGER
     +   ISYM(LPCV)
C
C  LOCAL SCALARS
      REAL
     +   CI,CIMID
      INTEGER
     +   I,II,NLIM
C
C  LOCAL ARRAYS
      REAL
     +   S(40)
C
C  INTRINSIC FUNCTIONS
      INTRINSIC MOD,REAL,SQRT
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     REAL CI, CIMID
C        THE WIDTH AND MIDPOINT OF THE TEST INTERVAL FOR WHITE NOISE.
C     REAL FREQ(NF)
C        THE ARRAY IN WHICH THE FREQUENCIES AT WHICH THE PERIODOGRAM
C        WAS ESTIMATED ARE STORED.
C     INTEGER I
C        AN INDEX VARIABLE
C     INTEGER II
C        AN INDEX VARIABLE
C     INTEGER ISYM(LPCV)
C        THE ARRAY CONTAINING THE CODE FOR THE PLOT SYMBOLS.
C     INTEGER LPCV
C        THE LENGTH OF THE PLOT CO-ORDINATE VECTORS.
C     INTEGER N
C        THE ACTUAL NUMBER OF OBSERVATIONS IN THE SERIES FROM WHICH
C        THE PERIODOGRAM WAS COMPUTED.
C     INTEGER NF
C        THE NUMBER OF FREQUENCIES FOR WHICH THE SPECTRAL ESTIMATES
C        ARE ESTIMATED.
C     INTEGER NLIM
C        THE NUMBER OF POINTS AT WHICH THE WHITE NOISE LIMITS ARE TO
C        PLOTTED.
C     INTEGER NPTS
C        THE NUMBER OF X, Y CO-ORDINATES TO BE PLOTTED.
C     REAL PERI(NF)
C        THE ARRAY CONTAINING THE INTEGRATED PERIODOGRAM VALUES.
C     REAL S(40)
C        VALUES USED TO COMPUTE THE CONFIDENCE LIMITS.
C     REAL XAXIS(LPCV), YAXIS(LPCV)
C        THE X, Y CO-ORDINATES FOR THE SPECTRAL PLOTS.
C
      DATA S(1)/0.975E0/, S(2)/0.842E0/, S(3)/0.708E0/
      DATA S(4)/0.624E0/, S(5)/0.563E0/
      DATA S(6)/0.519E0/, S(7)/0.483E0/, S(8)/0.454E0/
      DATA S(9)/0.430E0/, S(10)/0.409E0/
      DATA S(11)/0.391E0/, S(12)/0.375E0/, S(13)/0.361E0/
      DATA S(14)/0.349E0/, S(15)/0.338E0/
      DATA S(16)/0.327E0/, S(17)/0.318E0/, S(18)/0.309E0/
      DATA S(19)/0.301E0/, S(20)/0.294E0/
      DATA S(21)/0.287E0/, S(22)/0.281E0/, S(23)/0.275E0/
      DATA S(24)/0.269E0/, S(25)/0.264E0/
      DATA S(26)/0.259E0/, S(27)/0.254E0/, S(28)/0.250E0/
      DATA S(29)/0.246E0/, S(30)/0.242E0/
      DATA S(31)/0.238E0/, S(32)/0.234E0/, S(33)/0.231E0/
      DATA S(34)/0.227E0/, S(35)/0.224E0/
      DATA S(36)/0.221E0/, S(37)/0.218E0/, S(38)/0.215E0/
      DATA S(39)/0.213E0/, S(40)/0.210E0/
C
      I = N + MOD(N,2) - 1
C
      IF (I .LE. 40) THEN
         CI = S(I)
      ELSE
         CI = 1.36E0 / SQRT(REAL(I))
      END IF
C
      DO 10 I = 1, NF
         XAXIS(I) = FREQ(I)
         YAXIS(I) = PERI(I)
         ISYM(I) = 1
   10 CONTINUE
C
      NLIM = 101
C
      II = NF
C
      DO 30 I = 1, NLIM, 2
         CIMID = REAL(I-1) / REAL(NLIM-1)
         IF (CIMID - CI .LT. 0.0E0) GO TO 20
         II = II + 1
         XAXIS(II) = CIMID / 2.0E0
         YAXIS(II) = CIMID - CI
         ISYM(II) = 2
   20    IF (CIMID + CI .GT. 1.0E0) GO TO 30
         II = II + 1
         XAXIS(II) = CIMID / 2.0E0
         YAXIS(II) = CIMID + CI
         ISYM(II) = 2
   30 CONTINUE
C
      NPTS = II
C
      RETURN
      END
