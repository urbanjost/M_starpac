*GFORD
      SUBROUTINE GFORD (FREQ, GAIN, ISORT, NF, XORD, YORD,
     +   NORD, YPLTMN, YPLTMX, NPRT, IGFERR)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS ROUTINE PRODUCES ORDANENTS FOR THE GAIN FUNCTION PLOTS.
C
C     WRITTEN BY - JANET R. DONALDSON
C                  STATISTICAL ENGINEERING DIVISION
C                  NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE  -  NOVEMBER 26, 1980
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      REAL
     +   YPLTMN,YPLTMX
      INTEGER
     +   IGFERR,NF,NORD,NPRT
C
C  ARRAY ARGUMENTS
      REAL
     +   FREQ(NF),GAIN(NF),XORD(NF),YORD(NF)
      INTEGER
     +   ISORT(NF)
C
C  LOCAL SCALARS
      REAL
     +   GAINMN,GAINMX,YMAX
      INTEGER
     +   I,J
C
C  EXTERNAL SUBROUTINES
      EXTERNAL SPCCK
C
C  INTRINSIC FUNCTIONS
      INTRINSIC LOG10
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     REAL FREQ(NF)
C        THE VECTOR OF FREQUENCIES AT WHICH THE SPECTRUM IS TO BE
C        ESTIMATED.
C     REAL GAIN(NF)
C        THE VECTOR IN WHICH THE GAIN FUNCTION IS STORED.
C     REAL GAINMN, GAINMX
C        THE MINIMUM AND MAXIMUM GAIN FUNCTION VALUE TO BE PLOTTED.
C     INTEGER I
C        AN INDEX VARIABLE
C     INTEGER IGFERR
C        AN ERROR FLAG INDICATING WHETHER COMPUTATIONS SEEM
C        TO HAVE PRODUCED REASONABLE RESULTS.
C     INTEGER ISORT(NF)
C        THE ARRAY USED FOR SORTING.
C     INTEGER J
C        AN INDEXING VARIABLE.
C     INTEGER NF
C        THE NUMBER OF FREQUENCIES FOR WHICH THE SPECTRAL ESTIMATES
C        ARE TO BE ESTIMATED.
C     INTEGER NORD
C        THE NUMBER OF ORDINATES TO BE PLOTTED.
C     INTEGER NPRT
C        A CODE USED TO SPECIFY THE TYPE OF PLOT, WHERE IF
C        NPRT < 0 THE PLOT IS DECIBELS/LINEAR
C        NPRT = 0 THE PLOT IS SUPPRESSED
C        NPRT > 0 THE PLOT IS LOG/LINEAR
C     REAL XORD(NF)
C        THE X ORDINATES FOR THE SPECTRUM PLOT.
C     REAL YMAX
C        THE MAXIMUM ACTUAL SPECTRUM VALUE (IN DECIBELS) TO BE PLOTTED.
C     REAL YORD(NF)
C        THE Y ORDINATES FOR THE SPECTRUM PLOTS.
C     REAL YPLTMN, YPLTMX
C        THE MINIMUM AND MAXIMUM VAUES TO BE PLOTTED FOR THE Y AXIS.
C
      IGFERR = 0
C
      CALL SPCCK (GAIN, ISORT, NF, GAINMN, GAINMX, NORD, IGFERR)
C
      IF (IGFERR .NE. 0) RETURN
C
      J = 0
C
      IF (NPRT.GE.1) GO TO 30
C
C     SET ORDINATES FOR DECIBLE PLOTS
C
      YMAX = 10.0E0 * LOG10(GAINMX)
C
      YPLTMX = 0.0E0
      YPLTMN = 10.0E0 * LOG10(GAINMN) - YMAX
C
      DO 10 I = 1, NF
         IF (GAIN(I) .LT. GAINMN) GO TO 10
         J = J + 1
         XORD(J) = FREQ(I)
         YORD(J) = 10.0E0 * LOG10(GAIN(I)) - YMAX
   10 CONTINUE
C
      RETURN
C
   30 CONTINUE
C
      YPLTMX = GAINMX
      YPLTMN = GAINMN
C
C     SET ORDINATES FOR LOG PLOTS
C
      DO 50 I = 1, NF
         IF (GAIN(I) .LT. GAINMN) GO TO 50
         J = J + 1
         XORD(J) = FREQ(I)
         YORD(J) = GAIN(I)
   50 CONTINUE
C
      RETURN
      END
