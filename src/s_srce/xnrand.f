*XNRAND
      SUBROUTINE XNRAND(LDSTAK)
C
C     LATEST REVISION  -  03/15/90  (JRD)
C
C     THIS PROGRAM TESTS FEATURES OF THE NRAND FAMILY TO ENSURE THAT
C     ALL ASPECTS OF THE NRAND FAMILY ROUTINES WORK CORRECTLY.
C
C     WRITTEN BY  -  JOHN E. KOONTZ, JANET R. DONALDSON
C                    STATISTICAL ENGINEERING DIVISION
C                    NATIONAL BUREAU OF STANDARDS, BOULDER, COLORADO
C
C     CREATION DATE  -  MAY 17, 1982
C
C
C  VARIABLE DECLARATIONS
C
C  SCALAR ARGUMENTS
      INTEGER
     +   LDSTAK
C
C  SCALARS IN COMMON
      INTEGER
     +   IERR
C
C  ARRAYS IN COMMON
      DOUBLE PRECISION DSTAK(12)
C
C  LOCAL SCALARS
      REAL
     +   SIGMA,YMEAN
      INTEGER
     +   I,IPRT,ISEED,N
C
C  LOCAL ARRAYS
      REAL
     +   Y(1000)
C
C  EXTERNAL SUBROUTINES
      EXTERNAL HIST,IPRINT,NRAND,NRANDC
C
C  COMMON BLOCKS
      COMMON /CSTAK/DSTAK
      COMMON /ERRCHK/IERR
C
C     VARIABLE DEFINITIONS (ALPHABETICALLY)
C
C     DOUBLE PRECISION DSTAK(12)
C        THE DOUBLE PRECISION VERSION OF THE /CSTAK/ WORK AREA.
C     INTEGER I
C        AN INDEX VARIABLE
C     INTEGER IERR
C        FLAG TO INDICATE PRESENCE OF ERROR DETECTED BY PRECEDING
C        STARPAC CALL.  (0 IS OK, 1 IS ERROR)
C     INTEGER IPRT
C        LOGICAL OUTPUT UNIT.
C     INTEGER ISEED
C        THE SEED FOR THE RANDOM NUMBER GENERATOR.
C     INTEGER LDSTAK
C        AMOUNT OF WORK AREA.  SIZE OF DSTAK.
C     INTEGER N
C        THE LENGTH OF THE VECTOR Y.
C     REAL SIGMA
C        THE S.D. OF THE SAMPLE.
C     REAL Y(1000)
C        DATA VECTOR FOR TESTS.
C     REAL YMEAN
C        THE MEAN OF THE SAMPLE.
C
C
C     DEFINE IPRT, THE CURRENT OUTPUT UNIT.
C
      CALL IPRINT(IPRT)
C
C     CHECK FOR SUFFICIENT WORK AREA LENGTH.
C
      IF (LDSTAK.LT.1000) THEN
        WRITE (IPRT, 1000)
         RETURN
      END IF
C
C     HEADING.
C
      WRITE (IPRT,1150)
C
C     TEST 1.  CHECK ALL ERROR MESSAGES.
C
      WRITE (IPRT,1160)
C
C     ERROR 1, ZERO OR FEWER ELEMENTS OR NEGATIVE STANDARD DEVIATION.
C
      ISEED = 0
      SIGMA = -1
      N = 0
      YMEAN = 0.0E0
      CALL NRAND(Y, N, ISEED)
      WRITE (IPRT,1170) IERR
      CALL NRANDC(Y, N, ISEED, YMEAN, SIGMA)
      WRITE (IPRT,1170) IERR
C
C     COMPARE RESULTS
C
      ISEED = 334
      N = 10
      YMEAN = 0.0E0
      SIGMA = 1.0E0
C
      WRITE (IPRT, 1120) N, ISEED
      CALL NRAND (Y, N, ISEED)
      WRITE (IPRT, 1100) (Y(I),I=1,N)
C
      ISEED = 333
      WRITE (IPRT, 1130) N, YMEAN, SIGMA, ISEED
      CALL NRANDC (Y, N, ISEED, YMEAN, SIGMA)
      WRITE (IPRT, 1100) (Y(I),I=1,N)
C
      ISEED = 13531
      N = 1000
      YMEAN = 0.0E0
      SIGMA = 1.0E0
C
      WRITE (IPRT, 1120) N, ISEED
      CALL NRAND (Y, N, ISEED)
      CALL HIST (Y, N, LDSTAK)
C
      WRITE (IPRT, 1130) N, YMEAN, SIGMA, ISEED
      CALL NRANDC (Y, N, ISEED, YMEAN, SIGMA)
      CALL HIST (Y, N, LDSTAK)
C
      ISEED = 99999
      N = 1000
      YMEAN = 4.0E0
      SIGMA = 4.0E0
C
      WRITE (IPRT, 1120) N, ISEED
      CALL NRAND (Y, N, ISEED)
      CALL HIST (Y, N, LDSTAK)
C
      WRITE (IPRT, 1130) N, YMEAN, SIGMA, ISEED
      CALL NRANDC (Y, N, ISEED, YMEAN, SIGMA)
      CALL HIST (Y, N, LDSTAK)
C
      RETURN
C
C     FORMATS
C
 1000 FORMAT ('1THE DIMENSION OF DSTAK AND THE VALUE OF LDSTAK NEEDED'/
     +  ' FOR NRANDX MUST EQUAL OR EXCEED 1000.  CHANGE DRIVER'/
     +  ' AND RECALL NRANDX.')
 1100 FORMAT (5E15.8)
 1120 FORMAT ('1GENERATE ', I4,
     +  ' STANDARD NORMAL NUMBERS USING ISEED = ', I5)
 1130 FORMAT ('1GENERATE ', I4,
     +  ' NORMALLY DISTRIBUTED NUMBERS WITH YMEAN = ', F5.2,
     +  ' AND SIGMA = ', F5.2, ' USING ISEED = ', I5)
 1150 FORMAT ('1TEST RUNS FOR THE NRAND FAMILY OF ROUTINES.')
 1160 FORMAT(' TEST 1.  GENERATE EACH OF THE POSSIBLE ',
     +   15HERROR MESSAGES.)
 1170 FORMAT(/22H THE VALUE OF IERR IS , I4//)
      END
